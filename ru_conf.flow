import ds/tree;
import fs/filesystem;
import net/url_parameter;
import sys/concurrent;
import lingo/linecolumn;
import math/stringmath;

export {
	RuPlace(file : string, pos : int);
	
	RuConf(
		file : string,
		ext : string,
		importRoots : [string],
		workingDir : string,
        verbose : int,
		opts : Tree<string, string>,
		threadId : int,
		onError : (string, [RuPlace]) -> void
	);

	ruNoConf = RuConf("__no", "__no", [], "", 0, makeTree(), 0, nop2);

	ruMakeConf(onError : (string, [RuPlace]) -> void) -> Maybe<RuConf>;
	ruTrimPath(file : string, conf : RuConf, ext : string) -> string;
	ruFindPath(file : string, conf : RuConf, ext : string) -> string;
	ruAddErr2Conf(conf : RuConf, msg : string) -> RuConf;
	ruAddErrCounter(conf : RuConf, err_count : ref int) -> RuConf;
	ruPlace2s(place : RuPlace) -> string;
}

ruMakeConf(onError : (string, [RuPlace]) -> void) -> Maybe<RuConf> {
	conf_opts = ruReadConfFileFromDir(resolveRelativePath("."));
	opts = fold(getAllUrlParametersArray(), conf_opts, \acc, p -> setTree(acc, p[0], p[1]));
	file = foldTree(opts, "", \name, val, acc -> 
		if (name == "file" || val == "") {
			if (acc == "") {
				if (name == "file") val else name
			} else if (acc != val) {
				onError("target file is set up more then once: '" + val + "' and '" + acc + "'", []);
				""
			} else acc;
		} else acc
	);
	ext = getFileExt(file);
	verbose = s2i(lookupTreeDef(opts, "verbose", "0"));
	importRoots = map(strSplit(lookupTreeDef(opts, "import-roots", ""), ","), \imp -> strReplace(imp, "\\", "/"));
	workingDir = lookupTreeDef(opts, "working-dir", "");
	conf1 = RuConf(file, ext, importRoots, workingDir, verbose, opts, s2i(getThreadId()), onError);
	if (file == "") {
		onError("target file is not set", []);
		None() 
	} else {
		Some(RuConf(
			ruTrimPath(resolveRelativePath(file), conf1, ext), ext,
			importRoots, workingDir, verbose, opts, s2i(getThreadId()), onError
		));
	}
}

ruReadConfFileFromDir(dir : string) -> Tree<string, string> {
	path = dir + "/russell.conf";
	if (fileExists(path)) {
		config = getFileContent(path);
		lines = strSplit(config, "\n");
		fold(lines, makeTree(), \acc, l -> {
			// Skip comment lines in russell.conf
			if (!startsWith(ltrim2(l, " \t"), "#") && !startsWith(ltrim2(l, " \t"), "//")) {
				opts = strSplit(l, "=");
				if (length(opts) == 1) {
					opt = trim2(opts[0], " \t\r");
					setTree(acc, opt, "");
				} else if (length(opts) == 2) {
					opt = trim2(opts[0], " \t\r");
					val = trim2(opts[1], " \t\r");
					setTree(acc, opt, val)
				} else acc;
			} else acc;
		});
	} else if (dir == "/" || dir == "") {
		makeTree();
	} else {
		ruReadConfFileFromDir(dirName(dir));
	}
}

ruTrimPath(file : string, conf : RuConf, ext : string) -> string {
	noext = strReplace(if (endsWith(file, ext)) strsubsmart(file, 0, -strlen(ext)) else file, "\\", "/");
	shift = if (conf.workingDir == "") "" else relatingPath(conf.workingDir, resolveRelativePath("."));
	pathShift = if (shift == "") shift else shift + "/";

	root_found = ref false;
	processImportRoot = \acc, impRoot -> {
		resolved = strReplace(resolveRelativePath(impRoot), "\\", "/");
		if (startsWith(toLowerCase(acc), toLowerCase(resolved))) {
			root_found := true;
			strRight(acc, strlen(resolved))
		} else if (startsWith(toLowerCase(acc), toLowerCase(impRoot))) {
			root_found := true;
			strRight(acc, strlen(impRoot))
		} else acc
	};

	if (^root_found) {
	// Find the path-less path
		trim2(fold(conf.importRoots, noext, \acc, impRoot -> {
			acc1 = if (pathShift != "") {
				processImportRoot(acc, pathShift + impRoot)
			} else acc;
			processImportRoot(acc1, impRoot)
		}), "/")
	} else {
		noext
	}
}

ruFindPath(file : string, conf : RuConf, ext : string) -> string {
	withRus = if (endsWith(file, ext)) file else file + ext;
	shift = if (conf.workingDir == "") "" else relatingPath(conf.workingDir, resolveRelativePath("."));
	if (fileExists(withRus)) {
		resolveRelativePath(withRus);
	} else {
		ruDoFindPath(withRus, if (shift == "") shift else shift + "/", conf.importRoots);
	}
}

ruDoFindPath(withRus : string, pathShift : string, importRoots : [string]) -> string {
	if (importRoots == []) withRus
	else {
		head = importRoots[0];
		directPath = head + (if (endsWith(head, "/")) "" else "/") + withRus;
		if (fileExists(directPath)) {
			resolveRelativePath(directPath);
		} else {
			shiftedPath = pathShift + directPath;
			if (fileExists(shiftedPath)) {
				resolveRelativePath(shiftedPath);
			} else {
				ruDoFindPath(withRus, pathShift, tail(importRoots));
			}
		}
	}
}

ruAddErr2Conf(conf : RuConf, msg : string) -> RuConf {
	RuConf(conf with onError = \str, pos -> conf.onError(str + msg, pos))
}

ruAddErrCounter(conf : RuConf, err_count : ref int) -> RuConf {
	RuConf(conf with onError = 
		\err, places -> {
			err_count := ^err_count + 1;
			conf.onError(err, places);
		}
	)
}

ruPlace2s(place : RuPlace) -> string {
	if (place.file == "" || place.pos == -1) "" else {
		code = getFileContent(place.file);
		resolver = makeLineResolver(code);
		pos = findLine(resolver, place.pos);
		"file " + place.file + ": line " + i2s(pos.lineno) + ", col " + i2s(pos.column)
	}
}
