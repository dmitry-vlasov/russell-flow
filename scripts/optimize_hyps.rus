/**
@help { 
	Iterates optimization of:
	- remove-duplicate-steps
	- remove-unused-steps
	- remove-unused-hyps
	until these actions make no effect.
	After oprimization writes the result.
}
@autoindent
@category refactor
*/

if !isdef(have_read) {
	read;
}

iteration_hyps = 1;
optimized_hyps = 1;
was_optimized_hyps = false;

print "Optimizing hyps ...";
print "";

while (optimized_hyps > 0) {
    print ("iteration " + iteration_hyps); 
    iteration_hyps = (iteration_hyps + 1);
    remove-duplicate-steps;
	print ("duplicate steps: " + remove-duplicate-steps.optimized-steps + " in " + time2s(remove-duplicate-steps.time));
    remove-unused-steps;
	print ("unused steps: " + remove-unused-steps.optimized-steps + " in " + time2s(remove-unused-steps.time));
    remove-unused-hyps;
	print ("remove-unused-hyps: " + remove-unused-hyps.optimized-hyps + " in " + time2s(remove-unused-hyps.time));
    optimized_hyps = (
        remove-duplicate-steps.optimized-theorems +
        remove-unused-steps.optimized-theorems +
        remove-unused-hyps.optimized-theorems
    );
    if (optimized_hyps > 0) {
        was_optimized_hyps = true;
    }
    print ("optimized " + optimized_hyps); 
}

if was_optimized_hyps {
	print ("optimized " + optimized_hyps + " theorems");
	output_mem_stats;
	info-volume;
	verify;
	if verify.success {
    	print "verification is ok, writing optimized hyps math ... ";
		output_mem_stats;
		info-volume;
    	write-ru;
		print ("optimized hyps math are written in " + time2s(write-ru.time));
	} else {
		print "ERROR IN VERIFICATION";
	}
} else {
    print "nothing was optimzied";
}
