import math/stringmath;
import parse/ru_parse_exp;
import parse/ru_parse_src;
import ru_math;

export {
	ruCollectMath(parsed : Tree<string, RuParseSource>, conf : RuConf) -> RuMath {
		ruDoCollectMath(parsed, conf, RuMath(makeTree(), makeTree(), makeTree(), makeTree(), makeTree(), conf));
	}
	ruSuperTypeName(super : string, infer : string) -> string {
		infer + "-" + super
	}
}

ruDoCollectMath(left : Tree<string, RuParseSource>, conf : RuConf, acc : RuMath) -> RuMath {
	if (sizeTree(left) == 0) acc else {
		ready = filter(
			getTreeValues(left), 
			\src -> forall(src.imports, \imp -> !containsKeyTree(left, imp.path))
		);
		//println("modules to process: " + strGlue(map(ready, \s -> s.info.file), ", "));
		new_acc = fold(ready, acc, \ac, src -> ruCollectSource(src, conf, ac));
		new_left = fold(ready, left, \ac, src -> removeFromTree(ac, src.info.file));
		ruDoCollectMath(new_left, conf, new_acc);
	}
}

ruCollectSource(src : RuParseSource, conf : RuConf, acc : RuMath) -> RuMath {
	start = timestamp();
	switch (lookupTree(acc.sources, src.info.file)) {
		Some(__): {
			conf.onError("source " + src.info.file + " is already defined", []);
			acc
		}
		None(): {
			imps = map(src.imports, \imp -> RuImport(imp.path));
			acc1 = fold(src.decls, acc, \ac, block -> 
				switch(block) {
					RuParseDecl(): ruCollectDecl(block, conf, ac);
					RuParseComment(__): ac;
				}
			);
			decls = fold(src.decls, [], \ac, block -> 
				switch (block) {
					RuParseConst(name,__,__):            concat(ac, [lookupTreeDef(acc1.consts, name, ruNoConst)]);
					RuParseType(name,__):                concat(ac, [lookupTreeDef(acc1.types, name, ruNoTypeEnv).type]);
					RuParseRule(name,__,__):             concat(ac, [lookupTreeDef(acc1.rules, name, ruNoRule)]);
					RuParseAxiom(name,__,__,__,__):      concat(ac, [lookupTreeDef(acc1.assertions, name, ruNoAxiom)]);
					RuParseTheorem(name,__,__,__,__,__): concat(ac, [lookupTreeDef(acc1.assertions, name, ruNoTheorem)]);
					RuParseDef(name,__,__,__,__,__,__):  concat(ac, [lookupTreeDef(acc1.assertions, name, ruNoAxiom)]);
					RuParseComment(text):                concat(ac, [RuComment(text)]);
					RuParseTheory(name, __):             ac; // TODO
				}
			);
			if (conf.verbose > 0) {
				println("collected: " + src.info.file + " in " + d2st((timestamp() - start)/ 1000.0, 4) + " s");
			}
			RuMath(
				acc1 with sources = setTree(acc1.sources, src.info.file, RuSource(imps, decls, src.info))
			);
		}
	}
}

ruCollectDecl(decl : RuParseDecl, conf : RuConf, acc : RuMath) -> RuMath {
	switch (decl) {
		RuParseConst(__,__,__):            ruCollectConst(decl, conf, acc);
		RuParseType(__,__):                ruCollectType(decl, conf, acc);
		RuParseRule(__,__,__):             ruCollectRule(decl, conf, acc);
		RuParseAxiom(__,__,__,__,__):      ruCollectAxiom(decl, conf, acc);
		RuParseTheorem(__,__,__,__,__,__): ruCollectTheorem(decl, conf, acc);
		RuParseDef(__,__,__,__,__,__,__):  ruCollectDef(decl, conf, acc);
		RuParseComment(__):                acc;
		RuParseTheory(name, decls):        acc; // TODO 
	}
}

ruCollectConst(const : RuParseConst, conf : RuConf, acc : RuMath) -> RuMath {
	switch (lookupTree(acc.consts, const.symb)) {
		Some(__): {
			conf.onError("constant " + const.symb + " is already defined", []);
			acc
		}
		None(): {
			RuMath(acc with consts = setTree(acc.consts, const.symb, RuConst(const.symb, const.ascii, const.latex)));
		}
	}
}

ruCollectType(type : RuParseType, conf : RuConf, acc : RuMath) -> RuMath {
	switch (lookupTree(acc.types, type.name)) {
		Some(__): {
			conf.onError("type " + type.name + " is already defined", []);
			acc
		}
		None(): {
			infer = type.name;
			super_rule = \super -> {
				RuRule(
					ruSuperTypeName(super, infer), 
					makeTree1("x", RuVar("x", infer)), 
					RuTerm(super, [RuVarRef("x", infer)])
				);
			}
			supers = fold(type.supers, makeTree(), \ac, s ->
				switch (lookupTree(acc.types, s)) {
					Some(env): {
						foldTree(env.supers,
							setTree(ac, s, super_rule(s)), 
							\super, __, a ->
								setTree(a, super, super_rule(super))
						)
					}
					None(): {
						conf.onError("unknown super type " + s + " for a type" + infer, []);
						ac
					}
				}
			);
			types = foldTree(supers, acc.types, 
				\sup_name, sup_rule, ac -> {
					env = lookupTreeDef(ac, sup_name, ruNoTypeEnv);
					setTree(ac, sup_name, RuTypeEnv(env with infers = setTree(env.infers, infer, super_rule(sup_name))))
				}
			);
			RuMath(acc with 
				types = setTree(types, type.name, 
					RuTypeEnv(
						RuType(type.name, type.supers), 
						ruAddTerm2RuTrie(
							RuTerm(type.name, [RuVarRef("x", type.name)]), 
							None(), //Some(\n, len -> [RuVarRef(n.name, type.name)]), 
							ruTrieEmpty
						), 
						supers, makeTree()
					)
				),
				rules = foldTree(supers, acc.rules, \__, rule, ac -> setTree(ac, rule.name, rule))
			);
		}
	}
}

ruCollectRule(rule : RuParseRule, conf : RuConf, acc : RuMath) -> RuMath {
	switch (lookupTree(acc.rules, rule.name)) {
		Some(__): {
			conf.onError("rule " + rule.name + " is already defined", []);
			acc
		}
		None(): {
			type = rule.term.type;
			vars = fold(rule.vars, makeTree(), \ac, v ->
				switch (lookupTree(acc.types, v.type)) {
					Some(tp): setTree(ac, v.name, RuVar(v.name, v.type));
					None(): {
						conf.onError("unknown var " + v.name + " type " + v.type, []);
						ac
					}
				}
			);
			new_rule = RuRule(rule.name, vars, ruMakeTerm(rule.term, vars, acc.consts, ruAddErr(conf, "\nat rule: " + rule.name)));
			switch (lookupTree(acc.types, rule.term.type)) {
				Some(env): {
					new_types = setTree(acc.types, env.type.name,
						RuTypeEnv(env with 
							trie = ruAddTerm2RuTrie(new_rule.term, 
								Some(\expr -> concat(expr, [RuRuleRef(rule.name, length(expr))])),
								env.trie
							)
						)
					);
					RuMath(acc with 
						rules = setTree(acc.rules, rule.name, new_rule),
						types = new_types
					);
				}
				None(): {
					conf.onError("unknown type " + rule.term.type, []);
					acc;
				}
			}
		}
	}
}

ruMakeTerm(term : RuParseExp, vars : Tree<string, RuVar>, consts : Tree<string, RuConst>, conf : RuConf) -> RuTerm {
	expr = fold(term.symbs, [], \ac, symb -> {
		switch (lookupTree(consts, symb)) {
			Some(s): concat(ac, [RuConstRef(symb)]);
			None(): 
				switch (lookupTree(vars, symb)) {
					Some(v): concat(ac, [RuVarRef(symb, v.type)]);
					None(): {
						conf.onError("unknown symbol " + symb, []);
						ac
					}
				}
			}
		}
	);
	RuTerm(term.type, expr);
}

arrMaybe2maybeArr(arr : [Maybe<?>]) -> Maybe<[?]>  {
	fold(arr, Some([]), \acc, x ->
		switch (acc) {
			None(): acc;
			Some(a): maybeMap(x, \y -> concat(a, [y]));
		}
	)
}

ruCollectAxiom(ax : RuParseAxiom, conf : RuConf, acc : RuMath) -> RuMath {
	switch (lookupTree(acc.assertions, ax.name)) {
		Some(__): {
			conf.onError("assertion with name " + ax.name + " is already defined", []);
			acc
		}
		None(): {
			conf1 = ruAddErr(conf, "\nat axiom " + ax.name);
			vars = ruCollectVars(ax.vars, conf, acc);
			disjs = map(ax.disjs, \d -> RuDisj(d.vars));
			maxiom = maybeMap2(\prop, hyps -> RuAxiom(ax.name, vars, disjs, hyps, prop))
			(
				ruCollectExp(ax.prop, vars, conf1, acc),
				arrMaybe2maybeArr(map(ax.hyps, \h -> ruCollectHyp(h, vars, conf1, acc)))
			);
			switch (maxiom) {
				Some(axiom): RuMath(acc with assertions = setTree(acc.assertions, ax.name, axiom));
				None(): {
					conf.onError("error in assertion " + ax.name, []);
					acc;
				}
			}
		}
	}
}

ruCollectExp(ex : RuParseExp, vars : Tree<string, RuVar>, conf : RuConf, acc : RuMath) -> Maybe<RuExp> {
	ruParseExp(ruMakeTerm(ex, vars, acc.consts, conf), acc.types, conf)
}

ruCollectVars(vars : [RuParseVar], conf : RuConf, acc : RuMath) -> Tree<string, RuVar> {
	fold(vars, makeTree(), \ac, v ->
		switch (lookupTree(acc.types, v.type)) {
			Some(tp): setTree(ac, v.name, RuVar(v.name, v.type));
			None(): {
				conf.onError("unknown var " + v.name + " type " + v.type, []);
				ac
			}
		}
	)
}

ruCollectHyp(hyp : RuParseHyp, vars : Tree<string, RuVar>, conf : RuConf, acc : RuMath) -> Maybe<RuHyp> {
	maybeMap(ruCollectExp(hyp.expr, vars, conf, acc), \e -> RuHyp(hyp.ind, e))
}

ruCollectTheorem(th : RuParseTheorem, conf : RuConf, acc : RuMath) -> RuMath {
	switch (lookupTree(acc.assertions, th.name)) {
		Some(__): {
			conf.onError("assertion with name " + th.name + " is already defined", []);
			acc
		}
		None(): {
			conf1 = ruAddErr(conf, "\nat theorem " + th.name);
			vars = ruCollectVars(th.vars, conf, acc);
			disjs = map(th.disjs, \d -> RuDisj(d.vars));
			mtheorem = maybeBind(
				ruCollectProof(th.proof, vars, conf1, acc),
				\proof ->
					maybeMap2(\prop, hyps -> RuTheorem(th.name, vars, disjs, hyps, prop, proof))
					(
						ruCollectExp(th.prop, vars, conf1, acc),
						arrMaybe2maybeArr(map(th.hyps, \h -> ruCollectHyp(h, vars, conf1, acc)))
					)
			);
			switch (mtheorem) {
				Some(theorem): RuMath(acc with assertions = setTree(acc.assertions, th.name, theorem));
				None(): {
					conf.onError("error in theorem " + th.name, []);
					acc;
				}
			}
		}
	}
}

ruCollectProof(proof : RuParseProof, vars : Tree<string, RuVar>, conf : RuConf, acc : RuMath) -> Maybe<RuProof> {
	proof_vars = ruCollectVars(proof.vars, conf, acc);
	proof_disjs = map(proof.disjs, \d -> RuDisj(d.vars));
	all_vars = mergeTree(vars, proof_vars);
	do_ref = \r -> switch (r) {
		RuParseHypRef(i): RuHypRef(i);
		RuParseStepRef(i): RuStepRef(i);
	}
	msteps = fold(proof.steps, [], 
		\ac, s -> concat(ac, 
			[maybeMap(ruCollectExp(s.expr, all_vars, conf, acc),
			\e -> RuStep(s.ind, s.assertion, map(s.refs, do_ref), e))]
		)
	);
	switch (arrMaybe2maybeArr(msteps)) {
		Some(steps): Some(RuProof(proof_vars, proof_disjs, steps));
		None(): None();
	}
}


ruCollectDef(df : RuParseDef, conf : RuConf, acc : RuMath) -> RuMath {
	switch (lookupTree(acc.assertions, df.name)) {
		Some(__): {
			conf.onError("assertion with name " + df.name + " is already defined", []);
			acc
		}
		None(): {
			conf1 = ruAddErr(conf, "\nat definitoin " + df.name);
			vars = ruCollectVars(df.vars, conf, acc);
			disjs = map(df.disjs, \d -> RuDisj(d.vars));
			prop_ex = fold(df.def.symbs, [], \ac, s ->
				if (s == "defiendum") concat(ac, df.defm.symbs) else
				if (s == "definiens") concat(ac, df.defs.symbs) else
				concat(ac, [s])
			);
			mdef = 
			maybeBind(ruCollectExp(df.defm, vars, conf1, acc), \dfm ->
				maybeBind(ruCollectExp(df.defs, vars, conf1, acc), \dfs ->
					maybeMap2(\prop, hyps -> RuDef(df.name, vars, disjs, hyps, dfm, dfs, prop))
					(
						ruCollectExp(RuParseExp(df.def.type, prop_ex), vars, conf1, acc),
						arrMaybe2maybeArr(map(df.hyps, \h -> ruCollectHyp(h, vars, conf1, acc)))
					)
				)
			);
			switch (mdef) {
				Some(def): RuMath(acc with assertions = setTree(acc.assertions, df.name, def));
				None(): {
					conf.onError("error in assertion " + df.name, []);
					acc;
				}
			}
		}
	}
}
