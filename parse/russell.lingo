source   = '\uFEFF'? ws toplevel*;
toplevel = scomment | mcomment | import | const | type | rule | axiom | def | theorem;

scomment = "//" (!'\n' char_)* '\n' ws;
mcomment = "/*" (!"*/" char_)* "*/" ws;

import   = "import" ws symb ws ";;" ws;

const   = "constant" ws '{' ws symbol ascii? latex? '}' ws ;
symbol  = "symbol" ws symb ws ";;" ws;
ascii   = "ascii"  ws symb ws ";;" ws;
latex   = "latex"  ws symb ws ";;" ws;

type    = "type" ws id ws supers? ";;" ws;
supers  = ':' ws id ws super*;
super   = ',' ws id ws;

rule    = "rule"       ws id ws vars? '{' ws "term" ws expr '}' ws;
axiom   = "axiom"      ws id ws vars? disjs? '{' ws hyps_bar? "prop" ws expr '}' ws;
def     = "definition" ws id ws vars? disjs? '{' ws hyps? defm defs "-----" '-'* ws "prop" ws expr '}' ws;
theorem = "theorem"    ws id ws vars? disjs? '{' ws hyps_bar? "prop" ws expr '}' ws proof;
proof   = "proof"      ws vars? disjs? '{' ws step* '}' ws;

hyps_bar = hyp+ "-----" '-'* ws;
hyps  = hyp+;
hyp   = "hyp" ws ind ws expr;
defm  = "defiendum" ws expr;
defs  = "definiens" ws expr;

step     = "step" ws ind ws type_id '=' ws id ws refs "|-" ws symbs ";;" ws;
refs     = '(' ws refs0? ')' ws;
refs0    = ref refs1*;
refs1    = ',' ws ref;
ref      = hyp_ref | step_ref;
hyp_ref  = "hyp" ws ind ws;
step_ref = "step" ws ind ws;

disjs  = "disjointed(" ws disjs0? ')' ws;
disjs0 = disj disjs1*;
disjs1 = ',' ws disj;
disj   = dis0_v dis1_v+;
dis0_v = id ws;
dis1_v = id ws;

vars  = '(' ws vars0? ')' ws;
vars0 = var vars1*;
vars1 = ',' ws var;
var   = id ws type_id;
type_id = ':' ws id ws;

expr  = type_id '=' ws ('#' | "|-") ws symbs ";;" ws;

symbs  = symbws*;
symbws = symb ws;

symb  = (!";;" !space char_)+;
ind   = digit+;
id    = id_char+;
id_char = 'a'-'z' | 'A'-'Z' | '0'-'9' | '_' | '.' | '-' | '\'';
letter  = 'a'-'z' | 'A'-'Z';
digit   = '0'-'9';

char_ = '\u0000'-'\uFFFF';
ws    = space*;
space = ' ' | '\t' | '\n' | '\r';
