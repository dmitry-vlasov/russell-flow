import parse/ru_collect_types;

export {
	ruParseExp(term : RuTerm, types : Tree<string, RuTypeEnv>, conf : RuConf) -> Maybe<RuExp> {
		if (length(term.expr) == 0) {
			conf.onError("empty expression met", []);
			None()
		} else {
			switch (lookupTree(types, term.type)) {
				Some(tenv): {
					ret = ruParseExpr(term.expr, 0, tenv, tenv.table, types, conf);
					if (ret.pos < length(term.expr)) {
						conf.onError("unparsed rest: " + ruTerm2s(term), []);
						None();
					} else {
						if (ret.expr == []) {
							conf.onError("expr was not successfully parsed: " + ruTerm2s(term), []);
							None();
						} else {
							Some(RuExp(ret.expr));
						}
					}
				}
				None(): {
					conf.onError("unknown type: " + term.type, []);
					None();
				}
			}
		}
	}
}

RuParseExpRet(
	pos : int,
	expr : [RuExpNode]
);

ruParseExpr(nodes : [RuTermNode], pos : int, tenv : RuTypeEnv, tab : RuTab, types : Tree<string, RuTypeEnv>, conf : RuConf) -> RuParseExpRet {
	if (pos == length(nodes)) {
		RuParseExpRet(pos + 1, []) 
	} else {
		switch (nodes[pos]) {
			RuVarRef(v, t): {
				switch (lookupTree(tab.vars, t)) {
					Some(n): RuParseExpRet(pos + 1, [RuVarRef(v, t)]);
					None(): {
						switch (lookupTree(tenv.infers, t)) {
							Some(r): RuParseExpRet(pos + 1, [RuRuleRef(r.name, 1), RuVarRef(v, t)]);
							None():  RuParseExpRet(pos + 1, []);
						}
					}
				}
			}
			RuConstRef(c): {
				switch (lookupTree(tab.consts, c)) {
					Some(n): {
						if (n.data == "") {
							ruParseExpr(nodes, pos + 1, tenv, n.table, types, conf);
						} else {
							x = ruParseExpr(nodes, pos + 1, tenv, n.table, types, conf);
							if (x.expr != []) x else {
								RuParseExpRet(x.pos, concat([RuRuleRef(n.data, x.pos - pos)], x.expr));
							}
						}
					}
					None(): {
						foldTree(tab.vars, RuParseExpRet(pos + 1, []), \t, n, acc ->
							if (acc.expr != []) acc else {
								new_env = lookupTreeDef(types, t, ruNoTypeEnv);
								ruParseExpr(nodes, pos + 1, new_env, new_env.table, types, conf)
							}
						)
					}
				}
			}
		}
	}
}

