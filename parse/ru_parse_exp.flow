import parse/ru_type_env;

export {
	ruParseExp(term : RuTerm, types : Tree<string, RuTypeEnv>, conf : RuConf) -> Maybe<RuExp> {
		if (length(term.expr) == 0) {
			conf.onError("empty expression met", []);
			None()
		} else {
			ret = switch (lookupTree(types, term.type)) {
				Some(tenv): {
					//println("going to parse: " + ruTerm2s(term.expr));
					switch (ruParseExpr(
						RuParseEnv(term.expr, types, conf), 0, 
						RuParseStack(None(), 0, tenv.trie, [])
					)) {
						Some(expr): {
							//println("successfully parsed: " + ruExpRPN2s(expr));
							Some(RuExp(expr));
						}
						None(): {
							conf.onError("expr was not successfully parsed: " + ruTerm2s(term.expr), []);
							None();
						}
					}
				}
				None(): {
					conf.onError("unknown type: " + term.type, []);
					None();
				}
			}
			if (ret == None()) {
				quit(0);
			}
			ret
		}
	}
}

RuParseEnv(
	nodes : [RuTermNode],
	types : Tree<string, RuTypeEnv>, 
	conf : RuConf,
);

RuParseStack(
	parent : Maybe<RuParseStack>,
	start : int,
	trie : RuTrie<string, ([RuExpNode])->[RuExpNode]>,
	expr : [RuExpNode]
);

stackDepth(stack : RuParseStack) -> int {
	switch (stack.parent) {
		Some(p): 1 + stackDepth(p);
		None(): 0;
	}
}

printStack(stack : RuParseStack) -> string {
	prn_keys = \mp -> 
	strGlue(
		map(getTreeKeys(mp), 
			\key -> 
				if (isSome(lookupTreeDef(mp, key, ruTrieEmpty).data)) "*" + key else key
		),
		", "
	);
	strGlue([
		"depth: " + i2s(stackDepth(stack)) + ", start: " + i2s(stack.start),
		//"expr: " + ruExpRPN2s(stack.expr),
		"trie keys: {" + prn_keys(stack.trie.map) + "}",
		switch (stack.parent) {
			Some(parent): "parent keys: {" + prn_keys(parent.trie.map) + "}";
			None(): "no parent";
		},
		"expr: " + ruExpRPN2s(stack.expr)
	], ", ")
}

showNode(n : RuTermNode) -> string {
	switch (n) {
		RuVarRef(v, t): "\"" + v + ":" + t + "\"";
		RuConstRef(c): "\"" + c + "\"";
	}
}

ruPopExprStack(env : RuParseEnv, pos : int, stack : RuParseStack) -> RuParseStack {
	switch (stack.trie.data) {
		Some(rule_action): {
			switch (stack.parent) {
				Some(parent): {
					ruPopExprStack(env, pos, 
						RuParseStack(parent with expr = concat(parent.expr, rule_action(stack.expr)))
					);
				}
				None(): stack;
			}
		}
		None(): stack;
	}
}

ruParseExpr(env : RuParseEnv, pos : int, stack : RuParseStack) -> Maybe<[RuExpNode]> {
	if (pos == length(env.nodes)) {
		//None() 
		switch (stack.trie.data) {
			Some(rule_action): Some(rule_action(stack.expr));
			None(): Some(stack.expr);
		}
	} else {
		//println("exr[" + i2s(pos) + "]= " + showNode(env.nodes[pos]) + ", stack: " + printStack(stack));

		do_step = \st, n -> {
			poped = ruPopExprStack(env, pos, RuParseStack(st with trie = n));
			ruParseExpr(env, pos + 1, poped);
		}
		switch (env.nodes[pos]) {
			RuVarRef(v, t):
				switch (lookupTree(stack.trie.map, t)) {
					Some(n): {
						//ret = do_step(RuParseStack(stack with expr = concat(stack.expr, [RuVarRef(v, t)])), n);
						ret = ruParseExpr(env, pos + 1, 
							ruPopExprStack(env, pos, RuParseStack(stack with 
								trie = n,
								expr = concat(stack.expr, [RuVarRef(v, t)])
							))
						);
						if (isSome(ret)) ret else {
							supers = lookupTreeDef(env.types, t, ruNoTypeEnv).supers;
							foldTree(stack.trie.map, None(), \tp, m, ac -> 
								if (ac != None()) ac else {
									switch (lookupTree(supers, tp)) {
										Some(r): 
											//do_step(RuParseStack(stack with 
											//	expr = concat(stack.expr, [RuVarRef(v, t), RuRuleRef(r.name, 1)])
											//), m);
											ruParseExpr(env, pos + 1, 
												ruPopExprStack(env, pos, RuParseStack(stack with 
													trie = m,
													expr = concat(stack.expr, [RuVarRef(v, t), RuRuleRef(r.name, 1)])
												))
											);
										None(): ac;
									}
								}
							);
						}
					}
					None(): {
						supers = lookupTreeDef(env.types, t, ruNoTypeEnv).supers;
						foldTree(stack.trie.map, None(), \tp, n, ac -> 
							if (ac != None()) ac else {
								switch (lookupTree(supers, tp)) {
									Some(r): 
										//do_step(RuParseStack(stack with 
										//	expr = concat(stack.expr, [RuVarRef(v, t), RuRuleRef(r.name, 1)])
										//), n);
										ruParseExpr(env, pos + 1, 
											ruPopExprStack(env, pos, RuParseStack(stack with 
												trie = n,
												expr = concat(stack.expr, [RuVarRef(v, t), RuRuleRef(r.name, 1)])
											))
										);
									None(): ac;
								}
							}
						);
					}
				}
			RuConstRef(c): {
				switch (lookupTree(stack.trie.map, c)) {
					Some(n): 
						//do_step(stack, n); 
						ruParseExpr(env, pos + 1, ruPopExprStack(env, pos, RuParseStack(stack with trie = n)));
					None(): {
						foldTree(stack.trie.map, None(), \t, n, ac ->
							if (ac != None()) ac else {
								switch (lookupTree(env.types, t)) {
									Some(tenv):
										if (tenv.trie == stack.trie) ac else
										ruParseExpr(env, pos, 
											RuParseStack(
												Some(RuParseStack(stack with trie = n)), 
												pos, tenv.trie, []
											)
										);
									None(): ac;
								}
							}
						)
					}
				}
			}
		}
	}
}


