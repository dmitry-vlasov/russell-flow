import fs/filesystem;
import sys/concurrent;
import lingo/compiler/syntaxtree_util;
import ru_math;
import parse/ru_load;
import parse/mm_parse_file;
import parse/mm_parse_ast;
import mm_src;

import lingo/pegcode/driver;


export {
	mmParse(loaded : [RuLoadSource], conf : RuConf) -> Maybe<Tree<string, MmParseSource>>;
}

mmParse(loaded : [RuLoadSource], conf0 : RuConf) -> Maybe<Tree<string, MmParseSource>> {
	err_count = ref 0;
	conf = ruAddErrCounter(conf0, err_count);
	start = timestamp();
	parsed = fold(
		filtermap(
			concurrent(true, map(loaded, \l -> \-> mmParseSource(l, conf))),
			idfn
		),
		makeTree(),
		\acc, src -> setTree(acc, src.info.file, src)
	);
	if (conf.verbose > 0) {
		println("parsed " +i2s(sizeTree(parsed)) + " files in: " + d2st((timestamp() - start)/ 1000.0, 2) + "s");
	}
	if (^err_count == 0) {
		Some(parsed);
	} else {
		None();
	}
}

mmParseSource(loaded : RuLoadSource, conf : RuConf) -> Maybe<MmParseSource> {
	start = timestamp();
	pr1 = parse_mm_source(loaded.src);
	st = pr1.first;
	err = pr1.second;
	if (st.end != strlen(loaded.src)) {
		conf.onError(
			"st.end: " + i2s(st.end) + ", strlen(loaded.src): " + i2s(strlen(loaded.src)) + " Syntax error: expected one of " + strGlue(map(err.expected, \e -> "'" + e + "'"), ", ") + 
			(if (err.met != "") " while met: '" + err.met + "'" else ""), 
			[RuPlace(loaded.info.file, err.pos)]
		);
		None()
	} else {
		parsed = mmMakeSrc(loaded, st, conf);
		if (conf.verbose > 1) {
			println("parsed: " + loaded.info.file + " in " + d2st((timestamp() - start)/ 1000.0, 4) + " s");
		}
		Some(parsed);
	}
}

mmMakeSrc(loaded : RuLoadSource, st : SyntaxTree, conf : RuConf) -> MmParseSource {
	elems = ref [];
	includes = ref [];
	iter(get_mm_source_mm_element_s(st),
		\elem -> {
			if (elem.rule == st_mm_comment) {
				refArrayPush(elems, mmMakeComment(loaded, elem, conf));
			} else if (elem.rule == st_mm_include) {
				inc = mmMakeInclude(loaded, elem, conf);
				refArrayPush(includes, inc.file);
				refArrayPush(elems, inc);
			} else if (elem.rule == st_mm_const) {
				refArrayPush(elems, mmMakeConst(loaded, elem, conf));
			} else if (elem.rule == st_mm_vars) {
				refArrayPush(elems, mmMakeVars(loaded, elem, conf));
			} else if (elem.rule == st_mm_disj) {
				refArrayPush(elems, mmMakeDisj(loaded, elem, conf));
			} else if (elem.rule == st_mm_floating) {
				refArrayPush(elems, mmMakeFloating(loaded, elem, conf));
			} else if (elem.rule == st_mm_essential) {
				refArrayPush(elems, mmMakeEssential(loaded, elem, conf));
			} else if (elem.rule == st_mm_axiomatic) {
				refArrayPush(elems, mmMakeAxiomatic(loaded, elem, conf));
			} else if (elem.rule == st_mm_provable) {
				refArrayPush(elems, mmMakeProvable(loaded, elem, conf));
			} else if (elem.rule == st_mm_block) {
				refArrayPush(elems, mmMakeBlock(loaded, elem, conf));
			}
		}
	);
	MmParseSource(^includes, ^elems, loaded.info);
}

mmMakeComment(loaded : RuLoadSource, st : SyntaxTree, conf : RuConf) -> MmComment {
	MmComment(grabSTText(get_mm_source_mm_comment_text(st), loaded.src));
}

mmMakeInclude(loaded : RuLoadSource, st : SyntaxTree, conf : RuConf) -> MmInclude { 
	path = ruTrimPath(grabSTText(get_mm_source_mm_path(st), loaded.src), conf, ".mm");
	MmInclude(path);
}

mmMakeConst(loaded : RuLoadSource, st : SyntaxTree, conf : RuConf) -> MmConst {
	MmConst(mmMakeSymbs(loaded, st, conf));
}

mmMakeVars(loaded : RuLoadSource, st : SyntaxTree, conf : RuConf) -> MmVars {
	MmVars(mmMakeSymbs(loaded, st, conf));
}

mmMakeDisj(loaded : RuLoadSource, st : SyntaxTree, conf : RuConf) -> MmDisj {
	MmDisj(mmMakeSymbs(loaded, st, conf));
}

mmMakeFloating(loaded : RuLoadSource, st : SyntaxTree, conf : RuConf) -> MmFloating {
	MmFloating(
		mmMakeLabel(loaded, get_mm_source_mm_label_sp(st), conf),
		mmMakeSymb(loaded, get_mm_source_mm_type(st), conf),
		mmMakeSymb(loaded, get_mm_source_mm_var(st), conf)
	);
}

mmMakeEssential(loaded : RuLoadSource, st : SyntaxTree, conf : RuConf) -> MmEssential {
	MmEssential(
		mmMakeLabel(loaded, get_mm_source_mm_label_sp(st), conf),
		mmMakeExpr(loaded, get_mm_source_mm_expr(st), conf)
	);
}

mmMakeAxiomatic(loaded : RuLoadSource, st : SyntaxTree, conf : RuConf) -> MmAxiomatic {
	MmAxiomatic(
		mmMakeLabel(loaded, get_mm_source_mm_label_sp(st), conf),
		mmMakeExpr(loaded, get_mm_source_mm_expr(st), conf)
	);
}

mmMakeProvable(loaded : RuLoadSource, st : SyntaxTree, conf : RuConf) -> MmProvable {
	MmProvable(
		mmMakeLabel(loaded, get_mm_source_mm_label_sp(st), conf),
		mmMakeExpr(loaded, get_mm_source_mm_expr(st), conf),
		mmMakeProof(loaded, get_mm_source_mm_proof(st), conf)
	);
}

mmMakeProof(loaded : RuLoadSource, st : SyntaxTree, conf : RuConf) -> MmProof {
	if (st.rule == st_mm_normal_proof) {
		MmNormalProof(mmMakeLabels(loaded, st, conf));
	} else {
		MmCompressedProof(
			mmMakeLabels(loaded, st, conf), 
			mmMakeSteps(loaded, get_mm_source_mm_step_s(st), conf)
		);
	}
}

mmMakeSteps(loaded : RuLoadSource, st : [SyntaxTree], conf : RuConf) -> [MmCompressedStep] {
	map(st, \s -> 
		if (s.rule == st_mm_index) {
			least = getCharCodeAt(trim2(grabSTText(get_mm_source_mm_least_significant_digit(s), loaded.src), " \t\n\r"), 0) - 65;
			most_digits = map(get_mm_source_mm_most_significant_digit_s(s), \d -> {
				getCharCodeAt(trim2(grabSTText(d, loaded.src), " \t\n\r"), 0) - 84
			});
			most = fold(reverseA(most_digits), Pair(0, 1), \acc, d -> 
				Pair(acc.first + d * acc.second, acc.second * 5)
			).first;
			ind = 20 * most + least;
			MmProofIndex(ind);
		} else {
			MmProofZ();
		}
	)
}

mmMakeSymb(loaded : RuLoadSource, st : SyntaxTree, conf : RuConf) -> string {
	grabSTText(get_mm_source_mm_symb(st), loaded.src);
}

mmMakeSymbs(loaded : RuLoadSource, st : SyntaxTree, conf : RuConf) -> [string] {
	map(get_mm_source_mm_symb_sp_s(st), \w_sp -> 
		grabSTText(get_mm_source_mm_symb(w_sp), loaded.src)
	);
}

mmMakeExpr(loaded : RuLoadSource, st : SyntaxTree, conf : RuConf) -> MmExpr {
	MmExpr(map(get_mm_source_mm_symb_sp_s(st), \w_sp -> 
		MmSymb(grabSTText(get_mm_source_mm_symb(w_sp), loaded.src), false)
	));
}

mmMakeLabel(loaded : RuLoadSource, st : SyntaxTree, conf : RuConf) -> string {
	grabSTText(get_mm_source_mm_label(st), loaded.src);
}

mmMakeLabels(loaded : RuLoadSource, st : SyntaxTree, conf : RuConf) -> [string] {
	map(get_mm_source_mm_label_sp_s(st), \l_sp -> 
		grabSTText(get_mm_source_mm_label(l_sp), loaded.src)
	);
}

mmMakeBlock(loaded : RuLoadSource, st : SyntaxTree, conf : RuConf) -> MmBlock {
	elems = ref [];
	iter(get_mm_source_mm_element_s(st),
		\elem -> {
			if (elem.rule == st_mm_comment) {
				refArrayPush(elems, mmMakeComment(loaded, elem, conf));
			} else if (elem.rule == st_mm_include) {
				refArrayPush(elems, mmMakeInclude(loaded, elem, conf));
			} else if (elem.rule == st_mm_const) {
				refArrayPush(elems, mmMakeConst(loaded, elem, conf));
			} else if (elem.rule == st_mm_vars) {
				refArrayPush(elems, mmMakeVars(loaded, elem, conf));
			} else if (elem.rule == st_mm_disj) {
				refArrayPush(elems, mmMakeDisj(loaded, elem, conf));
			} else if (elem.rule == st_mm_floating) {
				refArrayPush(elems, mmMakeFloating(loaded, elem, conf));
			} else if (elem.rule == st_mm_essential) {
				refArrayPush(elems, mmMakeEssential(loaded, elem, conf));
			} else if (elem.rule == st_mm_axiomatic) {
				refArrayPush(elems, mmMakeAxiomatic(loaded, elem, conf));
			} else if (elem.rule == st_mm_provable) {
				refArrayPush(elems, mmMakeProvable(loaded, elem, conf));
			} else if (elem.rule == st_mm_block) {
				refArrayPush(elems, mmMakeBlock(loaded, elem, conf));
			}
		}
	);
	MmBlock(^elems);
}
