import ds/set;
import ru_fileinfo;
import ru_conf;

export {

	MmParseSource(includes : [string], toplevel : [MmElement], info : RuFileInfo,);

	MmElement ::= MmComment, MmDisj, MmEssential, MmProvable, MmBlock, MmAxiomatic, MmConst, MmVars, MmFloating, MmInclude;
		MmComment(text : string);
		MmInclude(file : string);
		MmDisj(vars : [string]);
		MmConst(symbs : [string]);
		MmVars(symbs : [string]);
		MmEssential(label : string, expr : MmExp);
		MmFloating(label : string, type : string, var : string);
		MmAxiomatic(label : string, expr : MmExp);
		MmProvable(label : string, expr : MmExp, proof : MmProof);
		MmBlock(elements : [MmElement]);

	MmProof ::= MmNormalProof, MmCompressedProof;
	MmNormalProof(labels : [string]);
	MmCompressedProof(labels : [string], steps : [MmCompressedStep]);
	MmCompressedStep ::= MmProofIndex, MmProofZ;
		MmProofIndex(ind : int);
		MmProofZ();

	MmExp(symbs : [MmSymb]);
	MmSymb(literal : string, isVar : bool);

	MmHyp ::= MmEssential, MmFloating;
	MmStat ::= MmAxiomatic, MmProvable;

	MmAssertion(
		disjs : [MmDisj],
		hyps  : [MmHyp],
		inner : [MmHyp],
		stat  : MmStat
	);
	mmNoAssertion = MmAssertion([], [], [], MmAxiomatic("__no", MmExp([])));

	MmLabeled ::= MmHyp, MmAssertion;

	MmDecl ::= MmAssertion, MmConst, MmComment;

	MmSource(
		includes : [MmInclude],
		decls : [MmDecl],
		info : RuFileInfo,
	);
	
	MmAny ::= MmParseSource, MmComment, MmDisj, MmEssential, MmProvable, MmBlock, MmAxiomatic, MmConst, MmVars, MmFloating, MmInclude,
		MmProof, MmExp, MmSymb, MmAssertion, MmSource, MmCompressedStep;

	mmApplySubst(expr : MmExp, subst : Tree<string, MmExp>) -> MmExp;
	mmSubst2s(s : Tree<string, MmExp>) -> string;
	mmStack2s(s : List<MmExp>) -> string;
	mm2s(a : MmAny) -> string;
	mmExprVars(e : MmExp) -> Set<string>;
	mmAssertionVars(a : MmAssertion) -> Set<string>;
	
	MmMath(
		sources : Tree<string, MmSource>,
		labeled : Tree<string, MmLabeled>,
		consts  : Set<string>,
		conf : RuConf
	);

	mmMath2Source(math : MmMath, file : string) -> MmSource;
	mmFindLeafSources(math : MmMath) -> [MmSource];
	mmStripComments(src : MmSource) -> MmSource;
	mmMathAssertions(math : MmMath) -> [MmAssertion];
	mmMathAxiomatic(math : MmMath) -> [MmAssertion];
	mmMathProvable(math : MmMath) -> [MmAssertion];
}

mmFindLeafSources(math : MmMath) -> [MmSource] {
	filter(getTreeValues(math.sources), \src -> src.includes == [])
}

mmApplySubst(expr : MmExp, subst : Tree<string, MmExp>) -> MmExp {
	applied = ref [];
	iter(expr.symbs, \symb ->
		switch (lookupTree(subst, symb.literal)) {
			Some(e): iter(e.symbs, \s -> refArrayPush(applied, s));
			None(): refArrayPush(applied, symb);
		}
	);
	MmExp(^applied)
}

mm2s(a : MmAny) -> string {
	vars2s = \hs -> {
		str = strGlue(filtermap(hs, \h -> 
			switch (h) {
				MmFloating(lab, type, var): Some(var);
				default: None();
			}
		), " ");
		if (str == "") "" else "$v " + str + " $.\n\t";
	};
	ind2s = \i -> {
		least = i % 20;
		most = ref [];
		for(i / 20, \k -> (k > 0), \k -> {
			refArrayPush(most, k % 5);
			k / 5
		});
		fold(reverseA(^most), "", \acc, digit -> acc + fromCharCode(digit + 84)) + fromCharCode(least + 65)
	}
	switch (a) {
		MmParseSource(__,toplevel,__): strGlue(map(toplevel, mm2s), "\n") + "\n";
		MmComment(text):  "$( " + text + " $)\n";
		MmInclude(file):  "$[ " + file + " $]\n";
		MmDisj(vars):     "$d " + strGlue(vars, " ") + " $.";
		MmConst(symbs):   "$c " + strGlue(symbs, " ") + " $.\n";
		MmVars(symbs):     "$v " + strGlue(symbs, " ") + " $.";
		MmEssential(label, expr):       label + " $e " + mm2s(expr) + " $.";
		MmFloating(label, type, var):   label + " $f " + type + " " + var + " $.";
		MmAxiomatic(label, expr):       label + " $a " + mm2s(expr) + " $.";
		MmProvable(label, expr, proof): label + " $p " + mm2s(expr) + mm2s(proof);
		MmBlock(es): "${\n" + "\t" + strGlue(map(es, mm2s), "\n\t") + "\n$}";
		MmNormalProof(labels):  " $= " + strGlue(labels, " ") + " $.";
		MmCompressedProof(labels, steps): " $= ( " + strGlue(labels, " ") + " ) " + strGlue(map(steps, mm2s), "") + " $.";
		MmProofIndex(ind): ind2s(ind); //i2s(ind) + " ";
		MmProofZ(): "Z";
		MmExp(symbs): strGlue(map(symbs, \s -> s.literal), " ");
		MmSymb(literal, __): literal;
		MmAssertion(disj, hyps, inner, stat): 
			"${\n\t" +
				vars2s(concat(hyps, inner)) + 
				(if (disj == []) "" else strGlue(map(disj, mm2s), "\n\t") + "\n\t") +
				(if (hyps == []) "" else strGlue(map(hyps, mm2s), "\n\t") + "\n\t" ) +
				(if (inner == []) "" else strGlue(map(inner, mm2s), "\n\t") + "\n\t" ) +
				mm2s(a.stat) +
			"\n$}\n";
		MmSource(includes, decls, info): 
			strGlue(map(includes, mm2s), "\n") + "\n" + strGlue(map(decls, mm2s), "\n") + "\n\n";
	}
}

mmSubst2s(s : Tree<string, MmExp>) -> string {
	foldTree(s, "", \var, expr, str -> str + "\n" + var + " --> " + mm2s(expr))
}

mmStack2s(s : List<MmExp>) -> string {
	foldList(s, "", \acc, e -> acc + "\n\t" + mm2s(e)) + "\n";
}

mmExprVars(e : MmExp) -> Set<string> {
	buildSet(filtermap(e.symbs, \s -> if (s.isVar) Some(s.literal) else None()))
}

mmAssertionVars(a : MmAssertion) -> Set<string> {
	fold(a.hyps, makeSet(), \acc, h -> {
			switch (h) {
				MmFloating(__,v,__): insertSet(acc, v);
				default: acc;
			}
		}
	)
}

mmMath2Source(math : MmMath, file : string) -> MmSource {
	MmSource([],
		foldTree(math.sources, Pair([], makeSet()), \__,src, acc ->
			mmMathCollectSource(src, math, acc)
		).first,
		RuFileInfo(file, file, 0.0, 0.0)
	)
}

mmMathCollectSource(src : MmSource, math : MmMath, acc : Pair<[MmDecl], Set<string>>) -> Pair<[MmDecl], Set<string>> {
	if (containsSet(acc.second, src.info.file)) acc else {
		acc1 = fold(src.includes, Pair(acc.first, insertSet(acc.second, src.info.file)), 
			\ac, inc -> {
				switch (lookupTree(math.sources, inc.file)) {
					Some(inc_src): mmMathCollectSource(inc_src, math, ac);
					None(): ac;
				}
			}
		);
		Pair(concat(acc1.first, src.decls), acc1.second)
	}
}

mmStripComments(src : MmSource) -> MmSource {
	MmSource(src with decls = filter(src.decls, \decl -> 
		switch (decl) {
			MmComment(__): false;
			default: true;
		}
	))
}

mmMathAssertions(math : MmMath) -> [MmAssertion] {
	filtermap(getTreeValues(math.labeled), \lab ->
		switch (lab) {
			MmAssertion(__,__,__,__): Some(lab)
			default: None();
		}
	);
}

mmMathAxiomatic(math : MmMath) -> [MmAssertion] {
	filter(mmMathAssertions(math), \ass ->
		switch (ass.stat) {
			MmAxiomatic(__,__): true;
			default: false;
		}
	);
}

mmMathProvable(math : MmMath) -> [MmAssertion] {
	filter(mmMathAssertions(math), \ass ->
		switch (ass.stat) {
			MmProvable(__,__,__): true;
			default: false;
		}
	);
}
