import string;
import ds/tree;
import ru_fileinfo;

export {
	RuSource(imps : [RuImport], decls : [RuBlock], info : RuFileInfo);
	RuImport(imp : string);
	RuBlock ::= RuDecl, RuComment;
	RuDecl ::= RuConst, RuType, RuRule, RuAxiom, RuDef, RuTheorem, RuTheory;
	RuComment(text : string);
	RuTheory(name : string, decls : [RuDecl]);
	RuConst(symb : string, ascii : string, latex : string);
	RuType(name : string, supers : [string]);
	RuRule(name : string, vars : Tree<string, RuVar>, term : RuTerm);
	RuAxiom(name : string, vars : Tree<string, RuVar>, disj : [RuDisj], hyps : [RuHyp], prop : RuExp);
	RuVar(name : string, type : string);
	RuHyp(ind : int, expr : RuExp);
	RuDisj(vars : [string]);
	RuTheorem(name : string, vars : Tree<string, RuVar>, disj : [RuDisj], hyps : [RuHyp], prop : RuExp, proof : RuProof);
	RuDef(name : string, vars : Tree<string, RuVar>, disj : [RuDisj], hyps : [RuHyp], defm : RuExp, defs : RuExp, prop : RuExp);
	RuProof(vars : Tree<string, RuVar>, disj : [RuDisj], steps : [RuStep]);
	RuStep(ind : int, assertion : string, refs : [RuRef], expr : RuExp);
	RuRef ::= RuHypRef, RuStepRef;
	RuHypRef(ind : int);
	RuStepRef(ind : int);

	RuTerm(type : string, expr : [RuTermNode]);
	RuExp(nodes : [RuExpNode]);
	RuExpNode ::= RuRuleRef, RuVarRef;
	RuTermNode ::= RuConstRef, RuVarRef;
	RuRuleRef(name : string, len : int);
	RuVarRef(name : string, type : string);
	RuConstRef(name : string);

	RuAssertion ::= RuAxiom, RuTheorem, RuDef;
	RuAny ::= 
		RuSource, RuImport, RuConst, RuType, RuRule, RuAxiom, RuDef, RuTheorem, RuTheory, RuComment,
		RuStep, RuExp, RuHyp, RuProof, RuDisj, RuHypRef, RuStepRef, RuTerm, RuVar, RuVarRef, RuConstRef;

	
	ruNoConst = RuConst("__no", "", "");
	ruNoType = RuType("__no", []);
	ruNoRule = RuRule("__no", makeTree(), ruNoTerm);
	ruNoAxiom = RuAxiom("__no", makeTree(), [], [], ruNoExp);
	ruNoTheorem = RuTheorem("__no", makeTree(), [], [], ruNoExp, ruNoProof);
	ruNoProof = RuProof(makeTree(), [], []);
	
	ruNoTerm = RuTerm("__no", []);
	ruNoExpNode = RuVarRef("__no", "__no");
	ruNoExp = RuExp([]);
		
	ruAny2s(a : RuAny, rules : Tree<string, RuRule>) -> string;
	ruExp2s(nodes : [RuExpNode], rules : Tree<string, RuRule>) -> string;
	ruExp2Term(nodes : [RuExpNode], rules : Tree<string, RuRule>) -> RuTerm;

	ruExpType(nodes : [RuExpNode], rules : Tree<string, RuRule>) -> string;
	ruTerm2s(nodes : [RuTermNode]) -> string {
		strGlue(map(nodes, \n -> n.name), " ")
	}
	ruExpRPN2s(nodes : [RuExpNode]) -> string {
		strGlue(map(nodes, \n -> {
			switch (n) {
				RuRuleRef(r, l): r + "-" + i2s(l);
				RuVarRef(v, t): v;
			}
		}), " ")
	}
}

ruExpType(nodes : [RuExpNode], rules : Tree<string, RuRule>) -> string {
	switch (lastElement(nodes, ruNoExpNode)) {
		RuRuleRef(name, __):
			lookupTreeDef(rules, name, ruNoRule).term.type;
		RuVarRef(name, type): type;
	}
}

ruExp2s(nodes : [RuExpNode], rules : Tree<string, RuRule>) -> string {
	ruTerm2s(ruExp2Term(nodes, rules).expr)
}

ruExp2Term(nodes : [RuExpNode], rules : Tree<string, RuRule>) -> RuTerm {
	switch (fold(nodes, None(), \acc, n -> ruExp2sAccOperation(n, acc, rules))) {
		Some(stack): RuTerm(ruExpType(nodes, rules), stack.nodes);
		None(): ruNoTerm;
	}
}

RuExp2sStack(
	parent : Maybe<RuExp2sStack>,
	nodes : [RuTermNode]
);

ruExp2sAccOperation(node : RuExpNode, stack : Maybe<RuExp2sStack>, rules : Tree<string, RuRule>) -> Maybe<RuExp2sStack> {
	switch (node) {
		RuVarRef(var, __): 
			Some(RuExp2sStack(stack, [node]));
		RuRuleRef(name, len): {
			rule = lookupTreeDef(rules, name, ruNoRule);
			stack_str = fold(reverseA(rule.term.expr), Pair(stack, []), 
				\ac, symb -> {
					switch (symb) {
						RuVarRef(__,__): {
							switch (ac.first) {
								Some(st): Pair(st.parent, concat(ac.second, reverseA(st.nodes)));
								None(): ac;
							}
						}
						RuConstRef(s):
							Pair(ac.first, concat(ac.second, [symb]));
					}
				}
			);
			Some(RuExp2sStack(stack_str.first, reverseA(stack_str.second)));
		}
	}
}

ruAny2s(a : RuAny, rules : Tree<string, RuRule>) -> string {
	ru2s    = \x -> ruAny2s(x, rules);
	vars2s  = \vars -> if (sizeTree(vars) == 0) "" else "(" + strGlue(map(getTreeValues(vars), ru2s), ", ") + ") ";
	disjs2s = \disjs -> if (disjs == []) "" else "disjointed(" + strGlue(map(disjs, ru2s), ", ") + ") ";
	exp2s   = \expr, delim -> ": " + ruExpType(expr.nodes, rules) + " = " + delim + " " + ruExp2s(expr.nodes, rules) + " ;;";
	prop2s  = \prop -> "\tprop " + exp2s(prop, "|-") + "\n";
	refs2s  = \refs -> "(" + strGlue(map(refs, ru2s), ", ") + ")";
	hyps2s  = \hyps -> if (hyps == []) "" else strGlue(map(hyps, ru2s), "\n") + "\n";
	switch (a) {
		RuSource(imps, decls, info): 
			(if (imps == []) "" else strGlue(map(imps, ru2s), "\n") + "\n\n") +
			strGlue(map(decls, ru2s), "\n");
		RuImport(path):
			"import " + path + ";;";
		RuConst(symbol, ascii, latex): 
			"constant {\n" + 
				"\tsymbol " + symbol + ";;\n" +
				(if (ascii != "") "\tascii " + ascii + ";;\n" else "") + 
				(if (latex != "") "\tlatex " + latex + ";;\n" else "") +
			"}\n";
		RuType(name, supers): 
			"type " + name + (if (supers == []) "" else " : " + strGlue(supers, ", ")) + " ;;\n";
		RuRule(name, vars, term):
			"rule " + name + " " + vars2s(vars) + "{\n" +
				"\tterm " + ru2s(term) + "\n" +
			"}\n";
		RuAxiom(name, vars, disjs, hyps, prop): 
			"axiom " + name + " " + vars2s(vars) + disjs2s(disjs) + "{\n" +
				hyps2s(hyps) +
				(if (hyps == []) "" else "\t----------\n") +
				prop2s(prop) +
			"}\n";
		RuDef(name, vars, disjs, hyps, defm, defs, prop): 
			"definition " + name + " " + vars2s(vars) + disjs2s(disjs) + "{\n" +
				hyps2s(hyps) +
				"\tdefiendum " + exp2s(defm, "#") + "\n" +
				"\tdefiniens " + exp2s(defs, "#") + "\n" +
				"\t-------------\n" +
				prop2s(ruMakeDefProp(prop.nodes, defm.nodes, defs.nodes, rules)) +
			"}\n";
		RuTheorem(name, vars, disjs, hyps, prop, proof): 
			"theorem " + name + " " + vars2s(vars) + disjs2s(disjs) + "{\n" +
				hyps2s(hyps) + 
				(if (hyps == []) "" else "\t----------\n") +
				prop2s(prop) +
			"} " + ru2s(proof);
		RuHyp(ind, expr): 
			"\thyp " + i2s(ind) + " " + exp2s(expr, "|-");
		RuProof(vars, disjs, steps): 
			"proof " + vars2s(vars) + disjs2s(disjs) + "{\n" +
				strGlue(map(steps, ru2s), "\n") + "\n" +
			"}\n";
		RuStep(ind, assertion, refs, expr):
			"\tstep " + i2s(ind) + " : " + ruExpType(expr.nodes, rules) + " = " + 
				assertion + " " + refs2s(refs) + " |- " + ruExp2s(expr.nodes, rules) + " ;;";
		RuTheory(name, ds): 
			"theory " + name + " {\n" +
				strGlue(map(ds, ru2s), "\n") + "\n" +
			"}\n";
		RuComment(text): text;
		RuDisj(disj): strGlue(disj, " ");
		RuExp(nodes): ruExp2s(nodes, rules);
		RuHypRef(i): "hyp " + i2s(i);
		RuStepRef(i): "step " + i2s(i);
		RuVar(name, type): name + " : " + type;
		RuTerm(type, expr): 
			": " + type + " = # " + strGlue(map(expr, ru2s), " ") + " ;;";
		RuVarRef(name, type): name;
		RuConstRef(name): name;
	}
}

ruIndOfArr(arr1 : [?], arr2 : [?]) -> int {
	foldi(arr1, -1, \i, ac, x -> 
		if (ac >= 0) ac else {
			if (i <= length(arr1) - length(arr2)) {
				ind = fold(arr2, i, \j, y ->
					if (j == -1) -1 else
					if (y == arr1[j]) j + 1 else -1
				);
				if (ind != -1) i else -1;
			} else {
				-1
			}
		}
	)
}

ruMakeDefProp(prop : [RuExpNode], dfs: [RuExpNode], dfm : [RuExpNode], rules : Tree<string, RuRule>) -> RuExp {
	dfs_i = ruIndOfArr(prop, dfs);
	dfm_i = ruIndOfArr(prop, dfm);
	dfs_len = length(dfs);
	dfm_len = length(dfm);
	if (dfs_i == -1 || dfm_i == -1) {
		RuExp([])
	} else {
		RuExp(foldi(prop, [], \i, ac, s ->
			if (i == dfs_i) concat(ac, [RuVarRef("definiens", "__no")]) else
			if (dfs_i < i && i < dfs_i + dfs_len) ac else
			if (i == dfm_i) concat(ac, [RuVarRef("defiendum", "__no")]) else
			if (dfm_i < i && i < dfm_i + dfm_len) ac else
			concat(ac, [s])
		))
	}
}
