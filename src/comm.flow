import base/util/timer;
import script/script;
import script/value;
import unit;

export {
	RuCommArg(
		name : string,
		optional : bool,
		help : string,
		defval : string
	);
	// Commands may affect script variables and set some of them to specific return values.
	RuCommRet(
		name : string,
		type : string,
		help : string
	);
	RuCommInfo(
		name : string,
		category : string,
		help : string,
		defarg : string,
		autofile : bool,
		autoindent : bool,
		args : [RuCommArg],
		rets : [RuCommRet],
	);
	RuComm(
		info : RuCommInfo,
		run : (RuUnit) -> Maybe<RuState>
	);

	ruCommArg2s(arg : RuCommArg) -> string;
	ruCommInfo2s(info : RuCommInfo, with_args : bool) -> string;
	ruComm2s(comm : RuComm, with_args : bool) -> string;
	ruComms2s(comms : [RuComm], with_args : bool) -> string;
	ruApplyToProjectRoots(comm : RuComm) -> RuComm;
}

ruCommArg2s(arg : RuCommArg) -> string {
	arg.name + " " + 
	(if (arg.optional) "(optional) " else "") + arg.help + 
	(if (arg.defval == "") "" else " defval: " + arg.defval);
}

ruCommArgs2s(args : [RuCommArg]) -> string {
	has_defval = exists(args, \arg -> arg.defval != "");
	arg_matr = filtermap([
		Some(map(args, \arg -> arg.name)),
		Some(map(args, \arg -> b2s(arg.optional))),
		Some(map(args, \arg -> arg.help)),
		if (has_defval) Some(map(args, \arg -> arg.defval)) else None(),
	], idfn);
	headers = filtermap([
		Some("Name"), 
		Some("Optional"),
		Some("Help"),
		if (has_defval) Some("Default") else None()
	], idfn);
	ruStringTable2s(arg_matr, Some(headers));
}

ruCommRets2s(rets : [RuCommRet]) -> string {
	arg_matr = [
		map(rets, \ret -> ret.name),
		map(rets, \ret -> ret.type),
		map(rets, \ret -> ret.help)
	];
	headers = ["Name", "Type", "Help"];
	ruStringTable2s(arg_matr, Some(headers));
}

ruCommInfo2s(info : RuCommInfo, with_args : bool) -> string {
	"Command:\n\t" + info.name + "\n" + 
	(if (info.category == "") "" else "Category:\n\t" + info.category + "\n") + 
	(if (info.defarg == "") "" else "Default argument:\n\t" + info.defarg + "\n") + 
	"Description:\n" + strIndent(info.help) + "\n" +
	(if (!with_args || info.args == []) "" else {
		"Arguments:\n" + strIndent(ruCommArgs2s(info.args)) + "\n";
	}) +
	(if (!with_args || info.rets == []) "" else {
		"Return values:\n" + strIndent(ruCommRets2s(info.rets)) + "\n";
	})
}

ruComm2s(comm : RuComm, with_args : bool) -> string {
	ruCommInfo2s(comm.info, with_args);
}

ruComms2s(comms : [RuComm], with_args : bool) -> string {
	comm_matr = filtermap([
		Some(map(comms, \comm -> comm.info.name)),
		Some(map(comms, \comm -> comm.info.category)),
		Some(map(comms, \comm -> comm.info.help)),
		if (with_args) Some(map(comms, \comm -> strGlue(map(comm.info.args, ruCommArg2s), ", "))) else None()
	], idfn);
	headers = filtermap([
		Some("Command name"), 
		Some("Category"),
		Some("Help"),
		if (with_args) Some("Arguments") else None()
	], idfn);
	ruStringTable2s(comm_matr, Some(headers)) + "\n";
}

ruApplyToProjectRoots(comm : RuComm) -> RuComm {
	RuComm(comm with
		info = RuCommInfo(comm.info with
			args = map(comm.info.args, \arg -> if (arg.name != "file") arg else RuCommArg(arg with optional = true))
		),
		run = \unit -> {
			task = unit.task;
			state = unit.state;
			if (containsKeyTree(task.args, "file")) {
				comm.run(unit);
			} else {
				fold(state.conf.projectRoots, Some(state), \acc, file ->
					maybeBind(acc, \s -> 
						comm.run(RuUnit(ScTask(task with args = setTree(task.args, "file", file)), s, unit.out))
					)
				);
			}
		}
	);
}
