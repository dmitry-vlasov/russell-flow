import server/http;
import ru_lsp;
import ru_run;

export {
	// A language server working over http.
	ruHttpServer(conf : RuConf) -> void;

	// A language server using stdin/stdout to communicate. Usually is invoked as a child process of an IDE.
	ruConsoleServer(conf : RuConf) -> void;
}

ruDecodeConfigRequest(s : string) -> Maybe<RuConf> {
	conf = deserialize(s, IllegalStruct());
	if (conf == IllegalStruct()) None() else {
		switch (conf) {
			RuConf(__,__,__,__,__,__,__,__,__): {
				Some(RuConf(conf with threadId = s2i(getThreadId())));
			}
			default: None();
		}
	}
}

ruProcessRequest(serverConf : RuConf, req : string, out_cb : (int, string) ->  void, loop_cb : () -> void, exit_cb : () -> void, run : (RuConf, (int) -> void) -> void) -> void {
	switch (ruJson2Conf(parseJsonSafe(req))) {
		Some(conf): {
			if (ruOptIsTrue("shutdown-server", conf)) {
				exit_cb();
			} else {
				run(conf, \code -> {
					out_cb(code, ruOutput(conf));
					ruClearOutput(conf);
					loop_cb();
				});
			}
		}
		None(): {
			json = ruDecodeJsonRequest(req);
			if (json == JsonNull()) {
				out_cb(200, "WARNING: Illegal request.");
				loop_cb();
			} else {
				ruLspProcessRequest(serverConf, json, \msg -> out_cb(0, msg), loop_cb, exit_cb, run);
			}
		}
	}
}

ruConsoleServer(conf : RuConf) -> void { 
	ruServerLog("Console server started.", conf);
	ruSetSkipPrinting(conf, true);
	ruRunConsoleServer(conf);
}

ruRunConsoleServer(conf : RuConf) -> void {
	switch (ruLspReadRequest()) {
		Some(req): {
			ruServerLog("Console Request: " + strReplaces(req, ["\n", "\\n", "\r", "\\r", "\t", "\\t"]), conf);
			ruProcessRequest(conf, req, 
				\__, resp -> {
					ruServerLog("Console Response: " + strReplaces(resp, ["\n", "\\n", "\r", "\\r", "\t", "\\t"]), conf);
					println(resp);
				},
				\-> ruRunConsoleServer(conf),
				\-> {
					ruServerLog("Console server stopped.", conf);
					quit(0);
				},
				ruRun
			);
		}
		None(): {
			ruServerLog("Failed to read a request.", conf);
			ruRunConsoleServer(conf);
		}
	}
}

ruHttpServer(conf : RuConf) -> void {
	port = s2i(ruOptDef("server-port", "20001", conf));
	server = ref nop;
	server := createHttpServer(port,
		\-> {
			ruServerLog("Http server started.", conf);
			ruPrintln("Http server started.", conf)
		},
		\request, response -> {
			start_time = timestamp();
			ruPrintln("Processing '" + conf.file + "' on http server.", conf);
			ruServerLog("Http Request: " + strReplaces(request.body, ["\n", "\\n", "\r", "\\r", "\t", "\\t"]), conf);
			ruProcessRequest(conf, request.body,
				\code, output -> {
					ruPrintln("finished '" + conf.file + "' in " + d2st((timestamp() - start_time) / 1000.0, 2) + " s.", conf);
					ruServerLog("Http Response: " + output + " done in " + d2st((timestamp() - start_time) / 1000.0, 2) + " s.", conf);
					HttpResponse(200 + code, output, []) |> response
				},
				nop,
				\-> {
					^server();
					ruPrintln("Http server stopped.", conf);
					ruServerLog("Http server stopped.", conf);
					quit(0);
				},
				ruRunLocal
			)
		}
	);
}
