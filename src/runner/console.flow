import process;

export {
	// A console interactive Russell server.
	ruConsoleServer(env : RuEnv) -> void;
}

ruConsoleServer(env : RuEnv) -> void {
	ruConsoleLog("Server started.", env.conf);
	env.out.println(
		"Enter a script (command) and press enter to execute.\n"
		"Type 'exit' to leave, 'help' to see the help message."
	);
	ruRunConsoleServer(env);
	quit(0);
}

ruRunConsoleServer(env : RuEnv) -> void {
	conf = env.conf;
	print("> ");
	script_src = trim2(readUntil("\n"), " \r\n\t");
	if (!(script_src == "exit" || script_src == "quit" || script_src == "bye")) {
		if (script_src == "") {
			// Empty input
			ruRunConsoleServer(env);
		} else {
			switch (ruParseScComm(script_src)) {
				Some(src): {
					uuid = ruMakeUuid();
					timer = ruMakeTimer();
					ruConsoleLog("<" + uuid + "> Script started: " + ruServerEscapeString(script_src), conf);
					ruProcess(uuid, src.comm, 
						\code -> {
							time_msg = "[" + ruTimePassed2s(timer) + "]";
							code_msg = if(code == 0) "" else "(code: " + i2s(code) + ") ";
							ruConsoleLog("<" + uuid + "> Script finished " + code_msg + time_msg, conf);
							ruRunConsoleServer(env);
						},
						ruMakeEnv(conf, env.out.println, 
							\json -> println(json2string(json)), 
							\meth, json -> println(meth + ":\n" + json2string(json))
						)
					);
				}
				None(): {
					env.out.println("Syntax error in script: '" + escapeStr(script_src) + "'");
					ruConsoleLog(
						"Syntax error in script: '" + escapeStr(script_src) + "'",
						//+ " = [" + strGlue(map(s2a(script_src), i2s), ", ") + "]", 
						conf
					);
					ruRunConsoleServer(env);
				}
			}
		}
	} else {
		ruConsoleLog("Server stopped.", conf);
		env.out.println("Bye.");
	}
}

