import process;

export {
	// A console interactive Russell server.
	ruConsoleServer(env : RuEnv) -> void;
}

ruConsoleServer(env : RuEnv) -> void {
	ruConsoleLog("Server started.", env.conf);
	env.out.onMessage(
		"Enter a script (command) and press enter to execute.\n"
		"Type 'exit' to leave, 'help' to see the help message."
	);
	ruRunConsoleServer(env);
	quit(0);
}

ruRunConsoleServer(env : RuEnv) -> void {
	conf = env.conf;
	print("> ");
	script_src = trim2(readUntil("\n"), " \r\n\t");
	if (!(script_src == "exit" || script_src == "quit" || script_src == "bye")) {
		if (script_src == "") {
			// Empty input
			ruRunConsoleServer(env);
		} else {
			switch (ruParseScript(script_src)) {
				Some(src): {
					uuid = ruMakeUuid();
					timer = ruMakeTimer();
					ruConsoleLog("<" + uuid + "> Script started: " + ruServerEscapeString(script_src), conf);
					datas = ref [];
					ruProcess(uuid, src.comm, 
						\code -> {
							time_msg = "[" + ruTimePassed2s(timer) + "]";
							code_msg = if(code == 0) "" else "(code: " + i2s(code) + ") ";
							ruConsoleLog("<" + uuid + "> Script finished " + code_msg + time_msg, conf);
							ruRunConsoleServer(env);
						},
						ruMakeEnv(conf, env.out.onMessage, \data -> refArrayPush(datas, data))
					);
					// After a script is execurted, output the result.
					if (length(^datas) > 0) {
						env.out.onMessage(concatStrings(^datas));
					}
				}
				None(): {
					env.out.onMessage("Syntax error in script: '" + escapeStr(script_src) + "'");
					ruConsoleLog(
						"Syntax error in script: '" + escapeStr(script_src) + "'",
						//+ " = [" + strGlue(map(s2a(script_src), i2s), ", ") + "]", 
						conf
					);
					ruRunConsoleServer(env);
				}
			}
		}
	} else {
		ruConsoleLog("Server stopped.", conf);
		env.out.onMessage("Bye.");
	}
}

