import process;
import lsp/methods/lifecycle;
import lsp/methods/textDocument;
import lsp/methods/workspace;

export {
	// A language server using stdin/stdout to communicate. Usually is invoked as a child process of an IDE.
	ruLspServer(env : RuEnv) -> void;
}

ruLspServer(env : RuEnv) -> void { 
	ruLspServerLog("Server started.", env.conf);
	params = LspInitializeParams("", LspClientInfo("", ""), "", "", "", JsonNull(), JsonNull(), "off", []);
	for(
		RuLsp(env, ruMakeLspLog(env.conf), params, 1, makeTree(), makeSet(), 0),
		\state -> state.exit != 2,
		ruRunLspServer
	);
	quit(0);
}

ruRunLspRequest(id : Json, method : RuLspMethod, params : Json, log_message : bool, state : RuLsp) -> void {
	timer = ruMakeTimer();
	env = state.env;
	conf = env.conf;
	data = ref JsonNull();
	errs = ref [];
	log = if (log_message) state.log else ruNoLspLog; 
	out = RuOut(
		\msg -> if (msg != "") ruLspWriteNotification("console/message", JsonString(msg), log),
		\json -> data := json,
		\meth, json -> ruLspWriteNotification(meth, json, log),
		\err, __ -> refArrayPush(errs, err)
	);
	script = ruSetScCommTaskArg(method.script(params, RuEnv(env with out = out)), "lsp", "1");
	ruProcess(json2string(id), script, 
		\code -> {
			if (method.responses) {
				if (length(^errs) == 0) {
					if (!containsSet(state.processed, id)) {
						ruLspWriteResponse(id, ^data, log);
					} else {
						ruLspWriteError(id, lspParseError, "Error: server already processed the request: " + json2string(id), JsonNull(), log);
					}
				} else {
					ruLspWriteError(id, lspRequestFailed, strGlue(^errs, ", "), JsonArray(map(^errs, \err -> JsonString(err))), log);
				}
			}
		},
		RuEnv(env.conf, out)
	);
}

ruRunLspNotification(method : RuLspMethod, params : Json, log_message : bool, state : RuLsp) -> void {
	env = state.env;
	log = if (log_message) state.log else ruNoLspLog;
	script = ruSetScCommTaskArg(method.script(params, env), "lsp", "1");
	timer = ruMakeTimer();
	errs = ref [];
	out = RuOut(
		\msg -> if (msg != "") ruLspWriteNotification("console/message", JsonString(msg), log),
		nop1, nop2,
		\err, __ -> refArrayPush(errs, err)
	);
	ruProcess(ruMakeUuid(), script, \code -> {}, RuEnv(env.conf, out));
}

ruRunLspServer(state : RuLsp) -> RuLsp {
	env = state.env;
	conf = env.conf;
	switch (ruLspReadMessage(env, state.log)) {
		Some(m): {
			// Special watermark for non-logging
			log_message = !strContains(m.raw, "do_not_log_this");
			log = if (log_message) state.log else ruNoLspLog;
			message = m.message;
			switch (message) {
				LspRequestMessage(__, id, method, params): {
					if (state.exit > 0 && method != "exit") {
						ruLspServerLog("Error: server is shutdown, but received a request: " + m.raw, conf);
					}
					switch (lookupTree(ruLspMethods, method)) {
						Some(meth): {
							ruRunLspRequest(id, meth, params, log_message, state);
							new_state = meth.callback(params, state);
							RuLsp(new_state with processed = insertSet(new_state.processed, id));
						}
						None(): {
							ruLspWriteError(id, lspMethodNotFound, "request method '" + method + "' is not found", JsonNull(), log);
							state;
						}
					}
				}
				LspResponseMessage(__, id, result, errors): {
					if (state.exit > 0) {
						ruLspServerLog("Error: server is shutdown, but received a response: " + m.raw, env.conf);
					}
					switch (lookupTree(state.requests, id)) {
						Some(req): {
							ruLspServerLog(blueprint("<%id%> Processing the response: %result%",
								[
									"id", json2string(id), 
									"result", json2string(result)
								]
							), conf);
							req.callback(result, state);
						}
						None(): {
							ruLspServerLog("Server: unknown request: " + json2string(id), conf);
							state;
						}
					}
				}
				LspNotificationMessage(__, method, params): {
					if (state.exit > 0 && method != "exit") {
						ruLspServerLog("Error: server is shutdown, but received a non-exit notification: " + m.raw, env.conf);
					}
					switch (lookupTree(ruLspMethods, method)) {
						Some(meth): {
							ruRunLspNotification(meth, params, log_message, state);
							meth.callback(params, state);
						}
						None(): {
							ruLspServerLog("request method '" + method + "' is not found", conf);
							state;
						}
					}
				}
			}
		}
		None(): state;
	}
}

ruLspMethods = mergeTree(
	mergeTree(ruLspLifecycleMethods(), ruLspWorkspaceMethods()), 
	ruLspTextDocumentMethods()
);
