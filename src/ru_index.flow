import ru_exp;

export {

	RuIndex(
		rules : Tree<string, RuIndexNode<?>>,
		vars  : Tree<string, RuIndexNode<?>>,
	);

	ruMakeIndex() -> RuIndex<?>;

	ruSetIndex(key : RuExp, data : Maybe<?>, index : RuIndex<?>, conflict : (?, ?) -> ?) -> RuIndex<?>;

	ruIndexSize(ind : RuIndex<?>) -> int;

	RuUnified(
		sub : Tree<RuVar, RuExp>,
		data : ?
	);
	ruUnifyIndex(e : RuExp, index : RuIndex<?>, math : RuMath) -> [RuUnified<?>];
	ruIndex2s(index : RuIndex<?>, show_data : (?) -> string, math : RuMath) -> string;
	ruIndexRaw2s(index : RuIndex<?>) -> string;
}

RuIndexNode(
	name  : string, // var or rule name
	type  : string, 
	arity : int,    // -1 means that it is a variable, not rule

	mutable ends : Set<RuIndexNode<?>>, // ends of subterms, starting at this node
	lens  : Set<int>, // distances to the beginnings of subterms, starting at this node

	data  : Maybe<?>,
	expr  : Maybe<RuExp>,

	index : RuIndex<?>, // children nodes
	mutable parent : Maybe<RuIndexNode<?>>,
);

ruMakeIndex() -> RuIndex<?> {
	ruIndexEmpty
}

ruIndexEmpty = RuIndex(makeTree(), makeTree());

ruIndexSize(ind : RuIndex<?>) -> int {
	s1 = foldTree(ind.rules, 0, \__, n, acc -> acc + ruIndexSize(n.index));
	s2 = foldTree(ind.vars, 0, \__, n, acc -> acc + ruIndexSize(n.index));
	s1 + s2 + 1;
}

ruSetIndex(key : RuExp, data : Maybe<?>, index : RuIndex<?>, conflict : (?, ?) -> ?) -> RuIndex<?> {
	insert_int = \t, i, v -> {
		w = lookupTreeDef(t, i, makeSet());
		setTree(t, i, insertSet(w, v));
	}
	//println("adding: " + ruExpRPN2s(key.nodes));
	env0 = RuIndexAddEnv(key, data, conflict, makeTree(), makeTree(), ref makeTree());
	env = foldi(key.nodes, env0, \i, acc, n ->
		switch (n) {
			RuRuleRef(__,__,__, len): 
				RuIndexAddEnv(acc with 
					lens = insert_int(acc.lens, i + len, len),
					ends = insert_int(acc.ends, i, i + len),
				);
			RuVar(__,__): acc;
		}
	);
	ruDoAdd2Index(0, index, env);
}

RuIndexAddEnv(
	key : RuExp, 
	data : Maybe<?>,
	conflict : (?, ?) -> ?,
	lens : Tree<int, Set<int>>,
	ends : Tree<int, Set<int>>,
	nodes : ref Tree<int, RuIndexNode<?>>
);

ruDoAdd2Index(i : int, index : RuIndex<?>, env : RuIndexAddEnv<?>) -> RuIndex<?> {
	make_new_node = \n, t, a -> 
		RuIndexNode(n, t, a, makeSet(), makeSet(), None(), None(), ruIndexEmpty, None());
	switch (env.key.nodes[i]) {
		RuRuleRef(name, type, arity, len): {
			n = lookupTreeDef(index.rules, name, make_new_node(name, type, arity));
			m = ruDoAdd2IndexNode(i, n, env);
			RuIndex(index with rules = setTree(index.rules, name, m));
		}
		RuVar(name, type): {
			n = lookupTreeDef(index.vars, name, make_new_node(name, type, -1));
			m = ruDoAdd2IndexNode(i, n, env);
			RuIndex(index with vars = setTree(index.vars, name, m));
		}
	}
}

ruDoAdd2IndexNode(i : int, n : RuIndexNode<?>, env : RuIndexAddEnv<?>) -> RuIndexNode<?> {
	insert_data = \md1, md2 ->
		eitherMap(md1, \d1 -> eitherMap(md2, \d2 -> Some(env.conflict(d1, d2)), md1), md2);
	n1 = if (i + 1 == length(env.key.nodes)) {
		RuIndexNode(n with data = insert_data(n.data, env.data), expr = Some(env.key));
	} else {
		RuIndexNode(n with index = ruDoAdd2Index(i + 1, n.index, env));
	}
	n2 = RuIndexNode(n1 with lens = mergeSets(n1.lens, lookupTreeDef(env.lens, i, makeSet())));
	switch (lookupTree(env.ends, i)) {
		Some(ends): iterSet(ends, \j ->
			if (i == j) n2.ends ::= insertSet(n2.ends, n2) else
			switch (lookupTree(^(env.nodes), j)) {
				Some(m): n2.ends ::= insertSet(n2.ends, m);
				None(): {
					println("node not found - error");
					quit(-1);
				}
			}
		);
		None(): { }
	}
	traverseInOrder(n2.index.rules, \__, m -> m.parent ::= Some(n2));
	traverseInOrder(n2.index.vars, \__, m -> m.parent ::= Some(n2));
	env.nodes := setTree(^(env.nodes), i, n2);
	n2
}

ruUnifyIndex(e : RuExp, index : RuIndex<?>, math : RuMath) -> [RuUnified<?>] {
	ruDoUnifyIndex(0, index, math, RuUnifyEnv(e, makeTree()))
}

RuUnifyEnv(
	expr : RuExp,
	sub : Tree<RuVar, RuExp>
);

ruIndexBranch(beg : RuIndexNode<?>, end : RuIndexNode<?>, acc : [RuIndexNode<?>]) -> [RuIndexNode<?>] {
	if (beg == end) concat([beg], acc) else {
		ruIndexBranch(beg, either(end.parent, beg), concat([end], acc));
	}
}

ruIndexSubexpr(beg : RuIndexNode<?>, end : RuIndexNode<?>) -> RuExp {
	branch = ruIndexBranch(beg, end, []);
	lens = foldi(branch, makeTree(), \i, acc, n -> 
		foldSet(n.lens, acc, \ac, len -> setTree(ac, i - len, len))
	);
	RuExp(
		foldi(branch, [], \i, acc, n ->
			if (n.arity == -1) {
				concat(acc, [RuVar(n.name, n.type)]);
			} else {
				switch (lookupTree(lens, i)) {
					Some(len): concat(acc, [RuRuleRef(n.name, n.type, n.arity, len)]);
					None(): {
						println("must not happen: i=" + i2s(i) + ", n.name=" + n.name);
						println("lens:\n" + foldTree(lens, "", \j, len, ac -> ac + i2s(j) + " --> " + i2s(len) + "\n"));
						println("nodes:\n" + strGlue(map(branch, \m -> 
							m.name + ", lens = " + strGlue(map(set2array(m.lens), i2s), ", ") + "\n" 
						), " "));
						quit(-1);
						acc
					}
				}
			}
		)
		, -1
	);
}

ruDoUnifyIndex(i : int, index : RuIndex<?>, math : RuMath, env : RuUnifyEnv) -> [RuUnified<?>] {
	unify_step = \j, var, end, ex ->
		switch (ruAdd2Subst(env.sub, var, ex)) {
			Some(s): 
				ruDoUnifyIndexNode(j, end, math, RuUnifyEnv(env with sub = s));
			None(): {
				println("ruAdd2Subst failed: ");
				println("var: " + ru2s(var, math.rules));
				println("ex: " + ru2s(ex, math.rules));
				println("sub:\n" + ruSubst2s(env.sub, math));
				[];
			}
		}
	unify_var = \j, var, ex_type, end, e ->
		if (ex_type == var.type) unify_step(j, var, end, e())
		else {
			switch (ruSuperRule(var.type, ex_type, math)) {
				Some(sr): {
					ex = RuExp(concat([RuRuleRef(sr.name, var.type, 1, 1)], e().nodes), -1);
					unify_step(j, var, end, ex);
				}
				None(): []
			}
		}
	if (i == length(env.expr.nodes)) [] else {
		switch (env.expr.nodes[i]) {
			RuRuleRef(name, type, arity, len): {
				r1 = switch (lookupTree(index.rules, name)) {
					Some(n): ruDoUnifyIndexNode(i, n, math, env);
					None(): [];
				}
				r2 = foldTree(index.vars, [], \v, n, acc -> 
					concat(acc, unify_var(i + len, RuVar(n.name, n.type), type, n, 
						\-> RuExp(subrange(env.expr.nodes, i, len + 1), -1)
					))
				);
				concat(r1, r2);
			}
			RuVar(name, type): {
				r1 = foldTree(index.rules, [], \nm, n, acc -> {
					foldSet(n.ends, acc, \ac, end ->
						concat(ac, unify_var(i, RuVar(name, type), n.type, end, 
							\-> {
								ex = ruIndexSubexpr(n, end);
								//println("ex: " + ru2s(ex, math.rules) + " : " + ruExpType(ex.nodes));
								ex
							}
						))
					)
				});
				r2 = foldTree(index.vars, [], \v, n, acc -> 
					concat(acc, unify_var(i, RuVar(n.name, n.type), type, n, 
						\-> RuExp([RuVar(name, type)], -1)
					))
				);
				concat(r1, r2);
			}
		}
	}
}

ruDoUnifyIndexNode(i : int, node : RuIndexNode<?>, math : RuMath, env : RuUnifyEnv) -> [RuUnified<?>] {
	r = if (i + 1 == length(env.expr.nodes)) {
		switch (node.data) {
			Some(d): {
				switch (node.expr) {
					Some(ex): {
						e1 = ruApplySubst(ex, env.sub);
						e2 = ruApplySubst(env.expr, env.sub);
						if (e1.nodes != e2.nodes) {
							println("wrong unification");
							println("e1: " + ru2s(ex, math.rules));
							//println("ex(raw): " + ruExpRPN2s(ex.nodes));
							println("e2: " + ru2s(env.expr, math.rules));
							println("s(e1): " + ru2s(e1, math.rules));
							println("s(e2): " + ru2s(e2, math.rules));
							println("subst:\n" + ruSubst2s(env.sub, math));
							//println("substRPN:\n" + ruSubstRPN2s(env.sub));
							//println("e1(1): " + ru2s(ruApplySubst(ex, env.sub), math.rules));
							//println("e1(raw): " + ruExpRPN2s(ruApplySubst(ex, env.sub).nodes));
							//println("e2(raw): " + ruExpRPN2s(ruApplySubst(env.expr, env.sub).nodes));
							quit(-1);
						}
					}
					None(): println("impossible");
				};
				[RuUnified(env.sub, d)];
			}
			None(): {
				println("must not happen");
				quit(-1);
				[];
			}
		}
	} else [];
	concat(r, ruDoUnifyIndex(i + 1, node.index, math, env))
}

ruIndex2s(index : RuIndex<?>, show_data : (?) -> string, math : RuMath) -> string {
	ruDoIndex2s(index, show_data, None(), "", math)
}

ruDoIndex2s(index : RuIndex<?>, show_data : (?) -> string, beg : Maybe<RuIndexNode<?>>, acc : string, math : RuMath) -> string {
	begin = \n -> switch (beg) {
		Some(b): b;
		None(): n;
	}
	acc1 = foldTree(index.rules, acc, \__, n, ac ->
		ruDoIndexNode2s(n, show_data, begin(n), ac, math)
	);
	foldTree(index.vars, acc1, \__, n, ac ->
		ruDoIndexNode2s(n, show_data, begin(n), ac, math)
	);
}

ruDoIndexNode2s(node : RuIndexNode<?>, show_data : (?) -> string, beg : RuIndexNode<?>, acc : string, math : RuMath) -> string {
	acc1 = switch (node.data) {
		Some(d): acc + ru2s(ruIndexSubexpr(beg, node), math.rules) + " --> " + show_data(d) + "\n";
		None(): acc;
	}
	ruDoIndex2s(node.index, show_data, Some(beg), acc1, math);
}

ruIndexRaw2s(index : RuIndex<?>) -> string {
	ruDoIndexRaw2s(index, [], "")
}

ruDoIndexRaw2s(index : RuIndex<?>, nodes : [RuIndexNode<?>], acc : string) -> string {
	acc1 = foldTree(index.rules, acc, \__, n, ac ->
		ruDoIndexNodeRaw2s(n, concat(nodes, [n]), ac)
	);
	foldTree(index.vars, acc1, \__, n, ac ->
		ruDoIndexNodeRaw2s(n, concat(nodes, [n]), ac)
	);
}

ruDoIndexNodeRaw2s(node : RuIndexNode<?>, nodes : [RuIndexNode<?>], acc : string) -> string {
	acc1 = switch (node.data) {
		Some(d): acc + 
			strGlue(
				map(nodes, \n -> 
					n.name + ":" + n.type + "[" + strGlue(map(set2array(n.lens), i2s), ", ") + "]" + 
					(if (isSome(n.data)) "*" else "")
				), 
			" ") + "\n";
		None(): acc;
	}
	ruDoIndexRaw2s(node.index, nodes, acc1);
}
