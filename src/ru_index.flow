import ru_exp;

export {

	RuIndex(
		rules : Tree<string, RuIndexNode<?>>,
		vars  : Tree<string, RuIndexNode<?>>,
	);

	ruMakeIndex() -> RuIndex<?>;

	ruSetIndex(key : RuExp, data : Maybe<?>, index : RuIndex<?>, conflict : (?, ?) -> ?) -> RuIndex<?>;

	ruIndexSize(ind : RuIndex<?>) -> int;

	RuUnified(
		sub : Tree<RuVar, RuExp>,
		data : ?
	);
	ruUnifyIndex(e : RuExp, index : RuIndex<?>, math : RuMath) -> [RuUnified<?>];
	ruIndex2s(index : RuIndex<?>, show_data : (?) -> string, math : RuMath) -> string;
	ruIndexRaw2s(index : RuIndex<?>, show_data : (?) -> string) -> string;
}

RuIndexNode(
	name  : string, // var or rule name
	type  : string, 
	arity : int,    // -1 means that it is a variable, not rule

	ends  : [RuIndexNode<?>], // ends of subterms, starting at this node
	lens  : [int],            // distances to the beginnings of subterms, starting at this node

	data  : Maybe<?>,
	expr  : Maybe<RuExp>,

	index : RuIndex<?>, // children nodes
	mutable parent : Maybe<RuIndexNode<?>>,
);

ruMakeIndex() -> RuIndex<?> {
	ruIndexEmpty
}

ruIndexEmpty = RuIndex(makeTree(), makeTree());

ruIndexSize(ind : RuIndex<?>) -> int {
	s1 = foldTree(ind.rules, 0, \__, n, acc -> acc + ruIndexSize(n.index));
	s2 = foldTree(ind.vars, 0, \__, n, acc -> acc + ruIndexSize(n.index));
	s1 + s2 + 1;
}

ruSetIndex(key : RuExp, data : Maybe<?>, index : RuIndex<?>, conflict : (?, ?) -> ?) -> RuIndex<?> {
	ruDoAdd2Index(0, None(), index, RuIndexStacks(None(), None()), RuIndexAddEnv(key, data, conflict)).ind
}

RuIndexAddEnv(
	key : RuExp, 
	data : Maybe<?>,
	conflict : (?, ?) -> ?,
);

RuIndexEndStack(
	parent : Maybe<RuIndexEndStack>,
	i   : int,
	len : int
);

RuIndexBegStack(
	parent : Maybe<RuIndexBegStack<?>>,
	i    : int,
	len  : int,
	node : RuIndexNode<?>
);

RuIndexStacks(
	ends : Maybe<RuIndexEndStack>,
	begs : Maybe<RuIndexBegStack<?>>
);

RuIndexAddRet(
	ind  : RuIndex<?>,
	node : RuIndexNode<?>,
	begs : Maybe<RuIndexBegStack<?>>,
);

ruProcIndexEnd(i : int, n : RuIndexNode<?>, stacks : RuIndexStacks) -> RuIndexStacks {
	switch (stacks.ends) {
		Some(end):
			if (end.i + end.len != i) stacks else {
				ruProcIndexEnd(i, n,
					RuIndexStacks(
						end.parent, 
						Some(RuIndexBegStack(stacks.begs, end.i, end.len, 
							RuIndexNode(n with lens = concat(n.lens, [end.len])))
						)
					)
				)
			}
		None(): stacks;
	}
}

ruProcIndexBeg(i : int, n : RuIndexNode<?>, ret : RuIndexAddRet) -> RuIndexAddRet {
	r = RuIndexAddRet(ret with node = RuIndexNode(n with index = ret.ind));
	switch (ret.begs) {
		Some(beg):
			if (beg.i != i) r else {
				ruProcIndexBeg(i, n,
					RuIndexAddRet(ret with
						node = RuIndexNode(n with 
							ends = concat(ret.node.ends, [beg.node]),
							index = ret.ind
						),
						begs = beg.parent
					)
				)
			}
		None(): r;
	}
}

ruDoAdd2Index(i : int, parent : Maybe<RuIndexNode<?>>, index : RuIndex<?>, stacks : RuIndexStacks<?>, env : RuIndexAddEnv<?>) -> RuIndexAddRet {
	insert_data = \md1, md2 ->
		eitherMap(md1, \d1 -> eitherMap(md2, \d2 -> Some(env.conflict(d1, d2)), md1), md2);
	update_node = \n, s -> {
		r = if (i + 1 == length(env.key.nodes)) {
			//ruProcIndexBeg(i, n, 
				RuIndexAddRet(
					index, 
					RuIndexNode(n with data = insert_data(n.data, env.data), expr = Some(env.key)), 
					ruProcIndexEnd(i, n, s).begs
					//stacks.begs
				)
			//);
		} else {
			ruProcIndexBeg(i, n, ruDoAdd2Index(i + 1, Some(n), n.index, ruProcIndexEnd(i, n, s), env));
		}
		traverseInOrder(r.node.index.rules, \__, m -> m.parent ::= Some(r.node));
		traverseInOrder(r.node.index.vars, \__, m -> m.parent ::= Some(r.node));
		r
	}
	switch (env.key.nodes[i]) {
		RuRuleRef(name, type, arity, len): {
			n = lookupTreeDef(index.rules, name, RuIndexNode(name, type, arity, [], [], None(), None(), ruIndexEmpty, parent));
			r = update_node(n, RuIndexStacks(stacks with ends = Some(RuIndexEndStack(stacks.ends, i, len))));
			RuIndexAddRet(r with ind = RuIndex(index with rules = setTree(index.rules, name, r.node)));
		}
		RuVar(name, type): {
			n = lookupTreeDef(index.vars, name, RuIndexNode(name, type, -1, [], [], None(), None(), ruIndexEmpty, parent));
			r = update_node(n, stacks);
			RuIndexAddRet(r with ind = RuIndex(index with vars = setTree(index.vars, name, r.node)));
		}
	}
}

ruUnifyIndex(e : RuExp, index : RuIndex<?>, math : RuMath) -> [RuUnified<?>] {
	ruDoUnifyIndex(0, index, math, RuUnifyEnv(e, makeTree()))
}

RuUnifyEnv(
	expr : RuExp,
	sub : Tree<RuVar, RuExp>
);

ruIndexBranch(beg : RuIndexNode<?>, end : RuIndexNode<?>, acc : [RuIndexNode<?>]) -> [RuIndexNode<?>] {
	if (beg == end) concat([beg], acc) else {
		ruIndexBranch(beg, either(end.parent, beg), concat([end], acc));
	}
}

ruIndexSubexpr(beg : RuIndexNode<?>, end : RuIndexNode<?>) -> RuExp {
	branch = ruIndexBranch(beg, end, []);
	lens = foldi(branch, makeTree(), \i, acc, n -> 
		fold(n.lens, acc, \ac, len -> setTree(acc, i - len, len))
	);
	RuExp(
		foldi(branch, [], \i, acc, n ->
			if (n.arity == -1) {
				concat(acc, [RuVar(n.name, n.type)]);
			} else {
				concat(acc, [RuRuleRef(n.name, n.type, n.arity, lookupTreeDef(lens, i, -1))]);
			}
		)
		, -1
	);
}

ruDoUnifyIndex(i : int, index : RuIndex<?>, math : RuMath, env : RuUnifyEnv) -> [RuUnified<?>] {
	unify_step = \j, var, end, ex ->
		switch (ruAdd2Subst(env.sub, var, ex)) {
			Some(s): 
				ruDoUnifyIndexNode(j, end, math, RuUnifyEnv(env with sub = s));
			None(): [];
		}
	unify_var = \j, var, ex_type, end, e ->
		if (ex_type == var.type) unify_step(j, var, end, e())
		else {
			switch (ruSuperRule(var.type, ex_type, math)) {
				Some(sr): {
					ex = RuExp(concat([RuRuleRef(sr.name, var.type, 1, 1)], e().nodes), -1);
					unify_step(j, var, end, ex);
				}
				None(): []
			}
		}
	if (i == length(env.expr.nodes)) [] else {
		switch (env.expr.nodes[i]) {
			RuRuleRef(name, type, arity, len): {
				r1 = switch (lookupTree(index.rules, name)) {
					Some(n): ruDoUnifyIndexNode(i, n, math, env);
					None(): [];
				}
				r2 = foldTree(index.vars, [], \v, n, acc -> 
					unify_var(i + len, RuVar(n.name, n.type), type, n, 
						\-> RuExp(subrange(env.expr.nodes, i, len + 1), -1)
					)
				);
				concat(r1, r2);
			}
			RuVar(name, type): {
				r1 = foldTree(index.rules, [], \nm, n, acc -> {
					println("nm: " + nm + ", ends: " + i2s(length(n.ends)));
					fold(n.ends, acc, \ac, end ->
						unify_var(i, RuVar(name, type), n.type, n, 
							\-> {
								ex = ruIndexSubexpr(n, end);
								println("ex: " + ru2s(ex, math.rules));
								ex
							}
						)
					)
				});
				r2 = foldTree(index.vars, [], \v, n, acc -> 
					unify_var(i, RuVar(n.name, n.type), type, n, 
						\-> RuExp([RuVar(name, type)], -1)
					)
				);
				concat(r1, r2);
			}
		}
	}
}

ruDoUnifyIndexNode(i : int, node : RuIndexNode<?>, math : RuMath, env : RuUnifyEnv) -> [RuUnified<?>] {
	r = if (i + 1 == length(env.expr.nodes)) {
		switch (node.data) {
			Some(d): {
				switch (node.expr) {
					Some(ex): {
						e1 = ruApplySubst(ex, env.sub);
						e2 = ruApplySubst(env.expr, env.sub);
						if (e1.nodes != e2.nodes) {
							println("wrong unification");
							println("ex: " + ru2s(ex, math.rules));
							println("ex(raw): " + ruExpRPN2s(ex.nodes));
							println("env.expr: " + ru2s(env.expr, math.rules));
							println("e1: " + ru2s(e1, math.rules));
							println("e2: " + ru2s(e2, math.rules));
							println("subst:\n" + ruSubst2s(env.sub, math));
							println("e1(1): " + ru2s(ruApplySubst(ex, env.sub), math.rules));
							println("e1(raw): " + ruExpRPN2s(ruApplySubst(ex, env.sub).nodes));
							quit(-1);
						}
					}
					None(): println("impossible");
				};
				[RuUnified(env.sub, d)];
			}
			None(): [];
		}
	} else [];
	concat(r, ruDoUnifyIndex(i + 1, node.index, math, env))
}

ruIndex2s(index : RuIndex<?>, show_data : (?) -> string, math : RuMath) -> string {
	ruDoIndex2s(index, show_data, None(), "", math)
}

ruDoIndex2s(index : RuIndex<?>, show_data : (?) -> string, beg : Maybe<RuIndexNode<?>>, acc : string, math : RuMath) -> string {
	begin = \n -> switch (beg) {
		Some(b): b;
		None(): n;
	}
	acc1 = foldTree(index.rules, acc, \__, n, ac ->
		ruDoIndexNode2s(n, show_data, begin(n), ac, math)
	);
	foldTree(index.vars, acc1, \__, n, ac ->
		ruDoIndexNode2s(n, show_data, begin(n), ac, math)
	);
}

ruDoIndexNode2s(node : RuIndexNode<?>, show_data : (?) -> string, beg : RuIndexNode<?>, acc : string, math : RuMath) -> string {
	acc1 = switch (node.data) {
		Some(d): acc + ru2s(ruIndexSubexpr(beg, node), math.rules) + " --> " + show_data(d) + "\n";
		None(): acc;
	}
	ruDoIndex2s(node.index, show_data, Some(beg), acc1, math);
}

ruIndexRaw2s(index : RuIndex<?>, show_data : (?) -> string) -> string {
	ruDoIndexRaw2s(index, show_data, [], "")
}

ruDoIndexRaw2s(index : RuIndex<?>, show_data : (?) -> string, nodes : [RuIndexNode<?>], acc : string) -> string {
	acc1 = foldTree(index.rules, acc, \__, n, ac ->
		ruDoIndexNodeRaw2s(n, show_data, concat(nodes, [n]), ac)
	);
	foldTree(index.vars, acc1, \__, n, ac ->
		ruDoIndexNodeRaw2s(n, show_data, concat(nodes, [n]), ac)
	);
}

ruDoIndexNodeRaw2s(node : RuIndexNode<?>, show_data : (?) -> string, nodes : [RuIndexNode<?>], acc : string) -> string {
	acc1 = switch (node.data) {
		Some(d): acc + strGlue(map(nodes, \n -> n.name + ":" + n.type), " ") + " --> " + show_data(d) + "\n";
		None(): acc;
	}
	ruDoIndexRaw2s(node.index, show_data, nodes, acc1);
}
