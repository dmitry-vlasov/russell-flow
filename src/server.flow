import server/http;
import lsp/process;
//import base/println;

export {
	// A language server working over http.
	ruHttpServer(conf : RuConf) -> void;

	// A language server using stdin/stdout to communicate. Usually is invoked as a child process of an IDE.
	ruLspServer(conf : RuConf) -> void;

	// A console interactive Russell server.
	ruConsoleServer(conf : RuConf) -> void;
}

ruProcessRequest(
	conf : RuConf, req : string,
	msg : (string) -> void,
	out : (int, string) ->  void, 
	exit : () -> void, 
	process : (ScScript, (int) -> void, RuOut) -> void
) -> void {
	switch (ruJson2Script(parseJsonSafe(req))) {
		Some(script): {
			shutdown = exists(ruScriptTasks(script), \task -> task.command == "server-shutdown");
			lines = ref makeList();
			process(script,
				\code -> {
					if (shutdown) {
						exit();
					} else {
						out(code, concatStrings(map(list2array(^lines), \line -> line + "\n")));
					}
				},
				ruMakeOut(
					\line -> {
						msg(line);
						lines := Cons(line, ^lines);
					}, 
					conf.opts
				)
			);
		}
		None(): {
			json = ruLspDecodeJsonRequest(req);
			if (json == JsonNull()) {
				out(200, "WARNING: Illegal request.");
			} else {
				ruLspProcessRequest(conf, json, \s -> out(0, s), exit, process);
			}
		}
	}
}

ruLspServer(conf : RuConf) -> void { 
	ruLspServerLog("Server started.", conf);
	ruRunLspServer(conf);
}

ruRunLspServer(conf : RuConf) -> void {
	switch (ruLspReadRequest()) {
		Some(req): {
			// Special watermark for non-logging
			uuid = ruMakeUuid();
			log_request = !strContains(req.request, "do_not_log_this");
			if (log_request) {
				ruLspServerLog("<" + uuid  + "> Client: " + ruCutLongString(ruServerEscapeString(req.request)), conf);
			}
			timer = ruMakeTimer();
			ruProcessRequest(conf, req.json, nop1,
				\__, resp -> {
					if (log_request) {
						ruLspServerLog("<" + uuid  + "> Server [" + ruTimePassed2s(timer) + "]: " + ruCutLongString(ruServerEscapeString(resp)), conf);
					}
					print(resp);
				},
				\-> {
					ruLspServerLog("Server stopped.", conf);
					quit(0);
				},
				ruProcess
			);
		}
		None(): {
			ruLspServerLog("Failed to read a request.", conf);
		}
	}
	ruRunLspServer(conf);
}

ruHttpServer(conf : RuConf) -> void {
	port = s2i(lookupTreeDef(conf.opts, "server-port", "20001"));
	server = ref nop;
	server := createHttpServer(port,
		\-> {
			ruHttpServerLog("Server started.", conf);
			println("Http server started.")
		},
		\request, response -> {
			timer = ruMakeTimer();
			req_string = ruServerEscapeString(request.body);
			// Special watermark for non-logging
			log_request = !strContains(req_string, "do_not_log_this");
			uuid = ruMakeUuid();
			if (log_request) {
				ruHttpServerLog("<" + uuid  + "> Client: " + ruCutLongString(ruServerEscapeString(request.body)), conf);
			}
			ruProcessRequest(conf, 
				request.body, 
				if (log_request) println else nop1,
				\code, output -> {
					if (log_request) {
						ruHttpServerLog("<" + uuid  + "> Server [" + ruTimePassed2s(timer) + "]: " + ruCutLongString(ruServerEscapeString(output)), conf);
					}
					HttpResponse(200 + code, output, []) |> response
				},
				\-> {
					println("Http server stopped.");
					ruHttpServerLog("Server stopped.", conf);
					HttpResponse(200, "Http server stopped.", []) |> response;
					^server();
					quit(0);
				},
				ruProcessLocal
			)
		}
	);
}

ruConsoleServer(conf : RuConf) -> void {
	ruConsoleLog("Server started.", conf);
	println("Enter a script (command) and press enter to execute. Type 'exit' to leave.");
	ruRunConsoleServer(conf);
	quit(0);
}

ruRunConsoleServer(conf : RuConf) -> void {
	print("> ");
	script_src = trim2(readUntil("\n"), " \r\n\t");
	if (script_src != "exit") {
		switch (ruParseScript(script_src)) {
			Some(src): {
				uuid = ruMakeUuid();
				timer = ruMakeTimer();
				ruConsoleLog("<" + uuid + "> Script started: " + ruServerEscapeString(script_src), conf);
				ruProcess(src.script, 
					\code -> {
						time_msg = "[" + ruTimePassed2s(timer) + "]";
						code_msg = if(code == 0) "" else "(code: " + i2s(code) + ") ";
						ruConsoleLog("<" + uuid + "> Script finished " + code_msg + time_msg, conf);
						ruRunConsoleServer(conf);
					},
					ruMakeOut(println, conf.opts)
				);
			}
			None(): {
				println("Syntax error in script: '" + escapeStr(script_src) + "'");
				ruConsoleLog(
					"Syntax error in script: '" + escapeStr(script_src) + "'",
					//+ " = [" + strGlue(map(s2a(script_src), i2s), ", ") + "]", 
					conf
				);
				ruRunConsoleServer(conf);
			}
		}
	} else {
		ruConsoleLog("Server stopped.", conf);
		println("Bye.");
	}
}

