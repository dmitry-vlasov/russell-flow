import refactor/ru_refactor;
import ru/ru_verify;
import ru/prover/ru_prover_util;
import unify/ru_unify_indexes;

export {
	ruShortenTheorem(theorem : RuTheorem, math : RuMath, conf : RuConf) -> Maybe<RuTheorem>;
	ruShortenMath(math : RuMath, conf : RuConf) -> RuMath;
	ruCommShortenProofs(task : RuTask, env : RuEnv) -> RuEnv;
}

ruSubProofVolume(i : int, steps : [RuStep]) -> int {
	fold(steps[i].refs, 1, \acc, ref -> acc + ruRefVolume(ref, steps));
}

ruRefVolume(ref : RuRef, steps : [RuStep]) -> int {
	switch (ref) {
		RuHypRef(__,__): 0;
		RuStepRef(j, __): ruSubProofVolume(j, steps);
	}
}

ruRefsEq(r1 : RuRef, r2 : RuRef) -> bool {
	switch (r1) {
		RuHypRef(i, __): 
			switch (r2) {
				RuHypRef(j, __): i == j;
				RuStepRef(__, __): false;
			}
		RuStepRef(i, __):
			switch (r2) {
				RuHypRef(__, __): false;
				RuStepRef(j, __): i == j;
			}
	}
}

ruStepsEq(s1 : RuStep, s2 : RuStep) -> bool {
	s1.ind == s2.ind &&
	s1.assertion == s2.assertion && 
	s1.expr.nodes == s2.expr.nodes &&
	length(s1.refs) == length(s2.refs) &&
	forall(zipWith(s1.refs, s2.refs, \r1, r2 -> Pair(r1, r2)),
		\p -> ruRefsEq(p.first, p.second)
	);
}

RuShortenAcc(
	theorem : RuTheorem,
	optimized : [RuStep],
	assertions : RuIndex<RuAssertion>,
	premises : RuIndex<RuRef>,
	steps : [RuStep],
	math : RuMath,
	conf : RuConf
);

ruShortenTheorem(th : RuTheorem, math0 : RuMath, conf : RuConf) -> Maybe<RuTheorem> {
	start = timestamp();
	cth = ruConstTheorem(th, math0);
	math = cth.env.math;
	
	/*if (math.conf.verbose > 1) {
		conf.onMessage("started shortening therem " + th.name + " ...");
	}*/
	
	assertions = ruInitAssertionsIndex(th.name, math, conf);
	initPremises = fold(cth.const.hyps, ruMakeIndex(), \acc, hyp -> ruSetIndex(hyp.expr, RuHypRef(hyp.ind, -1), acc));
	optimized = foldi(cth.const.proof.steps, RuShortenAcc(th, [], assertions, initPremises, cth.const.proof.steps, math, conf), \i, acc, step -> {
		least = ruStepVariants(step.ind, cth.const.proof.steps, th.disjs, acc);
		RuShortenAcc(acc with
			optimized = concat(acc.optimized, [least]),
			premises = ruSetIndex(step.expr, RuStepRef(step.ind, -1), acc.premises)
		);
	}).optimized;
	ret = ruRemoveTheoremUnusedSteps(RuTheorem(th with 
		proof = ruTransformProof(RuProof(th.proof with steps = optimized), cth.env.unconst)
	), conf);
	/*if (math.conf.verbose > 1) {
		conf.onMessage("finished shortening therem " + th.name + " ...");
	}*/
	switch (ret) {
		Some(th_opt): {
			if (conf.verbose > 0) {
				conf.onMessage("theorem " + th.name + " shortened by " + i2s(length(th.proof.steps) - length(th_opt.proof.steps)) + " steps in " + d2st((timestamp() - start)/ 1000.0, 5) + "s");
			}
			if (isNone(ruVerifyTheorem(th_opt, math, conf))) {
				conf.onMessage("ERROR IN SHORTENING");
				conf.onMessage("optimized:");
				conf.onMessage(ru2s(th_opt, math.rules));
				conf.onMessage("original:");
				conf.onMessage(ru2s(th, math.rules));
				quit(-1);
			}
		}
		None(): 
			if (conf.verbose > 1) {
				conf.onMessage("therem " + th.name + " was not shortened in " + d2st((timestamp() - start)/ 1000.0, 2) + "s");
			}
	}
	ret
}

RuShortenLeast(
	step : RuStep, // Current least step
	vol  : int     // volume of current least step
);

RuShortenStepAcc(
	up : RuShortenAcc,
	assertion : string,
	sub : Tree<RuVar, RuExp>,
	refs : [RuRef],
	least : ref RuShortenLeast,
	vol : int,
	start : double,
	max_time : double
);

ruStepVariants(i : int, steps : [RuStep], disjs : Set<RuDisj>, acc : RuShortenAcc) -> RuStep {
	step = acc.steps[i];
	least = ref RuShortenLeast(acc.steps[i], ruSubProofVolume(i, acc.steps));
	max_time = s2d(lookupTreeDef(acc.conf.opts, "max_time", "1.0"));
	iter(ruUnifyIndex(step.expr, acc.assertions, makeTree(), acc.math, acc.conf), \unif -> {
		if (unif.data.hyps != [] && ruCheckDisjs(unif.sub, unif.data.disjs) && ruCheckDisjs(unif.sub, disjs)) {
			/*hyps = CartProd(map(unif.data.hyps, \h -> {
				unified = ruUnifyIndex(ruApplySubst(h.expr, unif.sub), acc.premises, unif.sub, acc.math);
				mergeSort(unified, \u1, u2 -> ruRefVolume(u1.data, acc.steps) - ruRefVolume(u2.data, acc.steps));
			}));
			if (sizeCartProd(hyps) > 0) {
				stepAcc = RuShortenStepAcc(acc, unif.data.name, unif.sub, [], least, 1, timestamp(), max_time);
				/*if (ruSizeCart(hyps) > 10000) {
					conf.onMessage("ruSizeCart(hyps) (TOO MUCH): " + i2s(ruSizeCart(hyps)));
				}* /
				ruRefsVariants2(initCartProd(hyps), hyps, stepAcc);
			}*/
			
			stepAcc = RuShortenStepAcc(acc, unif.data.name, unif.sub, [], least, 1, timestamp(), max_time);
			ruRefsVariants1(0, unif.data.hyps, stepAcc);
		}
	});
	^least.step
}

ruRefsVariants2(v : [int], prod : CartProd<RuUnified<RuRef>>, acc : RuShortenStepAcc) -> void {
	if (timestamp() - acc.start < acc.max_time) {
		refs = mapi(v, \i, n -> prod.sets[i][n].data);
		step = RuStep(^(acc.least).step with refs = refs, assertion = acc.assertion);
		vol = ruSubProofVolume(step.ind, replace(acc.up.steps, step.ind, step));
		if (vol < ^(acc.least).vol) {
			sub = fold(mapi(v, \i, n -> prod.sets[i][n].sub), Some(acc.sub), \ac, sub ->
				maybeBind(ac, \s -> ruMergeSubst(s, sub))
			);
			switch (sub) {
				Some(__): {
					acc.least := RuShortenLeast(step, vol);
				}
				None(): { }
			}
		}
		switch(nextCartProd(v, prod)) {
			Some(n): ruRefsVariants2(n, prod, acc);
			None(): { }
		}
	} else {
		//conf.onMessage("time limit reached for " + acc.up.theorem.name);
	}
}

ruRefsVariants1(j : int, hyps : [RuHyp], acc : RuShortenStepAcc) -> void {
	if (j == length(hyps)) {
		new_step = RuStep(^(acc.least).step with refs = acc.refs, assertion = acc.assertion);
		acc.least := RuShortenLeast(new_step, acc.vol);
	} else {
		if (timestamp() - acc.start < acc.max_time) {
			unified = ruUnifyIndex(ruApplySubst(hyps[j].expr, acc.sub), acc.up.premises, acc.sub, acc.up.math, acc.up.conf);
			sorted = mergeSort(unified, \u1, u2 -> ruRefVolume(u1.data, acc.up.steps) - ruRefVolume(u2.data, acc.up.steps));
			iter(sorted, \un -> 
				if (timestamp() - acc.start < acc.max_time) {
					vol = acc.vol + ruRefVolume(un.data, acc.up.steps);
					if (vol < ^(acc.least).vol) {
						ruRefsVariants1(j + 1, hyps, RuShortenStepAcc(acc with 
							refs = concat(acc.refs, [un.data]),
							sub = un.sub,
							vol = vol
						))
					}
				}
			)
		}
	}
}

ruShortenMath(math : RuMath, conf : RuConf) -> RuMath {
	start = timestamp();
	shortened = filtermap(ruConcurrent(
		map(getTreeValues(ruMathTheorems(math)), 
			\th -> \ -> ruShortenTheorem(th, math, conf)
		)
	), idfn);
	ret = fold(shortened, Pair(math, 0), \acc, th -> {
		orig = ruTheorem(th.name, acc.first, conf);
		gain = length(orig.proof.steps) - length(th.proof.steps);
		Pair(ruReplaceAssertion(th, acc.first, conf), acc.second + gain);
	});
	if (conf.verbose > 0) {
		conf.onMessage("shortened " + i2s(length(shortened)) + " theorems,  " +
		"reduced " + i2s(ret.second) + " steps " +
		"in " + d2st((timestamp() - start)/ 1000.0, 2) + "s");
	}
	ret.first;
}

ruCommShortenProofs(task : RuTask, env : RuEnv) -> RuEnv {	
	math = env.ruMath;
	conf = env.conf;
	file = lookupTreeDef(task.args, "file", "");
	target0 = lookupTreeDef(task.args, "target", file);
	target = ruTrimPath(target0, env.conf, ".ru");
	if (conf.verbose > 0) {
		conf.onMessage("shortening target: " + target);
	}
	ret = if (target == "1" || target == "all" || target == "") {
		ruShortenMath(math, conf);
	} else {
		switch (lookupTree(math.assertions, target)) {
			Some(ws): {
				th = ws.item;
				switch (th) {
					RuTheorem(__,__,__,__,__,__,__,__): {
						start = timestamp();
						shortened = ruShortenTheorem(th, math, conf);
						r = eitherMap(shortened, \t -> ruReplaceAssertion(t, math, conf), math);
						if (conf.verbose > 0 && isSome(shortened)) {
							conf.onMessage("theorem " + target + " shortened in " + d2st((timestamp() - start)/ 1000.0, 2) + "s");
						}
						r;
					}
					default: {
						conf.onMessage("assertion " + target + " is not a theorem");
						math;
					}
				}
			}
			None(): {
				switch (lookupTree(math.sources, target)) {
					Some(src): {
						start = timestamp();
						shortened = filtermap(ruConcurrent(
							map(ruSourceTheorems(src), 
								\th -> \ -> ruShortenTheorem(th, math, conf)
							)
						), idfn);
						r = fold(shortened, math, \acc, th -> ruReplaceAssertion(th, acc, conf));
						if (conf.verbose > 0) {
							conf.onMessage("shortened " + i2s(length(shortened)) + " theorems in " + d2st((timestamp() - start)/ 1000.0, 2) + "s");
						}
						r;
					}
					None(): {
						conf.onMessage("shortening target " + target + " is not found");
						math;
					}
				}
			}
		}
	}
	RuEnv(env with ruMath = ret);
}
