import base/util/fs;
import base/util/range;
import base/util/str;
import base/println;

export {
	RuPlace(file : string, pos : int);

	RuConf(
		importRoots : [string],
		projectRoots : [string],
		workingDir : string,
        verbose : int,
		opts : Tree<string, string>,
		onError : (string, [RuPlace]) -> void,
		onMessage : (string) -> void
	);

	ruTrimPath(file : string, conf : RuConf, ext : string) -> string;
	ruFindPath(file : string, conf : RuConf, ext : string) -> string;
	ruResolveRelativePath(path : string, working_dir : string) -> string;
	ruResolveRelativeFile(file : string, working_dir : string) -> string;
	ruResolveRelativeDir(dir : string, working_dir : string) -> string;
	ruIncVerbose(conf : RuConf) -> RuConf;
	ruDecVerbose(conf : RuConf) -> RuConf;
	ruAddErr2Conf(conf : RuConf, msg : string) -> RuConf;
	ruAddErrCounter(conf : RuConf, err_count : ref int) -> RuConf;
	ruPlace2s(place : RuPlace) -> string;
	getRussellDir() -> string;
	findRussellDir() -> string; // silent version of getRussellDir
	ruFindConfDir(dir : string) -> string;

	ruParams2Tree(params : [[string]]) -> Tree<string, string>;
	ruMakeConf(dir_ : string, params : [[string]]) -> RuConf;
	ruMakeConfDef() -> RuConf;
	ruMakeOnError(opts : Tree<string, string>, onMessage : (string) -> void) -> (string, [RuPlace]) -> void;
	ruMergeOpts(opts1 : Tree<string, string>, opts2 : Tree<string, string>) -> Tree<string, string>;

	ruReadConfFromDir(dir : string) -> Tree<string, string>;
	ruConf2Tree(conf : RuConf) -> Tree<string, string>;
	ruTree2Conf(opts : Tree<string, string>) -> RuConf;
	ruMergeConfs(conf1 : RuConf, conf2 : RuConf) -> RuConf;
	ruSetConf(conf1 : RuConf, opts : Tree<string, string>) -> RuConf;
	ruUnsetConf(conf : RuConf, opts : Tree<string, string>) -> RuConf;
	ruConf2s(conf : RuConf) -> string;
	ruVerbose(conf : RuConf) -> int;

	ruCacheDir(conf : RuConf) -> string;
	ruCachedPath(file : string, conf : RuConf) -> string;
	ruCachedFile(path : string, conf : RuConf) -> string;
}

ruTrimPath(file : string, conf : RuConf, ext0 : string) -> string {
	ext = if (startsWith(ext0, ".")) ext0 else "." + ext0;
	noext = strReplace(ruRemoveExt(file, ext), "\\", "/");
	shift = if (conf.workingDir == "") "" else relatingPath(conf.workingDir, resolveRelativePath("."));
	pathShift = if (shift == "") shift else shift + "/";
	processImportRoot = \acc, impRoot -> {
		resolved = strReplace(resolveRelativePath(impRoot), "\\", "/");
		if (startsWith(toLowerCase(acc), toLowerCase(resolved))) {
			strRight(acc, strlen(resolved));
		} else if (startsWith(toLowerCase(acc), toLowerCase(impRoot))) {
			strRight(acc, strlen(impRoot));
		} else acc
	};
	trimmed = fold(conf.importRoots, noext, \acc, impRoot -> {
		acc1 = if (pathShift != "") {
			processImportRoot(acc, pathShift + impRoot)
		} else acc;
		processImportRoot(acc1, impRoot)
	});
	if (trimmed == noext) noext else trim2(trimmed, "/");
}

ruFindPath(file : string, conf : RuConf, ext0 : string) -> string {
	ext = if (startsWith(ext0, ".")) ext0 else "." + ext0;
	with_ext = if (endsWith(file, ext)) file else file + ext;
	shift = if (conf.workingDir == "") "" else relatingPath(conf.workingDir, resolveRelativePath("."));
	if (fileExists(with_ext) && length(conf.importRoots) == 0) {
		resolveRelativePath(with_ext);
	} else {
		ruDoFindPath(with_ext, if (shift == "") shift else shift + "/", conf.importRoots);
	}
}

ruResolveRelativePath(path : string, working_dir : string) -> string {
	if (isAbsolutePath(path)) path else {
		if (working_dir == "" || fileExists(path) || isDirectory(path)) {
			resolveRelativePath(path);
		} else {
			resolveRelativePath(working_dir + "/" + path);
			//shifted = working_dir + "/" + file;
			//if (fileExists(shifted)) shifted else resolveRelativePath(file);
		}
	}
}

ruResolveRelativeFile(file : string, working_dir : string) -> string {
	if (isAbsolutePath(file)) file else {
		if (working_dir == "" || fileExists(file)) {
			resolveRelativePath(file);
		} else {
			wd_file = working_dir + "/" + file;
			if (fileExists(wd_file)) resolveRelativePath(wd_file) else "";
		}
	}
}

ruResolveRelativeDir(dir : string, working_dir : string) -> string {
	if (isAbsolutePath(dir)) dir else {
		if (working_dir == "" || isDirectory(dir)) {
			resolveRelativePath(dir);
		} else {
			wd_dir = working_dir + "/" + dir;
			if (isDirectory(wd_dir)) resolveRelativePath(wd_dir) else "";
		}
	}
}

ruDoFindPath(with_ext : string, pathShift : string, importRoots : [string]) -> string {
	if (importRoots == []) with_ext
	else {
		head = importRoots[0];
		directPath = head + (if (endsWith(head, "/")) "" else "/") + with_ext;
		if (fileExists(directPath)) {
			resolveRelativePath(directPath);
		} else {
			shiftedPath = pathShift + directPath;
			if (fileExists(shiftedPath)) {
				resolveRelativePath(shiftedPath);
			} else {
				ruDoFindPath(with_ext, pathShift, tail(importRoots));
			}
		}
	}
}

ruIncVerbose(conf : RuConf) -> RuConf {
	RuConf(conf with verbose = conf.verbose + 1);
}

ruDecVerbose(conf : RuConf) -> RuConf {
	RuConf(conf with verbose = max(0, conf.verbose - 1));
}

ruAddErr2Conf(conf : RuConf, msg : string) -> RuConf {
	RuConf(conf with onError = \str, pos -> conf.onError(str + msg, pos))
}

ruAddErrCounter(conf : RuConf, err_count : ref int) -> RuConf {
	RuConf(conf with onError = 
		\err, places -> {
			err_count := ^err_count + 1;
			conf.onError(err, places);
		}
	)
}

ruPlace2s(place : RuPlace) -> string {
	if (place.file == "") "" else {
		if (fileExists(place.file)) {
			if (place.pos == -1) {
				"file '" + place.file + "'";
			} else {
				code = getFileContent(place.file);
				resolver = makeLineResolver(code);
				pos = findLine(resolver, place.pos);
				"file '" + place.file + "': line " + i2s(pos.lineno) + ", col " + i2s(pos.column);
			}
		} else {
			"file '" + place.file + "' doesn't exist. Position is : " + i2s(place.pos);
		}
	}
}

ruRussellDir : ref Maybe<string> = ref None();

getRussellDir() -> string {
	russell_dir = findRussellDir();
	if (russell_dir != "") russell_dir else {
		ruPrintln("WARNING: Could not determine the russell directory.");
		ruPrintln("Defaults to ../russell/. Use bin-dir=<path> to specify where the flow folder is");
		"../russell/";
	}
}

findRussellDir() -> string {
	init_dir = \-> {
		// Use the 'print-flow-dir=1' option of flowc
		appPath = strReplace(getApplicationPath(), "\\", "/");
		russellDirByRussell = \-> {
			if (fileNameOnly(appPath) == "russell.jar") {
				// russell must not use itsef in this function, 
				// otherwise there's a fork bomb.
				"" 
			} else {
				dir_lines = ref "";
				err_lines = ref "";
				code = execSystemProcess("russell", ["russell-dir"], ".", 
					\out -> dir_lines := ^dir_lines + out, 
					\err -> {
						println(err);
						err_lines := err;
					}
				);
				filtered = filtermap(strSplit(^dir_lines, "\n"), \line -> {
					line1 = trim2(line, " \n\t\r");
					if (line1 == "") None() else Some(line1);
				});
				if (code != 0 || ^err_lines != "" || length(filtered) != 1) "" else {
					filtered[0];
				}
			}
		}
		// Use the path of the executable
		russellDefDir = \-> {
			if (strContains(appPath, "russell/bin/")) {
				takeBefore(appPath, "/bin/", appPath) + "/"
			} else if (strContains(appPath, "russell-flow/bin/")) {
				takeBefore(appPath, "/bin/", appPath)
			} else if (strContains(appPath, "russell-flow/src/russell")) {
				takeBefore(appPath, "src/russell", appPath)
			} else if (strContains(appPath, "russell/src/russell")) {
				takeBefore(appPath, "src/russell", appPath)
			} else if (strContains(appPath, "bin/")) {
				takeBefore(appPath, "bin/", appPath)
			} else {
				russellDirByRussell();
			}
		}
		binDir = getUrlParameter("bin-dir");
		russelldir = if (binDir == "") {
			russellDefDir();
		} else {
			if (endsWith(binDir, "bin/") || endsWith(binDir, "bin\\"))
				substring(binDir, 0, strlen(binDir)-4)
			else
				substring(binDir, 0, strlen(binDir)-3);
		}
		strReplace(russelldir, "\\", "/");
    }
    onlyOnce(ruRussellDir, init_dir);
}

ruFindConfDir(dir : string) -> string {
	path = dir + "/russell.conf";
	if (fileExists(path)) dir else 
	if (dir == "/" || dir == "") "" else {
		ruFindConfDir(ruDirName(dir));
	}
}

ruMergeOpts(opts1 : Tree<string, string>, opts2 : Tree<string, string>) -> Tree<string, string> {
	mergeTreeCustom(opts1, opts2, \opt, val1, val2 -> if (val1 == "") val2 else val1 + "," + val2);
}

ruMakeOnError(opts : Tree<string, string>, onMessage : (string) -> void) -> (string, [RuPlace]) -> void {
	\err, places -> {
		onMessage("Error: " + err + (if (places == []) "" else "\n" + strGlue(map(places, ruPlace2s), "\n")));
		if (lookupTreeDef(opts, "halt-on-errors", "") == "1") {
			onMessage("Aborting russell run, because halt-on-errors=1");
			quit(-1);
		}
	}
}

ruMakeConf(dir_ : string, params : [[string]]) -> RuConf {
	dir = rtrim2(strReplace(resolveRelativePath(dir_), "\\", "/"), "/");
	conf_dir = ruFindConfDir(dir);
	opts = ruMergeOpts(
		ruReadConfFromDir(conf_dir), 
		ruParams2Tree(params)
	);
	ruTree2Conf(opts);
}

ruMakeConfDef() -> RuConf {
	ruMakeConf(resolveRelativePath("."), []);
}

ruParams2Tree(params : [[string]]) -> Tree<string, string> {
	fold(params, makeTree(), \acc, param ->
		if (length(param) == 0) {
			acc;
		} else {
			key = trim2(param[0], " \t\r\n");
			if (strlen(key) == 0) acc else {
				val = if (length(param) > 1) trim2(param[1], " \t\r\n") else "";
				setTree(acc, key, val);
			}
		}
	);
}

ruReadConfFromDir(dir : string) -> Tree<string, string> {
	path = dir + "/russell.conf";
	if (!fileExists(path)) makeTree() else {
		config = getFileContent(path);
		lines = strSplit(config, "\n");
		fold(lines, makeTree(), \acc, l -> {
			// Skip comment and empty lines in russell.conf
			line = trim2(l, " \t\r");
			if (line != "" && !startsWith(line, "#") && !startsWith(line, "//")) {
				opts = strSplit(line, "=");
				if (length(opts) == 1) {
					opt = trim2(opts[0], " \t\r");
					if (opt == "") acc else setTree(acc, opt, "");
				} else if (length(opts) == 2) {
					opt = trim2(opts[0], " \t\r");
					val = trim2(opts[1], " \t\r");
					setTree(acc, opt, val)
				} else acc;
			} else acc;
		});
	}
}

ruConf2Json(conf : RuConf) -> Json {
	JsonObject([
		Pair("importRoots", JsonArray(map(conf.importRoots, \root -> JsonString(root)))),
		Pair("projectRoots", JsonArray(map(conf.projectRoots, \root -> JsonString(root)))),
		Pair("workingDir", JsonString(conf.workingDir)),
		Pair("verbose", JsonString(i2s(conf.verbose))),
		Pair("opts", JsonArray(map(tree2pairs(conf.opts), 
			\opt -> JsonObject([
				Pair("key", JsonString(opt.first)),
				Pair("value", JsonString(opt.second))
			])
		)))
	]);
}

ruJson2Conf(json : Json) -> Maybe<RuConf> {
	if (exists(["importRoots", "projectRoots", "workingDir", "verbose", "opts"], \field -> !jsonObjectHasField(json, field))) None() else {
		opts = pairs2tree(map(getJsonArrayField(json, "opts"), 
			\opt -> Pair(getJsonStringField(opt, "key", ""), getJsonStringField(opt, "value", ""))
		));
		Some(RuConf(
			map(getJsonArrayField(json,  "importRoots"), \root -> getJsonStringValue(root, "__no__")),
			map(getJsonArrayField(json,  "projectRoots"), \root -> getJsonStringValue(root, "__no__")),
			getJsonStringField(json,     "workingDir", "__no__"),
			s2i(getJsonStringField(json, "verbose", "-1")),
			opts,
			ruMakeOnError(opts, ruPrintln),
			ruPrintln
		));
	}
}

ruConf2Tree(conf : RuConf) -> Tree<string, string> {
	mergeTree(
		conf.opts,
		pairs2tree(filtermap([
			if (conf.importRoots == []) None() else Some(Pair("import-roots", strGlue(conf.importRoots, ","))),
			if (conf.projectRoots == []) None() else Some(Pair("project-roots", strGlue(conf.projectRoots, ","))),
			if (conf.workingDir == "") None() else Some(Pair("working-dir", conf.workingDir)),
			if (conf.verbose == -1) None() else Some(Pair("verbose", i2s(conf.verbose)))
		], idfn))
	);
}

ruMakePaths(opts : Tree<string, string>, opt : string) -> [string] {
	switch (lookupTree(opts, opt)) {
		Some(paths): {
			working_dir = lookupTreeDef(opts, "working-dir", "");
			uniq(filtermap(strSplit(paths, ","), \p -> {
				path = rtrim2(strReplace(p, "\\", "/"), "/");
				if (path == "") None() else {
					Some(ruResolveRelativePath(path, working_dir));
				}
			}));
		}
		None(): [];
	}
}

ru_conf_fields = ["import-roots", "project-roots", "working-dir", "verbose"];

ruTree2Conf(opts : Tree<string, string>) -> RuConf {
	RuConf(
		ruMakePaths(opts, "import-roots"),
		ruMakePaths(opts, "project-roots"),
		lookupTreeDef(opts, "working-dir", ""),
		s2i(lookupTreeDef(opts, "verbose", "-1")),
		filterTree(opts, \opt,__ -> !contains(ru_conf_fields, opt)),
		ruMakeOnError(opts, ruPrintln),
		ruPrintln
	);
}

ruMergeConfs(conf1 : RuConf, conf2 : RuConf) -> RuConf {
	RuConf(
		uniq(concat(conf1.importRoots, conf2.importRoots)),
		uniq(concat(conf1.projectRoots, conf2.projectRoots)),
		if (conf2.workingDir != "") conf2.workingDir else conf1.workingDir,
		if (conf2.verbose != -1) conf2.verbose else conf1.verbose,
		mergeTree(conf1.opts, conf2.opts),
		conf1.onError,
		conf1.onMessage
	);
}

ruSetConf(conf : RuConf, opts : Tree<string, string>) -> RuConf {
	RuConf(
		if (!containsKeyTree(opts, "import-roots")) conf.importRoots else ruMakePaths(opts, "import-roots"),
		if (!containsKeyTree(opts, "project-roots")) conf.projectRoots else ruMakePaths(opts, "project-roots"),
		lookupTreeDef(opts, "working-dir", conf.workingDir),
		s2i(lookupTreeDef(opts, "verbose", i2s(conf.verbose))),
		mergeTree(conf.opts, filterTree(opts, \opt,__ -> !contains(ru_conf_fields, opt))),
		conf.onError,
		conf.onMessage
	);
}

ruUnsetConf(conf : RuConf, opts : Tree<string, string>) -> RuConf {
	conf.onMessage("UNSETTING OPTS: " + strGlue(getTreeKeys(opts), ", "));
	ret = RuConf(
		if (!containsKeyTree(opts, "import-roots")) conf.importRoots else [],
		if (!containsKeyTree(opts, "project-roots")) conf.projectRoots else [],
		if (!containsKeyTree(opts, "working-dir")) conf.workingDir else "",
		if (!containsKeyTree(opts, "verbose")) conf.verbose else -1,
		filterTree(conf.opts, \opt,__ -> !containsKeyTree(opts, opt)),
		conf.onError, 
		conf.onMessage
	);
	conf.onMessage("RET: \n" + strIndent(ruConf2s(ret)));
	ret
}

ruConf2s(conf : RuConf) -> string {
	(if (conf.importRoots == [])   "" else "import-roots=" + strGlue(conf.importRoots, ",") + "\n") +
	(if (conf.projectRoots == [])  "" else "project-roots=" + strGlue(conf.projectRoots, ",") + "\n") +
	(if (conf.workingDir == "")    "" else "working-dir=" + conf.workingDir + "\n") +
	(if (conf.verbose == -1)       "" else "verbose=" + i2s(conf.verbose) + "\n") +
	(if (sizeTree(conf.opts) == 0) "" else 
		strGlue(filtermap(tree2pairs(conf.opts), \p -> 
			if (contains(ru_conf_fields, p.first)) None() else
			Some(if (p.second == "") p.first else p.first + "=" + p.second)
		), "\n")
	);
}

ruVerbose(conf : RuConf) -> int {
	if (conf.verbose == -1) 1 else conf.verbose;
}

ruCacheDir(conf : RuConf) -> string {
	lookupTreeDef(conf.opts, "cache-dir", getRussellDir() + "/cache");
}

ruCachedPath(file : string, conf : RuConf) -> string {
	dir = ruCacheDir(conf) + "/" + ruDirName(file);
	ensureDirectoryExists(dir);
	ruCacheDir(conf) + "/" + strReplace(file, ":", "_CCOOLLOONN_")  + ".cache";
}

ruCachedFile(path : string, conf : RuConf) -> string {
	cache_dir = ruCacheDir(conf);
	if (!startsWith(path, cache_dir)) "" else {
		changeFileExt(
			strReplace(
				strRight(path, strlen(cache_dir)),
				"_CCOOLLOONN_", ":"
			),
			""
		);
	}
}
