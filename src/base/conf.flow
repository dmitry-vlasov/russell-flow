import base/util/fs;
import base/util/range;
import base/util/str;

export {
	RuConf(
		project : string,
		importRoots : [string],
		projectRoots : [string],
		workingDir : string,
        verbose : int,
		opts : Tree<string, Json>
	);
	ruEmptyConf = RuConf("", [], [], "", -1, makeTree());

	ruOptsString(opts: Tree<string, Json>, opt: string, defval: string) -> string;

	ruTrimPath(file : string, conf : RuConf, ext : string) -> string;
	ruFindPath(file : string, conf : RuConf, ext : string) -> string;
	ruResolveRelativePath(path : string, working_dir : string) -> string;
	ruResolveRelativeFile(file : string, working_dir : string) -> string;
	ruResolveRelativeDir(dir : string, working_dir : string) -> string;
	ruIncVerboseConf(conf : RuConf) -> RuConf;
	ruDecVerboseConf(conf : RuConf) -> RuConf;
	ruFindConfDir(dir : string) -> string;

	ruParams2Tree(params : [[string]]) -> Tree<string, string>;
	ruParams2Json(params : [[string]]) -> Tree<string, Json>;

	ruMakeConf(dir_ : string, params : [[string]]) -> RuConf;
	ruMakeConfDef() -> RuConf;

	ruReadConfFromDir(dir : string) -> RuConf;

	ruConf2Tree(conf : RuConf) -> Tree<string, string>;
	ruTree2Conf(opts : Tree<string, string>) -> RuConf;

	ruConf2Json(conf: RuConf) -> Json;
	ruJson2Conf(json: Json) -> RuConf;

	ruMergeConfs(conf1 : RuConf, conf2 : RuConf) -> RuConf;
	ruSetConf(conf1 : RuConf, opts : Tree<string, string>) -> RuConf;
	ruSetConfOpt(conf : RuConf, opt : string, val : string) -> RuConf;
	ruUnsetConf(conf : RuConf, opts : Tree<string, string>) -> RuConf;
	ruConf2s(conf : RuConf) -> string;
	ruVerbose(conf : RuConf) -> int;

	ru_conf_fields = ["project", "import-roots", "project-roots", "working-dir", "verbose", "verb"];
}

ruOptsString(opts: Tree<string, Json>, opt: string, defval: string) -> string {
	switch (lookupTree(opts, opt)) {
		Some(val): {
			switch (val) {
				JsonString(str): str;
				default: defval;
			}
		}
		None(): defval;
	}
}

ruTrimPath(file : string, conf : RuConf, ext0 : string) -> string {
	ext = if (startsWith(ext0, ".")) ext0 else "." + ext0;
	noext = strReplace(ruRemoveExt(file, ext), "\\", "/");
	shift = if (conf.workingDir == "") "" else relatingPath(conf.workingDir, resolveRelativePath("."));
	pathShift = if (shift == "") shift else shift + "/";
	processImportRoot = \acc, impRoot -> {
		resolved = strReplace(resolveRelativePath(impRoot), "\\", "/");
		if (startsWith(toLowerCase(acc), toLowerCase(resolved))) {
			strRight(acc, strlen(resolved));
		} else if (startsWith(toLowerCase(acc), toLowerCase(impRoot))) {
			strRight(acc, strlen(impRoot));
		} else acc
	};
	trimmed = fold(conf.importRoots, noext, \acc, impRoot -> {
		acc1 = if (pathShift != "") {
			processImportRoot(acc, pathShift + impRoot)
		} else acc;
		processImportRoot(acc1, impRoot)
	});
	if (trimmed == noext) noext else trim2(trimmed, "/");
}

ruFindPath(file : string, conf : RuConf, ext0 : string) -> string {
	ext = if (startsWith(ext0, ".") || ext0 == "") ext0 else "." + ext0;
	with_ext = if (endsWith(file, ext) || ext == "") file else file + ext;
	shift = if (conf.workingDir == "") "" else relatingPath(conf.workingDir, resolveRelativePath("."));
	if (fileExists(with_ext) && length(conf.importRoots) == 0) {
		resolveRelativePath(with_ext);
	} else {
		ruDoFindPath(with_ext, if (shift == "") shift else shift + "/", conf.importRoots);
	}
}

ruDoFindPath(with_ext : string, pathShift : string, importRoots : [string]) -> string {
	if (importRoots == []) with_ext
	else {
		head = importRoots[0];
		directPath = head + (if (endsWith(head, "/")) "" else "/") + with_ext;
		if (fileExists(directPath)) {
			resolveRelativePath(directPath);
		} else {
			shiftedPath = pathShift + directPath;
			if (fileExists(shiftedPath)) {
				resolveRelativePath(shiftedPath);
			} else {
				ruDoFindPath(with_ext, pathShift, tail(importRoots));
			}
		}
	}
}

ruResolveRelativePath(path : string, working_dir : string) -> string {
	if (isAbsolutePath(path)) path else {
		if (working_dir == "" || fileExists(path) || isDirectory(path)) {
			resolveRelativePath(path);
		} else {
			resolveRelativePath(working_dir + "/" + path);
		}
	}
}

ruResolveRelativeFile(file : string, working_dir : string) -> string {
	if (isAbsolutePath(file)) file else {
		if (working_dir == "" || fileExists(file)) {
			resolveRelativePath(file);
		} else {
			wd_file = working_dir + "/" + file;
			if (fileExists(wd_file)) resolveRelativePath(wd_file) else "";
		}
	}
}

ruResolveRelativeDir(dir : string, working_dir : string) -> string {
	if (isAbsolutePath(dir)) dir else {
		if (working_dir == "" || isDirectory(dir)) {
			resolveRelativePath(dir);
		} else {
			wd_dir = working_dir + "/" + dir;
			if (isDirectory(wd_dir)) resolveRelativePath(wd_dir) else "";
		}
	}
}

ruIncVerboseConf(conf : RuConf) -> RuConf {
	RuConf(conf with verbose = conf.verbose + 1);
}

ruDecVerboseConf(conf : RuConf) -> RuConf {
	RuConf(conf with verbose = max(0, conf.verbose - 1));
}

ruFindConfDir(dir : string) -> string {
	path = dir + "/russell.conf";
	if (fileExists(path)) dir else 
	if (dir == "/" || dir == "") "" else {
		ruFindConfDir(ruDirName(dir));
	}
}

// In case merging fails the second value overwrites the first one
ruMergeJsons(val1: Json, val2: Json) -> Json {
	switch (val1) {
		JsonString(str1): {
			switch (val2) {
				JsonString(str2): if (str1 == "") val2 else JsonString(str1 + "," + str2);
				default: val2;
			}
		}
		JsonArray(arr1): {
			switch (val2) {
				JsonArray(arr2): JsonArray(concat(arr1, arr2));
				default: val2;
			}
		}
		JsonObject(obj1): {
			switch (val2) {
				JsonObject(obj2): {
					JsonObject(tree2pairs(ruMergeOpts(
						pairs2tree(obj1),
						pairs2tree(obj2),
					)));
				}
				default: val2;
			}
		}
		default: val2;
	}
}

ruMergeOpts(opts1 : Tree<string, Json>, opts2 : Tree<string, Json>) -> Tree<string, Json> {
	mergeTreeCustom(opts1, opts2, \opt, val1, val2 -> ruMergeJsons(val1, val2));
}

ruMakeConf(dir_ : string, params : [[string]]) -> RuConf {
	dir = rtrim2(strReplace(resolveRelativePath(dir_), "\\", "/"), "/");
	ruMergeConfs(
		ruReadConfFromDir(ruFindConfDir(dir)), 
		ruJson2Conf(JsonObject(tree2pairs(ruParams2Json(params))))
	);
}

ruMakeConfDef() -> RuConf {
	ruMakeConf(resolveRelativePath("."), []);
}

ruIsJsonStirng(val: string) -> bool {
	!strContains(val, "{") && !strContains(val, "\"") && !strContains(val, "[")
}

ruTree2JsonTree(tree: Tree<string, string>) -> Tree<string, Json> {
	mapTree(tree, \val -> if (ruIsJsonStirng(val)) JsonString(val) else parseJson(val));
}

ruParams2Tree(params : [[string]]) -> Tree<string, string> {
	fold(params, makeTree(), \acc, param ->
		if (length(param) == 0) {
			acc;
		} else {
			key = trim2(param[0], " \t\r\n");
			val = if (length(param) > 1) trim2(param[1], " \t\r\n") else "";
			if (strlen(key) == 0 || (key == "server" && val != "")) acc else {
				setTree(acc, key, val);
			}
		}
	);
}

ruParams2Json(params : [[string]]) -> Tree<string, Json> {
	ruTree2JsonTree(ruParams2Tree(params));
}

ruReadConfFromDir(dir : string) -> RuConf {
	path = dir + "/russell.conf";
	if (!fileExists(path)) ruMakeConfDef() else {
		config = getFileContent(path);
		if (startsWith(config, "{")) {
			// JSON format of config
			ruJson2Conf(parseJson(config));
		} else {
			// ini format of conig
			lines = strSplit(config, "\n");
			ruTree2Conf(fold(lines, makeTree(), \acc, l -> {
				// Skip comment and empty lines in russell.conf
				line = trim2(l, " \t\r");
				if (line != "" && !startsWith(line, "#") && !startsWith(line, "//")) {
					opts = strSplit(line, "=");
					if (length(opts) == 1) {
						opt = trim2(opts[0], " \t\r");
						if (opt == "") acc else setTree(acc, opt, "");
					} else if (length(opts) == 2) {
						opt = trim2(opts[0], " \t\r");
						val = trim2(opts[1], " \t\r");
						setTree(acc, opt, val)
					} else acc;
				} else acc;
			}));
		}
	}
}

ruConf2Json(conf : RuConf) -> Json {
	JsonObject([
		Pair("project", JsonString(conf.project)),
		Pair("importRoots", JsonArray(map(conf.importRoots, \root -> JsonString(root)))),
		Pair("projectRoots", JsonArray(map(conf.projectRoots, \root -> JsonString(root)))),
		Pair("workingDir", JsonString(conf.workingDir)),
		Pair("verbose", JsonString(i2s(conf.verbose))),
		Pair("opts", JsonObject(tree2pairs(conf.opts)))
	]);
}

ru_conf_json_fields = ["project", "importRoots", "projectRoots", "workingDir", "verbose", "opts"];

ruJson2Conf(json : Json) -> RuConf {
	RuConf(
		getJsonStringField(json, "project", ""),
		filtermap(getJsonArrayField(json, "importRoots"), getJsonStringValueM),
		filtermap(getJsonArrayField(json, "projectRoots"), getJsonStringValueM),
		getJsonStringField(json, "workingDir", ""),
		getJsonIntField(json, "verbose", -1),
		pairs2tree(getJsonObjectField(json, "opts").members),
	);
}

ruConf2Tree(conf : RuConf) -> Tree<string, string> {
	mergeTree(
		mapTree(conf.opts, \val -> json2string(val)),
		pairs2tree(filtermap([
			if (conf.project == "") None() else Some(Pair("project", conf.project)),
			if (conf.importRoots == []) None() else Some(Pair("import-roots", strGlue(conf.importRoots, ","))),
			if (conf.projectRoots == []) None() else Some(Pair("project-roots", strGlue(conf.projectRoots, ","))),
			if (conf.workingDir == "") None() else Some(Pair("working-dir", conf.workingDir)),
			if (conf.verbose < 0) None() else Some(Pair("verbose", i2s(conf.verbose)))
		], idfn))
	);
}

ruMakePaths(opts : Tree<string, string>, opt : string) -> [string] {
	switch (lookupTree(opts, opt)) {
		Some(paths): {
			working_dir = lookupTreeDef(opts, "working-dir", "");
			uniq(filtermap(strSplit(paths, ","), \p -> {
				path = rtrim2(strReplace(p, "\\", "/"), "/");
				if (path == "") None() else {
					Some(ruResolveRelativePath(path, working_dir));
				}
			}));
		}
		None(): [];
	}
}

ruTree2Conf(opts : Tree<string, string>) -> RuConf {
	ruSetConf(ruEmptyConf, opts);
}

ruMergeConfs(conf1 : RuConf, conf2 : RuConf) -> RuConf {
	RuConf(
		if (conf1.project != "") conf1.project else conf2.project,
		uniq(concat(conf1.importRoots, conf2.importRoots)),
		uniq(concat(conf1.projectRoots, conf2.projectRoots)),
		if (conf2.workingDir != "") conf2.workingDir else conf1.workingDir,
		if (conf2.verbose != -1) conf2.verbose else conf1.verbose,
		ruMergeOpts(conf1.opts, conf2.opts)
	);
}

ruTrimOpts(opts : Tree<string, string>) -> Tree<string, string> {
	foldTree(opts, makeTree(), \key, val, acc ->
		setTree(acc,
			trim2(key, "\t\n\r "),
			trim2(val, " \t\n\r")
		)
	);
}

ruSetConf(conf : RuConf, opts0 : Tree<string, string>) -> RuConf {
	opts = ruTrimOpts(opts0);
	RuConf(
		lookupTreeDef(opts, "project", conf.project),
		if (!containsKeyTree(opts, "import-roots")) conf.importRoots else ruMakePaths(opts, "import-roots"),
		if (!containsKeyTree(opts, "project-roots")) conf.projectRoots else ruMakePaths(opts, "project-roots"),
		lookupTreeDef(opts, "working-dir", conf.workingDir),
		s2i(lookupTreeDef(opts, "verbose", lookupTreeDef(opts, "verb", i2s(conf.verbose)))),
		ruMergeOpts(conf.opts, ruTree2JsonTree(filterTree(opts, \opt, __ -> !contains(ru_conf_json_fields, opt))))
	);
}

ruSetConfOpt(conf : RuConf, opt : string, val : string) -> RuConf {
	ruSetConf(conf, makeTree1(opt, val));
}

ruUnsetConf(conf : RuConf, opts0 : Tree<string, string>) -> RuConf {
	opts = ruTrimOpts(opts0);
	RuConf(
		if (!containsKeyTree(opts, "project")) conf.project else "",
		if (!containsKeyTree(opts, "import-roots")) conf.importRoots else [],
		if (!containsKeyTree(opts, "project-roots")) conf.projectRoots else [],
		if (!containsKeyTree(opts, "working-dir")) conf.workingDir else "",
		if (!(containsKeyTree(opts, "verbose") || containsKeyTree(opts, "verb"))) conf.verbose else -1,
		filterTree(conf.opts, \opt,__ -> !containsKeyTree(opts, opt))
	);
}


ruConf2s(conf : RuConf) -> string {
	(if (conf.project == "")       "" else "project=" + conf.project + "\n") +
	(if (conf.importRoots == [])   "" else "import-roots=" + strGlue(conf.importRoots, ",") + "\n") +
	(if (conf.projectRoots == [])  "" else "project-roots=" + strGlue(conf.projectRoots, ",") + "\n") +
	(if (conf.workingDir == "")    "" else "working-dir=" + conf.workingDir + "\n") +
	(if (conf.verbose == -1)       "" else "verbose=" + i2s(conf.verbose) + "\n") +
	(if (sizeTree(conf.opts) == 0) "" else 
		strGlue(filtermap(tree2pairs(conf.opts), \p -> 
			if (contains(ru_conf_fields, p.first)) None() else
			Some(if (p.second == JsonNull()) p.first else p.first + "=" +  json2string(p.second))
		), "\n")
	);
}

ruVerbose(conf : RuConf) -> int {
	if (conf.verbose <= 0) 0 else conf.verbose;
}
