import ds/set;
import ds/hashset;
import math/math;

export {
	ruiLogN(n : int, N : int) -> int;
	ruiExpN(n : int, N : int) -> int;

	rudLogN(n : double, N : int) -> int;
	rudExpN(n : double, N : int) -> int;

	ruSetsIntersect(s1 : Set<?>, s2 : Set<?>) -> bool;
	ruHashSetsIntersect(s1 : HashSet<?>, s2 : HashSet<?>) -> bool;
	ruIsSubset(s1 : Set<?>, s2 : Set<?>) -> bool;
	ruExistsi(a : [?], fn : (int, ?) -> bool) -> bool;

	ruMaxIntFromArray(ints : [int]) -> int;
	ruMinIntFromArray(ints : [int]) -> int;
	ruSumIntsFromArray(ints : [int]) -> int;
	ruAvgIntFromArray(ints : [int]) -> int;

	ruMaxDoubleFromArray(doubles : [double]) -> double;
	ruMinDoubleFromArray(doubles : [double]) -> double;
	ruSumDoublesFromArray(doubles : [double]) -> double;
	ruAvgDoubleFromArray(doubles : [double]) -> double;

	ruTransposeMatrix(m : [[?]]) -> [[?]];
}

ruiLogN(n : int, N : int) -> int {
	if (n < 0) ruiLogN(-n, N) else 
	if (n < N) 1 else {
		1 + ruiLogN(n / N, N);
	}
}

ruiExpN(n : int, N : int) -> int {
	if (n < 0) ruiExpN(-n, N) else 
	if (n == 0) 1 else {
		N * ruiExpN(n - 1, N);
	}
}

rudLogN(n : double, N : int) -> int {
	if (n < 0.0) rudLogN(-n, N) else 
	if (n < i2d(N)) 1 else {
		1 + rudLogN(n / i2d(N), N);
	}
}

rudExpN(n : double, N : int) -> int {
	if (n < 0.0) rudExpN(-n, N) else 
	if (n == 0.0) 1 else {
		N * rudExpN(n - 1.0, N);
	}
}

ruSetsIntersect(s1 : Set<?>, s2 : Set<?>) -> bool {
	if (sizeSet(s2) < sizeSet(s1)) {
		ruSetsIntersect(s2, s1);
	} else {
		existsSet(s1, \v -> containsSet(s2, v));
	}
}

ruHashSetsIntersect(s1 : HashSet<?>, s2 : HashSet<?>) -> bool {
	if (sizeHashSet(s2) < sizeHashSet(s1)) {
		ruHashSetsIntersect(s2, s1);
	} else {
		existsHashSet(s1, \v -> containsHashSet(s2, v));
	}
}

ruIsSubset(s1 : Set<?>, s2 : Set<?>) -> bool {
	!existsSet(s1, \x -> !containsSet(s2, x));
}

ruExistsi(a : [?], fn : (int, ?) -> bool) -> bool {
	iteriUntil(a, fn) != length(a);
}

ruMaxIntFromArray(ints : [int]) -> int {
	fold(ints, 0, \acc, i -> max(acc, i));
}

ruMinIntFromArray(ints : [int]) -> int {
	fold(ints, 0, \acc, i -> min(acc, i));
}

ruSumIntsFromArray(ints : [int]) -> int {
	fold(ints, 0, \acc, i -> acc + i);
}

ruAvgIntFromArray(ints : [int]) -> int {
	ruSumIntsFromArray(ints) / length(ints);
}

ruMaxDoubleFromArray(doubles : [double]) -> double {
	fold(doubles, 0.0, \acc, i -> max(acc, i));
}

ruMinDoubleFromArray(doubles : [double]) -> double {
	fold(doubles, 0.0, \acc, i -> min(acc, i));
}

ruSumDoublesFromArray(doubles : [double]) -> double {
	fold(doubles, 0.0, \acc, i -> acc + i);
}

ruAvgDoubleFromArray(doubles : [double]) -> double {
	ruSumDoublesFromArray(doubles) / i2d(length(doubles));
}

ruTransposeMatrix(m : [[?]]) -> [[?]] {
	if (length(m) == 0) [] else {
		dimx = length(m);
		dimy = length(m[0]);
		map(enumFromTo(0, dimy - 1), \i ->
			map(enumFromTo(0, dimx - 1), \j -> m[j][i])
		);
	}
}
