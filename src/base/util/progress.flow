import base/util/timer;

export {
	RuProgress(
		isOn     : ref bool, // to switch off a progress tracker set this to 'false'
		counter  : ref int,  // current progress value
		total    : int,      // total value of what is progressing
		delta_ms : int,      // a timeout in ms, when a progress is reported
		timer    : RuTimer,  // the timer of  a progress
		init     : bool      // the first call to track progress
	);

	ruTrackProgress(progress : RuProgress, out : RuOut, msg : () -> string) -> void;
	ruMakeProgress(time_limit : double, total : int, delta : int) -> RuProgress;
}

ruTrackProgress(progress : RuProgress, out : RuOut, msg : () -> string) -> void {
	if (^(progress.isOn)) {
		percent = i2d(^(progress.counter)) * 100.0 / i2d(progress.total);
		m = msg();
		if (!progress.init) {
			out.onMessage(
				"  " + d2st(percent, 1) + " % (" + i2s(^(progress.counter)) + " of " + i2s(progress.total) + "), " +
				("passed: " + ruTimePassed2s(progress.timer)) + 
				(if (progress.timer.limit <= 0.0) "" else ", left: " + ruTimeLeft2s(progress.timer)) +
				(if (m == "") "" else  ", " + m)
			);
		}
		if (^(progress.counter) < progress.total) {
			timer(progress.delta_ms, \-> ruTrackProgress(RuProgress(progress with init = false), out, msg));
		}
	}
}

ruMakeProgress(time_limit : double, total : int, delta : int) -> RuProgress {
	RuProgress(ref true, ref 0, total, delta, ruMakeLimitedTimer(time_limit), true);
}
