import base/util/timer;

export {
	RuProgress(
		isOn     : ref bool,  // to switch off a progress tracker set this to 'false'
		counter  : ref int,   // current progress value
		total    : int,       // total value of what is progressing
		delta_ms : int,       // a timeout in ms, when a progress is reported
		timer    : RuTimer,   // the timer of  a progress
		init     : bool,      // the first call to track progress
		inform   : () -> void // is called report progress (each delta ms)
	);

	ruTrackProgress(progress : RuProgress) -> void;
	ruMakeProgress(time_limit : double, total : int, delta : int) -> RuProgress;
	ruProgressMessage(progress : RuProgress) -> string;

	RuInformer(
		isOn     : ref bool,  // to switch off a progress tracker set this to 'false'
		delta_ms : int,       // a timeout in ms, when a progress is reported
		timer    : RuTimer,   // the timer of  a progress
		init     : bool,      // the first call to track progress
		inform   : () -> void // is called report progress (each delta ms)
	);
	ruTrackInformer(informer : RuInformer) -> void;
	ruMakeInformer(time_limit : double, delta : int) -> RuInformer;
	ruInformerMessage(informet : RuInformer) -> string;
}

ruTrackProgress(progress : RuProgress) -> void {
	if (^(progress.isOn)) {
		if (!progress.init) {
			progress.inform();
		}
		if (^(progress.counter) < progress.total) {
			timer(progress.delta_ms, \-> ruTrackProgress(RuProgress(progress with init = false)));
		}
	}
}

ruMakeProgress(time_limit : double, total : int, delta : int) -> RuProgress {
	RuProgress(ref true, ref 0, total, delta, ruMakeLimitedTimer(time_limit), true, nop);
}

ruProgressMessage(progress : RuProgress) -> string {
	percent = i2d(^(progress.counter)) * 100.0 / i2d(progress.total);
	d2st(percent, 1) + " % (" + i2s(^(progress.counter)) + " of " + i2s(progress.total) + "), " +
	("passed: " + ruTimePassed2s(progress.timer)) + 
	(if (progress.timer.limit <= 0.0) "" else ", left: " + ruTimeLeft2s(progress.timer)) 
}

ruTrackInformer(informer : RuInformer) -> void {
	if (^(informer.isOn)) {
		if (!informer.init) {
			informer.inform();
		}
		if (!ruTimeIsOver(informer.timer)) {
			timer(informer.delta_ms, \->
				ruTrackInformer(RuInformer(informer with init = false))
			);
		}
	}
}

ruMakeInformer(time_limit : double, delta : int) -> RuInformer {
	RuInformer(ref true, delta, ruMakeLimitedTimer(time_limit), true, nop);
}

ruInformerMessage(informer : RuInformer) -> string {
	("passed: " + ruTimePassed2s(informer.timer)) + 
	(if (informer.timer.limit <= 0.0) "" else ", left: " + ruTimeLeft2s(informer.timer)) 
}
