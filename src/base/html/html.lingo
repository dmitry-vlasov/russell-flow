source = ws element*;
element =  entity | tag | comment | text;

text = (!(('<' (letter | '/' letter | "!--")) | '&' !s) any)+ $t { HtmlText($t) };
comment = "<!--" comment_text :s "-->" { HtmlComment(:s) };
	comment_text = (!"-->" any)* $s { $s };
entity  = entity_prefix:p entity_body:b (ws ';')? { HtmlEntity(:p, :b) };
	entity_prefix = ("&#x" | "&#" | '&') $p { $p };
	entity_body = (hex_digit | letter)+ $b { $b };
tag = tag_open:op id:t ws attrib*:as tag_close:cl { makeHtmlTag(:t, :as, :op, :cl) };
	tag_open = ("</" | '<') $op { $op };
	tag_close = ("/>" | '>') $cl { $cl };

attrib = attrib1 | attrib2 | attrib3 | attrib4;
attrib1 = id:k ws '=' ws '\'' attrib1_val:v '\'' ws { makeAttrib2(:k, :v) };
	attrib1_val = (!'\'' any)* $v { $v };
attrib2 = id:k ws '=' ws "\"" attrib2_val:v "\"" ws { makeAttrib2(:k, :v) };
	attrib2_val = (!"\"" any)* $v { $v };
attrib3 = id:k ws '=' ws attrib3_val:v ws { makeAttrib2(:k, :v) };
	attrib3_val = (letter | dec_digit | '_' | '-')+ $v { $v };
attrib4 = id:k ws { makeAttrib1(:k) };

dec_digit = '0'-'9';
hex_digit = dec_digit | 'A'-'F' | 'a'-'f';
letter = 'a'-'z' | 'A'-'Z';
id = (letter (letter | dec_digit)*) $i { $i };

s = ws_+;
ws = ws_*;
ws_ = ' ' | '\t' | '\n' | '\r';

any = '\u0000'-'\uFFFF';
