import sys/concurrent;
import math/uuid;
import base/env;

export {
	ruConcurrent(jobs : [(RuEnv) -> ?], env : RuEnv) -> [?];
	ruConcurrentVoid(jobs : [(RuEnv) -> void], env : RuEnv) -> void;
	ruConcurrentLimited(jobs : [(RuEnv) -> ?], limit : int, env : RuEnv) -> [?];
	ruConcurrentLimitedVoid(jobs : [(RuEnv) -> void], limit : int, env : RuEnv) -> void;
	ruSequential(jobs : [(RuEnv) -> ?], env : RuEnv) -> [?];
	ruSequentialVoid(jobs : [(RuEnv) -> void], env : RuEnv) -> void;

	ruMakeAtomicArrayPush(arr : ref [?]) -> (?) -> ?;
	ruMakeAtomicSetTree(tree : ref Tree<?, ??>) -> (?, ??) -> ??;
	ruAtomicDoubleAdd(v : ref double) -> (double) -> double;
	ruAtomicIntAdd(v : ref int) -> (int) -> int;
	ruAtomicIntInc(v : ref int) -> (int) -> void;
	ruAtomicRefArrayPush(arr : ref [?]) -> (?) -> void;

	ruSetConcurrencyEnabled(enabled : bool) -> void;
	ruGetConcurrencyEnabled() -> bool;
}

ru_concurrency_enabled = ref true;

ruSetConcurrencyEnabled(enabled : bool) -> void {
	ru_concurrency_enabled := enabled;
}

ruGetConcurrencyEnabled() -> bool {
	^ru_concurrency_enabled;
}

ruConcurrentVoid(jobs : [(RuEnv) -> void], env : RuEnv) -> void {
	ruConcurrent(map(jobs, \job -> \cf -> { job(cf); 0; }), env);
	{}
}

ruConcurrent(jobs : [(RuEnv) -> ?], env : RuEnv) -> [?] {
	if (^ru_concurrency_enabled && !containsKeyTree(env.conf.opts, "in-concurrent-thread")) {
		concurrent(true, map(jobs, \job -> \-> job(ruSetEnvOpt(env, "in-concurrent-thread", "1"))));
	} else {
		sequential(true, map(jobs, \job -> \-> job(env)));
	}
}

ruSequential(jobs : [(RuEnv) -> ?], env : RuEnv) -> [?] {
	sequential(true, map(jobs, \job -> \ -> job(env)));
}

ruSequentialVoid(jobs : [(RuEnv) -> void], env : RuEnv) -> void {
	ruSequential(map(jobs, \job -> \cf -> { job(cf); 0; }), env);
	{}
}

ruConcurrentLimited(jobs : [(RuEnv) -> ?], limit : int, env : RuEnv) -> [?] {
	concatA(
		map(
			splitByNumber(jobs, limit),
			\chunk -> ruConcurrent(chunk, env)
		)
	);
}

ruConcurrentLimitedVoid(jobs : [(RuEnv) -> void], limit : int, env : RuEnv) -> void {
	iter(
		splitByNumber(jobs, limit),
		\chunk -> ruConcurrentVoid(chunk, env)
	);
}

ruMakeAtomicArrayPush(arr : ref [?]) -> (?) -> ? {
	synchronizedUnaryFn(arr, \x -> { refArrayPush(arr, x); x });
}

ruMakeAtomicSetTree(tree : ref Tree<?, ??>) -> (?, ??) -> ?? {
	synchronizedBinaryFn(tree, \k, v -> { tree := setTree(^tree, k, v); v });
}

ruAtomicDoubleAdd(v : ref double) -> (double) -> double {
	synchronizedUnaryFn(v, \delta -> { v := ^v + delta; ^v });
}

ruAtomicIntAdd(v : ref int) -> (int) -> int {
	synchronizedUnaryFn(v, \delta -> { v := ^v + delta; ^v });
}

ruAtomicIntInc(v : ref int) -> (int) -> void {
	add = ruAtomicIntAdd(v);
	\delta -> { add(delta); {} }
}

ruAtomicRefArrayPush(arr : ref [?]) -> (?) -> void {
	push = synchronizedUnaryFn(arr, \x -> { refArrayPush(arr, x); x });
	\x -> { push(x); {}	}
}
