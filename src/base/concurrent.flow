import ds/vector;
import sys/concurrent;
import math/uuid;
import base/env;
import base/util/timed;

export {
	ruConcurrent(jobs : [(RuEnv) -> ?], env : RuEnv) -> [?];
	ruConcurrentVoid(jobs : [(RuEnv) -> void], env : RuEnv) -> void;
	ruConcurrentLimited(jobs : [(RuEnv) -> ?], limit : int, env : RuEnv) -> [?];
	ruConcurrentLimitedVoid(jobs : [(RuEnv) -> void], limit : int, env : RuEnv) -> void;
	ruSequential(jobs : [(RuEnv) -> ?], env : RuEnv) -> [?];
	ruSequentialVoid(jobs : [(RuEnv) -> void], env : RuEnv) -> void;

	ruMakeAtomicArrayPush(arr : ref [?]) -> (?) -> ?;
	ruMakeAtomicSetTree(tree : ref Tree<?, ??>) -> (?, ??) -> ??;
	ruAtomicDoubleAdd(v : ref double) -> (double) -> double;
	ruAtomicIntAdd(v : ref int) -> (int) -> int;
	ruAtomicIntInc(v : ref int) -> (int) -> void;
	ruAtomicRefArrayPush(arr : ref [?]) -> (?) -> void;
	ruAtomicVectorPush(vect : Vector<?>) -> (?) -> void;

	ruThreadPoolSize(num: int) -> void;
	ruGetConcurrencyEnabled() -> bool;

	ruDeferredCall(fn: () -> void) -> void;
/*
	RuTimeoutTask(
		fn : (RuEnv) -> ?, // the task itself
		timeout : double, // timeout for execution in milliseconds
		defval : ?        // the value, which is used, when function is interrupted by timeout
	);
	ruConcurrentTimeout(jobs : [RuTimeoutTask<?>], timeout : double, env : RuEnv) -> [?];
*/
}

ru_thread_pool: ref Maybe<native> = ref None();

ruThreadPoolSize(num : int) -> void {
	if (num > 1) {
		ru_thread_pool := Some(newThreadPool(num));
	} else {
		ru_thread_pool := None()
	}
}

ruGetConcurrencyEnabled() -> bool {
	isSome(^ru_thread_pool);
}

ruConcurrentVoid(jobs : [(RuEnv) -> void], env : RuEnv) -> void {
	ruConcurrent(map(jobs, \job -> \cf -> { job(cf); 0; }), env);
	{}
}

ruConcurrent(jobs : [(RuEnv) -> ?], env : RuEnv) -> [?] {
	switch (^ru_thread_pool) {
		Some(tp): {
			if (!containsKeyTree(env.conf.opts, "in-concurrent-thread")) {
				concurrent(tp, map(jobs, \job -> \-> job(ruSetEnvOpt(env, "in-concurrent-thread", "1"))));
			} else {
				sequential(true, map(jobs, \job -> \-> job(env)));
			}
		}
		None(): {
			sequential(true, map(jobs, \job -> \-> job(env)));
		}
	}
}

ruSequential(jobs : [(RuEnv) -> ?], env : RuEnv) -> [?] {
	sequential(true, map(jobs, \job -> \ -> job(env)));
}

ruSequentialVoid(jobs : [(RuEnv) -> void], env : RuEnv) -> void {
	ruSequential(map(jobs, \job -> \cf -> { job(cf); 0; }), env);
	{}
}

ruConcurrentLimited(jobs : [(RuEnv) -> ?], limit : int, env : RuEnv) -> [?] {
	concatA(
		map(
			splitByNumber(jobs, limit),
			\chunk -> ruConcurrent(chunk, env)
		)
	);
}

ruConcurrentLimitedVoid(jobs : [(RuEnv) -> void], limit : int, env : RuEnv) -> void {
	iter(
		splitByNumber(jobs, limit),
		\chunk -> ruConcurrentVoid(chunk, env)
	);
}

ruMakeAtomicArrayPush(arr : ref [?]) -> (?) -> ? {
	synchronizedUnaryFn(arr, \x -> { refArrayPush(arr, x); x });
}

ruMakeAtomicSetTree(tree : ref Tree<?, ??>) -> (?, ??) -> ?? {
	synchronizedBinaryFn(tree, \k, v -> { tree := setTree(^tree, k, v); v });
}

ruAtomicDoubleAdd(v : ref double) -> (double) -> double {
	synchronizedUnaryFn(v, \delta -> { v := ^v + delta; ^v });
}

ruAtomicIntAdd(v : ref int) -> (int) -> int {
	synchronizedUnaryFn(v, \delta -> { v := ^v + delta; ^v });
}

ruAtomicIntInc(v : ref int) -> (int) -> void {
	add = ruAtomicIntAdd(v);
	\delta -> { add(delta); {} }
}

ruAtomicRefArrayPush(arr : ref [?]) -> (?) -> void {
	push = synchronizedUnaryFn(arr, \x -> { refArrayPush(arr, x); x });
	\x -> { push(x); {}	}
}

ruAtomicVectorPush(vect : Vector<?>) -> (?) -> void {
	push = synchronizedUnaryFn(vect, \x -> { pushVector(vect, x); x });
	\x -> { push(x); {}	}
}

ruDeferredCall(fn: () -> void) -> void {
	switch (^ru_thread_pool) {
		Some(thread_pool): {
			concurrentAsyncCallback(thread_pool,
				\callback, force_exit -> fn(),
				\val -> { },
				\err -> { println(err); }
			);
		}
		None(): fn();
	}
}

/*
ruConcurrentTimeout(jobs : [RuTimeoutTask<?>], timeout : double, env : RuEnv) -> [?] {
	tasks = map(jobs, \job -> 
		TimeoutTask(
			\-> job.fn(ruSetEnvOpt(env, "in-concurrent-thread", "1")),
			job.timeout,
			job.defval
		)
	);
	if (^ru_concurrency_enabled && !containsKeyTree(env.conf.opts, "in-concurrent-thread")) {
		concurrentTimeout(tasks, timeout);
	} else {
		sequentialTimeout(tasks, timeout);
	}
}
*/