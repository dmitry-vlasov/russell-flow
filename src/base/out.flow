import base/log;
import base/util/str;
import base/place;

export {
	RuOut(
		println : (string) -> void,
		respond : (Json) -> void,
		notify : (string, Json) -> void,
		error : (string, [RuPlace]) -> void,
		trace : (string, () -> string) -> void
	);

	ruDefaultOut = RuOut(println, nop1, nop2, ruMakeOnError(println, makeTree()), nop2);
	ruSilentOut = RuOut(nop1, nop1, nop2, ruMakeOnError(nop1, makeTree()), nop2);

	ruAddErr2Out(out : RuOut, msg : string) -> RuOut;
	ruAddErrCounter2Out(out : RuOut, err_count : ref int) -> RuOut;

	ruMakeOnError(println : (string) -> void, opts : Tree<string, Json>) -> (string, [RuPlace]) -> void;
	ruMakeOut(println : (string) -> void, respond : (Json) -> void, notify : (string, Json) -> void, opts : Tree<string, Json>) -> RuOut;

	ruIndentOut(out : RuOut) -> RuOut;
	ruUnindentOut(out : RuOut) -> RuOut;
}

ruAddErr2Out(out : RuOut, msg : string) -> RuOut {
	RuOut(out with error = \str, pos -> out.error(str + msg, pos))
}

ruAddErrCounter2Out(out : RuOut, err_count : ref int) -> RuOut {
	RuOut(out with error = 
		\err, places -> {
			err_count := ^err_count + 1;
			out.error(err, places);
		}
	)
}

ruMakeOnError(println : (string) -> void, opts : Tree<string, Json>) -> (string, [RuPlace]) -> void {
	\err, places -> {
		println("Error: " + err + (if (places == []) "" else "\n" + ruPlaces2s(places)));
		if (ruOptsString(opts, "halt-on-errors", "") == "1") {
			ruCrash("Aborting russell run, because halt-on-errors=1");
		}
	}
}

ruMakeOut(println : (string) -> void, respond : (Json) -> void, notify : (string, Json) -> void, opts : Tree<string, Json>,) -> RuOut {
	RuOut(println, respond, notify, ruMakeOnError(println, opts), nop2);
}

ruIndentOut(out : RuOut) -> RuOut {
	RuOut(out with println = \s -> out.println(strIndent(s)));
}

ruUnindentOut(out : RuOut) -> RuOut {
	RuOut(out with println = \s -> out.println(ruUnindent(s)));
}
