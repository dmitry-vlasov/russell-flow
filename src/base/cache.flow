import base/dir;
import base/conf;
import base/out;

export {
	ruCacheDir(conf : RuConf, mode : string) -> string;
	ruCachedPath(file : string, mode : string, conf : RuConf) -> string;
	ruCachedFile(path : string, mode : string, conf : RuConf) -> string;
	ruEnsureFileDirectoryExists(file : string, out : RuOut) -> void;
	ruEnsureDirectoryExists(dir : string, out : RuOut) -> void;
}

ruCacheDir(conf : RuConf, mode : string) -> string {
	russell_dir = rtrim2(getRussellDir(), "/");
	base_cache = ruOptsString(conf.opts, "cache-dir", russell_dir + "/cache");
	dir = if (mode == "") base_cache else {
		rtrim2(base_cache, "/") + "/" + mode;
	}
	rtrim2(dir, "/");
}

ru_cache_dir_delimiter = "_DIIIR_";
ru_cache_colon_delimiter = "_CCOOLLOONN_";

ruCachedPath(file : string, mode : string, conf : RuConf) -> string {
	ruCacheDir(conf, mode) + "/" + 
	strReplace(strReplace(file, ":", ru_cache_colon_delimiter), "/", ru_cache_dir_delimiter) + 
	".cache";
}

ruCachedFile(path : string, mode : string, conf : RuConf) -> string {
	cache_dir = ruCacheDir(conf, mode);
	if (!startsWith(path, cache_dir)) "" else {
		changeFileExt(
			strReplace(
				strReplace(
					strRight(path, strlen(cache_dir) + 1),
					ru_cache_colon_delimiter, ":"
				),
				ru_cache_dir_delimiter, "/"
			),
			""
		);
	}
}

ruEnsureFileDirectoryExists(file : string, out : RuOut) -> void {
	dir = ruDirName(file);
	err = ensureDirectoryExists(dir);
	if (err != "") {
		out.error("cannot create directory '" + dir + "' for the file: '" + file + "':\n\t" + err, []);
	}
}

ruEnsureDirectoryExists(dir : string, out : RuOut) -> void {
	err = ensureDirectoryExists(dir);
	if (err != "") {
		out.error("cannot create directory: '" + dir + "':\n\t" + err, []);
	}
}
