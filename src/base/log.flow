import base/conf;

export {
	ruCustomLog(message : string, name : string, conf : RuConf) -> void;
	ruServerLog(message : string, conf : RuConf) -> void;
	ruDebugLog(message : string, conf : RuConf) -> void;
	ruScriptLog(message : string, conf : RuConf) -> void;

	ruCrash(message : string) -> void;
}

ruServerLog(message : string, conf : RuConf) -> void {
	ruCustomLog(message, lookupTreeDef(conf.opts, "server", "no-server"), conf);
}

ruDebugLog(message : string, conf : RuConf) -> void {
	ruCustomLog(message, "debug", conf);
}

ruScriptLog(message : string, conf : RuConf) -> void {
	ruCustomLog(message, "script", conf);
}

ruCrashLog(message : string) -> void {
	ruCustomLog(message, "crash", ruEmptyConf);
}

ruCrash(message : string) -> void {
	ruCrashLog(message);
	println(message);
	println([][0]);
	quit(1);
}

ruCustomLog(message : string, name : string, conf : RuConf) -> void {
	log_file = lookupTreeDef(conf.opts, name + "-log", getRussellDir() + "/log/russell_" + name + ".log");
	log_dir = ruDirName(log_file);
	if (log_dir != "") {
		err = ensureDirectoryExists(log_dir);
		if (err != "") {
			println(err);
		}
	}
	if (log_file != "") {
		log_size = s2i(lookupTreeDef(conf.opts, name + "log-size", "256"));
		ruWriteLogMessage(message, log_file, log_size);
	}
}

ruWriteLogMessage(message : string, log_name : string, log_size : int) -> void {
	message_with_limestamp = "[" + time2string(timestamp()) + "] " + message;
	log_file = if (fileExists(log_name)) getFileContent(log_name) else "";
	log_lines = strSplit(log_file, "\n");
	log_lines_cut = if (length(log_lines) > log_size) tailFrom(log_lines, log_size / 8) else log_lines;
	updated_log_lines = concat(log_lines_cut, [message_with_limestamp]);
	updated_log_file = strGlue(updated_log_lines, "\n");
	setFileContent(log_name, updated_log_file);
	{}
}