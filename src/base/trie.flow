import ds/tree;
import string;

export {
	RuTrie(
		data : Maybe<??>,
		map : Tree<?, RuTrie<?, ??>>,
	);
	ruTrieEmpty = RuTrie(None(), makeTree());

	ruAdd2Trie(key : [?], data : Maybe<??>, trie : RuTrie<?, ??>, conflict : (??, ??) -> ??) -> RuTrie<?, ??>;
	ruSetTrie(trie : RuTrie<?, ??>, key : [?], data : Maybe<??>) -> RuTrie<?, ??>;
	ruRemoveFromTrie(key : [?], trie : RuTrie<?, ??>) -> RuTrie<?, ??>;
	ruTrie2s(t : RuTrie<?, ??>, show_key : (?) -> string, show_val : (??) -> string) -> string;
	ruMergeRuTrie(t1 : RuTrie<?, ??>, t2 : RuTrie<?, ??>, conflict : (??, ??) -> ??) -> RuTrie<?, ??>;
	ruLookupTrie(trie : RuTrie<?, ??>, key : [?]) -> Maybe<??>;
	ruLookupTrieDef(trie : RuTrie<?, ??>, key : [?], def : ??) -> ??;
	ruContainsTrie(trie : RuTrie<?, ??>, key : [?]) -> bool;
	ruSizeTrie(trie : RuTrie<?, ??>) -> int;
}

ruAdd2Trie(key : [?], data : Maybe<??>, trie : RuTrie<?, ??>, conflict : (??, ??) -> ??) -> RuTrie<?, ??> {
	if (length(key) == 0) trie else doRuAdd2Trie(0, key, data, trie, conflict)
}

doRuAdd2Trie(i : int, key : [?], data : Maybe<??>, trie : RuTrie<?, ??>, conflict : (??, ??) -> ??) -> RuTrie<?, ??> {
	n = lookupTreeDef(trie.map, key[i], ruTrieEmpty);
	if (i + 1 == length(key)) {
		RuTrie(trie.data, setTree(trie.map, key[i], RuTrie(ruInsertData2Trie(n.data, data, conflict), n.map)));
	} else {
		RuTrie(trie.data, setTree(trie.map, key[i], doRuAdd2Trie(i + 1, key, data, n, conflict)));
	}
}

ruInsertData2Trie(md1 : Maybe<??>, md2 : Maybe<??>, conflict : (??, ??) -> ??) -> Maybe<??> {
	eitherMap(md1, \d1 -> eitherMap(md2, \d2 -> Some(conflict(d1, d2)), md1), md2)
}

ruSetTrie(trie : RuTrie<?, ??>, key : [?], data : Maybe<??>) -> RuTrie<?, ??> {
	ruAdd2Trie(key, data, trie, \old, new -> new);
}

ruRemoveFromTrie(key : [?], trie : RuTrie<?, ??>) -> RuTrie<?, ??> {
	if (length(key) == 0) trie else doRuRemoveFromTrie(0, key, trie)
}

doRuRemoveFromTrie(i : int, key : [?], trie : RuTrie<?, ??>) -> RuTrie<?, ??> {
	n = lookupTreeDef(trie.map, key[i], ruTrieEmpty);
	if (i + 1 == length(key)) {
		RuTrie(trie.data, setTree(trie.map, key[i], RuTrie(None(), n.map)));
	} else {
		RuTrie(trie.data, setTree(trie.map, key[i], doRuRemoveFromTrie(i + 1, key, n)));
	}
}

ruMergeRuTrie(t1 : RuTrie<?, ??>, t2 : RuTrie<?, ??>, conflict : (??, ??) -> ??) -> RuTrie<?, ??> {
	RuTrie(
		ruInsertData2Trie(t1.data, t2.data, conflict),
		mergeTreeCustom(t1.map, t2.map, \__, s1, s2 -> ruMergeRuTrie(s1, s2, conflict))
	)
}

ruTrie2s(t : RuTrie<?, ??>, show_key : (?) -> string, show_val : (??) -> string) -> string {
	foldTree(doRuTrie2tree(t, [], makeTree(), show_key, show_val), "",
		\path, val, acc ->
			acc + strGlue(path, " ") + " --> " + val + "\n"
	)
}

doRuTrie2tree(trie : RuTrie<?, ??>, path : [string], acc : Tree<[string], string>, show_key : (?) -> string, show_val : (??) -> string) -> Tree<[string], string> {
	foldTree(trie.map, acc, 
		\k, v, ac -> {
			path1 = concat(path, [show_key(k)]);
			ac1 = doRuTrie2tree(v, path1, ac, show_key, show_val);
			switch (v.data) {
				None(): ac1;
				Some(d): setTree(ac1, path1, show_val(d));
			}
		}
	)
}

ruLookupTrie(trie : RuTrie<?, ??>, key : [?]) -> Maybe<??> {
	ruDoLookupTrie(trie, key, 0);
}

ruDoLookupTrie(trie : RuTrie<?, ??>, key : [?], i : int) -> Maybe<??> {
	if (i == length(key)) trie.data else {
		maybeBind(lookupTree(trie.map, key[i]),
			\tr -> ruDoLookupTrie(tr, key, i + 1)
		);
	}
}

ruContainsTrie(trie : RuTrie<?, ??>, key : [?]) -> bool {
	isSome(ruLookupTrie(trie, key));
}

ruLookupTrieDef(trie : RuTrie<?, ??>, key : [?], def : ??) -> ?? {
	either(ruLookupTrie(trie, key), def);
}

ruSizeTrie(trie : RuTrie<?, ??>) -> int {
	foldTree(trie.map, 1, \__, tr, acc -> acc + ruSizeTrie(tr));
}
