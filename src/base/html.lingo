source = ws element*;
element =  entity | tag | comment | text;
text = (!(('<' (letter | '/' letter | "!--")) | '&' !s) any)+ $t { HtmlText($t) };
comment = ("<!--" (!"-->" any)* $s "-->") { HtmlComment($s) };

entity = dec_entity | hex_entity | code_entity;
dec_entity  = "&#"  dec_digit+ $c (ws ';')? { HtmlDecEntity($c) };
hex_entity  = "&#x" hex_digit+ $c (ws ';')? { HtmlHexEntity($c) };
code_entity = '&'   letter+    $c (ws ';')? { HtmlCodeEntity($c) };

tag = tag_clopen | tag_open | tag_close;

tag_clopen = '<'  ws id$tag ws attrib*:as "/>" { makeHtmlClopenTag($tag, :as) };
tag_open   = '<'  ws id$tag ws attrib*:as ">"  { makeHtmlOpenTag($tag, :as) };
tag_close  = "</" ws id$tag ws            ">"  { makeHtmlCloseTag($tag, emptyArray()) };

attrib = attrib1 | attrib2 | attrib3 | attrib4;
attrib1 = id$k ws '=' ws '\'' (!'\'' any)* $v '\'' ws { makeAttrib2($k, $v) };
attrib2 = id$k ws '=' ws "\"" (!"\"" any)* $v "\"" ws { makeAttrib2($k, $v) };
attrib3 = id$k ws '=' ws val$v ws { makeAttrib2($k, $v) };
attrib4 = id$k ws { makeAttrib1($k) };

dec_digit = '0'-'9';
hex_digit = dec_digit | 'A'-'F' | 'a'-'f';
letter = 'a'-'z' | 'A'-'Z';
id = letter (letter | dec_digit)*;
val = (letter | dec_digit | '_' | '-')+;

s = ws_+;
ws = ws_*;
ws_ = ' ' | '\t' | '\n' | '\r';

any = '\u0000'-'\uFFFF';
