import lsp/util;
import lsp/method;

export {
	ruLspWorkspaceMethods : Tree<string, RuLspMethod>;
}

ruLspWorkspaceMethods = pairs2tree([
	Pair("workspace/symbol", RuLspMethod(ruLspWorkspaceSymbol, ruLspKeepState, true, nop2, true)),
	Pair("workspace/executeCommand", RuLspMethod(ruLspWorkspaceExecuteCommand, ruLspKeepState, true, nop2, false)),
]);

ruLspWorkspaceSymbol(json : Json, env : RuEnv) -> ScComm {
	switch (json2LspWorkspaceSymbolParams(json)) {
		Some(symbol): {
			ScTask("info-symbol", [ScTaskArg("name", ScString(symbol.query))]);
		}
		None(): {
			env.out.onError("text position is invalid: " + json2string(json), []);
			ScAction("stop", []);
		}
	}
}

ruLspWorkspaceExecuteVerify(json : Json, env : RuEnv, commandParams: LspExecuteCommandParams) -> ScComm {
	if (length(commandParams.arguments) == 0) {
		env.out.onError("no file for verification is provided", []);
		ScAction("stop", []);
	} else {
		params = map(commandParams.arguments, \param -> getJsonStringValue(param, ""));
		switch (find(params, \param -> startsWith(param, "file="))) {
			Some(param): {
				file = substring(param, 5, strlen(param) - 5);
				conf = ruLspFileConfig(env.conf, file);
				target = if (length(params) < 2) file else params[1];
				ScCommSeq([
					ScTask("conf-set", ruTree2ScTaskArgs(ruConf2Tree(RuConf(conf with verbose=1)))),
					ScTask("read", [ScTaskArg("file", ScString(file)), ScTaskArg("proofless", ScString("1"))]),
					ScTask("verify", [ScTaskArg("target", ScString(target))])
				]);
			}
			None(): {
				env.out.onError("verify command demands a file=... parameter", []);
				ScAction("stop", []);
			}
		}
	}
}

ruLspWorkspaceExecuteFindSymbol(json : Json, env : RuEnv, commandParams: LspExecuteCommandParams) -> ScComm {
	if (length(commandParams.arguments) == 1) {
		name = getJsonStringValue(commandParams.arguments[0], "");
		if (name != "") {
			ScTask("info-find", [ScTaskArg("name", ScString(name))]);
		} else {
			env.out.onError("find-symbol first parameter must be a non-empty string", []);
			ScAction("stop", []);
		}
	} else {
		env.out.onError("find-symbol takes a parameter: a symbol name. Passed " + i2s(length(commandParams.arguments)) + " args.", []);
		ScAction("stop", []);
	}
}

ruLspWorkspaceExecuteMathInfo(json : Json, env : RuEnv, commandParams: LspExecuteCommandParams) -> ScComm {
	kind = if (length(commandParams.arguments) > 0) getJsonStringValue(commandParams.arguments[0], "") else "";
	ScTask("info-math", [ScTaskArg("kind", ScString(kind))]);
}

ruLspWorkspaceExecuteMathOntology(json : Json, env : RuEnv, commandParams: LspExecuteCommandParams) -> ScComm {
	ScTask("info-ontology", []);
}

ruLspWorkspaceExecuteGeneralize(json : Json, env : RuEnv, commandParams: LspExecuteCommandParams) -> ScComm {
	kind = if (length(commandParams.arguments) > 0) getJsonStringValue(commandParams.arguments[0], "") else "";
	ScTask("generalize", [ScTaskArg("kind", ScString(kind))]);
}

ruLspWorkspaceExecuteACommand(json : Json, env : RuEnv, commandParams: LspExecuteCommandParams) -> ScComm {
	if (length(commandParams.arguments) > 0) {
		script_src = getJsonStringValue(commandParams.arguments[0], "");
		switch (ruParseScComm(script_src)) {
			Some(src): {
				src.comm;
			}
			None(): {
				ruLspServerLog("Syntax error in script: \n" + script_src + "\n", env.conf);
				ScAction("stop", []);
			}
		}
	} else {
		env.out.onError("command takes at least one parameter: the command name itself. Passed " + i2s(length(commandParams.arguments)) + " args.", []);
		ScAction("stop", []);
	}
}

ruLspWorkspaceExecuteCommand(json : Json, env : RuEnv) -> ScComm {
	switch (json2LspExecuteCommandParams(json)) {
		Some(commandParams): {
			if (commandParams.command == "verify") {
				ruLspWorkspaceExecuteVerify(json, env, commandParams);
			} else if (commandParams.command == "find-symbol") {
				ruLspWorkspaceExecuteFindSymbol(json, env, commandParams);
			} else if (commandParams.command == "math-info") {
				ruLspWorkspaceExecuteMathInfo(json, env, commandParams);
			} else if (commandParams.command == "math-ontology") {
				ruLspWorkspaceExecuteMathOntology(json, env, commandParams);
			//} else if (commandParams.command == "generalize") {
			//	ruLspWorkspaceExecuteGeneralize(call, commandParams);
			} else if (commandParams.command == "command") {
				ruLspWorkspaceExecuteACommand(json, env, commandParams);
			} else {
				env.out.onError("command '" + commandParams.command + "' is unknown", []);
				ScAction("stop", []);
			}
		}
		None(): {
			env.out.onError("ExecuteCommand request is illegal", []);
			ScAction("stop", []);
		}
	}
}
