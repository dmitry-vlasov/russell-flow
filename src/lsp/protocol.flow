import formats/lsp;
import lsp/io;
import base/env;
import script/script;

export {
	RuLspMessage(
		raw : string,                  // the whole message raw string
		header : Tree<string, string>, // All key-value pairs from a header of a message
		message : LspMessage           // Content of a message
	);

	ruLspReadMessage(env : RuEnv) -> Maybe<RuLspMessage>;

	ruLspWriteNotification(method : string, params : Json) -> void;
	ruLspWriteResponse(id : Json, result : Json) -> void;
	ruLspWriteRequest(id : Json, method : string, params : Json) -> void;
	ruLspWriteError(id : Json, code : int, message : string, data : Json) -> void;

	LspMethod(
		request2script : (Json, RuEnv) -> ScComm,
		responses : bool,
		callback : () -> void
	);
}

ruLspWriteNotification(method : string, params : Json) -> void {
	notification = LspNotificationMessage("2.0", method, params);
	ruLspWriteJson(lspNotificationMessage2Json(notification));
}

ruLspWriteResponse(id : Json, result : Json) -> void {
	response = LspResponseMessage("2.0", id, result, None());
	ruLspWriteJson(lspResponseMessage2Json(response));
}

ruLspWriteRequest(id : Json, method : string, params : Json) -> void {
	request = LspRequestMessage("2.0", id, method, params);
	ruLspWriteJson(lspRequestMessage2Json(request));
}

ruLspWriteError(id : Json, code : int, message : string, data : Json) -> void {
	error = LspResponseMessage("2.0", id, JsonNull(), 
		Some(LspResponseError(code, message, data))
	);
	ruLspWriteJson(lspResponseMessage2Json(error));
}

ruLspReadMessage(env : RuEnv) -> Maybe<RuLspMessage> {
	switch (ruLspReadRawMessage()) {
		Some(req): {
			json = parseJsonSafe(req.json);
			switch (json2LspRequestMessage(json)) {
				Some(request): {
					Some(RuLspMessage(req.raw, req.header, request));
				}
				None(): {
					switch (json2LspResponseMessage(json)) {
						Some(response): {
							Some(RuLspMessage(req.raw, req.header, response));
						}
						None(): {
							switch (json2LspNotificationMessage(json)) {
								Some(notification): {
									Some(RuLspMessage(req.raw, req.header, notification));
								}
								None(): {
									ruLspServerLog("Illegal message: \n" + req.json, env.conf);
									ruLspWriteError(JsonNull(), 1, "Illegal message: \n" + req.json, JsonNull());
									None();
								}
							}
						}
					}
				}
			}
		}
		None(): {
			ruLspServerLog("Failed to read a request.", env.conf);
			ruLspWriteError(JsonNull(), 1, "Failed to read a request.", JsonNull());
			None();
		}
	}
}