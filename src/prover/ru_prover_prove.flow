import prover/ru_prover_expand;

export {
	ruInitProver(theorem : RuTheorem, math : RuMath, asses : Maybe<RuIndex<RuAssertion>>) -> RuProverEnv;
	ruInitGeneralizer(theorem : RuTheorem, math : RuMath) -> RuProverEnv;
	
	ruProve(env : RuProverEnv, tactic : (RuProverTree) -> Maybe<RuProverProp>) -> RuProverEnv;
}

ruInitProver(th : RuTheorem, math : RuMath, asses : Maybe<RuIndex<RuAssertion>>) -> RuProverEnv {
	env = ruInitProverEnv(th, math, asses);
	ruExpandHyp(env.tree.root, env);
}

ruInitGeneralizer(th : RuTheorem, math : RuMath) -> RuProverEnv {
	env = ruInitGeneralizerEnv(th, math);
	ruExpandHyp(env.tree.root, env);
}

ruProve(env : RuProverEnv, tactic : (RuProverTree) -> Maybe<RuProverProp>) -> RuProverEnv {
	t0 = timestamp();
	choice = tactic(env.tree);
	if (ruOptIsTrue("reprove-stats", env.math.conf)) {
		ruAddStats("choice", RuStatsRow("choice ", 
			pairs2tree([
				Pair("size", sizeTree(env.tree.leafs)), 
				Pair("time", cast(timestamp() - t0 : double -> int)), 
				Pair("count", 1)
			])
		));
	}
	switch (choice) {
		Some(prop): {
			t1 = timestamp();
			env1 = ruExpandProp(prop, env);
			if (ruOptIsTrue("reprove-stats", env.math.conf)) {
				ruAddStats("ruExpandProp", RuStatsRow("expanding " + prop.assertion.name, 
					pairs2tree([
						Pair("time", cast(timestamp() - t1 : double -> int)), 
						Pair("count", 1)
					])
				));
			}
			t2 = timestamp();
			ruBuildPropProofs(prop.parent, ^(prop.proofs), env1);
			if (ruOptIsTrue("reprove-stats", env.math.conf)) {
				ruAddStats("ruBuildPropProofs", RuStatsRow("building proofs for " + prop.assertion.name, 
					pairs2tree([
						Pair("time", cast(timestamp() - t2 : double -> int)), 
						Pair("count", 1)
					])
				));
			}
			ruProve(env1, tactic);
		}
		None(): env;
	}
}
