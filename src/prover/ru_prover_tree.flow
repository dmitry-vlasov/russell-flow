import ru_exp;

export {

	RuProofNode ::= RuProofStep, RuProofHyp;
	RuProofStep(assertion : RuAssertion, children : [RuProofNode], expr : RuExp);
	RuProofHyp(hyp : RuHyp, expr : RuExp);
	ruMakeLinearProof(proof : RuProverProof) -> [RuStep];
	ruMakeTreeProof(theorem : RuTheorem, math : RuMath) -> RuProofStep;

	RuProverProof ::= RuProverProofHyp, RuProverProofProp;
	RuProverProofHyp ::= RuProverProofTop, RuProverProofExp;
		RuProverProofTop(
			node : RuProverHyp,
			expr  : RuExp,
			hyp  : RuHyp, 
			sub  : Tree<RuVar, RuExp>,
		);
		RuProverProofExp(
			node : RuProverHyp,
			expr  : RuExp, 
			sub   : Tree<RuVar, RuExp>,
			child : RuProverProofProp,
		);
		RuProverProofProp(
			node      : RuProverProp,
			sub       : Tree<RuVar, RuExp>,
			children  : [RuProverProofHyp],
		);

	RuProverNode ::= RuProverHyp, RuProverExp;
	RuProverHyp ::= RuProverExp, RuProverRoot;
		RuProverProp(
			assertion : RuAssertion,
			proofs    : ref [RuProverProofProp],
			sub       : Tree<RuVar, RuExp>,
			outer     : Tree<RuVar, RuExp>,
			children  : ref [RuProverExp],
			parent    : RuProverHyp,
		);
		RuProverExp(
			proofs   : ref [RuProverProofHyp],
			expr     : RuExp,
			children : ref [RuProverProp],
			parent   : RuProverProp,
		);
		RuProverRoot(
			proofs   : ref [RuProverProofHyp],
			expr     : RuExp,
			children : ref [RuProverProp]
		);

	RuProverTree(
		leafs : [RuProverProp],
		root  : RuProverRoot
	);
}

ruApplySubst2ProofNode(s : Tree<RuVar, RuExp>, n : RuProofNode) {
	switch (n) {
		RuProofStep(ass, children, ex):
			RuProofStep(
				ass, 
				map(children, \ch -> ruApplySubst2ProofNode(s, ch)), 
				ruApplySubst(ex, s)
			);
		RuProofHyp(h, ex): 
			RuProofHyp(h, ruApplySubst(ex, s));
	}
}

ruMakeProofNode(proof : RuProverProof) -> RuProofNode {
	switch (proof) {
		RuProverProofTop(node, expr, hyp, sub): RuProofHyp(hyp, expr);
		RuProverProofExp(node, expr, sub, child): ruMakeProofNode(child);
		RuProverProofProp(node, sub, children):
			ruApplySubst2ProofNode(sub, 
				RuProofStep(
					node.assertion, 
					map(children, ruMakeProofNode),
					node.parent.expr
				)
			);
	}
}

RuLienarProofAcc(
	steps : Tree<RuExp, int>,
	linear : [RuStep]
);

ruMakeLinearProof(proof : RuProverProof) -> [RuStep] {
	ruDoMakeLinearProof(ruMakeProofNode(proof), RuLienarProofAcc(makeTree(), [])).linear;
}

ruDoMakeLinearProof(proof : RuProofNode, acc : RuLienarProofAcc) -> RuLienarProofAcc {
	switch (proof) {
		RuProofHyp(hyp, expr): acc;
		RuProofStep(ass, children, expr): {
			acc1 = fold(children, acc, \ac, ch -> ruDoMakeLinearProof(ch, ac));
			switch (lookupTree(acc1.steps, expr)) {
				Some(__): acc1;
				None(): {
					ind = length(acc1.linear);
					refs = map(children, \ch -> 
						switch (ch) {
							RuProofHyp(h, __): RuHypRef(h.ind, -1);
							RuProofStep(__, __, ex): RuStepRef(lookupTreeDef(acc1.steps, ex, -1), -1);
						}
					);
					RuLienarProofAcc(
						setTree(acc1.steps, expr, ind),
						concat(acc1.linear, [RuStep(ind, ass.name, refs, expr, -1)])
					);
				}
			}
		}
	}
}

ruMakeTreeProof(theorem : RuTheorem, math : RuMath) -> RuProofStep {
	ruDoMakeTreeProof(theorem.proof.steps, theorem.hyps, math, length(theorem.proof.steps) - 1)
}

ruDoMakeTreeProof(steps : [RuStep], hyps : [RuHyp], math : RuMath, i : int) -> RuProofStep {
	step = steps[i];
	RuProofStep(
		lookupTreeDef(math.assertions, step.assertion, ruNoAxiom),
		map(step.refs, \ref -> 
			switch (ref) {
				RuStepRef(j, __): ruDoMakeTreeProof(steps, hyps, math, j);
				RuHypRef(j, __): RuProofHyp(hyps[j], hyps[j].expr);
			}
		),
		step.expr
	)
}
