import prover/ru_prover_tree;
import ru_index;

export {

	RuProverEnv(
		tree : RuProverTree,
		assertions : RuIndex<RuAssertion>,
		premises : RuIndex<RuHyp>,
		math : RuMath,
		fresher : (RuVar) -> RuVar
	);

	ruInitProver(theorem : RuTheorem, math : RuMath) -> RuProverEnv;
	ruExpandProp(prop : RuProverProp, env : RuProverEnv) -> RuProverEnv;
	ruProve(env : RuProverEnv, tactic : (RuProverTree) -> Maybe<RuProverProp>) -> RuProverEnv;
}

ruInitProver(th : RuTheorem, math : RuMath) -> RuProverEnv {
	th_vars = set2array(ruAssertion2Vars(th));
	cv_vars = filter(th_vars, \v -> !containsSet(th.meta, v.name));
	cv_name = \cv -> "__cv_" + cv.name;
	cv_rules = map(cv_vars, \cv -> RuRule(cv_name(cv), makeTree(), RuTerm(cv.type, [RuConstRef(cv.name)], -1), -1));
	cv_map = keys2tree(cv_vars, \cv -> RuRuleRef(cv_name(cv), cv.type, 0, 0));
	cv_exp = \e -> RuExp(map(e.nodes, \n -> lookupTreeDef(cv_map, n, n)), e.pos);
	th_prop = cv_exp(th.prop);
	th_hyps = map(th.hyps, \h -> RuHyp(h.ind, cv_exp(h.expr), h.pos));
	premises = fold(th_hyps, ruMakeIndex(), \acc, h -> ruSetIndex(h.expr, Some(h), acc, \h1, h2 -> h1));

	th_src = lookupTreeDef(math.names, th.name, ruNoSource);
	all_imported = removeSet(ruCollectAllImported(th.name, math), th_src);
	assertions = foldTree(math.assertions, ruMakeIndex(), \name, ass, acc -> {
			ass_src = lookupTreeDef(math.names, name, ruNoSource);
			if (containsSet(all_imported, ass_src)) {
				ruSetIndex(ass.prop, Some(ass), acc, \a1, a2 -> a1);
			} else if (ass_src == th_src) {
				if (ruIndexOfDecl(name, ass_src) < ruIndexOfDecl(th.name, th_src)) {
					ruSetIndex(ass.prop, Some(ass), acc, \a1, a2 -> a1);
				} else {
					acc
				}
			} else {
				acc
			}
		}
	);
	vars = ref makeTree();
	root = RuProverRoot(ref [], th_prop, ref []);
	env = RuProverEnv(
		RuProverTree(makeSet(), root),
		assertions,
		premises,
		math,
		\v -> {
			vars := setTree(^vars, v, lookupTreeDef(^vars, v, 0) + 1);
			RuVar(v.name + "_" + i2s(lookupTreeDef(^vars, v, 0)), v.type);
		}
	);
	ruExpandHyp(root, env);
}

ruExpandHyp(hyp : RuProverHyp, env : RuProverEnv) -> RuProverEnv {
	make_fresh = \vars, fresher -> foldSet(vars, makeTree(), \acc, v -> setTree(acc, v, RuExp([fresher(v)], -1)));
	unified = ruUnifyIndex(hyp.expr, env.assertions, env.math);
	iter(unified, \unif -> {
			a = unif.data;
			a_vars  = ruAssertion2Vars(a);
			fresher = make_fresh(differenceSets(a_vars, buildSet(getTreeKeys(unif.sub))), env.fresher);
			maybeApply(ruCompose(unif.sub, fresher), \s_all -> {
				sub = filterTree(s_all, \v, __ -> !containsSet(a_vars, v));
				refArrayPush(
					hyp.children,
					RuProverProp(a, ref [], 
						filterTree(s_all, \v, __ -> !containsSet(a_vars, v)),
						filterTree(s_all, \v, __ -> containsSet(a_vars, v)),
						ref [],
						hyp
					)
				);
			})
		}
	);
	leafs = switch (hyp) {
		RuProverExp(__,__,__, parent): removeSet(env.tree.leafs, parent);
		RuProverRoot(__,__,__): makeSet();
	}
	RuProverEnv(env with tree = RuProverTree(
		fold(^(hyp.children), leafs, \acc, p -> insertSet(acc, p)),
		env.tree.root
	));
}

ruExpandProp(prop : RuProverProp, env : RuProverEnv) -> RuProverEnv {
	if (length(prop.assertion.hyps) == 0) {
		refArrayPush(prop.proofs, RuProverProofProp(prop, prop.sub, []));
		env
	} else {
		fold(prop.assertion.hyps, env, \acc, h -> {
			ex = ruApplySubst(ruApplySubst(h.expr, prop.sub), prop.outer);
			hyp = RuProverExp(ref [], ex, ref [], prop);
			unified = ruUnifyIndex(ex, env.premises, env.math);
			iter(unified, \unif -> {
				hypProof = RuProverProofTop(hyp, unif.data, unif.sub);
				refArrayPush(hyp.proofs, hypProof);
				ruAddProof(prop, hypProof, env);
			});
			refArrayPush(prop.children, hyp);
			ruExpandHyp(hyp, acc);
		});
	}
}

ruNextCart(v : [int], prod : [[?]]) -> Maybe<[int]> {
	keep = ref false;
	next = mapi(v, \i, n -> {
		if (^keep) n else 
		if (n + 1 < length(prod[i])) {
			keep := true;
			n + 1;
		} else 0
	});
	if (^keep) Some(next) else None();
}

ruCardCart(prod : [[?]]) -> int {
	fold (prod, 1, \acc, p -> acc * length(p));
}

ruAddProof(prop : RuProverProp, hypProof : RuProverProofHyp, env : RuProverEnv) -> void {
	prod = map(^(prop.children), \hyp ->
		if (hyp == hypProof.node) [hypProof] else ^(hyp.proofs)
	);
	if (ruCardCart(prod) > 0) {
		ruAddProofVariant(prop, map(prod, \__ -> 0), prod, env);
	}
}

ruAddProofVariant(prop : RuProverProp, v : [int], prod : [[RuProverProofHyp]], env : RuProverEnv) -> void {
	hypProofs = mapi(v, \i, n -> prod[i][n]);
	switch (ruUnifySubstVect(map(hypProofs, \hp -> hp.sub), env.math)) {
		Some(s):
			maybeApply(ruCompose(prop.sub, s), \theta -> 
				if (ruCheckDisjs(theta, prop.assertion.disjs)) {
					refArrayPush(prop.proofs, RuProverProofProp(prop, theta, hypProofs))
				}
			);
		None(): { }
	}
	switch(ruNextCart(v, prod)) {
		Some(n): ruAddProofVariant(prop, n, prod, env);
		None(): { }
	}
}

ruBuildPropProofs(parent : RuProverHyp, proofs : [RuProverProofProp], env : RuProverEnv, ) -> void {
	n = length(^(parent.proofs));
	iter(proofs, \proof -> 
		refArrayPush(parent.proofs, 
			RuProverProofExp(
				parent,
				ruApplySubst(parent.expr, proof.sub),
				proof.sub,
				proof
			)
		)
	);
	switch (parent) {
		RuProverExp(__,__,__,grand): ruBuildHypProofs(grand, tailFrom(^(parent.proofs), n), env);
		RuProverRoot(__,__,__): { }
	}
}

ruBuildHypProofs(parent : RuProverProp, proofs : [RuProverProofHyp], env : RuProverEnv) -> void {
	n = length(^(parent.proofs));
	iter(proofs, \proof -> ruAddProof(parent, proof, env));
	ruBuildPropProofs(parent.parent, tailFrom(^(parent.proofs), n), env);
}

ruProve(env : RuProverEnv, tactic : (RuProverTree) -> Maybe<RuProverProp>) -> RuProverEnv {
	switch (tactic(env.tree)) {
		Some(prop): {
			env1 = ruExpandProp(prop, env);
			ruBuildPropProofs(prop.parent, ^(prop.proofs), env1);
			ruProve(env1, tactic);
		}
		None(): env;
	}
}
