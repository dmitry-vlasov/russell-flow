import math/uuid;
import text/utf8;
import formats/lsp;
import ru_run;
import ru_log;

export {
	ruDecodeJsonRequest(s : string) -> Json;
	ruLspReadRequest() -> Maybe<RuLspRequest>;
	RuLspRequest(
		request : string, // the whole request string
		length : int,  // Content-Length
		type : string, // Content-Type
		json : string  // the Json description of request
	);
	ruLspProcessRequest(conf : RuConf, json : Json,  out_cb : (string) ->  void, loop_cb : () -> void, exit_cb : () -> void, run : (RuConf, (int) -> void) -> void) -> void;
}

ruDecodeJsonRequest(s : string) -> Json {
	json = parseJsonSafe(s);
	switch (json) {
		JsonObject(members): if (jsonObjectHasField(json, "jsonrpc")) json else JsonNull();
		default: JsonNull();
	}
}

ruLspReadRequest() -> Maybe<RuLspRequest> {
	header = readUntil("\r\n\r\n"); // \r\n\r\n is included into header
	lines = filtermap(strSplit(header, "\n"), \line -> {
		trimmed = trim2(line, " \r\n\t");
		if (trimmed != "") Some(line) else None();
	});
	key_value = pairs2tree(map(lines, \line -> {
			key_val = strSplit(line, ":");
			if (length(key_val) == 1) {
				Pair(line, line);
			} else {
				Pair(trim2(key_val[0], " \t\n\r"), trim2(key_val[1], " \t\n\r"));
			}
	}));
	switch (lookupTree(key_value, "Content-Length")) {
		Some(len): {
			json = readBytes(s2i(len));
			Some(RuLspRequest(
				header + json,
				s2i(len), 
				lookupTreeDef(key_value, "Content-Type", ""),
				json
			));
		}
		None(): None();
	}
}

RuLspMethodCall(
	conf : RuConf,
	id : Json, 
	params : Json, 
	out_cb : (Json) ->  void, 
	loop_cb : () -> void, 
	exit_cb : () -> void,
	run : (RuConf, (int) -> void) -> void
);

ruLspMethods = pairs2tree([
	Pair("initialize", ruLspInitialize),
	Pair("initialized", ruLspInitialized),
	Pair("shutdown", ruLspShutdown),
	Pair("exit", ruLspExit),
	Pair("textDocument/didOpen", ruLspTextDocumentDidOpen),
	Pair("textDocument/didChange", ruLspTextDocumentDidChange),
	Pair("textDocument/didSave",ruLspTextDocumentDidSave),
	Pair("textDocument/documentSymbol", ruLspTextDocumentDocumentSymbol),
	Pair("textDocument/declaration", ruLspTextDocumentDeclaration),
	Pair("textDocument/definition", ruLspTextDocumentDefinition),
	Pair("textDocument/references", ruLspTextDocumentReferences),
	Pair("textDocument/hover", ruLspTextDocumentHover),
	Pair("textDocument/rename", ruLspTextDocumentRename),
	Pair("workspace/executeCommand", ruLspWorkspaceExecuteCommand),
]);

RuLspServerState(
	mutable clientConf : Json,
	mutable currId : int,
	mutable requests : Tree<Json, LspRequestMessage>
);

ruLspServerState = RuLspServerState(JsonNull(), 1, makeTree());

ruLspErrorResponse(id : Json, code : int, msg : string) -> Json {
	lspResponseMessage2Json(LspResponseMessage("2.0", id, JsonNull(), 
		Some(LspResponseError(code, msg, JsonNull()))
	));
}

ruLspNormalResponse(id : Json, result : Json) -> Json {
	lspResponseMessage2Json(LspResponseMessage("2.0", id, result, None()));
}

ruLspProcessRequest(conf : RuConf, json : Json, str_out_cb : (string) ->  void, loop_cb : () -> void, exit_cb : () -> void, run : (RuConf, (int) -> void) -> void) -> void {
	out_cb = \msg -> {
		js_out = json2string(msg);
		js_len = strlen(expandUtf8(js_out));
		str_out_cb("Content-Length: " + i2s(js_len) + "\r\n\r\n" + js_out);
	}
	switch (json2LspRequestMessage(json)) {
		Some(request): {
			if (request.method == "") {
				out_cb(ruLspErrorResponse(request.id, 1, "request method is not set"));
				loop_cb();
			} else {
				switch (lookupTree(ruLspMethods, request.method)) {
					Some(m): m(RuLspMethodCall(conf, request.id, request.params, out_cb, loop_cb, exit_cb, run));
					None(): {
						out_cb(ruLspErrorResponse(request.id, 1, "request method " + request.method + " is not found"));
						loop_cb();
					}
				}
			}
		}
		None(): {
			switch (json2LspResponseMessage(json)) {
				Some(response): {
					// TODO: handle properly
					switch (lookupTree(ruLspServerState.requests, response.id)) {
						Some(request): {
							ruDebugLog("response to the request: " + 
								json2string(lspRequestMessage2Json(request)) + ":\n" + 
								json2string(lspResponseMessage2Json(response))
								, conf
							);
						}
						None(): {
							ruDebugLog("unknown request: " + json2string(response.id), conf);
						}
					}
					loop_cb();
				}
				None(): {
					out_cb(ruLspErrorResponse(getJsonFieldValue(json, "id", JsonNull()), 1, "illegal request '" + json2string(json) + "'"));
					loop_cb();
				}
			}
		}
	}
}

ruLspShutdown(call : RuLspMethodCall) -> void {
	ruLspServerState.clientConf ::= JsonNull();
	ruLspServerState.currId ::= 1;
	ruLspServerState.requests ::= makeTree();
	ruClearCache(); // reset module cache
	call.out_cb(ruLspNormalResponse(call.id, JsonNull()));
	call.loop_cb();
}

ruLspExit(call : RuLspMethodCall) -> void {
	call.exit_cb();
}

ruLspInitialized(call: RuLspMethodCall) -> void {
	if (getJsonBoolField(getJsonNestedField(ruLspServerState.clientConf, ["capabilities", "workspace", "didChangeConfiguration"]), "dynamicRegistration", false)) {
		request = LspRequestMessage("2.0", JsonDouble(i2d(ruLspServerState.currId)), "client/registerCapability", 
			lspRegistrationParams2Json(LspRegistrationParams([
				LspRegistration(makeUuid(), "textDocument/didChange", 
					lspTextDocumentChangeRegistrationOptions2Json(
						LspTextDocumentChangeRegistrationOptions([LspDocumentFilter("flow", "file", "*.{flow}")], 1)
					)
				)
			]))
		);
		//ruDebugLog("didChangeConfiguration dynamic config: \n" + json2string(lspRequestMessage2Json(request)), call.conf);
		ruLspServerState.requests ::= setTree(ruLspServerState.requests, request.id, request);
		ruLspServerState.currId ::= ruLspServerState.currId + 1;
		call.out_cb(lspRequestMessage2Json(request));
	}
	call.loop_cb(); // It's a notification, no response.
}

ruLspInitialize(call: RuLspMethodCall) -> void {
	ruLspServerState.clientConf ::= call.params;
	call.out_cb(ruLspNormalResponse(call.id, 
		JsonObject([
			Pair("capabilities", JsonObject([
					Pair("declarationProvider", JsonBool(true)),
					Pair("definitionProvider", JsonBool(true)),
					Pair("referencesProvider", JsonBool(true)),
					Pair("workspaceSymbolProvider", JsonBool(true)),
					Pair("documentSymbolProvider", JsonBool(true)),
					Pair("hoverProvider", JsonBool(true)),
					Pair("renameProvider", JsonBool(true)),
					Pair("executeCommandProvider", 
						JsonObject([Pair("commands", JsonArray([
							JsonString("verify"), 
							JsonString("metamath"),
							JsonString("cache-info"),
							JsonString("find-symbol"),
						]))])
					),
					Pair("textDocumentSync",
						JsonObject([
							Pair("openClose", JsonBool(true)),
							Pair("change", JsonDouble(2.0))
						])
					)
				])
			)
		])
	));
	call.loop_cb();
}

ruLspTextDocumentDidOpen(call: RuLspMethodCall) -> void {
	switch (json2LspTextDocumentItem(getJsonObjectField(call.params, "textDocument"))) {
		Some(textitem): {
			//ruDebugLog("call.conf.file: " + call.conf.file, call.conf);
			//ruDebugLog("call.conf.path: " + call.conf.path, call.conf);
			//ruDebugLog("call.conf.ext: " + call.conf.ext, call.conf);
			//ruDebugLog("call.conf.importRoots: " + strGlue(call.conf.importRoots, ", "), call.conf);
		
			path = ruCorrectUriPath(textitem.uri.path);
			conf = ruLspFileConfig(call.conf, path);
			
			//ruDebugLog("conf1.file: " + conf.file, conf);
			//ruDebugLog("conf1.path: " + conf.path, conf);
			//ruDebugLog("conf1.ext: " + conf.ext, conf);
			//ruDebugLog("conf1.importRoots: " + strGlue(conf.importRoots, ", "), conf);
			
			ruDebugLog1("ruLspTextDocumentDidOpen start file: " + conf.file);
			
			call.run(
				RuConf(conf with opts = setTree(conf.opts, "update-cache", "1")),
				\code -> {
					ruDebugLog1("ruLspTextDocumentDidOpen finish");
					out = ruOutput(call.conf);
					ruClearOutput(call.conf);
					if (code != 0) {
						call.out_cb(ruLspErrorResponse(call.id, 1, "error at opening: " + path + "\n" + out));
						call.loop_cb() 
					} else {
						call.run(
							RuConf(conf with 
								opts = mergeTree(conf.opts, 
									pairs2tree([
										Pair("did-open", path),
										Pair("did-open-text", textitem.text)
									])
								)
							),
							\__ -> {
								ruClearOutput(call.conf);
								call.loop_cb();
							}
						);
					}
				}
			);
		}
		None(): {
			call.out_cb(ruLspErrorResponse(call.id, 1, "text document item is invalid: " + json2string(call.params)));
			call.loop_cb();
		}
	}
}

ruLspTextDocumentDidChange(call: RuLspMethodCall) -> void {
	switch (json2LspTextDocumentItem(getJsonObjectField(call.params, "textDocument"))) {
		Some(textitem): {
			path = ruCorrectUriPath(textitem.uri.path);
			conf = ruLspFileConfig(call.conf, path);
			call.run(
				RuConf(conf with
					opts = mergeTree(conf.opts, 
						pairs2tree([
							Pair("did-change", path),
							Pair("did-change-json", json2string(call.params)),
						])
					)
				),
				\code -> {
					ruClearOutput(call.conf);
					call.loop_cb();
				}
			);
		}
		None(): {
			call.out_cb(ruLspErrorResponse(call.id, 1, "text document item is invalid: " + json2string(call.params)));
			call.loop_cb();
		}
	}
}

ruLspTextDocumentDidSave(call: RuLspMethodCall) -> void {
	switch (json2LspDidSaveTextDocumentParams(call.params)) {
		Some(params): {
			path = ruCorrectUriPath(params.textDocument.uri.path);
			conf = ruLspFileConfig(call.conf, path);
			call.run(
				RuConf(conf with opts = setTree(conf.opts, "did-save", path)),
				\code_ -> {
					ruClearOutput(call.conf);
					call.loop_cb();
				}
			);
		}
		None(): {
			call.out_cb(ruLspErrorResponse(call.id, 1, "text document item is invalid: " + json2string(call.params)));
			call.loop_cb();
		}
	}
}

ruLspTextDocumentDocumentSymbol(call: RuLspMethodCall) -> void {
	switch (json2LspTextDocumentIdentifier(getJsonObjectField(call.params, "textDocument"))) {
		Some(textid): {
			conf = ruLspFileConfig(call.conf, ruCorrectUriPath(textid.uri.path));
			call.run(
				RuConf(conf with
					opts = setTree(conf.opts, "outline", "1")
				),
				\code -> {
					out = ruOutput(call.conf);
					ruClearOutput(call.conf);
					if (ruDetectLspError(out, call.conf)) {
						call.out_cb(ruLspNormalResponse(call.id, JsonNull()));
					} else {
						symbols = ruLspParseSymbols(out);
						call.out_cb(ruLspNormalResponse(call.id, JsonArray(symbols)));
					}
					call.loop_cb();
				}
			);
		}
		None(): {
			call.out_cb(ruLspErrorResponse(call.id, 1, "text document id is invalid: " + json2string(call.params)));
			call.loop_cb();
		}
	}
}

ruLspParseLocations(s : string) -> [Json] {
	isNum = \st -> st != "" && isDigits(st);
	filtermap(strSplit(s, "\n"), \l -> maybeMap(ruLspParseLocation(l), \p -> lspLocation2Json(p.first)));
}

ruLspParseSymbols(s : string) -> [Json] {
	isNum = \st -> st != "" && isDigits(st);
	filtermap(strSplit(s, "\n"), \l -> 
		maybeBind(ruLspParseLocation(l), \p -> {
			loc = p.first;
			descr_parts = strSplit(trim(p.second), " ");

			if (length(descr_parts) == 1) {
				Some(lspTextDocumentSymbol2Json(LspTextDocumentSymbol(p.second, "", 0, false, loc.range, loc.range, [])))
			} else if (length(descr_parts) > 1) {
				name = descr_parts[1];
				kind = if (descr_parts[0] == "const") {
					lookupTreeDef(lspSymbolKindName2id, "Object", 0);
				} else if (descr_parts[0] == "import") {
					lookupTreeDef(lspSymbolKindName2id, "Package", 0);
				} else if (descr_parts[0] == "type") {
					lookupTreeDef(lspSymbolKindName2id, "Struct", 0);
				} else if (descr_parts[0] == "union") {
					lookupTreeDef(lspSymbolKindName2id, "Enum", 0);
				} else if (descr_parts[0] == "theorem") {
					lookupTreeDef(lspSymbolKindName2id, "Function", 0);
				} else if (descr_parts[0] == "axiom") {
					lookupTreeDef(lspSymbolKindName2id, "Interface", 0);
				} else if (descr_parts[0] == "vardef") {
					lookupTreeDef(lspSymbolKindName2id, "Object", 0);
				} else if (descr_parts[0] == "rule") {
					lookupTreeDef(lspSymbolKindName2id, "Variable", 0);
				} else if (descr_parts[0] == "def") {
					lookupTreeDef(lspSymbolKindName2id, "Method", 0);
				} else if (descr_parts[0] == "natdecl") {
					lookupTreeDef(lspSymbolKindName2id, "Method", 0);
				} else {
					0
				}
				Some(lspTextDocumentSymbol2Json(LspTextDocumentSymbol(name, "", kind, false, loc.range, loc.range, [])));
			} else {
				None();
			}
		})
	);
}

ruLspParseLocation(s : string) -> Maybe<Pair<LspLocation, string>> {
	isNum = \st -> st != "" && isDigits(st);
	parts0 = strSplit(s, ":");
	if (length(parts0) < 3 || strContains(s, "Error: ") || strContains(s, "Fail: ")) None() else {
		// Windows path may start with C:/... and 
		parts = if (!hasTargetName("windows") || !isLetter(parts0[0])) parts0 else {
			concat(["/" + parts0[0] + ":" + parts0[1]], tailFrom(parts0, 2));
		}
		if (length(parts) < 3 || parts[0] == "" || !isNum(parts[1]) || !isNum(parts[2])) {
			None() ;
		} else {
			path = strReplace(parts[0], "\\", "/");
			sline = s2i(parts[1]) - 1;
			schar = s2i(parts[2]) - 1;
			if (length(parts) < 5 || !isNum(parts[3]) || !isNum(parts[4])) {
				Some(Pair(LspLocation(
					URI("file", URIAuthority("", "", ""), path, "", ""),
					LspRange(LspPosition(sline, schar), LspPosition(sline, schar + 1))
					), trim(parts[3]))
				);
			} else {
				eline = s2i(parts[3]) - 1;
				echar = s2i(parts[4]) - 1;
				Some(Pair(LspLocation(
					URI("file", URIAuthority("", "", ""), path, "", ""),
					LspRange(LspPosition(sline, schar), LspPosition(eline, echar))
					), trim(parts[5]))
				);
			}
		}
	}
}

ruLspTextDocumentDeclaration(call: RuLspMethodCall) -> void {
	switch (json2LspTextDocumentPositionParams(call.params)) {
		Some(textpos): {
			conf = ruLspFileConfig(call.conf, ruCorrectUriPath(textpos.textDocument.uri.path));
			call.run(
				RuConf(conf with
					opts = mergeTree(conf.opts, 
						pairs2tree([
							Pair("line", i2s(textpos.position.line)), 
							Pair("col", i2s(textpos.position.character)), 
							Pair("decl", "1")
						])
					)
				),
				\code -> {
					out = ruOutput(call.conf);
					ruClearOutput(call.conf);
					if (ruDetectLspError(out, call.conf)) {
						call.out_cb(ruLspNormalResponse(call.id, JsonNull()));
					} else {
						locations = ruLspParseLocations(out);
						call.out_cb(ruLspNormalResponse(call.id, JsonArray(locations)));
					}
					call.loop_cb();
				}
			);
		}
		None(): {
			call.out_cb(ruLspErrorResponse(call.id, 1, "text position is invalid: " + json2string(call.params)));
			call.loop_cb();
		}
	}
}

ruLspTextDocumentDefinition(call: RuLspMethodCall) -> void {
	switch (json2LspTextDocumentPositionParams(call.params)) {
		Some(textpos): {
			conf = ruLspFileConfig(call.conf, ruCorrectUriPath(textpos.textDocument.uri.path));
			call.run(
				RuConf(conf with
					opts = mergeTree(conf.opts, 
						pairs2tree([
							Pair("line", i2s(textpos.position.line)), 
							Pair("col", i2s(textpos.position.character)), 
							Pair("decl", "1")
						])
					)
				),
				\code -> {
					out = ruOutput(call.conf);
					ruClearOutput(call.conf);
					if (ruDetectLspError(out, call.conf)) {
						call.out_cb(ruLspNormalResponse(call.id, JsonNull()));
					} else {
						locations = ruLspParseLocations(out);
						call.out_cb(ruLspNormalResponse(call.id, JsonArray(locations)));
					}
					call.loop_cb();
				}
			);
		}
		None(): {
			call.out_cb(ruLspErrorResponse(call.id, 1, "text position is invalid: " + json2string(call.params)));
			call.loop_cb();
		}
	}
}

ruLspTextDocumentReferences(call: RuLspMethodCall) -> void {
	switch (json2LspTextDocumentPositionParams(call.params)) {
		Some(textpos): {
			conf = ruLspFileConfig(call.conf, ruCorrectUriPath(textpos.textDocument.uri.path));
			call.run(
				RuConf(conf with 
					opts = mergeTree(conf.opts, 
						pairs2tree([
							Pair("line", i2s(textpos.position.line)), 
							Pair("col", i2s(textpos.position.character)), 
							Pair("uses", "1")
						])
					)
				),
				\code -> {
					out = ruOutput(call.conf);
					ruClearOutput(call.conf);
					if (ruDetectLspError(out, call.conf)) {
						call.out_cb(ruLspNormalResponse(call.id, JsonNull()));
					} else {
						locations = ruLspParseLocations(out);
						call.out_cb(ruLspNormalResponse(call.id, JsonArray(locations)));
					}
					call.loop_cb();
				}
			);
		}
		None(): {
			call.out_cb(ruLspErrorResponse(call.id, 1, "text position is invalid: " + json2string(call.params)));
			call.loop_cb();
		}
	}
}

ruLspTextDocumentHover(call: RuLspMethodCall) -> void {
	switch (json2LspTextDocumentPositionParams(call.params)) {
		Some(textpos): {
			conf = ruLspFileConfig(call.conf, ruCorrectUriPath(textpos.textDocument.uri.path));
			call.run(
				RuConf(conf with 
					opts = mergeTree(conf.opts, 
						pairs2tree([
							Pair("line", i2s(textpos.position.line)), 
							Pair("col", i2s(textpos.position.character)), 
							Pair("hover", "1")
						])
					)
				),
				\code -> {
					out = ruOutput(call.conf);
					ruClearOutput(call.conf);
					if (ruDetectLspError(out, call.conf)) {
						call.out_cb(ruLspNormalResponse(call.id, JsonNull()));
					} else {
						if (isNotSpace(out)) {
							hover = LspHover([LspMarkedString("russell", out)], None());
							call.out_cb(ruLspNormalResponse(call.id, lspHover2Json(hover)));
						} else {
							call.out_cb(ruLspNormalResponse(call.id, JsonNull()));
						}
					}
					call.loop_cb();
				}
			);
		}
		None(): {
			call.out_cb(ruLspErrorResponse(call.id, 1, "text position is invalid: " + json2string(call.params)));
			call.loop_cb();
		}
	}
}

ruLspTextDocumentRename(call: RuLspMethodCall) -> void {
	switch (json2LspRename(call.params)) {
		Some(rename): {
			conf = ruLspFileConfig(call.conf, ruCorrectUriPath(rename.textDocument.uri.path));
			call.run(
				RuConf(conf with 
					opts = mergeTree(conf.opts, 
						pairs2tree([
							Pair("line", i2s(rename.position.line)), 
							Pair("col", i2s(rename.position.character)), 
							Pair("rename", "1"),
							Pair("to", rename.newName),
						])
					)
				),
				\code -> {
					out = ruOutput(call.conf);
					ruClearOutput(call.conf);
					if (ruDetectLspError(out, call.conf)) {
						call.out_cb(ruLspNormalResponse(call.id, JsonNull()));
					} else {
						call.out_cb(ruLspNormalResponse(call.id, JsonNull()));
					}
					call.loop_cb();
				}
			);
		}
		None(): {
			call.out_cb(ruLspErrorResponse(call.id, 1, "text position is invalid: " + json2string(call.params)));
			call.loop_cb();
		}
	}
}

ruLspWorkspaceExecuteVerify(call : RuLspMethodCall, commandParams: LspExecuteCommandParams) -> void {
	if (length(commandParams.arguments) == 0) {
		call.out_cb(ruLspErrorResponse(call.id, 1, "no file for verification is provided"));
		call.loop_cb();
	} else {
		file = getJsonStringValue(commandParams.arguments[0], "");
		if (file == "") {
			call.out_cb(ruLspErrorResponse(call.id, 1, "no file for verification"));
			call.loop_cb();
		} else {
			conf = ruLspFileConfig(call.conf, ruCorrectUriPath(file));
			ruDebugLog("verifying: " + conf.file, conf);
			call.run(
				RuConf(conf with opts = setTree(conf.opts, "verify", "1")),
				\code -> {
					ruDebugLog("code: " + i2s(code), conf);
					out = ruOutput(call.conf);
					ruClearOutput(call.conf);
					call.out_cb(ruLspNormalResponse(call.id, JsonString(out)));
					call.loop_cb();
				}
			);
		}
	}
}

ruLspWorkspaceExecuteMetamath(call : RuLspMethodCall, commandParams: LspExecuteCommandParams) -> void {
	// TODO
}

ruLspWorkspaceExecuteCacheInfo(call : RuLspMethodCall, commandParams: LspExecuteCommandParams) -> void {
	full = commandParams.arguments != [] && commandParams.arguments[0] == JsonString("full");
	info = ruCacheInfo(full);
	ruPrintln(info, call.conf);
	out = ruOutput(call.conf);
	ruClearOutput(call.conf);
	call.out_cb(ruLspNormalResponse(call.id, JsonString(out)));
	call.loop_cb();
}

ruLspWorkspaceExecuteFindSymbol(call : RuLspMethodCall, commandParams: LspExecuteCommandParams) -> void {
	if (length(commandParams.arguments) == 1) {
		name = getJsonStringValue(commandParams.arguments[0], "");
		if (name != "") {
			conf = RuConf(call.conf with opts = setTree(call.conf.opts, "find", name));
			call.run(
				conf,
				\code -> {
					out = ruOutput(call.conf);
					ruClearOutput(call.conf);
					if (ruDetectLspError(out, call.conf)) {
						call.out_cb(ruLspNormalResponse(call.id, JsonNull()));
					} else {
						locations = ruLspParseLocations(out);
						if (length(locations) != 1) {
							call.out_cb(ruLspNormalResponse(call.id, JsonNull()));
						} else {
							call.out_cb(ruLspNormalResponse(call.id, locations[0]));
						}
					}
					call.loop_cb();
				}
			);
		} else {
			call.out_cb(ruLspErrorResponse(call.id, 1, "find-symbol first parameter must be a non-empty string"));
			call.loop_cb();
		}
	} else {
		call.out_cb(ruLspErrorResponse(call.id, 1, "find-symbol takes a parameter: a symbol name. Passed " + i2s(length(commandParams.arguments)) + " args."));
		call.loop_cb();
	}
}

ruLspWorkspaceExecuteCommand(call: RuLspMethodCall) -> void {
	switch (json2LspExecuteCommandParams(call.params)) {
		Some(commandParams): {
			if (commandParams.command == "verify") {
				ruLspWorkspaceExecuteVerify(call, commandParams);
			} else if (commandParams.command == "metamath") {
				ruLspWorkspaceExecuteMetamath(call, commandParams);
			} else if (commandParams.command == "cache-info") {
				ruLspWorkspaceExecuteCacheInfo(call, commandParams);
			} else if (commandParams.command == "find-symbol") {
				ruLspWorkspaceExecuteFindSymbol(call, commandParams);
			} else {
				call.out_cb(ruLspErrorResponse(call.id, 1, "command '" + commandParams.command + "' is unknown"));
				call.loop_cb();
			}
		}
		None(): {
			call.out_cb(ruLspErrorResponse(call.id, 1, "ExecuteCommand request is illegal"));
			call.loop_cb();
		}
	}
}

ruDetectLspError(out : string, conf : RuConf) -> bool {
	if (strContains(out, "Error: ") || strContains(out, "Fail: ")) {
		ruServerLog("ERROR: " + out, conf);
		true
	} else false;
}

ruLspFileConfig(def : RuConf, path0 : string) -> RuConf {
	path = ruCorrectUriPath(path0);
	cwd_dir = strReplace(resolveRelativePath("."), "\\", "/");
	ext = getFileExt(path);
	file_dir = dirName(path);
	conf_dir = ruFindConfDir(file_dir);
	if (conf_dir == "") def else {
		switch (ruMakeConf(conf_dir)) {
			Some(conf): {
				if (conf_dir == "") def else {
					conf1 = RuConf(conf with 
						importRoots = uniq(concat(def.importRoots, map(conf.importRoots, \inc ->
							if (isAbsolutePath(inc)) inc else {
								if (inc == ".") {
									conf_dir;
								} else {
									conf_dir + "/" + inc;
								}
							}
						))),
						opts = setTree(mergeTree(def.opts, conf.opts), "working-dir", cwd_dir),
						ext = ext,
						path = path
					);
					RuConf(conf1 with 
						file = ruTrimPath(path, conf1, ext),
						//opts = setTree(conf1.opts, "verbose", "2")
					);
				}
			}
			None(): def;
		}
	}
}
