import ru_subst;
import ru_stats;

export {
	ruUnifyExps(es : [RuExp], sub : Tree<RuVar, RuExp>, math : RuMath, conf : RuConf) -> Maybe<Tree<RuVar, RuExp>>;
	ruUnifySubs(subs : [Tree<RuVar, RuExp>], math : RuMath, conf : RuConf) -> Maybe<Tree<RuVar, RuExp>>;
}

ruUnifyExps(es : [RuExp], sub : Tree<RuVar, RuExp>, math : RuMath, conf : RuConf) -> Maybe<Tree<RuVar, RuExp>> {
	if (length(es) <= 1) Some(sub) else {
		ruDoUnifyExps(es, RuUnifyAcc(sub, map(es, \__ -> 0), math, conf))
	}
}

RuUnifyAcc(
	sub : Tree<RuVar, RuExp>,
	inds : [int],
	math : RuMath,
	conf : RuConf
);

RuUnifyStepAcc(
	shift : [int],
	vars  : [RuVar],
	exprs : [RuExp],
	rule  : string,
	type  : string,
	ok    : bool,
	all_rules : bool
);

ruUnifyStepVar(step : RuUnifyStepAcc, math : RuMath, conf : RuConf) -> Maybe<RuVar> {
	maybeBind(
		fold(tail(step.vars), Some(step.vars[0]), \acc, v ->
			maybeBind(acc, \t -> 
				if (v.type == t.type || isSome(ruSuperRule(v.type, t.type, math, conf))) Some(v) else
				if (v.type == t.type || isSome(ruSuperRule(t.type, v.type, math, conf))) Some(t) else None()
			)
		),
		\v -> if (step.type == "" || v.type == step.type || isSome(ruSuperRule(v.type, step.type, math, conf))) Some(v) else None()
	)
}

ruDoUnifyStep(es : [RuExp], acc : RuUnifyAcc) -> RuUnifyStepAcc {
	foldi(
		mapi(es, \i, e -> e.nodes[acc.inds[i]]), 
		RuUnifyStepAcc([], [], [], "", "", true, true), 
		\i, ac, n ->
		switch (n) {
			RuVar(__,__): {
				switch (lookupTree(acc.sub, n)) {
					None():
						RuUnifyStepAcc(ac with
							shift = concat(ac.shift, [1]),
							vars = concat(ac.vars, [n]),
							all_rules = false
						);
					Some(ex): {
						m = ex.nodes[0];
						switch (m) {
							RuRuleRef(rule, type, __,len):
								RuUnifyStepAcc(
									concat(ac.shift, [1]),
									ac.vars,
									concat(ac.exprs, [ex]),
									rule, 
									type, 
									ac.ok && (ac.rule == "" || rule == ac.rule),
									false
								);
							RuVar(v, t):
								RuUnifyStepAcc(ac with 
									shift = concat(ac.shift, [1]),
									vars = concat(ac.vars, [m]),
									all_rules = false
								);
						}
					}
				}
			}
			RuRuleRef(rule, type,__, len): {
				RuUnifyStepAcc(
					concat(ac.shift, [len + 1]),
					ac.vars,
					concat(ac.exprs, [RuExp(subrange(es[i].nodes, acc.inds[i], len + 1), es[i].pos)]),
					rule, type, ac.ok && (ac.rule == "" || rule == ac.rule), ac.all_rules
				);
			}
		}
	);
}

ruDoUnifyExps(es : [RuExp], acc : RuUnifyAcc) -> Maybe<Tree<RuVar, RuExp>> {
	if (all(mapi(acc.inds, \i, n -> n == length(es[i].nodes)))) {
		Some(acc.sub) 
	} else {
		step = ruDoUnifyStep(es, acc);
		if (!step.ok) None() else {
			new_inds = \-> mapi(acc.inds, \j, i -> i + step.shift[j]);
			if (step.rule != "") {
				if (!step.all_rules) {
					maybeBind(ruUnifyExps(step.exprs, acc.sub, acc.math, acc.conf), \s -> {
						ex = ruApplySubst(step.exprs[0], s);
						maybeBind(
							fold(step.vars, Some(s), \ac, v -> 
								maybeBind(ac, \s1 -> ruAdd2Subst1(s1, v, ex, acc.math, acc.conf))
							),
							\s1 -> ruDoUnifyExps(es, 
								RuUnifyAcc(acc with 
									sub = s1,
									inds = new_inds()
								)
							)
						);
					});
				} else {
					ruDoUnifyExps(es, RuUnifyAcc(acc with inds = map(acc.inds, \i -> i + 1)));
				}
			} else {
				maybeBind(ruUnifyStepVar(step, acc.math, acc.conf), \w -> {
					ex = RuExp([w], -1);
					maybeBind(
						fold(step.vars, Some(acc.sub), \ac, v -> 
							maybeBind(ac, \s1 -> ruAdd2Subst1(s1, v, ex, acc.math, acc.conf))
						),
						\s1 -> ruDoUnifyExps(es, 
							RuUnifyAcc(acc with
								sub = s1,
								inds = new_inds()
							)
						)
					);
				});
			}
		}
	}
}

ruUnifySubs(subs : [Tree<RuVar, RuExp>], math : RuMath, conf : RuConf) -> Maybe<Tree<RuVar, RuExp>> {
	doms = map(subs, \sub -> buildSet(getTreeKeys(sub)));
	ims = map(subs, \sub -> foldTree(sub, makeSet(), \__, e, acc -> mergeSets(acc, ruExp2Vars(e.nodes))));

	t1 = timestamp();
	splited = foldi(subs, Pair(makeTree(), []), \i, acc, sub -> {
		p = foldTree(sub, Pair(acc.first, makeTree()), \v, e, ac -> {
			vs1 = insertSet(ruExp2Vars(e.nodes), v);
			if (ruExistsi(doms, \j, vs2 -> i != j && ruSetsIntersect(vs1, vs2)) || 
				ruExistsi(ims, \j, vs2 -> i != j && containsSet(vs2, v))
				) {
				Pair(ac.first, setTree(ac.second, v, e));
			} else {
				Pair(setTree(ac.first, v, e), ac.second);
			}
		})
		Pair(p.first, concat(acc.second, [p.second]));
	});
	if (lookupTreeDef(conf.opts, "reprove-stats", "") == "1") {
		stats = RuStatsRow("", 
			pairs2tree([
				Pair("size", i2d(fold(subs, 0, \acc, sub -> acc + sizeTree(sub)))), 
				Pair("time", timestamp() - t1), 
				//Pair("rets", length(rets)),
				Pair("cplx", i2d(fold(subs, 0, \acc, sub -> acc + ruSubComplexity(sub))))
			])
		);
		ruAddStats("ruUnifySubs-split", stats);
	}
	t2 = timestamp();
	ret1 = maybeMap(
		ruDoUnifySubs(splited.second, math, conf),
		\sub -> mergeTree(splited.first, sub)
	);
	if (lookupTreeDef(conf.opts, "reprove-stats", "") == "1") {
		stats = RuStatsRow("", 
			pairs2tree([
				Pair("size", i2d(fold(splited.second, 0, \acc, sub -> acc + sizeTree(sub)))), 
				Pair("time", timestamp() - t2), 
				//Pair("rets", ruSubComplexity(splited.second)),
				Pair("cplx", i2d(fold(splited.second, 0, \acc, sub -> acc + ruSubComplexity(sub))))
			])
		);
		ruAddStats("ruUnifySubs-ruDoUnifySubs", stats);
	}
	ret1
}

ruDoUnifySubs(subs : [Tree<RuVar, RuExp>], math : RuMath, conf : RuConf) -> Maybe<Tree<RuVar, RuExp>> {
	foldSet(
		fold(subs, makeSet(), \acc, s -> mergeSets(acc, buildSet(getTreeKeys(s)))),
		Some(makeTree()), 
		\acc, v ->
			maybeBind(acc, \sub -> {
				exprs = filtermap(subs, \s -> maybeMap(lookupTree(s, v), \e -> ruApplySubst(e, sub)));
				maybeBind(ruUnifyExps(exprs, sub, math, conf), \sub1 -> {
					e1 = ruApplySubst(exprs[0], sub1);
					switch (lookupTree(sub1, v)) {
						None(): ruAdd2Subst1(sub1, v, e1, math, conf);
						Some(e2):
							maybeBind(ruUnifyExps([e1, e2], sub1, math, conf), \unif -> 
								ruCompose(sub1, unif)
							);
					}
				});
			})
	);
}


/*
ruUnifySubs(subs : [Tree<RuVar, RuExp>], math : RuMath) -> Maybe<Tree<RuVar, RuExp>> {
	foldSet(
		fold(subs, makeSet(), \acc, s -> mergeSets(acc, buildSet(getTreeKeys(s)))),
		Some(makeTree()), 
		\acc, v ->
			maybeBind(acc, \sub -> {
				exprs = filtermap(subs, \s -> maybeMap(lookupTree(s, v), \e -> ruApplySubst(e, sub)));
				maybeBind(ruUnifyExps(exprs, sub, math), \sub1 -> {
					e1 = ruApplySubst(exprs[0], sub1);
					switch (lookupTree(sub1, v)) {
						None(): ruAdd2Subst1(sub1, v, e1, math);
						Some(e2):
							maybeBind(ruUnifyExps([e1, e2], sub1, math), \unif -> 
								ruCompose(sub1, unif)
							);
					}
				});
			})
	);
}
*/

