source = '\uFEFF'? rws element*;
element = 
	rcomment | include | const | vars | disj | 
	floating | essential | axiomatic | provable | block;
block   = "${" rws element* "$}" rws;

const = "$c" s symb_sp+ "$." rws;
vars  = "$v" s symb_sp+ "$." rws;
disj  = "$d" s symb_sp+ "$." rws;

floating  = label_sp "$f" s type var "$." rws;
essential = label_sp "$e" s expr "$." rws;
axiomatic = label_sp "$a" s expr "$." rws;
provable  = label_sp "$p" s expr "$=" s proof;
proof     = compressed_proof | normal_proof ;

normal_proof = label_sp+ "$." rws;

compressed_proof    = '(' s label_sp* ')' s step+ "$." rws;
step = index | 'Z' ws;
index = most_significant_digit* least_significant_digit;
least_significant_digit = 'A'-'T' ws;
most_significant_digit  = 'U'-'Y' ws;

type  = symb s;
var   = symb s;
expr  = symb_sp+;

symb_sp  = symb s;
symb     = symb_char+;

label_sp = label s;
label    = label_char+;

comment  = "$(" comment_text "$)" ws;
rcomment = "$(" comment_text "$)" rws;
include  = "$[" path "$]" rws;
path = (!"$]" any)*;
comment_text = (!"$)" any)*;
symb_char = '\u0021'-'\u0023' | '\u0025'-'\uFFFF';

label_char = '0'-'9' | 'a'-'z' | 'A'-'Z' | '-' | '_' | '.';

s = ws_+;
ws = ws_*;
ws_ = ' ' | '\t' | '\n' | '\r' | comment;

rs = rws_+;
rws = rws_*;
rws_ = ' ' | '\t' | '\n' | '\r';

any = '\u0000'-'\uFFFF';
