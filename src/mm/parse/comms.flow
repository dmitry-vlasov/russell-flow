import mm/parse/load;
import mm/parse/collect;
import mm/to_ru;
import mm/verify;

import command;

export {
	mmIoCommands : [RuComm] = [
		mmCommRead,
		mmCommWrite
	];
	mmRunRead(task : RuTask, env : RuEnv) -> RuEnv;
	mmRunWrite(task : RuTask, env : RuEnv) -> RuEnv;
}

mmCommRead = RuComm(
	"read-mm", "io", "read a Metamath file", "file",
	[RuCommArg("file", false, "input file", "")],
	mmRunRead
);

mmRunRead(task : RuTask, env : RuEnv) -> RuEnv {
	file = lookupTreeDef(task.args, "file", "");
	if (!endsWith(file, ".mm")) {
		env.conf.onError("Wrong extension of a file '" + file + "' , must be *.mm", []);
		env;
	} else {
		path = resolveRelativePath(file);
		module = ruTrimPath(path, env.conf, getFileExt(file));
		mm = maybeBind(mmLoad(env.conf, module), \loaded ->
			maybeBind(mmParse(getTreeValues(loaded), env.conf), \parsed ->
				maybeBind(mmCollectMath(parsed, env.conf), \collected ->
					mmVerifyMath(collected, env.conf)
				)
			)
		);
		eitherMap(mm, \math -> RuEnv(env with mmMath = math), env);
	}
}

mmCommWrite = RuComm(
	"write-mm", "io", "write a Metamath file to a filesystem", "file",
	[
		RuCommArg("file", true, "write the mm file", ""), 
		RuCommArg("module", true, "module to write", ""), 
		RuCommArg("all", true, "write all modules", ""), 
		RuCommArg("monolithic", true, "include all dependencies into a target file", ""),
		RuCommArg("strip-comments", true, "self evident", "")
	],
	mmRunWrite
);

mmRunWrite(task : RuTask, env : RuEnv) -> RuEnv {
	math = env.mmMath;
	conf = env.conf;
	write_mm = \src0 : MmSource -> {
		path = src0.info.path;
		ensureDirectoryExists(ruDirName(path));
		if (env.conf.verbose > 1) {
			conf.onMessage("\tmm writing : '" + path + "'");
		}
		src = if (lookupTreeDef(task.args, "strip-comments", "") == "1") mmStripComments(src0) else src0;
		if (!setFileContent(path, mm2s(src))) {
			conf.onMessage("error while writing '" + path + "'");
		}
	}
	write_module = \module -> {
		switch (lookupTree(math.sources, module)) {
			Some(src): {
				monolith =lookupTreeDef(task.args, "monolithic", "");
				if (monolith == "") {
					write_mm(src);
				} else {
					if (monolith != "1") {
						write_mm(mmMath2SingleSource(env.mmMath, monolith, env.conf));
					} else {
						write_mm(mmMath2SingleSource(env.mmMath, src.info.path, env.conf));
					}
				}
			}
			None(): conf.onMessage("unknown module: '" + module);
		}
	}
	if (lookupTreeDef(task.args, "all", "") == "1") {
		start = timestamp();
		iter(getTreeValues(math.sources), write_mm);
		time = (timestamp() - start);
		if (conf.verbose > 0 && time > 100.0) {
			conf.onMessage("mm written " + i2s(sizeTree(math.sources)) + " files in " + d2st(time/ 1000.0, 2) + "s");
		}
	} else if (lookupTreeDef(task.args, "module", "") != "") {
		module = lookupTreeDef(task.args, "module", "");
		write_module(module)
	} else if (lookupTreeDef(task.args, "file", "") != "") {
		file = lookupTreeDef(task.args, "file", "");
		if (!endsWith(file, ".mm")) {
			conf.onError("Wrong extension of a file " + file + ", mus be *.mm", []);
		} else {
			module = ruTrimPath(file, conf, ".mm");
			write_module(module)
		}
	} else {
		conf.onError("mm nothing to write", []);
	}
	env;
}
