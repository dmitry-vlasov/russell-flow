import base/util/util;
import ru/parse/load;
import mm/parse/file;
import mm/parse/ast;
import mm/src;

import lingo/pegcode/driver;

export {
	MmParseInfos(
		total : (string, string) -> void,
		source : (string, string) -> void,
	);
	mmParseInfosDef(env : RuEnv, args : Tree<string, flow>) -> MmParseInfos;
	

	mmParse(loaded : [RuRawSource], env : RuEnv, infos : MmParseInfos) -> Maybe<Tree<int, MmParseSource>>;
}

mmParseInfosDef(env : RuEnv, args : Tree<string, flow>) -> MmParseInfos { 
	total = switch (lookupTree(args, "info-total")) {
		Some(fn): {
			if (runtimeValueType(fn) != "function") {
				env.out.error("'info-total' parameter must be a function, got '" + runtimeValueType(fn) + "':\n" + ruScValue2s(fn), []);
			}
			cast(fn : flow -> (string, string) -> void);
		}
		None(): {
			\size, time -> env.out.println("mm parsed " +size + " files in: " + time)
		}
	}
	source = switch (lookupTree(args, "info-source")) {
		Some(fn): {
			if (runtimeValueType(fn) != "function") {
				env.out.error("'info-total' parameter must be a function, got '" + runtimeValueType(fn) + "':\n" + ruScValue2s(fn), []);
			}
			cast(fn : flow -> (string, string) -> void);
		}
		None(): {
			\src, time -> env.out.println("\tmm parsed: '" + src + "' in " + time)
		}
	}
	MmParseInfos(total, source);
}

mmParse(loaded : [RuRawSource], env0 : RuEnv, infos : MmParseInfos) -> Maybe<Tree<int, MmParseSource>> {
	err_count = ref 0;
	env = ruAddErrCounter2Env(env0, err_count);
	timer = ruMakeTimer();
	parsed = fold(
		filtermap(ruConcurrent(map(loaded, \l -> \env1 -> mmParseSource(l, env1, infos)), env), idfn),
		makeTree(),
		\acc, src -> setTree(acc, src.info.id, src)
	);
	if (ruVerbose(env.conf) > 0) {
		infos.total(i2s(sizeTree(parsed)), ruTimePassed2s(timer));
		//env.out.println("mm parsed " +i2s(sizeTree(parsed)) + " files in: " + ruTimePassed2s(timer));
	}
	if (^err_count == 0) {
		Some(parsed);
	} else {
		None();
	}
}

mmParseSource(loaded : RuRawSource, env : RuEnv, infos : MmParseInfos) -> Maybe<MmParseSource> {
	timer = ruMakeTimer();
	pr1 = parse_mm_source(loaded.src);
	st = pr1.first;
	err = pr1.second;
	if (st.end != strlen(loaded.src)) {
		env.out.error(
			"Syntax error:\n" + 
				"\t" + ruExpectError2s(err) + "\n" +
			"source:\n" +
				strIndent(ruHighlightErrorPos(loaded.src, st.end)), 
			[RuPlace(loaded.info.path, st.end)]
		);
		None()
	} else {
		parsed = mmMakeSrc(loaded, st, env);
		if (ruVerbose(env.conf) > 1) {
			infos.source(loaded.info.module, ruTimePassed2s(timer));
			//env.out.println("\tmm parsed: '" + loaded.info.module + "' in " + ruTimePassed2s(timer));
		}
		Some(parsed);
	}
}

mmMakeSrc(loaded : RuRawSource, st : SyntaxTree, env : RuEnv) -> MmParseSource {
	synt_trees = get_mm_source_mm_element_s(st);
	toplevel = map(synt_trees,
		\elem -> {
			if (elem.rule == st_mm_rcomment) {
				mmMakeComment(loaded, elem, env);
			} else if (elem.rule == st_mm_include) {
				mmMakeInclude(loaded, elem, env);
			} else if (elem.rule == st_mm_const) {
				mmMakeConst(loaded, elem, env);
			} else if (elem.rule == st_mm_vars) {
				mmMakeVars(loaded, elem, env);
			} else if (elem.rule == st_mm_disj) {
				mmMakeDisj(loaded, elem, env);
			} else if (elem.rule == st_mm_floating) {
				mmMakeFloating(loaded, elem, env);
			} else if (elem.rule == st_mm_essential) {
				mmMakeEssential(loaded, elem, env);
			} else if (elem.rule == st_mm_axiomatic) {
				mmMakeAxiomatic(loaded, elem, env);
			} else if (elem.rule == st_mm_provable) {
				mmMakeProvable(loaded, elem, env);
			} else if (elem.rule == st_mm_block) {
				mmMakeBlock(loaded, elem, env);
			} else {
				// Shouldn't happen
				MmComment("");
			}
		}
	);
	imports = filtermap(synt_trees, \elem ->
		if (elem.rule != st_mm_include) None() else {
			Some(mmMakeInclude(loaded, elem, env));
		} 
	);
	MmParseSource(imports, toplevel, loaded.info);
}

mmMakeComment(loaded : RuRawSource, st : SyntaxTree, env : RuEnv) -> MmComment {
	text = grabSTText(get_mm_source_mm_comment_text(st), loaded.src);
	if (startsWith(ltrim(text), "Begin $[") || startsWith(ltrim(text), "End $[") || startsWith(ltrim(text), "Skip $[")) {
		MmComment("ignore " + text);
	} else {
		MmComment(text);
	}
}

mmMakeInclude(loaded : RuRawSource, st : SyntaxTree, env : RuEnv) -> MmImport { 
	path = ruTrimPath(grabSTText(get_mm_source_mm_path(st), loaded.src), env.conf, ".mm");
	MmImport(s2id(path));
}

mmMakeConst(loaded : RuRawSource, st : SyntaxTree, env : RuEnv) -> MmConst {
	MmConst(mmMakeSymbs(loaded, st, env));
}

mmMakeVars(loaded : RuRawSource, st : SyntaxTree, env : RuEnv) -> MmVars {
	MmVars(mmMakeSymbs(loaded, st, env));
}

mmMakeDisj(loaded : RuRawSource, st : SyntaxTree, env : RuEnv) -> MmDisj {
	MmDisj(mmMakeSymbs(loaded, st, env));
}

mmMakeFloating(loaded : RuRawSource, st : SyntaxTree, env : RuEnv) -> MmFloating {
	MmFloating(
		mmMakeLabel(loaded, get_mm_source_mm_label_sp(st), env),
		mmMakeSymb(loaded, get_mm_source_mm_type(st), env),
		mmMakeSymb(loaded, get_mm_source_mm_var(st), env)
	);
}

mmMakeEssential(loaded : RuRawSource, st : SyntaxTree, env : RuEnv) -> MmEssential {
	MmEssential(
		mmMakeLabel(loaded, get_mm_source_mm_label_sp(st), env),
		mmMakeExpr(loaded, get_mm_source_mm_expr(st), env)
	);
}

mmMakeAxiomatic(loaded : RuRawSource, st : SyntaxTree, env : RuEnv) -> MmAxiomatic {
	MmAxiomatic(
		mmMakeLabel(loaded, get_mm_source_mm_label_sp(st), env),
		mmMakeExpr(loaded, get_mm_source_mm_expr(st), env)
	);
}

mmMakeProvable(loaded : RuRawSource, st : SyntaxTree, env : RuEnv) -> MmProvable {
	MmProvable(
		mmMakeLabel(loaded, get_mm_source_mm_label_sp(st), env),
		mmMakeExpr(loaded, get_mm_source_mm_expr(st), env),
		mmMakeProof(loaded, get_mm_source_mm_proof(st), env)
	);
}

mmMakeProof(loaded : RuRawSource, st : SyntaxTree, env : RuEnv) -> MmProof {
	if (st.rule == st_mm_normal_proof) {
		MmNormalProof(mmMakeLabels(loaded, st, env));
	} else {
		MmCompressedProof(
			mmMakeLabels(loaded, st, env), 
			mmMakeSteps(loaded, get_mm_source_mm_step_s(st), env)
		);
	}
}

mmMakeSteps(loaded : RuRawSource, st : [SyntaxTree], env : RuEnv) -> [int] {
	map(st, \s -> 
		if (s.rule == st_mm_index) {
			least = trim2(grabSTText(get_mm_source_mm_least_significant_digit(s), loaded.src), " \t\n\r");
			most = map(get_mm_source_mm_most_significant_digit_s(s), \d -> trim2(grabSTText(d, loaded.src), " \t\n\r"));
			ruDecompressInd(least, most);
		} else {
			-1;
		}
	)
}

mmMakeSymb(loaded : RuRawSource, st : SyntaxTree, env : RuEnv) -> int {
	s2id(grabSTText(get_mm_source_mm_symb(st), loaded.src));
}

mmMakeSymbs(loaded : RuRawSource, st : SyntaxTree, env : RuEnv) -> [int] {
	map(get_mm_source_mm_symb_sp_s(st), \w_sp -> 
		s2id(grabSTText(get_mm_source_mm_symb(w_sp), loaded.src))
	);
}

mmMakeExpr(loaded : RuRawSource, st : SyntaxTree, env : RuEnv) -> MmExp {
	MmExp(map(get_mm_source_mm_symb_sp_s(st), \w_sp -> 
		MmSymb(s2id(grabSTText(get_mm_source_mm_symb(w_sp), loaded.src)), false)
	));
}

mmMakeLabel(loaded : RuRawSource, st : SyntaxTree, env : RuEnv) -> int {
	s2id(grabSTText(get_mm_source_mm_label(st), loaded.src));
}

mmMakeLabels(loaded : RuRawSource, st : SyntaxTree, env : RuEnv) -> [int] {
	map(get_mm_source_mm_label_sp_s(st), \l_sp -> 
		s2id(grabSTText(get_mm_source_mm_label(l_sp), loaded.src))
	);
}

mmMakeBlock(loaded : RuRawSource, st : SyntaxTree, env : RuEnv) -> MmBlock {
	MmBlock(map(get_mm_source_mm_element_s(st), \elem -> 
		if (elem.rule == st_mm_rcomment) {
			mmMakeComment(loaded, elem, env);
		} else if (elem.rule == st_mm_include) {
			mmMakeInclude(loaded, elem, env);
		} else if (elem.rule == st_mm_const) {
			mmMakeConst(loaded, elem, env);
		} else if (elem.rule == st_mm_vars) {
			mmMakeVars(loaded, elem, env);
		} else if (elem.rule == st_mm_disj) {
			mmMakeDisj(loaded, elem, env);
		} else if (elem.rule == st_mm_floating) {
			mmMakeFloating(loaded, elem, env);
		} else if (elem.rule == st_mm_essential) {
			mmMakeEssential(loaded, elem, env);
		} else if (elem.rule == st_mm_axiomatic) {
			mmMakeAxiomatic(loaded, elem, env);
		} else if (elem.rule == st_mm_provable) {
			mmMakeProvable(loaded, elem, env);
		} else if (elem.rule == st_mm_block) {
			mmMakeBlock(loaded, elem, env);
		} else {
			// should never happen
			ruCrash("unexpected MM syntax element");
			MmComment("");
		}
	));
}
