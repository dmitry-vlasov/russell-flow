import ds/vector;
import base/id_consts;
import base/trie;
import ru/parse/exps/sources;
import ru/src;
import ru/refactor/left_recursion;
import mm/compress;
import mm/proof_tree;
import mm/symbols;

export {
	mm2ru(sources : Tree<int, MmSource>, env : RuEnv) -> Maybe<RuMath>;
}

mm2ru(sources : Tree<int, MmSource>, env : RuEnv) -> Maybe<RuMath> {
	transl_timer = ruMakeTimer();
	parse_srcs = mm2RuParseSources(sources, env);
	collect_timer = ruMakeTimer();
	math = ruCollectMathFast(parse_srcs, env);
	if (ruVerbose(env.conf) > 1) {
		env.out.println("\tru parsed " + i2s(sizeTree(sources)) + " files in: " + ruTimePassed2s(collect_timer));
	}
	if (ruVerbose(env.conf) > 0) {
		env.out.println("mm total translation time for " + i2s(sizeTree(sources)) + " files in: " + ruTimePassed2s(transl_timer));
	}
	math;
}

mm2RuParseSources(sources : Tree<int, MmSource>, env : RuEnv) ->  Tree<int, RuParseSource> {
	syntax = mmSources2Syntax(sources, env);
	if (ruVerbose(env.conf) > 2) {
		println("Syntax:\n" + strIndent(mmRuSyntax2s(syntax)));
	}
	assertions = mm2RuParseAssrtions(sources, syntax, env);

	src_timer = ruMakeTimer();
	parse_srcs = mapTree(sources, \src -> mmSource2ru(src, assertions, syntax, env));
	if (ruVerbose(env.conf) > 1) {
		env.out.println("\tmm translated " + i2s(sizeTree(sources)) + " sources in: " + ruTimePassed2s(src_timer));
	}
	parse_srcs;
}

mm2RuParseAssrtions(sources : Tree<int, MmSource>, syntax : MmRuSyntax, env : RuEnv) ->  Tree<int,[RuParseDecl]> {
	structs = mmSources2structs(sources);
	ass_timer = ruMakeTimer();
	mm_assertions = mmAssertions(sources);
	assertions = pairs2tree(concatA(ruConcurrent(mapi(
		mm2ruSplitAssertions(mm_assertions),
		\i, arr -> \env1 -> {
			t = ruMakeTimer();
			res = map(arr, \ass ->
				Pair(ass.stat.label, mmAssertion2ru(ass, structs, syntax, env1))
			);
			if (ruVerbose(env1.conf) > 2) {
				env1.out.println("\t\tmm translated slice " + i2s(i) + " of size: " + i2s(length(arr)) + " in " + ruTimePassed2s(t));
			}
			res;
		}
	), env)));
	if (ruVerbose(env.conf) > 1) {
		env.out.println("\tmm translated " + i2s(length(mm_assertions)) + " assertions in: " + ruTimePassed2s(ass_timer));
	}
	assertions;
}

mm2ruSplitAssertions(assertions : [MmAssertion]) -> [[MmAssertion]] {
	sum_length = fold(assertions, 0.0, \acc, ass -> acc + i2d(mmAssertionProofLength(ass)));
	length_per_processor = sum_length / i2d(availableProcessors());
	curr = makeVector(128);
	split = fold(assertions, Pair([], 0.0), \acc, ass -> {
		pushVector(curr, ass);
		sum =  acc.second + i2d(mmAssertionProofLength(ass));
		if (sum < length_per_processor) {
			Pair(acc with second = sum);
		} else {
			slice = vector2array(curr);
			clearVector(curr);
			Pair(concat(acc.first, [slice]), 0.0);
		}
	});
	if (sizeVector(curr) == 0) split.first else {
		concat(split.first, [vector2array(curr)]);
	}
}

RuParseDeclComment ::= RuParseDecl, RuParseComment;

mmSource2ru(src : MmSource, assertions : Tree<int, [RuParseDecl]>, syntax : MmRuSyntax, env : RuEnv) -> RuParseSource {
	timer = ruMakeTimer();
	decls = makeVector(128);
	decl_ids = ref makeSet();
	comms = fold(
		concatA(ruConcurrent(map(src.decls, \decl ->
			\env1 -> mmDecl2ru(decl, assertions, syntax, env1)
		), env)), 
		[], 
		\acc, block -> {
			switch (block) {
				RuParseComment(__,__,__): concat(acc, [block]);
				RuParseDecl(): {
					if (!containsSet(^decl_ids, block.id)) {
						pushVector(decls, ruSetParseDeclComments(block, acc));
						decl_ids := insertSet(^decl_ids, block.id);
					}
					[];
				}
			}
		}
	);
	ret = RuParseSource(
		map(src.imports, \inc -> RuParseImport(inc.module, -1)),
		vector2array(decls), comms, 
		ruNewFileInfo(src.info.module, env.conf, ".ru")
	);
	if (ruVerbose(env.conf) > 1) {
		env.out.println("\tmm translated: '" + src.info.module + "' in " + ruTimePassed2s(timer));
	}
	ret
}

mmDecl2ru(decl : MmDecl, assertions : Tree<int, [RuParseDecl]>, syntax : MmRuSyntax, env : RuEnv) -> [RuParseDeclComment] {
	switch (decl) {
		MmComment(text): 
			mmComment2ru(decl, env);
		MmConst(symbs): 
			mmConstant2ru(decl, syntax, env);
		MmAssertion(comm, disjs, outer, hyps, inner, stat): 
			lookupTreeDef(assertions, stat.label, []);
		MmFloating(__,__,__):
			[];
	}
}

mmConstant2ru(const : MmConst, syntax : MmRuSyntax, env : RuEnv) -> [RuParseDecl] {
	fold(const.symbs, [], \acc, s -> 
		switch (lookupTree(syntax.types, s)) {
			Some(t):
				if (containsSet(syntax.supers, t.id)) acc else {
					arrayPush(
						fold(t.supers, acc, \ac, sup -> 
							arrayPush(ac, lookupTreeDef(syntax.types, sup, ruNoParseType))
						),
						t
					)
				}
			None(): concat(acc, [mmConst2ru(s, env)]);
		}
	);
}

mmComment2ru(comm : MmComment, env : RuEnv) -> [RuParseDeclComment] {
	[RuParseComment(strReplace(strReplace(comm.text, "/*", "/ *"), "*/", "* /"), "", -1)]
}

mmSymb2ru(s : int, env : RuEnv, vars : Tree<int, int> ) -> int {
	literal = mmSymbLiteral(s);
	if (mmSymbIsVar(s)) {
		lookupTreeDef(vars, literal, literal);
	} else {
		mmConst2ru(literal, env).id
	}
}

mmConst2ru(s : int, env : RuEnv) -> RuParseConst {
	if (ruOptsString(env.conf.opts, "mm-ascii", "") == "1") {
		RuParseConst(s, [], -1, "", -1)
	} else {
		lookupTreeDef(mmSymbTable(), s, RuParseConst(s, [], -1, "", -1))
	}
}

mmAssertion2ru(ass : MmAssertion, structs : MmStructs, synt : MmRuSyntax , env : RuEnv) -> [RuParseDecl] {
	switch (lookupTree(synt.rules, ass.stat.label)) {
		Some(r):
			if (ruRuleIsSuper(r)) [] else [r];
		None(): {
			switch (ass.stat) {
				MmProvable(__,__,__): {
					thm = mmTheorem2ru(ass, ass.stat, structs, env, synt);
					if (ruVerbose(env.conf) > 2) {
						println("THM:\n" + strIndent(concatStrings(map(thm, ruParse2s))));
					}
					thm;
				}
				MmAxiomatic(l,__): {
					if (synt.is_def(ass.stat)) {
						def = mmDef2ru(ass, env, synt);
						if (ruVerbose(env.conf) > 2) {
							println("DEF:\n" + strIndent(concatStrings(map(def, ruParse2s))));
						}
						def
					} else {
						ax = mmAxiom2ru(ass, env, synt);
						if (ruVerbose(env.conf) > 2) {
							println("AX:\n" + strIndent(concatStrings(map(ax, ruParse2s))));
						}
						ax
					}
				}
			}
		}
	}
}

mmAxiom2ru(ass : MmAssertion, env : RuEnv, synt: MmRuSyntax) -> [RuParseDecl] {
	vars = mmVars2ru(ass.hyps, synt.vars);
	[RuParseAxiom(
		ass.stat.label, [], 
		vars, 
		mmDisjs2ru(ass.disjs, mmAssertionVars(ass), synt.vars), 
		mapi(mmAssertionHyps(ass.hyps), \i, h -> RuParseHyp(i, mmStmt2ru(h.expr.symbs, vars, env, synt), -1)), 
		mmStmt2ru(ass.stat.expr.symbs, vars, env, synt), -1
	)];
}

MmDefFactor(
	defm : RuParseTerm,
	defs : RuParseTerm,
	def : RuParseStmt,
);

mmDef2ru(ass : MmAssertion, env : RuEnv, synt: MmRuSyntax) -> [RuParseDecl] {
	vars = mmVars2ru(ass.hyps, synt.vars);
	defFactor = mmFactorDef(ass.stat.expr, vars, env, synt);
	[RuParseDef(
		ass.stat.label, [], 
		vars, 
		mmDisjs2ru(ass.disjs, mmAssertionVars(ass), synt.vars), 
		mapi(mmAssertionHyps(ass.hyps), \i, h -> RuParseHyp(i, mmStmt2ru(h.expr.symbs, vars, env, synt), -1)),
		defFactor.defm, defFactor.defs, defFactor.def, -1
	)];
}

mmFactorDef(ex : MmExp, vars: [RuParseVar], env : RuEnv, synt: MmRuSyntax) -> MmDefFactor {
	eq_ind = mmFactorDefEqInd(ex);
	class = ex.symbs[0];
	if (eq_ind != -1) {
		is_eq = mmSymbLiteral(ex.symbs[eq_ind]) == ruIdEquality();
		dfm_beg = if (is_eq) 1 else 2;
		dfm_len = if (is_eq) eq_ind - 1 else eq_ind - 2;
		dfs_beg = eq_ind + 1;
		dfs_len = if (is_eq) length(ex.symbs) - eq_ind - 1 else length(ex.symbs) - eq_ind - 2;
		type = if (is_eq) ruIdClass() else ruIdWff();
		MmDefFactor(
			mmTerm2ru(Some(type), subrange(ex.symbs, dfm_beg, dfm_len), env, synt.vars),
			mmTerm2ru(Some(type), subrange(ex.symbs, dfs_beg, dfs_len), env, synt.vars),
			{
				stmt = mmStmt2ru(
					if (is_eq) [
						class,
						mmMakeSymb(ruIdDefiendum(), true),
						mmMakeSymb(ruIdEquality(), false),
						mmMakeSymb(ruIdDefiniens(), true),
					] else [
						class,
						mmMakeSymb(ruIdLeftBracket(), false), 
						mmMakeSymb(ruIdDefiendum(), true),
						mmMakeSymb(ruIdEquivalence(), false), 
						mmMakeSymb(ruIdDefiniens(), true),
						mmMakeSymb(ruIdRightBracket(), false),
					], vars, env, synt);
				//println("STMT: " + ruParse2s(stmt));
				//println("EX DEF: " + mm2s(ex) + "\n\n");
				stmt;
			}
		);
	} else {
		dfm_end = mmFactorDefNotationInd(ex);
		type = ruIdPattern();
		MmDefFactor(
			mmTerm2ru(Some(type), subrange(ex.symbs, 1, dfm_end), env, synt.vars),
			mmTerm2ru(Some(type), subrange(ex.symbs, 1 + dfm_end, length(ex.symbs) - dfm_end - 1), env, synt.vars),
			mmStmt2ru([class, mmMakeSymb(ruIdDefiendum(), false), mmMakeSymb(ruIdDefiniens(), false)], vars, env, synt)
		);
	}
}

mmTerm2ru(mtype: Maybe<int>, symbs : [int], env : RuEnv, vars : Tree<int, int>) -> RuParseTerm {
	switch (mtype) {
		Some(type): RuParseTerm(type, RuParseExpr(map(symbs, \s -> mmSymb2ru(s, env, vars)), -1), -1);
		None(): RuParseTerm(mmSymbLiteral(symbs[0]), RuParseExpr(map(tail(symbs), \s -> mmSymb2ru(s, env, vars)), -1), -1);
	}
}

MmGuessTypeAcc(
	nodes: [RuTermNode],
	brackets: int,
	braces: int,
);

mmStmt2ru(symbs : [int], vars: [RuParseVar], env : RuEnv, synt: MmRuSyntax) -> RuParseStmt {
	class = mmSymbLiteral(symbs[0]);
	types = either(synt.stmt_type(MmExp(symbs)), [class]);
	RuParseStmt(class, types,
		RuParseExpr(
			map(tail(symbs), \s -> mmSymb2ru(s, env, synt.vars)),
			1
		),
		-1,
	);
}


ruRuleIsSuper(r : RuParseRule) -> bool {
	length(r.term.expr.symbs) == 1 && length(r.vars) == 1;
}

ruRuleSuperType(r : RuParseRule) -> int {
	r.term.type
}

ruRuleInferType(r : RuParseRule) -> int {
	r.vars[0].type
}

mmRule2ru(ass : MmAssertion, env : RuEnv, vars : Tree<int, int>) -> RuParseRule {
	RuParseRule(
		ass.stat.label, [],
		mmVars2ru(ass.hyps, vars), 
		mmTerm2ru(None(), ass.stat.expr.symbs, env, vars), 
		-1
	);
}

mmVars2ru(hyps : [MmHyp], vars : Tree<int, int>) -> [RuParseVar] {
	filtermap(hyps, \h ->
		switch (h) {
			MmFloating(__, t, v): {
				Some(RuParseVar(lookupTreeDef(vars, v, v), t, -1));
			}
			default: None();
		}
	)
}

mmDisjs2ru(disjs : [MmDisj], vars_1 : Set<int>, vars : Tree<int, int>) -> [RuParseDisj] {
	map(disjs, \d -> 
		RuParseDisj(
			filter(
				map(d.vars, \v -> lookupTreeDef(vars, v, v)), 
				\v -> containsSet(vars_1, v)
			), 
			-1
		)
	)
}

MmRuSyntax(
	vars   : Tree<int, int>,
	types  : Tree<int, RuParseType>,
	rules  : Tree<int, RuParseRule>,
	supers : Set<int>,
	is_rule: (MmStat) -> bool,
	is_def : (MmStat) -> bool,
	stmt_type : (MmExp) -> Maybe<[int]>,
);

mmRuSyntax2s(synt: MmRuSyntax) -> string {
	vars = concatStrings(map(getTreeValues(synt.vars), id2s));
	types = concatStrings(map(getTreeValues(synt.types), ruParse2s));
	rules = concatStrings(map(getTreeValues(synt.rules), ruParse2s));
	supers = superglue(set2array(synt.supers), id2s, ", ");
	"Vars:\n" + strIndent(vars) + "\n" +
	"Types:\n" + strIndent(types) + "\n" +
	"Rules:\n" + strIndent(rules) + "\n" +
	"Supers: " + supers + "\n"
}

mmVar2ru0(var : int) -> int {
	// Russell variables must not contain following three symbols: ')' ',' ':'
	// Replace all these characters with some other. 
	var0 = id2s(var);
	char_codes = s2a(var0);
	if (forall(char_codes, \i -> !(i == 41 || i == 44 || i == 58))) var else {
		var1 = concatStrings(map(char_codes, \i -> 
			if (i == 41) "_]_" else // ) is replaced with _]_
			if (i == 44) "_._" else // , is replaced with _._
			if (i == 58) "_;_" else // : is replaced with _;_
			fromCharCode(i)
		));
		s2id(var1);
	}
}

mmSymbs2TermNodes(symbs: [int], vars: [RuParseVar]) -> [RuTermNode] {
	map(symbs,
		\symb -> eitherMap(
			find(vars, \var -> var.id == symb),
			\var -> RuVar(var.id, var.type),
			RuConstRef(symb)
		)
	)
}

mmSources2Syntax(sources : Tree<int, MmSource>, env : RuEnv) -> MmRuSyntax {
	synt_timer = ruMakeTimer();
	flos = mmGatherAllFloatings(sources);
	vars = foldSet(flos, makeTree(), \acc, flo -> {
		v = flo.var;
		w = mmVar2ru0(v);
		if (w == v) acc else {
			setTree(acc, v, w);
		}
	});
	axioms = mmAxiomatic(sources);
	is_rule = mmStatIsRule(env.conf);
	rules = filtermap(axioms, \ax -> 
		if (is_rule(ax.stat)) {
			rule = mmRule2ru(ax, env, vars);
			//println("RULE:\n" + ruParse2s(rule));
			Some(rule);
		} else {
			None();
		}
	);
	types = set2array(fold(rules, makeSet(), \acc, r -> 
		fold(r.vars, insertSet(acc, r.term.type), \ac, var -> insertSet(ac, var.type))
	));
	superTypesMap = fold(filter(rules, ruRuleIsSuper), makeTree(), \acc, r ->
		setTree(acc, ruRuleInferType(r), 
			switch (lookupTree(acc, ruRuleInferType(r))) {
				Some(supers): insertSet(supers, ruRuleSuperType(r));
				None():       makeSet1(ruRuleSuperType(r));
			}
		)
	);
	typeMap = values2tree(map(types, \t ->
			RuParseType(t, [], set2array(lookupTreeDef(superTypesMap, t, makeSet())), -1)
		),
		\t -> t.id
	);
	ruleMap = values2tree(rules, \r -> r.id);
	super_rules = filter(rules, ruRuleIsSuper);
	supers = buildSet(map(super_rules, \r -> r.term.type));
	if (env.conf.project != "") {
		ruSetDbItem(env.conf.project, ["renamed_vars"], 
			JsonObject(map(tree2pairs(vars), \p -> 
				Pair(id2s(p.first), JsonString(id2s(p.second)))
			))
		);
		ruSetDbItem(env.conf.project, ["super_rules"], 
			JsonObject(map(super_rules, \rule -> {
				super = ruRuleSuperType(rule);
				infer = ruRuleInferType(rule);
				Pair(id2s(rule.id), JsonString(id2s(ruSuperTypeName(super, infer))))
			}))
		);
	}
	syntax = MmRuSyntax(
		vars,
		typeMap,
		ruleMap,
		supers,
		is_rule,
		mmStatIsDef(env.conf),
		mmStatType(env.conf),
	);
	if (ruVerbose(env.conf) > 1) {
		env.out.println("\tmm prepared syntax in: " + ruTimePassed2s(synt_timer));
	}
	syntax;
}

mmTheorem2ru(ass : MmAssertion, stat : MmProvable, structs : MmStructs, env : RuEnv, synt: MmRuSyntax) -> [RuParseDecl] {
	eitherMap(
		mmTheorem2MmRuProofTree(ass, stat, structs, synt.is_rule, env),
		\proof -> {
			th_hyps = mmAssertionHyps(ass.hyps);
			th_vars = mmVars2ru(ass.hyps, synt.vars);
			th_vars_map = fold(th_vars, makeTree(), \acc, v -> setTree(acc, v.id, v.type));
			steps = mmProofTree2ru(proof, th_vars, env, synt, ass);
			vars_map = fold(mmAssertionFlos(concat(ass.hyps, ass.inner)), makeTree(), 
				\acc, v -> setTree(acc, v.var, RuVar(lookupTreeDef(synt.vars, v.var, v.var), v.type))
			);
			make_var = \v -> lookupTreeDef(vars_map, v, ruNoVar);
			disj_set = fold(map(ass.disjs, \d -> d.vars), makeSet(), \acc, disj -> 
				foldi(disj, acc, \i, ac, v1 ->
					foldi(disj, ac, \j, a, v2 ->
						if (i < j && containsKeyTree(vars_map, v1) && containsKeyTree(vars_map, v2)) {
							insertSet(a, RuDisj(make_var(v1), make_var(v2))) 
						} else a
					)
				)
			);
			th_ds = filterSet(disj_set, \d -> containsKeyTree(th_vars_map, d.v1.id) && containsKeyTree(th_vars_map, d.v2.id));
			pr_ds = filterSet(disj_set, \d -> !(containsKeyTree(th_vars_map, d.v1.id) && containsKeyTree(th_vars_map, d.v2.id)));
			make_disj = \s -> map(ruDisjs2Array(s), \ds -> RuParseDisj(map(ds, \d -> d.id), -1));
			prop = mmStmt2ru(ass.stat.expr.symbs, th_vars, env, synt);
			prop_i = findiDef(steps, \step -> step.stmt.expr.symbs == prop.expr.symbs, -1);
			if (steps == []) [] else {
				[RuParseTheorem(
					ass.stat.label, [],
					th_vars, 
					make_disj(th_ds),
					RuParseMeta([], -1),
					mapi(th_hyps, \i, h -> RuParseHyp(i, mmStmt2ru(h.expr.symbs, th_vars, env, synt), -1)), 
					prop, 
					RuParseProof(
						mmVars2ru(ass.inner, synt.vars), 
						make_disj(pr_ds),
						take(steps, prop_i + 1), 
						-1
					), 
					-1
				)]
			}
		},
		[]
	);
}

MmRuProofAcc(
	steps : Tree<RuParseStmt, int>,
	linear : Vector<RuParseStep>
);

mmRemoveTrivialSteps(proof : MmRuProofTree) -> MmRuProofTree {
	switch (proof) {
		MmRuProofTreeStep(name, expr, refs): {
			new_proof = MmRuProofTreeStep(name, expr, map(refs, mmRemoveTrivialSteps));
			if (length(refs) != 1) {
				new_proof; 
			} else {
				switch (new_proof.refs[0]) {
					MmRuProofTreeStep(__, ex, __): 
						if (ex.symbs == expr.symbs) new_proof.refs[0] else new_proof;
					MmRuProofTreeHyp(__, ex): {
						if (ex.symbs == expr.symbs) new_proof.refs[0] else new_proof;
					}
					MmRuProofTreeNone(): new_proof;					
				}
			}
		}
		default: proof;
	}
}

mmProofTree2ru(proof : MmRuProofTree, vars: [RuParseVar], env : RuEnv, synt: MmRuSyntax, th: MmAssertion) -> [RuParseStep] {
	new_proof = mmRemoveTrivialSteps(proof);
	switch (new_proof) {
		MmRuProofTreeStep(__,__,__): 
			vector2array(mmDoProofTree2ru(new_proof, MmRuProofAcc(makeTree(), makeVector(128)), vars, env, synt, th).linear);
		default: [];
	}
}

mmDoProofTree2ru(proof : MmRuProofTreeStep, acc0 : MmRuProofAcc, vars: [RuParseVar],env : RuEnv, synt : MmRuSyntax, th: MmAssertion) -> MmRuProofAcc {
	acc1 = fold(proof.refs, acc0, \ac, aref -> 
		switch (aref) {
			MmRuProofTreeStep(__,__,__): mmDoProofTree2ru(aref, ac, vars, env, synt, th);
			default: ac;
		}
	);
	ru_stmt = mmStmt2ru(proof.expr.symbs, vars, env, synt);
	switch (lookupTree(acc1.steps, ru_stmt)) {
		Some(__): acc1;
		None(): {
			ind = sizeVector(acc1.linear);
			refs = fold(proof.refs, [], \ac, aref -> 
				switch (aref) {
					MmRuProofTreeStep(label, ex, refs):
						concat(ac, [RuParseStepRef(lookupTreeDef(acc1.steps, mmStmt2ru(ex.symbs, vars, env, synt), -1))]);
					MmRuProofTreeHyp(i,__): 
						concat(ac, [RuParseHypRef(i)]);
					MmRuProofTreeNone(): ac;
				}
			);
			pushVector(acc1.linear, RuParseStep(ind, proof.name, refs, ru_stmt, ruParseNoProof, -1));
			MmRuProofAcc(setTree(acc1.steps, ru_stmt, ind), acc1.linear);
		}
	}
}

ruNoParseType = RuParseType(-1, [], [], -1);
