import ds/vector;
import mm/parse/comms;
import mm/refactor/comms;
import mm/mm2ru;
import mm/verify;
import mm/comments;

export {
	mmCommands = concat3(
		mmIoCommands, 
		mmRefactorCommands,
		[
			mmCommMm2Ru, 
			mmCommMmCompressProofs,
			mmCommMmDecompressProofs,
			mmCommVerifyMm,
			mmCommHtml2md
		]
	);
}

mmCommMmCompressProofs = RuComm(
	RuCommInfo("mm-compress-proofs", "misc", "compress Metamath proofs", "", false, false, [], []),
	mmRunMmCompressProofs
);

mmCommMmDecompressProofs = RuComm(
	RuCommInfo("mm-decompress-proofs", "misc", "decompress Metamath proofs", "", false, false, [], []),
	mmRunMmDecompressProofs
);

mmRunMmCompressProofs(unit : RuUnit) -> Maybe<RuState> {
	env = ruUnitEnv(unit);
	state = unit.state;
	task = unit.task;
	sources = state.mm;
	timer = ruMakeTimer();
	arities = mmSources2AssertionsArities(sources, env);
	structs = mmSources2structs(sources);
	counter = ref 0;
	inc_counter = ruAtomicIntInc(counter);
	assertions = mmAssertions(sources);
	compressed_ass = values2tree(
		ruConcurrent(map(
			assertions,
			\ass -> \env1 -> {
				if (ruVerbose(env1.conf) > 2) {
					env.out.println("\t\tcompressing " + id2s(ass.stat.label) + " ...");
				}
				ret = mmCompressAssertion(ass, sources, structs, env1, arities);
				if (ruVerbose(env1.conf) > 2) {
					env1.out.println("\t\t" + id2s(ass.stat.label) + " is compressed");
				}
				if (ret != ass) { 
					inc_counter(1); 
				}
				ret
			}
		), env),
		\ass -> ass.stat.label
	);
	compressed_srcs = mapTree(sources, \src -> 
		MmSource(src with 
			decls = map(src.decls, \decl -> lookupTreeDef(compressed_ass, mmDeclLabel(decl), decl))
		)
	);
	if (ruVerbose(env.conf) > 0) {
		env.out.println("mm compressed " + i2s(^counter) + " proofs in " + ruTimePassed2s(timer));
	}
	Some(RuState(state with 
		mm = compressed_srcs,
		vars = setTree(state.vars, "mm-compress-proofs", pairs2tree([
			Pair("time", flow(ruTimePassed(timer))), 
			Pair("size", flow(^counter))
		]))
	));
}

mmRunMmDecompressProofs(unit : RuUnit) -> Maybe<RuState> {
	env = ruUnitEnv(unit);
	state = unit.state;
	task = unit.task;
	sources = state.mm;
	timer = ruMakeTimer();
	arities = mmSources2AssertionsArities(sources, env);
	counter = ref 0;
	inc_counter = ruAtomicIntInc(counter);
	decompressed = mapTree(sources, \src -> 
		MmSource(src with 
			decls = ruConcurrent(map(src.decls, 
				\decl -> \env1 -> {
					ret : MmDecl = mmDecompressDecl(decl, arities);
					if (ret != decl) { 
						inc_counter(1);
					}
					ret;
				}
			), env)
		)
	);
	if (ruVerbose(env.conf) > 0) {
		env.out.println("mm decompressed " + i2s(^counter) + " proofs in " + ruTimePassed2s(timer));
	}
	Some(RuState(state with 
		mm = decompressed,
		vars = setTree(state.vars, "mm-decompress-proofs", pairs2tree([
			Pair("time", flow(ruTimePassed(timer))), 
			Pair("size", flow(^counter))
		]))
	));
}

mmCommMm2Ru = RuComm(
	RuCommInfo(
		"mm-to-ru", "translate", "translate Metamath to Russell", "file", true, false, 
		[
			RuCommArg("file", false, "input file", ""),
			RuCommArg("fix-left-recursion", true, "fix left recursive grammar rules", "1")
		], []
	),
	mmRunMm2Ru
);

mmRunMm2Ru(unit : RuUnit) -> Maybe<RuState> {
	env = ruUnitEnv(unit);
	state = unit.state;
	task = unit.task;
	timer = ruMakeTimer();
	maybeMap(mm2ru(state.mm, env),
		\math -> {
			fixed_math = if (!ruTaskBoolArg(task, "fix-left-recursion", true)) {
				math 
			} else {
				if (ruVerbose(env.conf) > 0) {
					env.out.println("ru left-recursive rules are fixed");
				}
				ruFixLeftRecursion(math, env);
			}
			RuState(state with 
				ru = fixed_math.sources,
				vars = setTree(state.vars, "mm-to-ru", pairs2tree([
					Pair("time", flow(ruTimePassed(timer))), 
					Pair("size", flow(sizeTree(fixed_math.sources)))
				]))
			)
		}
	);
}

mmCommHtml2md = RuComm(
	RuCommInfo("mm-html2md", "misc", "convert Metamath HTML code from comments to markdown", "", false, false, [], []),
	mmRunHtml2md
);

mmRunHtml2md(unit : RuUnit) -> Maybe<RuState> {
	env = ruUnitEnv(unit);
	state = unit.state;
	timer = ruMakeTimer();
	sources = values2tree(mmHtml2md(getTreeValues(state.mm), env), \src -> src.info.id);
	if (ruVerbose(env.conf) > 0) {
		env.out.println("mm converted HTML in Metamath comments to markdown in " + ruTimePassed2s(timer));
	}
	Some(RuState(state with 
		mm = sources,
		vars = setTree(state.vars, "mm-html2md", pairs2tree([
			Pair("time", flow(ruTimePassed(timer)))
		]))
	));
}
