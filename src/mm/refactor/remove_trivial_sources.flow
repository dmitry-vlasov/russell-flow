import mm/src;
import comm;

export {
	mmCommRemoveTrivialSources : RuComm;
}

mmCommRemoveTrivialSources = RuComm(
	RuCommInfo(
		"mm-remove-trivial-sources", "refactor", "remove all sources, which contain only comments", "keep", false, false, 
		[RuCommArg("keep", true, "keep the sources, listed as CSV", "")], []
	),
	mmRunRemoveTrivialSources
);

mmRunRemoveTrivialSources(unit : RuUnit) -> Maybe<RuState> {
	env = ruUnitEnv(unit);
	task = unit.task;
	state = unit.state;
	sources = state.mm;
	conf = state.conf;
	timer = ruMakeTimer();
	to_keep0 = ruTaskStringArg(task, "keep", "");
	to_keep = buildSet(if (to_keep0 == "") [] else 
		map(strSplit(to_keep0, ","), \file -> s2id(ruTrimPath(file, conf, ".mm")))
	);
	num1 = sizeTree(sources);
	trivial_sources0 = filter(getTreeValues(sources), \src -> 
		mmIsTrivialSource(src) && !containsSet(to_keep, src.info.id)
	);
	trivial_sources = map(trivial_sources0, \src ->
		MmSource(src with 
			imports = filter(src.imports, \imp -> !exists(trivial_sources0, \triv -> imp.module == triv.info.id))
		)
	);
	if (ruVerbose(conf) > 1) {
		env.out.onMessage("mm going to remove " + i2s(length(trivial_sources)) + " trivial sources from: " + i2s(num1) + " modules:\n" +
			strIndent(strGlue(map(trivial_sources, \src -> id2s(src.info.id)), "\n")) + "\n" +
			"to keep: " + strGlue(map(set2array(to_keep), id2s), ", ")
		);
	}
	trivial_removed = fold(trivial_sources, sources, \acc, src ->
		mmRemoveSource(acc, src)
	);
	if (ruVerbose(conf) > 0) {
		env.out.onMessage("mm removed " + i2s(length(trivial_sources)) + " trivial sources in " + ruTimePassed2s(timer));
	}
	Some(RuState(state with 
		mm = trivial_removed,
		vars = setTree(state.vars, "mm-remove-trivial-sources", pairs2tree([
			Pair("time", flow(ruTimePassed(timer))), 
			Pair("size", flow(length(trivial_sources)))
		]))
	));
}

mmRemoveSource(sources : Tree<int, MmSource>, src_to_remove : MmSource) -> Tree<int, MmSource> {
	id_to_remove = src_to_remove.info.id;
	foldTree(sources, makeTree(), \id, src, acc ->
		if (id == id_to_remove) acc else {
			new_src = switch (findi(src.imports, \imp -> imp.module == id_to_remove)) {
				Some(i): {
					new_imports = set2array(mergeSets(
						buildSet(removeIndex(src.imports, i)), 
						buildSet(src_to_remove.imports)
					));
					MmSource(src with imports = new_imports);
				}
				None(): src;
			}
			setTree(acc, id, new_src)
		}
	);
}
