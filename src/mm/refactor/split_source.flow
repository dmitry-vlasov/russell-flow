import base/split;
import mm/src;
import comm;

export {
	mmCommSplitSource : RuComm;
	mmRunSplitSource(unit : RuUnit) -> Maybe<RuState>;
}

mmCommSplitSource = RuComm(
	RuCommInfo(
		"mm-split-source", "refactor", "split a single file into a filesystem", "file", false, false, 
		[
			RuCommArg("file", false, "input file", ""),
			RuCommArg("root", true, "generated root file", ""),
		], []
	),
	mmRunSplitSource
);

mmRunSplitSource(unit : RuUnit) -> Maybe<RuState> {
	env = ruUnitEnv(unit);
	task = unit.task;
	state = unit.state;
	sources = state.mm;
	conf = state.conf;
	file = ruTaskStringArg(task, "file", "");
	module = ruTrimPath(file, conf, ".mm");
	root = ruTaskStringArg(task, "root", module + "_root");
	timer = ruMakeTimer();
	num1 = sizeTree(sources);
	if (ruVerbose(conf) > 1) {
		env.out.println("mm going to split: " + i2s(num1) + " modules");
	}
	split_sources = foldTree(sources, makeTree(), \name, src, acc -> {
			if (ruVerbose(conf) > 1) {
				env.out.println("\tgoing to split module: '" + src.info.module + "'");
			}
			mergeTree(acc, mmSplitSources(src, env, module))
		}
	);
	root_info = ruNewFileInfo(root, conf, ".mm");
	if (ruVerbose(conf) > 1) {
		env.out.println("\troot module: '" + root_info.module + "', path: '" + root_info.path + "'");
	}
	root_src = MmSource(
		map(getTreeValues(split_sources), \src -> MmImport(src.info.id)),
		[], root_info
	);
	if (ruVerbose(conf) > 0) {
		env.out.println("mm split " + i2s(num1) + " files into " + i2s(sizeTree(split_sources) + 1) + " in " + ruTimePassed2s(timer));
	}
	Some(RuState(state with 
		mm = setTree(split_sources, s2id(module), root_src),
		vars = setTree(state.vars, "split-source-mm", pairs2tree([
			Pair("time", flow(ruTimePassed(timer))), 
			Pair("size", flow(sizeTree(split_sources) + 1))
		]))
	));
}

MmSplitSource(
	pattern : string,
	header : string,
	path : string,
	blocks : ref [MmDecl],
	subs : ref [MmSplitSource]
);

mmSplitLevel(src : MmSplitSource, pattern : string) -> MmSplitSource {
	makePath = \h -> ruStrReplaceAll(h, " /:.?!;$\\\"'`#", "_");
	addBlock = \acc, block -> { 
		refArrayPush(lastElement(^(acc.subs), acc).blocks, block); 
		acc 
	}
	addSrc = \acc, h -> {
		path = (if (src.path != "") src.path + "/" else "") + makePath(h);
		refArrayPush(acc.subs, MmSplitSource(pattern, h, path, ref [], ref [])); 
		acc 
	}
	getHeader = \txt -> trim2(strSplit(txt, "\n")[2], " \t\n\r");
	isHeader = \txt -> {
		if (!(strContains(txt, pattern) && strCountOf(txt, "\n") > 1)) false else {
			getHeader(txt) != "";
		}
	}
	fold(^(src.blocks), MmSplitSource(src with blocks = ref [], subs = ref []), \acc, block -> 
		switch (block) {
			MmComment(text): {
				if (!isHeader(text)) {
					addBlock(acc, block)
				} else {
					addBlock(addSrc(acc, getHeader(text)), block);
				}
			}
			default: addBlock(acc, block);
		}
	);
}

mmSplitSourceRec(src : MmSplitSource, p : RuSplitPattern) -> MmSplitSource {
	parts = mmSplitLevel(src, p.pattern);
	switch (p.child) {
		None(): parts;
		Some(ch_p):
			MmSplitSource(parts with
				subs = ref map(^(parts.subs), \part -> mmSplitSourceRec(part, ch_p))
			);
	}
}

mmDoSplitSource(src : MmSource, env : RuEnv) -> MmSplitSource {
	splitted = mmSplitSourceRec(
		MmSplitSource("", "", "", ref src.decls, ref []), 
		mm_split_pattern
	);
	if (^(splitted.subs) != []) splitted else {
		MmSplitSource(splitted with path = src.info.path);
	}
}

MmLinearizeAcc(
	srcs : Tree<int, MmSource>,
	last : Maybe<MmSource>
);

mmMakeSplittedFile(file : string, acc : MmLinearizeAcc) -> string {
	if (!containsKeyTree(acc.srcs, s2id(file))) file else {
		mmMakeSplittedFile(file + "1", acc)
	}
}

mmLinearizeSplitSources(original: MmSource, src : MmSplitSource, acc : MmLinearizeAcc, env : RuEnv, file0 : string) -> MmLinearizeAcc {
	file = mmMakeSplittedFile(if (src.path == "") file0 + "_init" else src.path, acc);
	module = ruTrimPath(file, env.conf, ".mm");
	header_pattern = lpad("", src.pattern, 40);
	curr_src = MmSource(
		eitherMap(acc.last, \last -> [MmImport(last.info.id)], original.imports), 
		^(src.blocks), 
		ruNewFileInfo(module, env.conf, ".mm")
	);
	if (ruVerbose(env.conf) > 1) {
		env.out.println("\tmm splitted file: '" + curr_src.info.module + "'");
	}
	fold(^(src.subs), 
		MmLinearizeAcc(setTree(acc.srcs, s2id(file), curr_src), Some(curr_src)), 
		\ac, sub -> mmLinearizeSplitSources(original, sub, ac, env, file0)
	);
}

mmSplitSources(src : MmSource, env : RuEnv, file : string) -> Tree<int, MmSource> {
	mmLinearizeSplitSources(src,
		mmDoSplitSource(src, env),
		MmLinearizeAcc(makeTree(), None()), 
		env, file
	).srcs;
}
