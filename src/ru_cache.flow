import text/binary;
import ru_src;
import ru_log;

export {
	RuCachedSource(
		source : RuSource, 
		dependencyChanged : () -> bool, 
		version : int
	);

	RuCachedDecl(
		decl : RuDecl,
		src : string, // name of a source of origin
	);

	// Cached source code.
	RuCachedSourceText(
		text : string,
		modified : double
	);

	ruToCache(src : RuSource, conf : RuConf) -> void;
	ruFromCache(file : string, conf : RuConf) -> Maybe<RuCachedSource>;
	ruLoadToCache(file : string, conf : RuConf) -> void;
	ruClearCache() -> void;
	ruSourcesCache() -> [RuSource];
	ruMakeCacheDir(conf : RuConf) -> void;

	ruCachedDecl(name : string) -> Maybe<RuCachedDecl>;

	// Lookups a cached file source, if not found - loads and stores.
	ruFileSource(path : string) -> string;
	// Stores a certain string as a source for some path.
	ruSetFileSource(path : string, src : string) -> void;
	// Loads file source from filesystem. May be used to undo fcSetFileSource function.
	ruLoadFileSource(path : string) -> void;

	ruRange2s(info : RuFileInfo, range : RuRange, msg : string) -> string;

	// Shows cache statistics
	ruCacheInfo(full : bool) -> string;

	ruDeclRange(d : RuDecl, src : RuSource) -> RuRange;
}

ruCacheVersion = 1;

ruSourceCache = initConcurrentHashMap();
ruNameCache = initConcurrentHashMap();
ruTextCache = initConcurrentHashMap(); // Sources of files as strings
ruDefCache = initConcurrentHashMap();  // Definitions

ruClearCache() -> void {
	clearConcurrentHashMap(ruSourceCache);
	clearConcurrentHashMap(ruNameCache);
	clearConcurrentHashMap(ruTextCache);
}

ruSourcesCache() -> [RuSource] {
	filtermap(valuesConcurrentHashMap(ruSourceCache), idfn);
}

ruMakeCacheDir(conf : RuConf) -> void {
	err = ensureDirectoryExists(ruCacheDir(conf));
	if (err != "") {
		ruPrintln(err, conf);
	}
}

ruCacheDir(conf : RuConf) -> string {
	ruOptDef("cache-dir", getRussellDir() + "/cache", conf);
}

ruCachedPath(file : string, conf : RuConf) -> string {
	ruCacheDir(conf) + "/" + strReplace(strReplace(file, "/", "_"), ":", "_")  + ".cache"
}

RuCachedFileSource(
	source : RuSource,
	version : int
);

ruDepChangedCallback(src : RuSource, conf : RuConf) -> () -> bool {
	\ -> {
		check = exists(src.imports, \imp ->
			switch (ruFromCache(imp.path, conf)) {
				Some(cached): {
					src.info.lastupdate < cached.source.info.lastupdate || cached.dependencyChanged();
				}
				None(): {
					conf.onError("a module " + imp.path + " must be already in cache, but is not there", []); true; 
				}
			}
		);
		setConcurrentHashMap(ruSourceCache, src.info.file, Some(RuCachedSource(src, \ -> check, ruCacheVersion)));
		check
	}
}

ruUpdateNameCache(src : RuSource, conf : RuConf) -> void {
	//ruDebugLog1("ruUpdateNameCache: started " + src.info.file);
	iter(src.decls, \decl ->
		switch (decl) {
			RuDecl(): setConcurrentHashMap(ruNameCache, ruDeclName(decl), Some(RuCachedDecl(decl, src.info.file)));
			default: { }
		}
	);
	//ruDebugLog1("ruUpdateNameCache: finished " + src.info.file);
}

ruUpdateDefCache(src : RuSource, conf : RuConf) -> void {
	//ruDebugLog1("ruUpdateDefCache: started " + src.info.file);
	iter(src.decls, \decl -> 
		switch (decl) {
			RuDef(__,__,__,__, defm, __,__,__): {
				switch (defm.nodes[0]) {
					RuRuleRef(rule_name,__,__,__): {
						switch (getConcurrentHashMap(ruNameCache, rule_name, None())) {
							Some(cached_rule): {
								rule = cast(cached_rule.decl : RuDecl -> RuRule);
								non_bracket_consts = filtermap(rule.term.expr, \n ->
									switch (n) {
										RuConstRef(name): 
											if (name != "(" && name != ")" && name != "," && name != ":") Some(name) 
											else None();
										RuVar(__,__): None(); 
									}
								);
								ruDebugLog1("DEFIENDUM: " + ruExpRPN2s(defm.nodes));
								ruDebugLog1("TERM: " + ruTerm2s(rule.term.expr));
								if (length(non_bracket_consts) > 0) {
									defiendum = non_bracket_consts[0];
									ruDebugLog1("ADDING DEF: " + defiendum);
									setConcurrentHashMap(ruDefCache, defiendum, Some(RuCachedDecl(decl, src.info.file)));
								} else {
									ruDebugLog1("ONLY BRACKETS");
								}
							}
							None(): conf.onError("Rule " + rule_name + " is not found in cache", []);
						}
					}
					default: { 
						ruDebugLog1("not a rule ref: " + ruExpRPN2s(defm.nodes));
					}
				}
			}
			default: { }
		}
	);
	//ruDebugLog1("ruUpdateDefCache: finished " + src.info.file);
}

ruToCache(src : RuSource, conf : RuConf) -> void {
	if (!ruOptIsFalse("caching", conf)) {
		cached = RuCachedSource(src, ruDepChangedCallback(src, conf), ruCacheVersion);
		setConcurrentHashMap(ruSourceCache, src.info.file, Some(cached));
		ruUpdateNameCache(src, conf);
		ruUpdateDefCache(src, conf);
		if (!setFileContent(ruCachedPath(src.info.file, conf), toBinary(RuCachedFileSource(cached.source, cached.version)))) {
			conf.onError("cannot save cached source " + src.info.file, []);
		}
		if (conf.verbose > 1) {
			ruPrintln("saved to cached: " + src.info.file, conf);
		}
	}
}

ruFromCache(file0 : string, conf : RuConf) -> Maybe<RuCachedSource> {
	file = ruTrimPath(file0, conf, ".ru");
	if (ruOptIsFalse("caching", conf)) None() else {
		if (containsConcurrentHashMap(ruSourceCache, file)) {
			getConcurrentHashMap(ruSourceCache, file, None());
		} else {
			path = ruCachedPath(file, conf);
			if (!fileExists(path)) None() else {
				deserialized = deserializeBinary(getFileContent(path), IllegalStruct());
				if (deserialized == IllegalStruct()) {
					conf.onError("WARNING: Illegal structure of cache file for " + file + ". Incremental file deleted.", []);
					ruDeleteFromCache(file, path, conf);
					None();
				} else {
					cached_file = cast(deserialized : flow -> RuCachedFileSource);
					if (cached_file.version == ruCacheVersion) {
						changed = ruFileChanged(cached_file.source.info);
						if (changed != "") {
							if (conf.verbose >= 1) {
								println(changed);
							}
							ruDeleteFromCache(file, path, conf);
							None();
						} else {
							if (conf.verbose > 1) {
								ruPrintln("loaded from cache: " + file, conf);
							}
							cached = RuCachedSource(cached_file.source, ruDepChangedCallback(cached_file.source, conf), cached_file.version);
							setConcurrentHashMap(ruSourceCache, file, Some(cached));
							ruUpdateNameCache(cached.source, conf);
							Some(cached);
						}
					} else {
						if (conf.verbose > 1) {
							ruPrintln("cached file " + file + " is has version " + i2s(cached_file.version) + " while current version is " + i2s(ruCacheVersion), conf);
							ruDeleteFromCache(file, path, conf);
						}
						None();
					}
				}
			}
		}
	}
}

ruLoadToCache(file0 : string,  conf : RuConf) -> void {
	file = ruTrimPath(file0, conf, ".ru");
	//ruDebugLog1("ruLoadToCache: started " + file);
	sources = ruDoLoadToCache(file, conf, [], makeSet()).first;
	//ruDebugLog1("ruLoadToCache: sources are loaded " + file);
	iter(sources, \src -> ruUpdateDefCache(src, conf));
	//ruDebugLog1("ruLoadToCache: finished " + file);
}

ruDoLoadToCache(file : string,  conf : RuConf, acc : [RuSource], visited : Set<string>) -> Pair<[RuSource], Set<string>> {
	if (containsSet(visited, file)) Pair(acc, visited) else {
		new_visited = insertSet(visited, file);
		switch (ruFromCache(file, conf)) {
			Some(cached):
				fold(
					cached.source.imports,
					Pair(concat(acc, [cached.source]), new_visited), 
					\ac, imp -> ruDoLoadToCache(imp.path, conf, ac.first, ac.second)
				);
			None(): Pair(acc, new_visited);
		}
	}
}

ruDeleteFromCache(file : string, path : string, conf : RuConf) -> void {
	if (conf.verbose > 1) {
		ruPrintln("Deleting outdated cached " + file, conf);
	}
	removeConcurrentHashMap(ruSourceCache, file);
	err = deleteFile(path);
	if (err != "") {
		if (fileExists(path)) {
			conf.onError("Could not delete " + path + ": " + err, []);
		}
	}
}

ruCachedDecl(name : string) -> Maybe<RuCachedDecl> {
	switch (getConcurrentHashMap(ruDefCache, name, None())) {
		Some(decl): Some(decl);
		None(): getConcurrentHashMap(ruNameCache, name, None());
	}
}

ruFileSource(path : string) -> string {
	if (!containsConcurrentHashMap(ruTextCache, path)) {
		setConcurrentHashMap(ruTextCache, path, RuCachedSourceText(getFileContent(path), fileModified(path)));
	}
	cached = getConcurrentHashMap(ruTextCache, path, RuCachedSourceText("", 0.0));
	modified = fileModified(path);
	if (cached.modified != modified) {
		// Reload outdated source.
		text = getFileContent(path);
		setConcurrentHashMap(ruTextCache, path, RuCachedSourceText(text, modified));
		text;
	} else {
		cached.text;
	}
}

ruSetFileSource(path : string, text : string) -> void {
	if (!containsConcurrentHashMap(ruTextCache, path)) {
		setConcurrentHashMap(ruTextCache, path, RuCachedSourceText(text, fileModified(path)));
	} else {
		cached = getConcurrentHashMap(ruTextCache, path, RuCachedSourceText("", 0.0));
		setConcurrentHashMap(ruTextCache, path, RuCachedSourceText(text, cached.modified));
	}
}

ruLoadFileSource(path : string) -> void {
	setConcurrentHashMap(ruTextCache, path, RuCachedSourceText(getFileContent(path), fileModified(path)));
}

ruRange2s(info : RuFileInfo, range : RuRange, msg : string) -> string {
	if (info.path == "") "" else {
		code = ruFileSource(info.path);
		resolver = makeLineResolver(code);
		s = findLine(resolver, range.start);
		if (range.end < 0) {
			info.path + ":" + i2s(s.lineno) + ":" + i2s(s.lineIndex + 1) + ":" + (if (msg == "") "" else " " + msg);
		} else {
			e = findLine(resolver, range.end);
			info.path + ":" + i2s(s.lineno) + ":" + i2s(s.lineIndex + 1) + ":" + i2s(e.lineno) + ":" + i2s(e.lineIndex + 1) + ":" + (if (msg == "") "" else " " + msg);
		}
	}
}

ruCacheInfo(full : bool) -> string {
	ret = 
	"cached sources: " + i2s(sizeConcurrentHashMap(ruSourceCache)) + "\n" +
	"cached names: " + i2s(sizeConcurrentHashMap(ruNameCache)) + "\n" +
	"cached source texts: " + i2s(sizeConcurrentHashMap(ruTextCache)) + "\n" +
	"cached definitions: " + i2s(sizeConcurrentHashMap(ruDefCache)) + "\n" +
	if (!full) "" else {
		"cached sources:\n" + strGlue(map(keysConcurrentHashMap(ruSourceCache), \n -> "\t" + n), "\n") + "\n"
		"cached source texts:\n" + strGlue(map(keysConcurrentHashMap(ruTextCache), \n -> "\t" + n),"\n") + "\n"
		"cached definitions:\n" + strGlue(map(keysConcurrentHashMap(ruDefCache), \n -> "\t" + n),"\n")
	};
	ruDebugLog1("ruCacheInfo:\n" + ret);
	ret
}

ruDeclRange(d : RuDecl, src : RuSource) -> RuRange {
	ind = ruIndexOfDecl(ruDeclName(d), src);
	text = ruFileSource(src.info.path);
	start = src.decls[ruIndexWithComments(ind, src)].pos;
	//start = src.decls[ind].pos;
	end = if (ind + 1 < length(src.decls)) {
		src.decls[ind + 1].pos;
	} else {
		strlen(text);
	}
	switch (d) {
		RuTheorem(__,__,__,__,__,__,__,__):
			RuRange(start, strRangeIndexOf(text, "proof {", start, end));
		default: RuRange(start, end);
	}
}
