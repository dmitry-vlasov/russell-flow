import base/conf;
import ru/cache/cache;
import ru/util/update;
import ru/parse/comms;
import comm;

export {
	ruSyncCommands : [RuComm];
	ruCacheCommands : [RuComm];
}

ruCacheCommands = [
	RuComm(
		RuCommInfo(
			"cache-load", "sync", "read cached sources", "file", false, false,
			[
				RuCommArg("file", true, "input file", ""),
				RuCommArg("full", true, "load complete files, not just declarations", "0"),
				RuCommArg("imports", true, "load all imported modules transitively", "0")
			], []
		),
		\unit -> {
			env = ruUnitEnv(unit);
			conf = env.conf;
			file = lookupTreeDef(unit.task.args, "file", "");
			mode = if (lookupTreeDef(unit.task.args, "full", "0") == "1") "exact" else "proofless";
			imports = (lookupTreeDef(unit.task.args, "imports", "0") == "1");
			timer = ruMakeTimer();
			if (file != "") {
				ruCacheLoad(ruFileId(file, env.conf), mode, imports, env); 
			} else {
				ruCacheLoadAll(mode, env);
			}
			if (ruVerbose(conf) > 0) {
				if (ruVerbose(conf) > 1 || ruTimePassed(timer) > 100.0) {
					unit.out.println("cache loaded in " + ruTimePassed2s(timer));
				}
			}
			Some(unit.state); 
		}
	),
	RuComm(
		RuCommInfo(
			"cache-clear", "sync", "clear up a cache: mem, disk or all", "what", false, false,
			[RuCommArg("what", false, "which cache: mem, disk or all to clear", "mem")], []
		),
		\unit -> {
			env = ruUnitEnv(unit);
			cache = lookupTreeDef(unit.task.args, "what", "mem");
			if (cache == "mem") {
				ruMemCacheClear();
			} else if (cache == "disk") {
				ruDiskCacheClear(env);
			} else if (cache == "all") {
				ruCacheClear(env);
			}
			Some(unit.state); 
		}
	),
	RuComm(
		RuCommInfo(
			"cache-save", "sync", "save cached sources", "file", false, false,
			[RuCommArg("file", true, "input file", "")], []
		),
		\unit -> { 
			env = ruUnitEnv(unit);
			file = lookupTreeDef(unit.task.args, "file", "");
			if (file == "") {
				ruCacheSaveAll(env); 
			} else {
				ruCacheSave(ruFileId(file, env.conf), env); 
			}
			Some(unit.state); 
		}
	),
	RuComm(
		RuCommInfo(
			"cache-info", "sync", "show cache info", "what", false, false,
			[
				RuCommArg("what", true, "what info to show: all, lang, disk,...", "all"),
				RuCommArg("full", true, "extended info or concise", "0")
			], []
		),
		\unit -> { 
			env = ruUnitEnv(unit);
			what = lookupTreeDef(unit.task.args, "what", "all");
			full = lookupTreeDef(unit.task.args, "full", "0") == "1";
			if (what == "all") {
				env.out.println(ruCacheInfo(full, env));
			} else if (what == "lang") {
				env.out.println(ruLangCacheInfo(full));
			} else if (what == "disk") {
				env.out.println(ruDiskCacheInfo(full, env));
			} else {
				// TODO
				env.out.println("'cache-info what=" + what + "'' is not yet implemented");
			}
			Some(unit.state); 
		}
	),
];

ruSyncSourceCode(id : int, env : RuEnv) -> void {
	switch (ruCacheGet(id, true, env)) {
		Some(exact): {
			upd_exact = ruUpdateSourcePositions(exact, env);
			ruCacheAdd(upd_exact, true, env);
			env.out.println("exact source '" + id2s(id) + "' positions are updated");
		}
		None(): {
			switch (ruCacheGet(id, false, env)) {
				Some(proofless): {
					upd_proofless = ruUpdateSourcePositions(proofless, env);
					ruCacheAdd(upd_proofless, false, env);
					env.out.println("proofless source '" + id2s(id) + "' positions are updated");
				}
				None(): {
					env.out.println("source '" + id2s(id) + "' positions are NOT updated (source is not cached)");
				}
			}
		}
	}
}

ruSyncCommands = [
	RuComm(
		RuCommInfo(
			"sync-file-source", "sync", "load an updated file source", "file", false, false,
			[RuCommArg("file", false, "input file", ""),  RuCommArg("text", false, "updated source", "")], []
		),
		\unit -> {
			env = ruUnitEnv(unit);
			file = lookupTreeDef(unit.task.args, "file", "");
			id = ruFileId(file, env.conf);
			code = lookupTreeDef(unit.task.args, "text", "");
			ruCodeCacheSet(id, code, env); 
			ruSyncSourceCode(id, env);
			Some(unit.state);
		}
	),
	RuComm(
		RuCommInfo(
			"sync-file-changed", "sync", "apply changes to a file source", "file", false, false,
			[
				RuCommArg("file", false, "input file", ""),  
				RuCommArg("change-json", false, "change in JSON format", "")
			], []
		),
		\unit -> {
			env = ruUnitEnv(unit);
			conf = env.conf;
			json = ruTaskStringArg(unit.task, "change-json", "");
			switch (json2LspDidChangeTextDocumentParams(parseJson(json))) {
				Some(params): {
					file = ruCorrectUriPath(params.textDocument.uri.path);
					id = ruFileId(file, conf);
					contents = ruCodeCacheGet(id, env).code;
					changed = fold(params.contentChanges, contents, \acc, changes -> lspApplyContentChangeEvent(changes, acc));
					ruCodeCacheSet(id, changed, env);
					ruSyncSourceCode(id, env);
				}
				None(): {
					unit.out.println("file-change: An option 'change-json' must be a vaid Json-encoded LSP DidChangeTextDocimentParams, got: " + json);
				}
			}
			Some(unit.state);
		}
	),
	RuComm(
		RuCommInfo(
			"sync-file-saved", "sync", "file was saved", "file", false, false,
			[RuCommArg("file", false, "input file", "")], []
		),
		\unit -> { 
			env = ruUnitEnv(unit);
			conf = env.conf;
			file = ruTaskStringArg(unit.task, "file", "");
			id = ruFileId(file, conf);
			path = ruFindPath(file, conf, ".ru");
			if (fileExists(path)) {
				code = getFileContent(path);
				ruCodeCacheSet(id, code, env); 
				ruSyncSourceCode(id, env);
				ruRunRead(unit);
			} else {
				env.out.error("file: " + file + " is not found at sync-file-saved", []);
				Some(unit.state);
			}
		}
	)
];
