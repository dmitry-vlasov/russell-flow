import text/binary;
import ru/util/src;
import base/util/fileinfo;

export {
	ruDiskCacheFiles(env : RuEnv, mode : string) -> [string];
	ruDiskCacheMakeDir(env : RuEnv) -> void;
	ruDiskCacheClear(env : RuEnv) -> void;
	ruDiskCacheSave(src : RuSource, env : RuEnv) -> void;
	ruDiskCacheDelete(id : int, env : RuEnv) -> void;
	ruDiskCacheLoad(id : int, mode : string, env : RuEnv) -> Pair<Maybe<RuSource>, string>;

	ruDiskCacheInfo(full : bool, env : RuEnv) -> string;
}

ruCacheVersion = 8;

RuCachedFileSource(
	source : RuSource,
	version : int
);

ruDiskCacheMakeDir(env : RuEnv) -> void {
	ruEnsureDirectoryExists(ruCacheDir(env.conf, "exact"), env.out);
	ruEnsureDirectoryExists(ruCacheDir(env.conf, "proofless"), env.out);
}

ruDiskCacheFiles(env : RuEnv, mode : string) -> [string] {
	ruDoDiskCacheFiles(ruCacheDir(env.conf, mode), env);
}

ruDoDiskCacheFiles(dir : string, env : RuEnv) -> [string] {
	dir_content = readDirectory(dir);
	fold(readDirectory(dir), [], \acc, name -> {
		path = rtrim2(dir, "/") + "/" + name;
		if (isDirectory(path)) {
			concat(acc, ruDoDiskCacheFiles(path, env));
		} else if (endsWith(path, ".cache")) {
			concat(acc, [path]);
		} else {
			acc
		}
	});
}

ruDiskCacheClear(env : RuEnv) -> void {
	errs = ruDeleteDir(ruCacheDir(env.conf, ""));
	iter(errs, \err -> env.out.error(err, []));
}

ruDiskCacheSave(s : RuSource, env : RuEnv) -> void {
	src = RuSource(s with info = ruUpdateFileInfo(s.info, env.conf.workingDir));
	ruDiskCacheDoSave(
		toBinary(RuCachedFileSource(src, ruCacheVersion)), 
		src.info.module, "exact", env
	);
	ruDiskCacheDoSave(
		toBinary(RuCachedFileSource(
			ruClearSourceProofs(src), 
			ruCacheVersion
		)), 
		src.info.module, "proofless", env
	);
	if (ruVerbose(env.conf) > 1) {
		env.out.println("\tsaved to cache: '" + src.info.module + "'");
	}
}

ruDiskCacheDoSave(binary : string, module: string, mode : string, env : RuEnv) -> void {
	path = ruCachedPath(module, mode, env.conf);
	ruEnsureFileDirectoryExists(path, env.out);
	if (!setFileContent(path, binary)) {
		env.out.error("cannot save cached source '" + module + "'", []);
	}
}

ruDiskCacheLoad(id : int, mode : string, env : RuEnv) -> Pair<Maybe<RuSource>, string> {
	conf = env.conf;
	if (ruOptsString(conf.opts, "disk-caching", "") == "0") {
		Pair(None(), "");
	} else {
		file = id2s(id);
		cached_path = ruCachedPath(file, mode, conf);
		if (!fileExists(cached_path)) {
			message = "Cache file: '" + cached_path + "' doesn't exist";
			env.out.trace("disk-cache", \-> message);
			if (conf.verbose > 2) {
				env.out.println(message);
			}
			Pair(None(), message);
		} else {
			serialized = getFileContent(cached_path);
			deserialized = deserializeBinary(serialized, IllegalStruct());
			ret = if (deserialized == IllegalStruct()) {
				message = "WARNING: Illegal structure of cache file for '" + file + "'. Cache file is deleted.";
				Pair(None(), message);
			} else {
				cached = cast(deserialized : flow -> RuCachedFileSource);
				file_path = ruFindPath(file, conf, ".ru");
				if (cached.version == ruCacheVersion) {
					src = cached.source;
					changed = ruFileChangedMessage(src.info, file_path);
					if (changed != "") {
						ruDiskCacheDelete(id, env);
						env.out.trace("disk-cache", \-> changed);
						if (conf.verbose > 2) {
							env.out.println(changed);
						}
						Pair(None(), changed);
					} else {
						if (src.info.ids_ver != ruIdsVersion()) {
							message = "Cached file " + file + " has outdated version of ids map: " + time2stringUtc(src.info.ids_ver) + 
									" while current ids version is: " + time2stringUtc(ruIdsVersion()) + ". Cache file is deleted.";
							ruDiskCacheDelete(id, env);
							env.out.trace("disk-cache", \-> message);
							if (conf.verbose > 2) {
								env.out.println(message);
							}
							Pair(None(), message);
						} else {
							if (file_path != src.info.path) {
								message = "Cached file " + file + " has different full path:\n" + 
									"\t" + src.info.path + "\n\t!=\n\t" + file_path + "\n";
								ruDiskCacheDelete(id, env);
								env.out.trace("disk-cache", \-> message);
								if (conf.verbose > 2) {
									env.out.println(message);
								}
								Pair(None(), message);
							} else {
								// Successfull cached file loaded
								//env.out.trace("disk-cache", \-> "successfully loaded: " + file);
								if (conf.verbose > 3) {
									env.out.println("successfully loaded from disk cache: " + file);
								}
								Pair(Some(src), "");
							}
						}
					}
				} else {
					message = "Cached file " + file + " has obsolete cache version:\n" + 
						"cached.version != ruCacheVersion: " + i2s(cached.version) + " != " + i2s(ruCacheVersion);
					ruDiskCacheDelete(id, env);
					if (conf.verbose > 2) {
						env.out.println(message);
					}
					env.out.trace("disk-cache", \-> message);
					Pair(None(), message);
				}
			}
			if (isNone(ret.first)) {
				ruDiskCacheDelete(id, env);
			}
			ret;
		}
	}
}

ruDiskCacheDelete(id : int, env : RuEnv) -> void {
	if (ruOptsString(env.conf.opts, "disk-caching", "") != "0") {
		file = id2s(id);
		del_path = \path -> {
			if (!fileExists(path)) {
				env.out.trace("disk-caching", \-> "source '" + file + "' is not cached on filesystem");
			} else {
				err = deleteFile(path);
				if (err != "") {
					env.out.println("error wile deleting source '" + file + "': " + err);
				}
			}
		}
		del_path(ruCachedPath(file, "exact", env.conf));
		del_path(ruCachedPath(file, "proofless", env.conf));
	}
}

ruDiskCacheInfo(full : bool, env : RuEnv) -> string {
	cached = ruDiskCacheFiles(env, "exact");
	if (!full) {
		"filesystem cache: " + i2s(length(cached))
	} else {
		"filesystem cache:\n" + strGlue(map(cached, \n -> "\t" + n), "\n")
	};
}
