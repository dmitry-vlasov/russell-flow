import base/util/timer;
import ru/cache/code;
import ru/cache/rule;
import ru/cache/decl;
import ru/cache/def;
import ru/cache/src;
import ru/cache/disk;

export {
	ruCacheAdd(src : RuSource, exact : bool, env : RuEnv) -> void;
	ruCacheGet(id : int, exact, env : RuEnv) -> Maybe<RuSource>;
	ruCacheRemove(id : int, env : RuEnv) -> void;

	ruCacheLoad(id : int, mode : string, env : RuEnv) -> void;
	ruCacheLoadAll(mode : string, env : RuEnv) -> void;

	ruCacheClear(env : RuEnv) -> void;
	ruMemCacheClear() -> void;

	ruCacheSaveAll(env : RuEnv) -> void;
	ruCacheSave(id : int, env : RuEnv) -> void;

	ruCachedDecl2s(id : int, env : RuEnv) -> string;
	ruCachedDecl2LspSymbolInfo(id : int, env : RuEnv) -> Maybe<LspSymbolInformation>;

	ruCacheInfo(full : bool, env : RuEnv) -> string;
}


ruCacheClear(env : RuEnv) -> void {
	ruDiskCacheClear(env);
	ruMemCacheClear();
}

ruMemCacheClear() -> void {
	ruCodeCacheClear();
	ruSourceCacheClear();
	ruRuleCacheClear();
	ruDefCacheClear();
	ruDeclCacheClear();
}

ruCacheAdd(src : RuSource, exact : bool, env : RuEnv) -> void {
	if (!ruSourceCacheContains(src.info.id, exact, env)) {
		ruSourceCacheAdd(src, exact, env);
		ruRuleCacheAdd(src, env);
		ruDefCacheAdd(src, env);
		ruDeclCacheAdd(src, env);
	}
}

ruCacheGet(id : int, exact : bool, env : RuEnv) -> Maybe<RuSource> {
	mode = if (exact) "exact" else "proofless";
	if (lookupTreeDef(env.conf.opts, "caching", "") == "0") None() else {
		switch (ruSourceCacheGet(id, exact, env)) {
			Some(cached): Some(cached);
			None(): {
				switch (ruDiskCacheLoad(id, mode, env)) {
					Some(loaded): {
						ruCacheAdd(loaded, true, env);
						Some(loaded);
					}
					None(): None();
				}
			}
		}	
	}
}

ruCacheRemove(id : int, env : RuEnv) -> void {
	maybeApply(ruCacheGet(id, false, env), \src -> {
		ruRuleCacheRemove(src, env);
		ruDefCacheRemove(src, env);
		ruDeclCacheRemove(src, env);
	});
	ruCodeCacheRemove(id, env);
	ruSourceCacheRemove(id, true, env);
	ruSourceCacheRemove(id, false, env);
}

ruCacheSaveAll(env : RuEnv) -> void {
	all_sources = concat(
		ruSourceCacheAll(true, env),
		ruSourceCacheAll(false, env)
	);
	iter(all_sources, 
		\src -> ruDiskCacheSave(src, env)
	);
}

ruCacheSave(id : int, env : RuEnv) -> void {
	switch (ruSourceCacheGet(id, true, env)) {
		Some(cached): ruDiskCacheSave(cached, env);
		None(): { }
	}
	switch (ruSourceCacheGet(id, false, env)) {
		Some(cached): ruDiskCacheSave(cached, env);
		None(): { }
	}
}

ruCacheLoad(id : int, mode : string, env : RuEnv) -> void {
	exact = (mode == "exact");
	if (!ruSourceCacheContains(id, exact, env)) {
		loaded = ruDoCacheLoad(id, mode, env, [], makeSet()).first;
		iter(loaded, \src -> ruCacheAdd(src, exact, env));
	}
}

ruDoCacheLoad(id : int, mode : string, env : RuEnv, acc : [RuSource], visited : Set<int>) -> Pair<[RuSource], Set<int>> {
	if (containsSet(visited, id) || ruSourceCacheContains(id, mode == "exact", env)) {
		Pair(acc, visited) 
	} else {
		new_visited = insertSet(visited, id);
		switch (ruDiskCacheLoad(id, mode, env)) {
			Some(cached): {
				fold(
					cached.imports,
					Pair(concat(acc, [cached]), new_visited), 
					\ac, imp -> ruDoCacheLoad(imp.id, mode, env, ac.first, ac.second)
				);
			}
			None(): {
				Pair(acc, new_visited);
			}
		}
	}
}

ruCacheLoadAll(mode : string, env : RuEnv) -> void {
	conf = env.conf;
	timer = ruMakeTimer();
	cached = ruDiskCacheFiles(env, mode);
	exact = (mode == "exact");
	ruConcurrentVoid(map(cached, \path -> \ -> {
		file = ruCachedFile(path, mode, env.conf);
		if (file == "") {
			env.out.onError("Cache path '" + path + "' was not converted to a cached module name", []);
		} else {
			module = ruTrimPath(file, env.conf, ".ru");
			if (ruVerbose(conf) > 1) {
				env.out.onMessage("\tmem cache loading: '" + module + "' module from disk");
			}
			maybeApply(ruDiskCacheLoad(s2id(module), mode, env), \loaded -> 
				ruCacheAdd(loaded, exact, env)
			);
		}
	}));
	if (ruVerbose(conf) > 0) {
		env.out.onMessage("mem cache loaded: " + i2s(length(cached)) + " modules in " + ruTimePassed2s(timer));
	}
}

ruCachedDecl2s(id : int, env : RuEnv) -> string {
	eitherMap(ruDeclCacheGet(id), \decl -> 
		eitherMap(ruCacheGet(decl.descr.src, false, env), \src -> 
			ruCodeCacheDeclCode(decl, src, env), ""
		), ""
	);
}

ruCachedDecl2LspSymbolInfo(id : int, env : RuEnv) -> Maybe<LspSymbolInformation> {
	maybeBind(ruDeclCacheGet(id), \decl -> 
		maybeMap(ruCacheGet(decl.descr.src, false, env), \src -> {
			range = ruCodeCacheDeclRange(decl, src, env);
			location = ruRangeCode2LspLocation(range, src.info.path, ruCodeCacheGet(decl.descr.src, env).code);
			LspSymbolInformation(id2s(id), ruDecl2LspSymbolKind(decl), false, location, "");
		})
	);
}

ruCacheInfo(full : bool, env : RuEnv) -> string {
	ruSourceCacheInfo(full) + "\n" +
	ruDeclCacheInfo(full) + "\n" +
	ruDefCacheInfo(full) + "\n" +
	ruRuleCacheInfo(full) + "\n" +
	ruCodeCacheInfo(full) + "\n" +
	ruDiskCacheInfo(full, env) + "\n";
}
