import ru/cache/code;

export {
	ruSourceCacheClear() -> void;
	ruSourceCacheAdd(src : RuSource, exact : bool, env : RuEnv) -> void;
	ruSourceCacheRemove(id : int, exact : bool, env : RuEnv) -> void;
	ruSourceCacheGet(id : int, exact : bool, env : RuEnv) -> Maybe<RuSource>;
	ruSourceCacheContains(id : int, exact : bool, env : RuEnv) -> bool;
	ruSourceCacheAll(exact : bool, env : RuEnv) -> [RuSource];

	ruSourceCacheInfo(full : bool) -> string;
}

ruSourceExactCache = initConcurrentHashMap();
ruSourceProoflessCache = initConcurrentHashMap();

ruSourceCacheClear() -> void {
	clearConcurrentHashMap(ruSourceExactCache);
	clearConcurrentHashMap(ruSourceProoflessCache);
}

ruSourceCacheAdd(src : RuSource, exact : bool, env : RuEnv) -> void {
	setConcurrentHashMap(ruSourceCacheSelect(exact), src.info.id, Some(src));
	if (exact) {
		setConcurrentHashMap(ruSourceProoflessCache, src.info.id, Some(ruClearSourceProofs(src)));
	}
}

ruSourceCacheRemove(id : int, exact : bool, env : RuEnv) -> void {
	removeConcurrentHashMap(ruSourceCacheSelect(exact), id);
}

ruSourceCacheGet(id : int, exact : bool, env : RuEnv) -> Maybe<RuSource> {
	getConcurrentHashMap(ruSourceCacheSelect(exact), id, None());
}

ruSourceCacheContains(id : int, exact : bool, env : RuEnv) -> bool {
	containsConcurrentHashMap(ruSourceCacheSelect(exact), id);
}

ruSourceCacheAll(exact : bool, env : RuEnv) -> [RuSource] {
	filtermap(
		map(keysConcurrentHashMap(ruSourceCacheSelect(exact)), \id -> ruSourceCacheGet(id, exact, env)), 
		idfn
	);
}

ruSourceCacheInfo(full : bool) -> string { 
	if (!full) {
		"cached exact sources: " + i2s(sizeConcurrentHashMap(ruSourceExactCache)) + "\n" +
		"cached proofless sources: " + i2s(sizeConcurrentHashMap(ruSourceProoflessCache));
	} else {
		"cached exact sources:\n" + 
			strGlue(map(keysConcurrentHashMap(ruSourceExactCache), \id -> "\t" + id2s(id)), "\n") + 
		"\n" +
		"cached proofless sources:\n" + 
			strGlue(map(keysConcurrentHashMap(ruSourceProoflessCache), \id -> "\t" + id2s(id)), "\n");
	};
}

ruSourceCacheSelect(exact : bool) -> native {
	if (exact) {
		ruSourceExactCache;
	} else {
		ruSourceProoflessCache;
	}
}