import base/path;
import ru/verify/math;
import ru/util/lemma;
import ru/math;
import ru/refactor/left_recursion;
import mm/src;
import comm;

export {
	ruCommRu2Mm : RuComm;
}

ruCommRu2Mm = RuComm(
	RuCommInfo(
		"ru-to-mm", "translate", "translate Russell to Metamath", "file", true, false, 
		[
			RuCommArg("file", false, "input file", ""),
			RuCommArg("unfix-left-recursion", true, "restore potentially left recursive grammar rules", "1")
		], []
	),
	\unit -> {
		env = ruUnitEnv(unit);
		state = unit.state;
		timer = ruMakeTimer();
		task = unit.task;
		math = ruMathFromSources(state.ru, env);
		unfixed_math = if (lookupTreeDef(task.args, "unfix-left-recursion", "1") == "0") {
			math 
		} else {
			if (ruVerbose(env.conf) > 0) {
				env.out.onMessage("ru left-recursive rules are unfixed");
			}
			ruUnfixLeftRecursion(math, env);
		}
		mm = ru2mm(unfixed_math, env);
		Some(RuState(state with 
			mm = mm,
			vars = setTree(setTree(state.vars, 
				"ru-to-mm.time", ValDouble(ruTimePassed(timer))),
				"ru-to-mm.size", ValInt(sizeTree(mm))
			)
		))
	}
);

ru2mm(math : RuMath, env : RuEnv) -> Tree<int, MmSource> {
	timer = ruMakeTimer();
	turnstile = ruMakeTurnstile(math);
	srcs = mapTree(math.sources, \src -> ruSource2mm(src, turnstile, math, env));
	mm = ruAddTurnstile(turnstile, math, srcs, env);
	if (ruVerbose(env.conf) > 0) {
		env.out.onMessage("ru translated in " + ruTimePassed2s(timer));
	}
	mm;
}

ruAddTurnstile(turnstile : int, math : RuMath, sources : Tree<int, MmSource>, env : RuEnv) -> Tree<int, MmSource> {
	leaf_srcs = filter(getTreeValues(sources), \src -> src.imports == []);
	common_dir = ruPath2s(ruCommonDir(map(leaf_srcs, \src -> ruSplitPath(src.info.path))));
	path = common_dir + "turnstile.mm";
	module = ruTrimPath(path, env.conf, ".mm");
	module_id = s2id(module);
	turnstile_src = MmSource([], 
		[
			MmComment("The analog of |- symbol in Metamath. Used this one, because |- may be used as a meaningful terminal in expression grammar."), 
			MmConst([turnstile])
		], 
		RuFileInfo(module_id, module, path, 0.0, ruIdsVersion())
	);
	fold(leaf_srcs, setTree(sources, module_id, turnstile_src), \acc, src ->
		setTree(acc, src.info.id, 
			MmSource([MmImport(module_id)], src.decls, src.info)
		)
	);
}

ruMakeTurnstile(math : RuMath) -> int {
	ascii_consts = buildSet(map(getTreeValues(math.decls.lang.consts), \c -> c.ascii));
	ruDoMakeTurnstile(ruIdTurnstile(), ascii_consts)
}

ruDoMakeTurnstile(turnstile : int, consts : Set<int>) -> int {
	if (!containsSet(consts, turnstile)) turnstile else {
		ruDoMakeTurnstile(s2id("|" + id2s(turnstile)), consts)
	}
}

ruSource2mm(src : RuSource, turnstile : int, math : RuMath, env : RuEnv) -> MmSource {
	timer = ruMakeTimer();
	ret = MmSource(
		map(src.imports, \imp -> MmImport(imp.id)),
		concatA(ruConcurrent(map(src.decls, \decl -> \env1 -> ruDecl2mm(decl, turnstile, math, env1)), env)),
		ruNewFileInfo(src.info.module, env.conf, ".mm")
	);
	if (ruVerbose(env.conf) > 1) {
		env.out.onMessage("\tru translated: '" + src.info.module + "' in " + ruTimePassed2s(timer));
	}
	ret
}

ruVar2mm(th : int, var : int) -> int {
	// Mm labels must be: "Only letters, digits, "_", "-", and "." are allowed in labels."
	// Replace all characters not falling into these classes with those, who fall. 
	s2id(id2s(th) + "_" + ruMakeValidMmId(id2s(var)));
}

ruDecl2mm(decl : RuDecl, turnstile : int, math : RuMath, env : RuEnv) -> [MmDecl] {
	const2mm = \c -> if (c.ascii != -1) c.ascii else c.info.id;
	term2mm = \t -> map(t.nodes, 
		\s -> switch (s) {
			RuVar(id,__): 
				MmSymb(id, true);
			RuConstRef(id):
				MmSymb(const2mm(lookupTreeDef(math.decls.lang.consts, id, ruNoConst)), false);
		}
	);
	tvars2mm = \n, t -> fold(t.nodes, [],
		\acc, s -> switch (s) {
			RuVar(id, type): 
				concat(acc, [MmFloating(ruVar2mm(n, id), type, id)]);
			RuConstRef(__):
				acc;
		}
	);
	expr2mm = \ex -> MmExp(concat([MmSymb(turnstile, false)], term2mm(ruExp2Term(ex, math.decls.lang.rules))));
	disj2mm = \disjs -> map(ruDisjs2Array(disjs), \ds -> MmDisj(map(ds, \d -> d.id)));
	vars2mm = \n, vs -> map(getTreeValues(vs), \v -> MmFloating(ruVar2mm(n, v.id), v.type, v.id));
	hyps2mm = \n, hs -> mapi(hs, \i, h -> MmEssential(s2id(id2s(n) + "_" + i2s(i)), expr2mm(h.expr)));
	comm2mm = \t -> {
		t0 = trim2(t, " \t\n\r");
		t1 = if (startsWith(t0, "/*") || startsWith(t0, "//")) strRight(t0, 2) else t0;
		if (endsWith(t1, "*/")) strLeft(t1, strlen(t1) - 2) else t1;
	}
	add_comments = \info, arr -> {
		concat(map(info.comments, \comm -> 
			MmComment(comm2mm(
				if (length(comm.annotation.members) == 0) {
					comm.text 
				} else {
					json2string(comm.annotation) + "\n-----\n" + comm.text
				}
			))
		), arr);
	}
	switch (decl) {
		RuConst(info, ascii,__):
			add_comments(info, [MmConst([const2mm(decl)])]);
		RuType(info, supers):
			add_comments(info, concat(
				[MmConst([info.id])], 
				concatA(filtermap(supers, \super -> 
					maybeMap(
						lookupTree(math.decls.lang.rules, ruSuperTypeName(super, info.id)),
						\r -> ruDecl2mm(r, turnstile, math, env)
					)
				))
			));
		RuRule(info, vars, term):
			add_comments(info, [MmAssertion(
				[], false,
				tvars2mm(info.id, term), [],
				MmAxiomatic(info.id, MmExp(concat([MmSymb(term.type, false)], term2mm(term))))
			)]);
		RuAxiom(info, stat):
			add_comments(info, [MmAssertion(
				disj2mm(stat.vars.disjs), false,
				concat(vars2mm(info.id, stat.vars.vars), hyps2mm(info.id, stat.hyps)), [],
				MmAxiomatic(info.id, expr2mm(stat.prop))
			)]);
		RuTheorem(info, stat, __, proof): {
			linearized = ruMergeAllLemmasIntoProof(proof, math, env);
			add_comments(info, [MmAssertion(
				disj2mm(mergeSets(stat.vars.disjs, linearized.vars.disjs)), false,
				concat(vars2mm(info.id, stat.vars.vars), hyps2mm(info.id, stat.hyps)),
				vars2mm(info.id, linearized.vars.vars),
				MmProvable(info.id, expr2mm(stat.prop), 
					MmNormalProof(ruProof2mmNormal(
						lastElement(linearized.steps, ruNoStep), 
						RuTheorem(decl with proof = linearized), math, env
					))
				)
			)]);
		}
		RuDef(info, stat,__,__):
			add_comments(info, [MmAssertion(
				disj2mm(stat.vars.disjs), false,
				concat(vars2mm(info.id, stat.vars.vars), hyps2mm(info.id, stat.hyps)), [],
				MmAxiomatic(info.id, expr2mm(stat.prop))
			)]);
	}
}

ruProofExp2mmNormal(i : int, nodes : [RuExpNode], name : int) -> Pair<[int], int> {
	switch (nodes[i]) {
		RuRuleNode(rule,__,arity, len): {
			args = fold(enumFromTo(1, arity), Pair([], i + 1), \acc, __ -> {
				arg = ruProofExp2mmNormal(acc.second, nodes, name);
				Pair(concat(acc.first, arg.first), arg.second);
			});
			Pair(concat(args.first, [rule]), args.second);
		}
		RuVar(id,__): Pair([ruVar2mm(name, id)], i + 1);
	}
}

ruProof2mmNormal(step : RuStep, th : RuTheorem, math : RuMath, env : RuEnv) -> [int] {
	switch (ruMatchStep(step, th.proof, th.stat.hyps, math, env)) {
		RuMatchedStep(ass, sub, ___): {
			subs = fold(getTreeValues(ass.stat.vars.vars), [], \acc, v ->
				switch (lookupTree(sub, v)) {
					None(): acc;
					Some(ex): concat(acc, ruProofExp2mmNormal(0, ex.nodes, th.info.id).first);
				}
			)
			refs = fold(step.refs, [], \acc, ref ->
				switch (ref) {
					RuHypRef(ind): concat(acc, [s2id(id2s(th.info.id) + "_" + i2s(ind))]);
					RuStepRef(ind): concat(acc, ruProof2mmNormal(th.proof.steps[ind], th, math, env));
				}
			);
			concat3(subs, refs, [ass.info.id]);
		}
		RuVerifyErr(): [];
	}
}
