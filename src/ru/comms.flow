import ru/cache/comms;
import ru/parse/comms;
import ru/prover/comms;
import ru/refactor/comms;
import ru/verify/comms;
import ru/info;
import ru/ru2mm;

export {
	ruCommands(exec : (ScScript, RuState, RuOut) -> RuState) -> [RuComm];
}

ruCommands(exec : (ScScript, RuState, RuOut) -> RuState) -> [RuComm] {
	concatA([
		[
			ruCommVerify,
			ruCommCyclicDeps,
			ruCommCyclicImports,
			ruApplyToProjectRoots(ruCommRu2Mm),
			ruCommActions,
			ruCommOrderSources
		],
		ruIoCommands,
		ruRefactorCommands(exec),
		ruProverCommands,
		ruInfoCommands,
		ruSyncCommands,
		ruCacheCommands,
		ruStatsCommands
	]);
}

ruCommActions = RuComm(
	RuCommInfo(
		"actions", "misc", "available actions at the given position", "", false, false, 
		[
			RuCommArg("file", false, "input file", ""), 
			RuCommArg("line", false, "clear", ""), 
			RuCommArg("col", false, "clear", "")
		], []
	), 
	ruRunActions
);

ruRunActions(unit : RuUnit) -> Maybe<RuState> {
	env = ruUnitEnv(unit);
	state = unit.state;
	task = unit.task;
	actions = ["verify", "prove", "reprove", "generalize"];
	file = lookupTreeDef(task.args, "file", "");
	line = s2i(lookupTreeDef(task.args, "line", "-1"));
	col = s2i(lookupTreeDef(task.args, "col", "-1"));
	module = ruTrimPath(file, env.conf, ".ru");
	code = ruCodeCacheGet(s2id_(module), env).code;
	name = ruNameByLineCol(line, col, code);
	json = if (name == "") JsonArray([]) else {
		JsonArray(map(actions, \action -> 
			lspCodeAction2Json(LspCodeAction(
				action + " " + name, action, [], false, None(),
				Some(LspCommand(
					action + " " + name,
					"command",
					map(["file=" + file, "read", ";", action, "target=" + name], 
						\arg -> JsonString(arg)
					)
				))
			))
		));
	}
	env.out.onMessage(json2string(json));
	Some(state);
}

ruCommCyclicDeps = RuComm(
	RuCommInfo(
		"cyclic-deps", "misc", "check on cyclic dependencies", "target", false, false, 
		[RuCommArg("target", false, "the target: a theorem name, file name or 'all' for verification of all math", "all")], []
	),
	\unit -> {
		env = ruUnitEnv(unit);
		state = unit.state;
		if (ruHasCyclicDependencies(ruMathFromSources(state.ru, env), env)) None() else Some(state);
	}
);

ruHasCyclicDependencies(math : RuMath, env : RuEnv) -> bool {
	all_deps = ruAssertionsDependencies(math.decls.assertions, env);
	ret = foldTree(math.decls.assertions, false, 
		\id, __, acc -> {
			deps = lookupTreeDef(all_deps, id, makeSet());
			ret = if (containsSet(deps, id)) {
				env.out.onMessage("Cyclic dependency found: " + id2s(id) + " dependes on: " + strGlue(map(set2array(deps), id2s), ", "));
				true;
			} else {
				if (ruVerbose(env.conf) >= 2) {
					details = if (ruVerbose(env.conf) >= 3) ": " + strGlue(map(set2array(deps), id2s), ", ") else ", ";
					env.out.onMessage("Assertion " + id2s(id) + " has " + i2s(sizeSet(deps)) + " dependencies" + details + "yet no cyclic are found");
				}
				false;
			}
			ret || acc;
		}
	)
	if (ruVerbose(env.conf) > 0 && !ret) {
		env.out.onMessage("No cyclic dependencies are found in " + i2s(sizeTree(math.decls.assertions)) + " assertions");
	}
	ret;
}

ruCommCyclicImports = RuComm(
	RuCommInfo(
		"cyclic-imports", "misc", "check on cyclic imports", "target", false, false, 
		[RuCommArg("target", false, "the target: a theorem name, file name or 'all' for verification of all math", "all")], []
	),
	\unit -> {
		env = ruUnitEnv(unit);
		state = unit.state;
		if (ruHasCyclicImports(ruMathFromSources(state.ru, env), env)) None() else Some(state);
	}
);

ruHasCyclicImports(math : RuMath, env : RuEnv) -> bool {
	all_deps = ruSourcesAllImported(math.sources, env);
	if (ruVerbose(env.conf) >= 3) {
		env.out.onMessage("\t\timports data collected");
	}
	ret = foldTree(math.sources, false, 
		\id, __, acc -> {
			deps = lookupTreeDef(all_deps, id, makeSet());
			ret = if (containsSet(deps, id)) {
				env.out.onMessage("Cyclic import is found: '" + id2s(id) + "' imports on:\n" + strIndent(strGlue(map(set2array(deps), id2s), "\n")));
				true;
			} else {
				if (ruVerbose(env.conf) >= 2) {
					details = if (ruVerbose(env.conf) >= 3) ": " + strGlue(map(set2array(deps), id2s), ", ") else ", ";
					env.out.onMessage("Module '" + id2s(id) + "' has " + i2s(sizeSet(deps)) + " imports" + details + "yet no cyclic are found");
				}
				false;
			}
			ret || acc;
		}
	)
	if (ruVerbose(env.conf) > 0 && !ret) {
		env.out.onMessage("No cyclic imports are found in " + i2s(sizeTree(math.sources)) + " modules");
	}
	ret;
}

ruCommOrderSources = RuComm(
	RuCommInfo("order-sources", "misc", "show the toposort of sources", "", false, false, [], []),
	\unit -> {
		env = ruUnitEnv(unit);
		order = ruSourcesOrder(unit.state.ru);
		env.out.onMessage("order of sources:\n" + strIndent(strGlue(map(order, \s -> s.info.module), "\n")));
		Some(unit.state);
	}
);

ruStatsCommands = [
	RuComm(RuCommInfo("stats-math", "stats", "show math statistics", "", false, false, [], []),
		\unit -> {
			env = ruUnitEnv(unit);
			unit.out.onMessage("Mathematics statistics:");
			unit.out.onMessage("-----------------------");
			unit.out.onMessage(ruMathStats2s(ruMathFromSources(unit.state.ru, env), env) + "\n");
			Some(unit.state); 
		}
	),
	RuComm(RuCommInfo("stats-math-1", "stats", "show math statistics", "", false, false, [], []),
		\unit -> {
			env = ruUnitEnv(unit);
			stats = ruMathStats(ruMathFromSources(unit.state.ru, env), env);
			Some(RuState(unit.state with
				vars = setTree(unit.state.vars, "stats-math-1.stats", stats);
			)); 
		}
	),
	RuComm(RuCommInfo("stats-mem", "stats", "show memory usage statistics", "", false, false, 
			[RuCommArg("show", true, "set this option to '1' in order to print memory statistics to the output", "")], []
		),
		\unit -> {
			if (lookupTreeDef(unit.task.args, "show", "") == "1") {
				unit.out.onMessage(
					memory2s(usedMemory()) + " + " + memory2s(freeMemory()) + " free = " + memory2s(totalMemory())
				);
			}
			Some(RuState(unit.state with
				vars = setTree(setTree(setTree(unit.state.vars, 
					"stats-mem.used", ValDouble(usedMemory())),
					"stats-mem.free", ValDouble(freeMemory())),
					"stats-mem.total", ValDouble(totalMemory())
				)
			)); 
		}
	),
	RuComm(
		RuCommInfo(
			"stats-cache", "stats", "show cache statistics", "", false, false,
			[RuCommArg("full", true, "show cached names", "")], []
		), 
		\unit -> {
			env = ruUnitEnv(unit);
			unit.out.onMessage(ruCacheInfo(lookupTreeDef(unit.task.args, "full", "") == "1", env));
			Some(unit.state);
		}
	)
];
