import ru/subst;

export {

	RuIndex(
		rules : Tree<string, RuIndexNode<?>>,
		vars  : Tree<string, RuIndexNode<?>>,
		nodes : ref [RuIndexNode<?>]
	);

	ruMakeIndex() -> RuIndex<?>;

	ruSetIndex(key : RuExp, data : ?, index : RuIndex<?>) -> RuIndex<?>;

	ruIndexNodeCount(ind : RuIndex<?>) -> int;
	ruIndexDataCount(ind : RuIndex<?>) -> int;

	ruFindIndex(e : RuExp, index : RuIndex<?>, math : RuMath) -> [?];
	ruIndex2s(index : RuIndex<?>, show_data : (?) -> string, math : RuMath) -> string;
	ruIndexRaw2s(index : RuIndex<?>) -> string;
	ruIndexValues(index : RuIndex<?>) -> [?];

	ruIterIndex(index : RuIndex<?>, f : (?) -> void) -> void;
	ruFoldIndex(index : RuIndex<?>, val : ??, f : (??, ?) -> ??) -> ??;

	ruIndexSubexpr(beg : RuIndexNode<?>, end : RuIndexNode<?>) -> RuExp;

	RuIndexNode(
		id    : int,
		name  : string, // var or rule name
		type  : string, 
		arity : int,    // -1 means that it is a variable, not rule

		ends  : Set<int>, // ends of subterms, starting at this node
		lens  : Set<int>, // distances to the beginnings of subterms, with end at this node

		data  : [?],
		expr  : Maybe<RuExp>,

		index : RuIndex<?>, // children nodes
		parent : int,
	);

	ruNoIndexNode = RuIndexNode(-1, "", "", -1, makeSet(), makeSet(), [], None(), ruMakeIndex(), -1);
}

ruIndexNode2s(n : RuIndexNode<?>) -> string {
	"id: " + i2s(n.id) + ", " + n.name + ":" + n.type + ", arity=" + i2s(n.arity) + ", " +
	"lens=[" + strGlue(map(set2array(n.lens), i2s), ", ") + "], " + 
	"ends=[" + strGlue(map(set2array(n.ends), i2s), ", ") + "], " + 
	"data.length=" + i2s(length(n.data)) + ", " +
	(switch (n.expr){ Some(e): "expr=" + ruExpRPN2s(e.nodes) + ", "; None(): ""}) + 
	"parent=" + i2s(n.parent)
}

ruMakeIndex() -> RuIndex<?> {
	RuIndex(makeTree(), makeTree(), ref []);
}

ruIndexNodeCount(ind : RuIndex<?>) -> int {
	s1 = foldTree(ind.rules, 0, \__, n, acc -> acc + ruIndexNodeCount(n.index));
	s2 = foldTree(ind.vars, 0, \__, n, acc -> acc + ruIndexNodeCount(n.index));
	s1 + s2 + 1;
}

ruIndexDataCount(ind : RuIndex<?>) -> int {
	s1 = foldTree(ind.rules, 0, \__, n, acc -> acc + ruIndexDataCount(n.index) + length(n.data));
	s2 = foldTree(ind.vars, 0, \__, n, acc -> acc + ruIndexDataCount(n.index) + length(n.data));
	s1 + s2;
}

ruIndexValues(index : RuIndex<?>) -> [?] {
	ret = ref [];
	ruDoIndexValues(index, ret);
	^ret;
}

ruDoIndexValues(index : RuIndex<?>, acc : ref [?]) -> void {
	traverseInOrder(index.rules, \__, n -> { refConcat(acc, n.data); ruDoIndexValues(n.index, acc); });
	traverseInOrder(index.vars, \__, n -> { refConcat(acc, n.data); ruDoIndexValues(n.index, acc); });
}

ruIterIndex(index : RuIndex<?>, fn : (?) -> void) -> void {
	traverseInOrder(index.rules, \__, n -> { iter(n.data, \d -> fn(d)); ruIterIndex(n.index, fn); });
	traverseInOrder(index.vars, \__, n -> { iter(n.data, \d -> fn(d)); ruIterIndex(n.index, fn); });
}

ruFoldIndex(index : RuIndex<?>, val : ??, fn : (??, ?) -> ??) -> ?? {
	foldTree(index.vars, 
		foldTree(index.rules, 
			val, 
			\__, n, acc -> fold(n.data, ruFoldIndex(n.index, acc, fn), \ac, d -> fn(ac, d))
		),
		\__, n, acc -> fold(n.data, ruFoldIndex(n.index, acc, fn), \ac, d -> fn(ac, d))
	);
}

ruSetIndex(key : RuExp, data : ?, index : RuIndex<?>) -> RuIndex<?> {
	insert_int = \t, i, v -> {
		w = lookupTreeDef(t, i, makeSet());
		setTree(t, i, insertSet(w, v));
	}
	env0 = RuIndexAddEnv(key, data, makeTree(), makeTree(), ref makeTree());
	env = foldi(key.nodes, env0, \i, acc, n ->
		switch (n) {
			RuRuleNode(__,__,__, len): 
				RuIndexAddEnv(acc with 
					lens = insert_int(acc.lens, i + len, len),
					ends = insert_int(acc.ends, i, i + len),
				);
			RuVar(__,__): acc;
		}
	);
	ruDoAdd2Index(0, index, -1, env);
}

RuIndexAddEnv(
	key : RuExp, 
	data : ?,
	lens : Tree<int, Set<int>>,
	ends : Tree<int, Set<int>>,
	branch : ref Tree<int, int>
);

ruDoAdd2Index(i : int, index : RuIndex<?>, parent : int, env : RuIndexAddEnv<?>) -> RuIndex<?> {
	new_node = \n, t, a -> {
		m = RuIndexNode(length(^(index.nodes)), n, t, a, makeSet(), makeSet(), [], None(), 
			RuIndex(makeTree(), makeTree(), index.nodes), parent
		);
		refArrayPush(index.nodes, m);
		m
	}
	switch (env.key.nodes[i]) {
		RuRuleNode(name, type, arity, len): {
			n = switch(lookupTree(index.rules, name)) {
				Some(x): x;
				None(): new_node(name, type, arity);
			}
			m = ruDoAdd2IndexNode(i, n, env);
			RuIndex(index with rules = setTree(index.rules, name, m));
		}
		RuVar(name, type): {
			n = switch(lookupTree(index.vars, name)) {
				Some(x): x;
				None(): new_node(name, type, -1);
			}
			m = ruDoAdd2IndexNode(i, n, env);
			RuIndex(index with vars = setTree(index.vars, name, m));
		}
	}
}

ruDoAdd2IndexNode(i : int, n : RuIndexNode<?>, env : RuIndexAddEnv<?>) -> RuIndexNode<?> {
	n1 = if (i + 1 == length(env.key.nodes)) {
		RuIndexNode(n with data = concat(n.data, [env.data]), expr = Some(env.key));
	} else {
		RuIndexNode(n with index = ruDoAdd2Index(i + 1, n.index, n.id, env));
	}
	n2 = RuIndexNode(n1 with 
		lens = mergeSets(n1.lens, lookupTreeDef(env.lens, i, makeSet())),
	);
	n3 = RuIndexNode(n2 with ends = 
		eitherMap(lookupTree(env.ends, i), \ends -> 
			mergeSets(n2.ends, mapSet(ends, \end -> 
				lookupTreeDef(^(env.branch), end, if (i == end) n.id else -1)
			)), 
		n2.ends)
	);
	env.branch := setTree(^(env.branch), i, n3.id);
	nodes = n.index.nodes;
	n.index.nodes := replace(^nodes, n.id, n3);
	n3
}

ruIndex2s(index : RuIndex<?>, show_data : (?) -> string, math : RuMath) -> string {
	ruDoIndex2s(index, show_data, None(), "", math)
}

ruDoIndex2s(index : RuIndex<?>, show_data : (?) -> string, beg : Maybe<RuIndexNode<?>>, acc : string, math : RuMath) -> string {
	begin = \n -> switch (beg) {
		Some(b): b;
		None(): n;
	}
	acc1 = foldTree(index.rules, acc, \__, n, ac ->
		ruDoIndexNode2s(n, show_data, begin(n), ac, math, index.nodes)
	);
	foldTree(index.vars, acc1, \__, n, ac ->
		ruDoIndexNode2s(n, show_data, begin(n), ac, math, index.nodes)
	);
}

ruDoIndexNode2s(node : RuIndexNode<?>, show_data : (?) -> string, beg : RuIndexNode<?>, acc : string, math : RuMath, nodes : ref [RuIndexNode<?>]) -> string {
	acc1 = if (node.data == []) acc else {
		acc + 
		ru2s(ruIndexSubexpr(beg, node), math.rules) + " --> {" + 
		strGlue(map(node.data, show_data), ", ") + "}\n";
	}
	ruDoIndex2s(node.index, show_data, Some(beg), acc1, math);
}

ruIndexRaw2s(index : RuIndex<?>) -> string {
	ruDoIndexRaw2s(index, [], "")
}

ruDoIndexRaw2s(index : RuIndex<?>, nodes : [RuIndexNode<?>], acc : string) -> string {
	acc1 = foldTree(index.rules, acc, \__, n, ac ->
		ruDoIndexNodeRaw2s(n, concat(nodes, [n]), ac)
	);
	foldTree(index.vars, acc1, \__, n, ac ->
		ruDoIndexNodeRaw2s(n, concat(nodes, [n]), ac)
	);
}

ruDoIndexNodeRaw2s(node : RuIndexNode<?>, nodes : [RuIndexNode<?>], acc : string) -> string {
	acc1 = if (node.data == []) acc else {
		acc + strGlue(
			map(nodes, \n -> 
				i2s(n.id) + "=" + n.name + ":" + n.type + 
				",lens=[" + strGlue(map(set2array(n.lens), i2s), ", ") + "]" + 
				",ends=[" + strGlue(map(set2array(n.ends), i2s), ", ") + "]" + 
				(if (length(n.data) > 0) "*" else "")
			), 
		" ") + " --> " + i2s(length(node.data)) + "\n";
	};
	ruDoIndexRaw2s(node.index, nodes, acc1);
}

ruFindIndex(e : RuExp, index : RuIndex<?>, math : RuMath) -> [?] {
	ruDoFindIndex(0, e, index, math)
}

ruDoFindIndex(i : int, e : RuExp, index : RuIndex<?>, math : RuMath) -> [?] {
	switch (e.nodes[i]) {
		RuRuleNode(name, type, arity, len):
			switch(lookupTree(index.rules, name)) {
				Some(n): 
					if (i + 1 == length(e.nodes)) n.data 
					else ruDoFindIndex(i + 1, e, n.index, math);
				None(): [];
			}
		RuVar(name, type): {
			switch(lookupTree(index.vars, name)) {
				Some(n):
					if (i + 1 == length(e.nodes)) n.data 
					else ruDoFindIndex(i + 1, e, n.index, math);
				None(): [];
			}
		}
	}
}

ruIndexBranch(beg : RuIndexNode<?>, end : int) -> [RuIndexNode<?>] {
	if (beg.id == end) [beg] else {
		n = ^(beg.index.nodes)[end];
		concat(ruIndexBranch(beg, n.parent), [n]);
	}
}

ruIndexSubexpr(beg : RuIndexNode<?>, end : RuIndexNode<?>) -> RuExp {
	branch = ruIndexBranch(beg, end.id);
	lens = foldi(branch, makeTree(), \i, acc, n -> 
		foldSet(n.lens, acc, \ac, len -> setTree(ac, i - len, len))
	);
	RuExp(
		foldi(branch, [], \i, acc, n ->
			if (n.arity == -1) {
				concat(acc, [RuVar(n.name, n.type)]);
			} else {
				switch (lookupTree(lens, i)) {
					Some(len): concat(acc, [RuRuleNode(n.name, n.type, n.arity, len)]);
					None(): {
						ruPrintln("ruIndexSubexpr: must not happen");
						ruPrintln("beg:\n" + ruIndent(ruIndexNode2s(beg)));
						ruPrintln("end:\n" + ruIndent(ruIndexNode2s(end)));
						quit(-1);
						acc
					}
				}
			}
		)
		, -1
	);
}
