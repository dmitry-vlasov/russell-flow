import string;
import ds/tree;
import base/fileinfo;

// At each change of any source struct/union bump ruCacheVersion in ru/cache.

export {
	// Source components
	RuSource(imports : [RuImport], decls : [RuDecl], comments : [RuComment], info : RuFileInfo);
	RuImport(module : string, pos : int);
	RuDecl ::= RuConst, RuType, RuRule, RuAxiom, RuDef, RuTheorem;
	RuComment(text : string, annotation : Json, pos : int);
	RuDescr(name : string, source : string, comments : [RuComment]);

	// Declarations
	RuConst(descr : RuDescr, ascii : string, latex : string, pos : int);
	RuType(descr : RuDescr, supers : [string], pos : int);
	RuRule(descr : RuDescr, vars : Tree<string, RuVar>, term : RuTerm, pos : int);
	RuAxiom(descr : RuDescr, vars : Tree<string, RuVar>, disjs : Set<RuDisj>, hyps : [RuHyp], prop : RuExp, pos : int);
	RuTheorem(descr : RuDescr, vars : Tree<string, RuVar>, disjs : Set<RuDisj>, meta : Set<string>, hyps : [RuHyp], prop : RuExp, proof : RuProof, pos : int);
	RuDef(descr : RuDescr, vars : Tree<string, RuVar>, disjs : Set<RuDisj>, hyps : [RuHyp], defm : RuExp, defs : RuExp, prop : RuExp, pos : int);

	// Declaration components
	RuVar(name : string, type : string);
	RuHyp(ind : int, expr : RuExp, pos : int);
	RuDisj(v1 : RuVar, v2 : RuVar);
	RuProof(vars : Tree<string, RuVar>, disjs : Set<RuDisj>, steps : [RuStep], pos : int);
	RuStep(ind : int, assertion : string, refs : [RuRef], expr : RuExp, pos : int);
	RuRef ::= RuHypRef, RuStepRef;
	RuHypRef(ind : int, pos : int);
	RuStepRef(ind : int, pos : int);

	// Expression components
	RuTerm(type : string, expr : [RuTermNode], pos : int);
	RuExp(nodes : [RuExpNode], pos : int);
	RuExpNode ::= RuRuleRef, RuVar;
	RuTermNode ::= RuConstRef, RuVar;
	RuRuleRef(name : string, type : string, arity : int, len : int);
	RuConstRef(name : string);

	// Unions
	RuAssertion ::= RuAxiom, RuTheorem, RuDef;
	RuBlock ::= RuDecl, RuComment;
	RuAny ::= 
		RuSource, RuImport, RuConst, RuType, RuRule, RuAxiom, RuDef, RuTheorem, RuComment,
		RuStep, RuExp, RuHyp, RuProof, RuDisj, RuHypRef, RuStepRef, RuTerm, RuVar, RuConstRef;

	// Default 'no'-values
	ruNoVar = RuVar("__no", "__no");
	ruNoDescr = RuDescr("__no", "", []);
	ruNoComment = RuComment("", JsonNull(), -1);

	ruNoConst = RuConst(ruNoDescr, "", "", -1);
	ruNoType = RuType(ruNoDescr, [], -1);
	ruNoRule = RuRule(ruNoDescr, makeTree(), ruNoTerm, -1);
	ruNoAxiom = RuAxiom(ruNoDescr, makeTree(), makeSet(), [], ruNoExp, -1);
	ruNoTheorem = RuTheorem(ruNoDescr, makeTree(), makeSet(), makeSet(), [], ruNoExp, ruNoProof, -1);

	ruNoProof = RuProof(makeTree(), makeSet(), [], -1);
	ruNoHyp = RuHyp(-1, ruNoExp, -1);
	ruNoStep = RuStep(-1, "__no", [], ruNoExp, -1);
	ruNoSource = RuSource([], [], [], ruNoFileInfo);
	
	ruNoTerm = RuTerm("__no", [], -1);
	ruNoExpNode = ruNoVar;
	ruNoExp = RuExp([], -1);
		
	ru2s(a : RuAny, rules : Tree<string, RuRule>) -> string;
	ruExp2s(e : RuExp, rules : Tree<string, RuRule>) -> string;
	ruExp2Term(e : RuExp, rules : Tree<string, RuRule>) -> RuTerm;
	ruSteps2s(steps : [RuStep], rules : Tree<string, RuRule>) -> string;
	ruIsVar(e : RuExp) -> bool;
	ruIsConst(e : RuExp) -> bool;
	ruExp2Var(e : RuExp) -> Maybe<RuVar>;
	ruExp2Const(e : RuExp) -> Maybe<Pair<string, string>>;
	ruDeclName(d : RuDecl) -> string;
	ruDeclSrc(d : RuDecl) -> string;
	ruDeclPos(d : RuDecl) -> int;
	ruDeclKind(d : RuDecl) -> string;
	ruSetDeclPos(d : RuDecl, pos : int) -> RuDecl;
	ruDecl2s(d : RuDecl, rules : Tree<string, RuRule>) -> string;
	ruDecl2Assertion(d : RuDecl) -> Maybe<RuAssertion>;
	ruAssertion2Theorem(a : RuAssertion) -> Maybe<RuTheorem>;
	ruAssertion2Axiom(a : RuAssertion) -> Maybe<RuAxiom>;
	ruAssertion2Def(a : RuAssertion) -> Maybe<RuDef>;
	ruAssertion2s(ass : RuAssertion, rules : Tree<string, RuRule>) -> string;
	ruProofQedStep(proof : RuProof) -> RuStep;

	ruExpType(nodes : [RuExpNode]) -> string;
	ruTerm2s(nodes : [RuTermNode]) -> string;
	ruTermWVars2s(nodes : [RuTermNode]) -> string;
	ruExpRPN2s(nodes : [RuExpNode]) -> string;
	ruExp2Vars(nodes : [RuExpNode]) -> Set<RuVar>;
	ruExpContainsVar(nodes : [RuExpNode], v : RuVar) -> bool;
	ruAssertion2Vars(ass : RuAssertion) -> Set<RuVar>;
	ruProof2Vars(proof : RuProof) -> Set<RuVar>;
	ruDisjs2Array(disjs : Set<RuDisj>) -> [Set<RuVar>];
	ruDisjs2s(disjs : Set<RuDisj>) -> string;
	
	ruContainsOnlyComments(src : RuSource) -> bool;
	ruIndexOfDecl(name : string, src : RuSource) -> int;

	// In case a declaration has comments before it, we take the start position of a first
	// comment right before the declaration. Otherwise the position of decl itself.
	ruStartPosition(decl : RuDecl) -> int;
	ruCheckDisjs(s : Tree<RuVar, RuExp>, disjs : Set<RuDisj>) -> bool;
	ruArity(a : RuAssertion) -> int;

	RuDAG(data : ?, children : [RuDAG<?>]);
	ruProofToDAG(proof : RuProof) -> RuDAG<string>;
	ruDAG2s(dag : RuDAG<?>, show_data : (?) -> string) -> string;
	ruProofDAG2s(dag : RuDAG<string>) -> string;

	ruUpdateTheoremVars(theorem : RuTheorem) -> RuTheorem;
	ruUpdateProofVars(proof : RuProof) -> RuProof;

	ruTransformTheorem(th: RuTheorem, f : (RuExp) -> RuExp) -> RuTheorem;
	ruTransformProof(th: RuProof, f : (RuExp) -> RuExp) -> RuProof;
	ruTransformAxiom(ax: RuAxiom, f : (RuExp) -> RuExp) -> RuAxiom;
	ruTransformDef(def: RuDef, f : (RuExp) -> RuExp) -> RuDef;

	ruSrc2Blocks(src : RuSource) -> [RuBlock];
	ruBclocks2Src(src : RuSource, blocks : [RuBlock]) -> RuSource;
	ruBlockName(block : RuBlock) -> string;
}

ruArity(a : RuAssertion) -> int {
	length(a.hyps);
}

ruIsVar(e : RuExp) -> bool {
	length(e.nodes) == 1 && 
	switch (e.nodes[0]) { 
		RuVar(__,__): true; 
		default: false 
	}
}

ruExp2Var(e : RuExp) -> Maybe<RuVar> {
	if (length(e.nodes) != 1) None() else {
		n = e.nodes[0];
		switch (n) { 
			RuVar(__,__): Some(n); 
			default: None(); 
		}
	}
}

ruIsConst(e : RuExp) -> bool {
	length(e.nodes) == 1 && 
	switch (e.nodes[0]) { 
		RuVar(__,__): false; 
		RuRuleRef(name, __, arity, len): 
			arity == 0 && len == 1;
	}
}

ruExp2Const(e : RuExp) -> Maybe<Pair<string, string>> {
	if (length(e.nodes) != 1) None() else {
		switch (e.nodes[0]) { 
			RuVar(__,__): None(); 
			RuRuleRef(name, type, arity, len): 
				if (arity == 0 && len == 1) Some(Pair(name, type)) else None();
		}
	}
}


ruTerm2s(nodes : [RuTermNode]) -> string {
	strGlue(map(nodes, \n -> n.name), " ")
}

ruTermWVars2s(nodes : [RuTermNode]) -> string {
	strGlue(map(nodes, \n -> 
		switch (n) {
			RuConstRef(name): name;
			RuVar(name, type): name + ":" + type;
		}
	), " ")
}

ruExpRPN2s(nodes : [RuExpNode]) -> string {
	strGlue(map(nodes, \n -> {
		switch (n) {
			RuRuleRef(r, t, a, l): r + "-" + i2s(l);
			RuVar(v, t): v;
		}
	}), " ")
}

ruExp2Vars(nodes : [RuExpNode]) -> Set<RuVar> {
	fold(nodes, makeSet(), \acc, n ->
		switch (n) {
			RuRuleRef(__,__,__,__): acc;
			RuVar(__,__): insertSet(acc, n);
		}
	)
}

ruExpContainsVar(nodes : [RuExpNode], v : RuVar) -> bool {
	exists(nodes, \n ->
		switch (n) {
			RuVar(__,__): n == v;
			default: false;
		}
	);
}

ruAssertion2Vars(ass : RuAssertion) -> Set<RuVar> {
	fold(ass.hyps, ruExp2Vars(ass.prop.nodes), \acc, h ->
		mergeSets(acc, ruExp2Vars(h.expr.nodes))
	)
}

ruProof2Vars(proof : RuProof) -> Set<RuVar> {
	fold(proof.steps, makeSet(), \acc, step ->
		mergeSets(acc, ruExp2Vars(step.expr.nodes))
	)
}

ruExpType(nodes : [RuExpNode]) -> string {
	nodes[0].type
}

ruDeclName(d : RuDecl) -> string {
	switch (d) {
		RuConst(descr,__,__,__):               descr.name;
		RuType(descr,__,__):                   descr.name;
		RuRule(descr,__,__,__):                descr.name;
		RuAxiom(descr,__,__,__,__,__):         descr.name;
		RuDef(descr,__,__,__,__,__,__,__):     descr.name;
		RuTheorem(descr,__,__,__,__,__,__,__): descr.name;
	}
}

ruDeclSrc(d : RuDecl) -> string {
	switch (d) {
		RuConst(descr,__,__,__):               descr.source;
		RuType(descr,__,__):                   descr.source;
		RuRule(descr,__,__,__):                descr.source;
		RuAxiom(descr,__,__,__,__,__):         descr.source;
		RuDef(descr,__,__,__,__,__,__,__):     descr.source;
		RuTheorem(descr,__,__,__,__,__,__,__): descr.source;
	}
}

ruDeclPos(d : RuDecl) -> int {
	switch (d) {
		RuConst(__,__,__,pos):                pos;
		RuType(__,__, pos):                   pos;
		RuRule(__,__,__, pos):                pos;
		RuAxiom(__,__,__,__,__, pos):         pos;
		RuDef(__,__,__,__,__,__,__, pos):     pos;
		RuTheorem(__,__,__,__,__,__,__, pos): pos;
	}
}

ruDeclKind(d : RuDecl) -> string {
	switch (d) {
		RuConst(__,__,__, __):              "const";
		RuType(__,__,__):                   "type";
		RuRule(__,__,__,__):                "rule";
		RuAxiom(__,__,__,__,__,__):         "axiom";
		RuDef(__,__,__,__,__,__,__,__):     "def";
		RuTheorem(__,__,__,__,__,__,__,__): "theorem";
	}
}

ruSetDeclPos(d : RuDecl, pos : int) -> RuDecl {
	switch (d) {
		RuConst(__,__,__,__):               RuConst(d with pos = pos);
		RuType(__,__,__):                   RuType(d with pos = pos);
		RuRule(__,__,__,__):                RuRule(d with pos = pos);
		RuAxiom(__,__,__,__,__,__):         RuAxiom(d with pos = pos);
		RuDef(__,__,__,__,__,__,__,__):     RuDef(d with pos = pos);
		RuTheorem(__,__,__,__,__,__,__,__): RuTheorem(d with pos = pos);
	}
}

ruDecl2s(d : RuDecl, rules : Tree<string, RuRule>) -> string {
	switch (d) {
		RuTheorem(descr, vs, ds, m, hs, p, proof, pos): {
			str = ru2s(RuTheorem(descr, vs, ds, m, hs, p, RuProof(proof with steps = []), pos), rules);
			takeBefore(str, "proof", str);
		}
		default: ru2s(d, rules);
	}
}

ruDecl2Assertion(d : RuDecl) -> Maybe<RuAssertion> {
	switch (d) {
		RuAxiom(__,__,__,__,__,__):         Some(d);
		RuDef(__,__,__,__,__,__,__,__):     Some(d);
		RuTheorem(__,__,__,__,__,__,__,__): Some(d);
		default: None()
	}
}

ruAssertion2Theorem(a : RuAssertion) -> Maybe<RuTheorem> {
	switch (a) {
		RuTheorem(__,__,__,__,__,__,__,__): Some(a);
		default: None()
	}
}

ruAssertion2Axiom(a : RuAssertion) -> Maybe<RuAxiom> {
	switch (a) {
		RuAxiom(__,__,__,__,__,__): Some(a);
		default: None()
	}
}

ruAssertion2Def(a : RuAssertion) -> Maybe<RuDef> {
	switch (a) {
		RuDef(__,__,__,__,__,__,__,__): Some(a);
		default: None()
	}
}

ruAssertion2s(ass : RuAssertion, rules : Tree<string, RuRule>) -> string {
	str = ru2s(ass, rules);
	strLeft(str, strIndexOf(str, "proof"));
}

ruProofQedStep(proof : RuProof) -> RuStep {
	proof.steps[length(proof.steps) - 1];
}

ruExp2s(e : RuExp, rules : Tree<string, RuRule>) -> string {
	ruTerm2s(ruExp2Term(e, rules).expr)
}

ruExp2Term(e : RuExp, rules : Tree<string, RuRule>) -> RuTerm {
	switch (foldr(e.nodes, None(), \acc, n -> ruExp2sAccOperation(n, acc, rules))) {
		Some(stack): RuTerm(ruExpType(e.nodes), stack.nodes, e.pos);
		None(): ruNoTerm;
	}
}

RuExp2sStack(
	parent : Maybe<RuExp2sStack>,
	nodes : [RuTermNode]
);

ruExp2sAccOperation(node : RuExpNode, stack : Maybe<RuExp2sStack>, rules : Tree<string, RuRule>) -> Maybe<RuExp2sStack> {
	switch (node) {
		RuVar(var, __): 
			Some(RuExp2sStack(stack, [node]));
		RuRuleRef(name, type, arity, len): {
			switch (lookupTree(rules, name)) {
				Some(rule): {
					stack_str = fold(rule.term.expr, Pair(stack, []), 
						\ac, symb -> {
							switch (symb) {
								RuVar(__,__): {
									switch (ac.first) {
										Some(st): Pair(st.parent, concat(ac.second, st.nodes));
										None(): ac;
									}
								}
								RuConstRef(s):
									Pair(ac.first, concat(ac.second, [symb]));
							}
						}
					);
					Some(RuExp2sStack(stack_str.first, stack_str.second));
				}
				None(): {
					ruPrintln("rule " + name + " is not found");
					quit(-1);
					None();
				}
			}
		}
	}
}

ru2s(a : RuAny, rules : Tree<string, RuRule>) -> string {
	aru2s   = \x -> ru2s(x, rules);
	vars2s  = \vars -> if (sizeTree(vars) == 0) "" else "(" + strGlue(map(getTreeValues(vars), \v -> v.name + " : " + v.type), ", ") + ") ";
	disjs2s = \disjs -> if (sizeSet(disjs) == 0) "" else
		"disjointed(" + strGlue(map(ruDisjs2Array(disjs), \s -> strGlue(map(set2array(s), \v -> v.name), " ")), ", ") + ") ";
	meta2s  = \meta -> if (sizeSet(meta) == 0) "" else "meta(" + strGlue(set2array(meta), ", ") + ") ";
	exp2s   = \expr, delim -> ": " + ruExpType(expr.nodes) + " = " + delim + " " + ruExp2s(expr, rules) + " ;;";
	prop2s  = \prop -> "\tprop " + exp2s(prop, "|-") + "\n";
	refs2s  = \refs -> "(" + strGlue(map(refs, aru2s), ", ") + ")";
	hyps2s  = \hyps -> if (hyps == []) "" else strGlue(map(hyps, aru2s), "\n") + "\n";
	comm2s  = \comms -> if (comms == []) "" else strGlue(map(comms, aru2s), "\n") + "\n";
	switch (a) {
		RuSource(imps, decls, comments, info): 
			(if (imps == []) "" else strGlue(map(imps, aru2s), "\n") + "\n\n") +
			strGlue(map(comments, aru2s), "\n") +
			strGlue(map(decls, aru2s), "\n");
		RuImport(path, __):
			"import " + path + ";;";
		RuConst(descr, ascii, latex, __): 
			comm2s(descr.comments) + 
			"constant {\n" + 
				"\tsymbol " + descr.name + " ;;\n" +
				(if (ascii != "") "\tascii " + ascii + " ;;\n" else "") + 
				(if (latex != "") "\tlatex " + latex + " ;;\n" else "") +
			"}\n";
		RuType(descr, supers, __): 
			comm2s(descr.comments) + 
			"type " + descr.name + (if (supers == []) "" else " : " + strGlue(supers, ", ")) + " ;;\n";
		RuRule(descr, vars, term, __):
			comm2s(descr.comments) + 
			"rule " + descr.name + " " + vars2s(vars) + "{\n" +
				"\tterm " + aru2s(term) + "\n" +
			"}\n";
		RuAxiom(descr, vars, disjs, hyps, prop, __): 
			comm2s(descr.comments) + 
			"axiom " + descr.name + " " + vars2s(vars) + disjs2s(disjs) + "{\n" +
				hyps2s(hyps) +
				(if (hyps == []) "" else "\t----------\n") +
				prop2s(prop) +
			"}\n";
		RuDef(descr,  vars, disjs, hyps, defm, defs, prop, __): 
			comm2s(descr.comments) + 
			"definition " + descr.name + " " + vars2s(vars) + disjs2s(disjs) + "{\n" +
				hyps2s(hyps) +
				"\tdefiendum " + exp2s(defm, "#") + "\n" +
				"\tdefiniens " + exp2s(defs, "#") + "\n" +
				"\t-------------\n" +
				prop2s(ruMakeDefProp(prop.nodes, defm.nodes, defs.nodes)) +
			"}\n";
		RuTheorem(descr,  vars, disjs, meta, hyps, prop, proof, __): 
			comm2s(descr.comments) + 
			"theorem " + descr.name + " " + vars2s(vars) + disjs2s(disjs) + meta2s(meta) + "{\n" +
				hyps2s(hyps) + 
				(if (hyps == []) "" else "\t----------\n") +
				prop2s(prop) +
			"} " + aru2s(proof);
		RuHyp(ind, expr, __): 
			"\thyp " + i2s(ind + 1) + " " + exp2s(expr, "|-");
		RuProof(vars, disjs, steps, __): 
			"proof " + vars2s(vars) + disjs2s(disjs) + "{\n" +
				strGlue(map(steps, aru2s), "\n") + "\n" +
			"}\n";
		RuStep(ind, assertion, refs, expr, __):
			"\tstep " + i2s(ind + 1) + " : " + ruExpType(expr.nodes) + " = " + 
				assertion + " " + refs2s(refs) + " |- " + ruExp2s(expr, rules) + " ;;";
		RuComment(text, ann, __): 
			if (ann == JsonNull()) "/*" + text + "*/\n" else
			"/**" +json2string(ann) + (if (text == "") "" else "\n-----\n" + text) + "*/\n";
		RuDisj(v1, v2): v1.name + " " + v2.name;
		RuExp(nodes, __): ruExp2s(a, rules);
		RuHypRef(i, __): "hyp " + i2s(i + 1);
		RuStepRef(i, __): "step " + i2s(i + 1);
		RuVar(name, type): name;
		RuTerm(type, expr, __): 
			": " + type + " = # " + strGlue(map(expr, aru2s), " ") + " ;;";
		RuConstRef(name): name;
	}
}

ruSteps2s(steps : [RuStep], rules : Tree<string, RuRule>) -> string {
	strGlue(map(steps, \s -> ru2s(s, rules)), "\n");
}

ruDisjs2Array(disjs : Set<RuDisj>) -> [Set<RuVar>] {
	make_disj = \v, w -> if (v.name < w.name) RuDisj(v, w) else RuDisj(w, v);
	may_be_added = \v, s ->
		forall(set2array(s), \w -> 
			if (v == w) true else {
				containsSet(disjs, make_disj(v, w))
			}
		);
	foldSet(disjs, [], \acc, disj ->
		if (exists(acc, \s -> containsSet(s, disj.v1) && containsSet(s, disj.v2))) acc else {
			switch (findi(acc, \s -> containsSet(s, disj.v1) && may_be_added(disj.v2, s))) {
				Some(i): replace(acc, i, insertSet(acc[i], disj.v2));
				None(): 
					switch (findi(acc, \s -> containsSet(s, disj.v2) && may_be_added(disj.v1, s))) {
						Some(i): replace(acc, i, insertSet(acc[i], disj.v1));
						None(): concat(acc, [insertSet(makeSet1(disj.v1), disj.v2)]);
					}
			}
		}
	);
}

ruDisjs2s(disjs : Set<RuDisj>) -> string {
	strGlue(map(set2array(disjs), \disj -> disj.v1.name + " " + disj.v2.name), ", ");
}

ruIndOfArr(arr1 : [?], arr2 : [?]) -> int {
	foldi(arr1, -1, \i, ac, x -> 
		if (ac >= 0) ac else {
			if (i <= length(arr1) - length(arr2)) {
				ind = fold(arr2, i, \j, y ->
					if (j == -1) -1 else
					if (y == arr1[j]) j + 1 else -1
				);
				if (ind != -1) i else -1;
			} else {
				-1
			}
		}
	)
}

ruMakeDefProp(prop : [RuExpNode], dfm : [RuExpNode], dfs: [RuExpNode]) -> RuExp {
	dfs_i = ruIndOfArr(prop, dfs);
	dfm_i = ruIndOfArr(prop, dfm);
	dfs_len = length(dfs);
	dfm_len = length(dfm);
	if (dfs_i == -1 || dfm_i == -1) {
		ruNoExp
	} else {
		RuExp(foldi(prop, [], \i, ac, s ->
			if (i == dfm_i) concat(ac, [RuVar("defiendum", "__no")]) else
			if (dfm_i < i && i < dfm_i + dfm_len) ac else
			if (i == dfs_i) concat(ac, [RuVar("definiens", "__no")]) else
			if (dfs_i < i && i < dfs_i + dfs_len) ac else
			concat(ac, [s])
		), -1)
	}
}

ruContainsOnlyComments(src : RuSource) -> bool {
	forall(src.decls, \decl -> 
		switch (decl) {
			RuComment(__,__,__): true;
			default: false;
		}
	)
}

ruIndexOfDecl(name : string, src : RuSource) -> int {
	findiDef(src.decls, 
		\decl -> switch (decl) {
			RuConst(d,__,__,__):               d.name == name;
			RuType(d,__,__):                   d.name == name;
			RuRule(d,__,__,__):                d.name == name;
			RuAxiom(d,__,__,__,__,__):         d.name == name;
			RuTheorem(d,__,__,__,__,__,__,__): d.name == name;
			RuDef(d,__,__,__,__,__,__,__):     d.name == name;
		},
		-1
	)
}

ruStartPosition(decl : RuDecl) -> int {
	if (decl.descr.comments == []) decl.pos else {
		lastElement(decl.descr.comments, ruNoComment).pos;
	}
}

ruCheckDisjs(s : Tree<RuVar, RuExp>, disjs : Set<RuDisj>) -> bool {
	forall(set2array(disjs), \disj -> {
		e1 = lookupTreeDef(s, disj.v1, RuExp([disj.v1], -1));
		e2 = lookupTreeDef(s, disj.v2, RuExp([disj.v2], -1));
		e1_vars = ruExp2Vars(e1.nodes);
		e2_vars = ruExp2Vars(e2.nodes);
		isEmptySet(intersectSets(e1_vars, e2_vars));
	})
}

ruProofToDAG(proof : RuProof) -> RuDAG<string> {
	ruDoProofToDAG(proof.steps, lastElement(proof.steps, ruNoStep));
}

ruDoProofToDAG(steps : [RuStep], curr : RuStep) -> RuDAG<string> {
	RuDAG(curr.assertion, map(curr.refs, \ref -> 
		switch(ref) {
			RuStepRef(i,__): ruDoProofToDAG(steps, steps[i]);
			RuHypRef(i,__): RuDAG("", []);
		}
	));
}

ruDAG2s(dag : RuDAG<?>, show_data : (?) -> string) -> string {
	show_data(dag.data) + " {\n" +
		ruIndent(strGlue(map(dag.children, \ch -> ruDAG2s(ch, show_data)), ""))
	+ "}\n"
}

ruProofDAG2s(dag : RuDAG<string>) -> string {
	ruDAG2s(dag, \str -> if (str == "") "hyp" else str);
}

ruUpdateProofVars(proof : RuProof) -> RuProof {
	proof_vars = ruProof2Vars(proof);
	RuProof(proof with
		vars = filterTree(proof.vars, \__,v -> containsSet(proof_vars, v)),
		disjs = filterSet(proof.disjs, \d -> containsSet(proof_vars, d.v1) && containsSet(proof_vars, d.v2))
	);
}

ruUpdateTheoremVars(theorem : RuTheorem) -> RuTheorem {
	ass_vars = ruAssertion2Vars(theorem);
	RuTheorem(theorem with 
		vars = filterTree(theorem.vars, \__,v -> containsSet(ass_vars, v)),
		disjs = filterSet(theorem.disjs, \d -> containsSet(ass_vars, d.v1) && containsSet(ass_vars, d.v2)),
		proof = ruUpdateProofVars(theorem.proof)
	);
}

ruTransformProof(proof: RuProof, f : (RuExp) -> RuExp) -> RuProof {
	RuProof(proof with 
		steps = map(proof.steps, \step ->
			RuStep(step with expr = f(step.expr))
		)
	);
}

ruTransformTheorem(th: RuTheorem, f : (RuExp) -> RuExp) -> RuTheorem {
	RuTheorem(th with
		hyps = map(th.hyps, \h -> RuHyp(h with expr = f(h.expr))),
		prop = f(th.prop),
		proof = ruTransformProof(th.proof, f)
	);
}

ruTransformAxiom(ax: RuAxiom, f : (RuExp) -> RuExp) -> RuAxiom {
	RuAxiom(ax with
		hyps = map(ax.hyps, \h -> RuHyp(h with expr = f(h.expr))),
		prop = f(ax.prop)
	);
}

ruTransformDef(def: RuDef, f : (RuExp) -> RuExp) -> RuDef {
	RuDef(def with
		hyps = map(def.hyps, \h -> RuHyp(h with expr = f(h.expr))),
		defm = f(def.defm),
		defs = f(def.defs),
		prop = f(def.prop)
	);
}

ruSrc2Blocks(src : RuSource) -> [RuBlock] {
	comms2blocks = \comms -> map(comms, \comm -> cast(comm : RuComment -> RuBlock));
	concat(
		comms2blocks(src.comments),
		fold(src.decls, [], \acc, decl ->
			concat3(acc, comms2blocks(decl.descr.comments), [cast(decl : RuDecl -> RuBlock)])
		)
	);
}

RuBclocks2SrcAcc(
	decls : [RuDecl],
	comms : [RuComment]
);

ruSetDeclComments(decl : RuDecl, comms : [RuComment]) -> RuDecl {
	switch (decl) {
		RuConst(__,__,__,__):
			RuConst(decl with descr = RuDescr(decl.descr with comments = comms));
		RuType(__,__,__):
			RuType(decl with descr = RuDescr(decl.descr with comments = comms));
		RuRule(__,__,__,__):
			RuRule(decl with descr = RuDescr(decl.descr with comments = comms));
		RuAxiom(__,__,__,__,__,__):
			RuAxiom(decl with descr = RuDescr(decl.descr with comments = comms));
		RuTheorem(__,__,__,__,__,__,__,__):
			RuTheorem(decl with descr = RuDescr(decl.descr with comments = comms));
		RuDef(__,__,__,__,__,__,__,__):
			RuDef(decl with descr = RuDescr(decl.descr with comments = comms));
	}
}

ruBclocks2Src(src : RuSource, blocks : [RuBlock]) -> RuSource {
	accum = fold(blocks, RuBclocks2SrcAcc([], []), \acc, block ->
		switch (block) {
			RuComment(__,__,__): 
				RuBclocks2SrcAcc(acc.decls, concat(acc.comms, [block]));
			RuDecl(): {
				RuBclocks2SrcAcc(
					concat(acc.decls, [ruSetDeclComments(block, acc.comms)]), 
					[]
				);
			}
		}
	);
	RuSource(src with 
		comments = accum.comms,
		decls = accum.decls
	);
}

ruBlockName(block : RuBlock) -> string {
	switch (block) {
		RuDecl(): ruDeclName(block);
		RuComment(text,__,__): "<comment>";
	}
}