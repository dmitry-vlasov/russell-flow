import ru/parse/collect;
import ru/parse/write;

export {
	ruIoCommands() -> [RuComm];
	ruCommWrite(task : RuTask, env : RuEnv) -> RuEnv;
	ruCommRead(task : RuTask, env : RuEnv) -> RuEnv;
}

ruIoCommands() -> [RuComm] {[
	ruApplyToProjectRoots(RuComm(
		"read-ru", "io", "read a Russell file", "file",
		[RuCommArg("file", false, "input file", "")],
		ruCommRead
	)),
	RuComm(
		"write-ru", "io", "write a Russell file to a filesystem", "file",
		[
			RuCommArg("file", true, "input file", ""), 
			RuCommArg("all", true, "write all files", ""), 
			RuCommArg("monolithic", true, "include all dependencies into a target file", "")
		],
		ruCommWrite
	),
]}

ruCommWrite(task : RuTask, env : RuEnv) -> RuEnv {
	math = env.ruMath;
	conf = env.conf;
	if (lookupTreeDef(task.args, "all", "") == "1") {
		start = timestamp();
		iter(getTreeValues(math.sources), 
			\src -> ruWriteSource(src, math, conf)
		);
		time = (timestamp() - start);
		if (conf.verbose > 0 && time > 100.0) {
			conf.onMessage("ru written " + i2s(sizeTree(math.sources)) + " files in " + d2st(time/ 1000.0, 2) + "s");
		}
	} else if (lookupTreeDef(task.args, "file", "") != "") {
		file = lookupTreeDef(task.args, "file", "");
		if (!endsWith(file, ".ru")) {
			conf.onError("Wrong extension of a file '" + file + "', must be *.ru", []);
		} else {
			module = ruTrimPath(file, conf, ".ru");
			switch (lookupTree(math.sources, module)) {
				Some(src): {
					monolith =lookupTreeDef(task.args, "monolithic", "");
					if (monolith == "") {
						ruWriteSource(src, math, conf);
					} else {
						if (monolith != "1") {
							ruWriteSource(ruMath2SingleSource(math, monolith, conf), math, conf);
						} else {
							ruWriteSource(ruMath2SingleSource(math, src.info.path, conf), math, conf);
						}
					}
				}
				None(): conf.onMessage("source: " + file + " is not found");
			}
		}
	} else {
		conf.onError("ru nothing to write", []);
	}
	env;
}

ruCommRead(task : RuTask, env : RuEnv) -> RuEnv {
	file = lookupTreeDef(task.args, "file", "");
	if (!endsWith(file, ".ru")) {
		env.conf.onError("Wrong extension of a file: '" + file + "', must be *.ru", []);
		env;
	} else {
		path = resolveRelativePath(file);
		module = ruTrimPath(path, env.conf, getFileExt(file));
		ru = maybeBind(
			maybeBind(ruLoadAll(env.conf, module, ruFromCache), \loaded ->
				ruParseAll(getTreeValues(loaded), env.conf)
			), 
			\parsed -> ruCollectMath(parsed, env.conf)
		);
		eitherMap(ru, \math -> RuEnv(env with ruMath = math), env);
	}
}
