import ru/update;

export {
	ruWriteSource(src : RuSource, math : RuMath, conf : RuConf) -> void;
	ruWriteRefactoredSources(srcs : Set<string>, math : RuMath, conf : RuConf) -> void;
}

ruWriteSource(src : RuSource, math : RuMath, conf : RuConf) -> void {
	path = src.info.path;
	ensureDirectoryExists(ruDirName(path));
	ext = ltrim2(getFileExt(path), ".");
	if (!setFileContent(src.info.path, ru2s(src, math.rules))) {
		conf.onMessage("failed to save file: '" + path + "'");
	} else {
		if (conf.verbose >= 2) {
			conf.onMessage("\t" + ext + " has written : '" + path + "'");
		}
	}
	ruToCache(ruUpdateSourcePositions(src, conf), conf);
}

ruWriteRefactoredSources(srcs : Set<string>, math : RuMath, conf : RuConf) -> void {
	start = timestamp();
	iterSet(srcs, \s -> 
		maybeApply(lookupTree(math.sources, s), \src ->
			ruWriteSource(src, math, conf)
		)
	);
	if (conf.verbose > 0) {
		msg = 
			"ru saved: " + i2s(sizeSet(srcs)) + " refactored sources in " + d2st((timestamp() - start)/ 1000.0, 2) + "s" + 
			(if (conf.verbose == 1) "" else
				"\n\tsaved sources:\n" + strGlue(map(set2array(srcs), \src -> "\t\t" + src), "\n")
			);
		conf.onMessage(msg);
	}
}
