import base/load;
import base/util/timer;
import ru/cache;

export {
	RuLoaded ::= RuRawSource, RuCachedSource;

	ruLoadAll(env : RuEnv, module : int, from_cache : (int, RuEnv) -> Maybe<RuCachedSource>) -> Maybe<Tree<int, RuLoaded>>;
	ruLoadOne(env : RuEnv, module : int, from_cache : (int, RuEnv) -> Maybe<RuCachedSource>) -> Maybe<RuLoaded>;

	ruLoadDescr = RuLoadDescr("/*", "*/", "//", "\n", "import", ";;", ".ru");
}

ruLoadAll(env : RuEnv, module : int, from_cache : (int, RuEnv) -> Maybe<RuCachedSource>) -> Maybe<Tree<int, RuLoaded>> {
	err_count = ref 0;
	timer = ruMakeTimer();
	if (ruVerbose(env.conf) > 1) {
		env.out.onMessage("\tloading: '" + id2s(module) + "'");
	}
	loaded = ruLoadImports([module], ruAddErrCounter2Env(env, err_count), from_cache, makeTree());
	if (ruVerbose(env.conf) > 0) {
		env.out.onMessage(strRight(ruLoadDescr.ext, 1) + " loaded: " + i2s(sizeTree(loaded)) + " files in " + ruTimePassed2s(timer));
	}
	if (^err_count == 0) {
		Some(loaded);
	} else {
		env.out.onError(strRight(ruLoadDescr.ext, 1) + " failed to load: '" + id2s(module) + "'", []);
		None();
	}
}

ruLoadOne(env : RuEnv, module : int, from_cache : (int, RuEnv) -> Maybe<RuCachedSource>) -> Maybe<RuLoaded> {
	switch (from_cache(module, env)) {
		Some(src): {
			if (ruVerbose(env.conf) > 2) {
				env.out.onMessage("\t\tgetting: " + id2s(module) + " from cache");
			}
			Some(src);
		}
		None(): {
			if (ruVerbose(env.conf) > 2) {
				env.out.onMessage("\t\tparsing source of: " + id2s(module));
			}
			cast(ruLoadRawSource(module, env, ruLoadDescr) : Maybe<RuRawSource> -> Maybe<RuLoaded>);
		}
	}
}

ruLoadImports(imports : [int], env : RuEnv, from_cache : (int, RuEnv) -> Maybe<RuCachedSource>, acc : Tree<int, RuLoaded>) -> Tree<int, RuLoaded> {
	loaded_file = \loaded -> {
		switch (loaded) {
			RuCachedSource(src, __,__): src.info.id;
			RuRawSource(__,__, info): info.id;
		}
	}
	loaded_imports = \loaded -> {
		switch (loaded) {
			RuCachedSource(src, __,__): map(src.imports, \imp -> imp.id);
			RuRawSource(imps,__, info): imps;
		}
	}
	if (imports == []) acc else {
		if (ruVerbose(env.conf) > 2) {
			env.out.onMessage(
				"\t\timports:\n" + 
				strIndent(strIndent(strIndent(strGlue(map(imports, id2s), "\n")))) + "\n"
			);
		}
		loaded : [RuLoaded]  = filtermap(ruConcurrent(map(imports, \imp -> \ -> ruLoadOne(env, imp, from_cache))), idfn);

		if (ruVerbose(env.conf) > 2) {
			env.out.onMessage(
				"\t\tloaded imports:\n" + 
				strIndent(strIndent(strIndent(strGlue(map(loaded, \l -> id2s(loaded_file(l))), "\n")))) + "\n"
			);
		}
		acc1 = fold(loaded, acc, \ac, l -> setTree(ac, loaded_file(l), l));
		new_imports = fold(loaded, makeSet(), \ac, l -> 
			fold(loaded_imports(l), ac, \a, p -> {
				imp = s2id(ruTrimPath(id2s(p), env.conf, ruLoadDescr.ext));
				if (containsKeyTree(acc1, imp)) a else insertSet(a, imp);
			})
		);
		if (ruVerbose(env.conf) > 2) {
			env.out.onMessage(
				"\t\tnew_imports:\n" + 
				strIndent(strIndent(strIndent(strGlue(map(set2array(new_imports), id2s), "\n")))) + "\n"
			);
		}
		ruLoadImports(set2array(new_imports), env, from_cache, acc1)
	}
}
