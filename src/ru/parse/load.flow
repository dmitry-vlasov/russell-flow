import base/load;
import ru/cache;

export {
	RuLoaded ::= RuRawSource, RuCachedSource;

	ruLoadAll(conf : RuConf, module : int, from_cache : (int, RuConf) -> Maybe<RuCachedSource>) -> Maybe<Tree<int, RuLoaded>>;
	ruLoadOne(conf : RuConf, module : int, from_cache : (int, RuConf) -> Maybe<RuCachedSource>) -> Maybe<RuLoaded>;

	ruLoadDescr = RuLoadDescr("/*", "*/", "//", "\n", "import", ";;", ".ru");
}

ruLoadAll(conf : RuConf, module : int, from_cache : (int, RuConf) -> Maybe<RuCachedSource>) -> Maybe<Tree<int, RuLoaded>> {
	err_count = ref 0;
	start = ruTime();
	if (ruVerbose(conf) > 1) {
		conf.onMessage("\tloading: '" + id2s(module) + "'");
	}
	loaded = ruLoadImports([module], ruAddErrCounter(conf, err_count), from_cache, makeTree());
	if (ruVerbose(conf) > 0) {
		conf.onMessage(strRight(ruLoadDescr.ext, 1) + " loaded: " + i2s(sizeTree(loaded)) + " files in " + ruTime2s(ruTime() - start));
	}
	if (^err_count == 0) {
		Some(loaded);
	} else {
		conf.onError(strRight(ruLoadDescr.ext, 1) + " failed to load: '" + id2s(module) + "'", []);
		None();
	}
}

ruLoadOne(conf : RuConf, module : int, from_cache : (int, RuConf) -> Maybe<RuCachedSource>) -> Maybe<RuLoaded> {
	switch (from_cache(module, conf)) {
		Some(src): Some(src);
		None():    cast(ruLoadRawSource(module, conf, ruLoadDescr) : Maybe<RuRawSource> -> Maybe<RuLoaded>);
	}
}

ruLoadImports(imports : [int], conf : RuConf, from_cache : (int, RuConf) -> Maybe<RuCachedSource>, acc : Tree<int, RuLoaded>) -> Tree<int, RuLoaded> {
	loaded_file = \loaded -> {
		switch (loaded) {
			RuCachedSource(src, __,__): src.info.id;
			RuRawSource(__,__, info): info.id;
		}
	}
	loaded_imports = \loaded -> {
		switch (loaded) {
			RuCachedSource(src, __,__): map(src.imports, \imp -> imp.id);
			RuRawSource(imps,__, info): imps;
		}
	}
	if (imports == []) acc else {
		loaded : [RuLoaded]  = filtermap(ruConcurrent(map(imports, \imp -> \ -> ruLoadOne(conf, imp, from_cache))), idfn);
		acc1 = fold(loaded, acc, \ac, l -> setTree(ac, loaded_file(l), l));
		new_imports = fold(loaded, makeSet(), \ac, l -> 
			fold(loaded_imports(l), ac, \a, p ->
				if (containsKeyTree(acc1, s2id(ruTrimPath(id2s(p), conf, ruLoadDescr.ext)))) a else insertSet(a, p)
			)
		);
		ruLoadImports(set2array(new_imports), conf, from_cache, acc1)
	}
}
