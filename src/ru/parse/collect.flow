import ru/parse/exp;
import ru/parse/parse;

import ru/verify;

export {
	ruCollectMath(parsed : Tree<string, RuParsed>, conf : RuConf, skip_proofs : bool) -> Maybe<RuMath>;
}

ruCollectMath(parsed : Tree<string, RuParsed>, conf0 : RuConf, skip_proofs : bool) -> Maybe<RuMath> {
	err_count = ref 0;
	conf = ruAddErrCounter(conf0, err_count);
	start = timestamp();
	collected = ruDoCollectMath(parsed, makeSet(), conf, ruMakeEmptyCollectEnv(skip_proofs)).math;
	if (conf.verbose > 0) {
		conf.onMessage("ru collected: " + i2s(sizeTree(collected.sources)) + " files in " + d2st((timestamp() - start)/ 1000.0, 2) + "s");
	}
	if (^err_count == 0) {
		Some(collected);
	} else {
		None();
	}
}

RuCollectEnv(
	skipProofs : bool,
	math : RuMath,
	tenv : Tree<string, RuTypeEnv>
);

ruMakeEmptyCollectEnv(skip_proofs : bool) -> RuCollectEnv {
	RuCollectEnv(skip_proofs, ruEmptyMath, makeTree());
}

ruMergeCollectEnv(e1 : RuCollectEnv, e2 : RuCollectEnv, conf : RuConf) -> RuCollectEnv {
	RuCollectEnv(
		e1.skipProofs,
		ruMergeMath(e1.math, e2.math, conf),
		ruMergeTypeEnvTree(e1.tenv, e2.tenv)
	);
}

ruDoCollectMath(left : Tree<string, RuParsed>, done : Set<string>, conf : RuConf, acc : RuCollectEnv) -> RuCollectEnv {
	if (sizeTree(left) == 0) acc else {
		ready = filter(
			getTreeValues(left), 
			\src -> forall(ruParsedImports(src), \imp -> containsSet(done, imp))
		);
		if (ready == []) {
			conf.onError("cyclic import detected while collecting:\n" + ruRevealCyclicImport(left), []);
			acc
		} else {
			if (conf.verbose > 2) {
				conf.onMessage("\t\tgoing to collect: \n" + strGlue(map(ready, \p -> "\t\t\t" + ruParsedModule(p)), "\n"));
			}
			collected = filtermap(ready, \parsed ->
				switch (parsed) {
					RuParseSource(__,__,info): {
						if (conf.verbose > 2) {
							conf.onMessage("\t\t\tstarted collecting parsed: '" + ruParsedModule(parsed) + "'");
						}
						r = ruCollectParsed(parsed, acc, conf);
						if (conf.verbose > 2) {
							conf.onMessage("\t\t\tfinished collecting parsed: '" + ruParsedModule(parsed) + "'");
						}
						Some(r);
					}
					RuCachedSource(src, depChanged,__): {
						if (conf.verbose > 2) {
							conf.onMessage("\t\t\tstarted collecting cached: '" + ruParsedModule(parsed) + "'");
						}
						if (!depChanged()) {
							start = timestamp();
							ret = Some(ruCollectCached(src, acc, conf)); 
							if (conf.verbose > 1) {
								conf.onMessage("\tru collected cached: '" + src.info.module + "' in " + d2st((timestamp() - start)/ 1000.0, 4) + " s");
							}
							ret;
						} else {
							start = timestamp();
							switch (ruVerifySource1(src, ruAddSource(src, acc.math, conf), conf)) {
								Some(verified): {
									ret = Some(ruCollectCached(src, acc, conf));
									if (conf.verbose > 1) {
										conf.onMessage("\tru collected verified: '" + src.info.module + "' in " + d2st((timestamp() - start)/ 1000.0, 4) + " s");
									}
									ret;
								}
								None(): {
									maybeBind(ruLoadOne(conf, src.info.module, \__,__ -> None()), \l ->
										maybeMap(ruParseOne(l, conf), \s ->
											ruCollectOne(s, acc, conf)
										)
									);
								}
							}
						}
					}
				}
			);
			if (conf.verbose > 2) {
				conf.onMessage("\t\tcollected: \n" + strGlue(map(ready, \p -> "\t\t\t" + ruParsedModule(p)), "\n"));
			}
			new_acc = ruMergeCollectEnv(acc, 
				fold(collected, 
					ruMakeEmptyCollectEnv(acc.skipProofs), 
					\ac, coll -> ruMergeCollectEnv(coll, ac, conf)), 
				conf
			);
			new_left = fold(ready, left, \ac, parsed -> removeFromTree(ac, ruParsedModule(parsed)));
			new_done = fold(ready, done, \ac, parsed -> insertSet(ac, ruParsedModule(parsed)));
			if (conf.verbose > 2) {
				conf.onMessage("\t\tfolded: \n" + strGlue(map(ready, \p -> "\t\t\t" + ruParsedModule(p)), "\n"));
			}
			ruDoCollectMath(new_left, new_done, conf, new_acc);
		}
	}
}

RuCollectAcc(
	skipProofs : bool,
	file : string,
	module : string,
	global : RuMath,
	local : RuMath,
	tenv : Tree<string, RuTypeEnv>,
	conf : RuConf
);

ruCollectCached(src : RuSource, acc : RuCollectEnv, conf : RuConf) -> RuCollectEnv {
	m = ruMakeMathFromSource(src, conf);
	RuCollectEnv(
		acc.skipProofs,
		RuMath(m with sources = setTree(m.sources, src.info.module, src)),
		mergeTree(
			acc.tenv,
			mapTree(m.types, \t -> ruMakeTypeEnv(t, conf))
		)
	);
}

ruCollectOne(parsed : RuParsed, acc : RuCollectEnv, conf : RuConf) -> RuCollectEnv {
	switch (parsed) {
		RuParseSource(__,__,__): ruCollectParsed(parsed, acc, conf);
		RuCachedSource(src, __,__): ruCollectCached(src, acc, conf);
	}
}

ruCollectParsed(src : RuParseSource, acc : RuCollectEnv, conf : RuConf) -> RuCollectEnv {
	start = timestamp();
	switch (lookupTree(acc.math.sources, src.info.module)) {
		Some(__): {
			conf.onError("source '" + src.info.module + "' is already defined", []);
			acc
		}
		None(): {
			imps = map(src.imports, \imp -> RuImport(imp.module, imp.pos));
			acc0 = RuCollectAcc(acc.skipProofs, src.info.path, src.info.module, acc.math, ruEmptyMath, acc.tenv, conf);
			acc1 = fold(src.decls, acc0, \ac, block -> 
				switch(block) {
					RuParseDecl(): ruCollectSyntax(block, ac);
					RuParseComment(__,__,__): ac;
				}
			);
			parse_decls : [RuParseDecl] = filtermap(src.decls, \block -> 
				switch(block) {
					RuParseDecl(): Some(block);
					RuParseComment(__,__,__): None();
				}
			);
			created_decls = ruConcurrent(map(parse_decls, \decl -> \-> ruCreateAssertion(decl, acc1)));
			//created_decls = map(parse_decls, \decl -> ruCreateAssertion(decl, acc1));
			assertions = filtermap(created_decls, idfn);
			acc2 = fold(assertions, acc1, \ac, ass -> 
				RuCollectAcc(ac with
					local = RuMath(ac.local with 
						assertions = setTree(ac.local.assertions, ass.descr.name, ass)
					)
				)
			);
			blocks = fold(src.decls, [], \ac, block -> 
				switch (block) {
					RuParseConst(name,__,__,__):
						eitherMap(lookupTree(acc2.local.consts, name), \c -> concat(ac, [c]), ac);
					RuParseType(name,__,__): 
						eitherMap(lookupTree(acc2.local.types, name), \t -> concat(ac, [t]), ac);
					RuParseRule(name,__,__,__):
						eitherMap(lookupTree(acc2.local.rules, name), \r -> concat(ac, [r]), ac);
					RuParseAxiom(name,__,__,__,__,__):
						eitherMap(lookupTree(acc2.local.assertions, name), \a -> concat(ac, [a]), ac);
					RuParseTheorem(name,__,__,__,__,__,__,__):
						eitherMap(lookupTree(acc2.local.assertions, name), \t -> concat(ac, [t]), ac);
					RuParseDef(name,__,__,__,__,__,__,__):
						eitherMap(lookupTree(acc2.local.assertions, name), \d -> concat(ac, [d]), ac);
					RuParseComment(text, json, pos):
						concat(ac, [RuComment(text, ruCollectAnnotation(json, pos, src.info.module, conf), pos)]);
					RuParseTheory(name, __,__):                ac; // TODO
				}
			);
			if (conf.verbose > 1) {
				conf.onMessage("\tru collected: '" + src.info.path + "' in " + d2st((timestamp() - start)/ 1000.0, 4) + " s");
			}
			ru_src = ruBclocks2Src(RuSource(imps, [], [], src.info), blocks);
			ruToMemCache(ru_src, conf);
			ruToDiskCache(ru_src, conf);
			RuCollectEnv(acc2.skipProofs,
				RuMath(acc2.local with sources = makeTree1(src.info.module, ru_src)),
				acc2.tenv
			);
		}
	}
}

ruCollectAnnotation(json_str : string, pos : int, file : string, conf : RuConf) -> JsonObject {
	if (json_str == "") JsonObject([]) else {
		json = parseJson(json_str);
		switch (json) {
			JsonObject(__): json;
			default: {
				conf.onError("Annotation must be a JSON object, got:\n\t" + json_str + "\n", [RuPlace(file, pos)]);
				JsonObject([]);
			}
		}
	}
}

ruCollectSyntax(decl : RuParseDecl, acc : RuCollectAcc) -> RuCollectAcc {
	switch (decl) {
		RuParseConst(__,__,__,__): ruCollectConst(decl, acc);
		RuParseType(__,__,__):     ruCollectType(decl, acc);
		RuParseRule(__,__,__,__):  ruCollectRule(decl, acc);
		default: acc;
	}
}

ruCreateAssertion(decl : RuParseDecl, acc : RuCollectAcc) -> Maybe<RuAssertion> {
	switch (decl) {
		RuParseAxiom(__,__,__,__,__,__):         ruCreateAxiom(decl, acc);
		RuParseTheorem(__,__,__,__,__,__,__,__): ruCreateTheorem(decl, acc);
		RuParseDef(__,__,__,__,__,__,__,__):     ruCreateDef(decl, acc);
		default: None();
	}
}

ruCollectConst(const : RuParseConst, acc : RuCollectAcc) -> RuCollectAcc {
	switch (ruLookupConst(acc, const.name)) {
		Some(__): {
			acc.conf.onError("constant " + const.name + " is already defined", [RuPlace(acc.file, const.pos)]);
			acc
		}
		None(): {
			RuCollectAcc(acc with
				local = RuMath(acc.local with 
					consts = setTree(acc.local.consts, const.name, 
						RuConst(RuDescr(const.name, acc.module, [], const.pos), const.ascii, const.latex)
					),
				)
			);
		}
	}
}

ruCollectType(type : RuParseType, acc : RuCollectAcc) -> RuCollectAcc {
	switch (lookupTree(acc.tenv, type.name)) {
		Some(__): {
			acc.conf.onError("type '" + type.name + "' is already defined", [RuPlace(acc.file, type.pos)]);
			acc
		}
		None(): {
			conf1 = ruAddErr2Conf(acc.conf, "\nat type '" + type.name + "'");
			new_type = RuType(RuDescr(type.name, acc.module, [], type.pos), type.supers);
			RuCollectAcc(acc with
				local = ruAddType(new_type, acc.local, conf1),
				tenv = setTree(acc.tenv, type.name, 
					ruMakeTypeEnv(new_type, conf1)
				)
			)
		}
	}
}

ruCollectRule(rule : RuParseRule, acc : RuCollectAcc) -> RuCollectAcc {
	switch (ruLookupRule(acc, rule.name)) {
		Some(__): {
			acc.conf.onError("rule '" + rule.name + "' is already defined", [RuPlace(acc.file, rule.pos)]);
			acc
		}
		None(): {
			conf1 = ruAddErr2Conf(acc.conf, "\nat rule " + rule.name);
			type = rule.term.type;
			vars = ruCollectVars(rule.vars, acc, conf1);
			term = ruMakeTerm(rule.term, vars, acc, ruAddErr2Conf(acc.conf, "\nat rule: " + rule.name));
			new_rule = RuRule(RuDescr(rule.name, acc.module, [], rule.pos), vars, term);
			switch (lookupTree(acc.tenv, rule.term.type)) {
				Some(tenv): {
					RuCollectAcc(acc with
						local = RuMath(acc.local with 
							rules = setTree(acc.local.rules, rule.name, new_rule),
						),
						tenv = setTree(acc.tenv, type,
							RuTypeEnv(tenv with 
								trie = ruAddTerm2RuTrie(new_rule.term, 
									Some(RuRuleNode(rule.name, type, sizeTree(vars), 0)),
									tenv.trie, conf1
								)
							)
						)
					);
				}
				None(): {
					acc.conf.onError("unknown type '" + rule.term.type + "'", [RuPlace(acc.file, rule.term.pos)]);
					acc;
				}
			}
		}
	}
}

ruMakeTerm(term : RuParseExp, vars : Tree<string, RuVar>, acc : RuCollectAcc, conf : RuConf) -> RuTerm {
	expr = fold(term.symbs, [], \ac, symb -> {
		switch (ruLookupConst(acc, symb)) {
			Some(s): concat(ac, [RuConstRef(symb)]);
			None(): 
				switch (lookupTree(vars, symb)) {
					Some(v): concat(ac, [RuVar(symb, v.type)]);
					None(): {
						conf.onError("unknown symbol '" + symb + "' in term '" + ruParse2s(term) + "'", [RuPlace(acc.file, term.pos)]);
						//println("glob consts:\n" + strGlue(map(getTreeKeys(acc.global.consts), \c -> "\t'" + c + "'"), "\n") + "\n");
						//println("local consts:\n" + strGlue(map(getTreeKeys(acc.local.consts), \c -> "\t'" + c + "'"), "\n") + "\n");
						quit(0);
						ac
					}
				}
			}
		}
	);
	RuTerm(term.type, expr, term.pos);
}

ruCreateAxiom(ax : RuParseAxiom, acc : RuCollectAcc) -> Maybe<RuAxiom> {
	switch (ruLookupAssertion(acc, ax.name)) {
		Some(__): {
			acc.conf.onError("assertion with name '" + ax.name + "' is already defined", [RuPlace(acc.file, ax.pos)]);
			None();
		}
		None(): {
			err = \-> { acc.conf.onError("error in axiom " + ax.name, [RuPlace(acc.file, ax.pos)]); None(); }
			conf1 = ruAddErr2Conf(acc.conf, "\nat axiom " + ax.name);
			vars = ruCollectVars(ax.vars, acc, conf1);
			disjs = ruCollectDisjs(ax.disjs, vars, acc, conf1);
			hyps = filtermap(ax.hyps, \h -> ruCollectHyp(h, vars, acc, conf1));
			if (length(hyps) != length(ax.hyps)) {
				err();
			} else {
				switch (ruCollectExp(ax.prop, vars, acc, conf1)) {
					Some(prop): {
						Some(RuAxiom(
							RuDescr(ax.name, acc.module, [], ax.pos), 
							RuStatement(vars, disjs, hyps, prop)
						));
					}
					None(): err();
				}
			}
		}
	}
}

ruCollectExp(ex : RuParseExp, vars : Tree<string, RuVar>, acc : RuCollectAcc, conf : RuConf) -> Maybe<RuExp> {
	ruParseExp(ruMakeTerm(ex, vars, acc, conf), acc.tenv, conf)
}

ruCollectVars(vars : [RuParseVar], acc : RuCollectAcc, conf : RuConf) -> Tree<string, RuVar> {
	fold(vars, makeTree(), \ac, v ->
		switch (lookupTree(acc.tenv, v.type)) {
			Some(tp): setTree(ac, v.name, RuVar(v.name, v.type));
			None(): {
				conf.onError("unknown var " + v.name + " type " + v.type, [RuPlace(acc.file, v.pos)]);
				ac
			}
		}
	)
}

ruCollectMeta(m : RuParseMeta, vars : Tree<string, RuVar>, acc : RuCollectAcc, conf : RuConf) -> Set<string> {
	fold(m.meta, makeSet(), \ac, v ->
		if (containsKeyTree(vars, v)) insertSet(ac, v) else {
			conf.onError("unknown var " + v, [RuPlace(acc.file, m.pos)]);
			ac
		}
	)
}

ruCollectDisjs(disjs : [RuParseDisj], vars : Tree<string, RuVar>, acc : RuCollectAcc, conf : RuConf) -> Set<RuDisj> {
	fold(disjs, makeSet(), \acc1, disj -> 
		fold(disj.vars, acc1, \acc2, v1 ->
			fold(disj.vars, acc2, \acc3, v2 -> {
					switch (lookupTree(vars, v1)) {
						Some(vr1): {
							switch (lookupTree(vars, v2)) {
								Some(vr2): {
									if (vr1.name < vr2.name) 
										insertSet(acc3, RuDisj(RuVar(vr1.name, vr1.type), RuVar(vr2.name, vr2.type))) 
									else if (vr2.name < vr1.name) 
										insertSet(acc3, RuDisj(RuVar(vr2.name, vr2.type), RuVar(vr1.name, vr1.type))) 
									else acc3 
								}
								None(): {
									conf.onError("unknown var '" + v2 + "' in disjointed set, vars: " + 
									strGlue(map(getTreeValues(vars), \v -> "'" + v.name + "'"), ", "), 
									[RuPlace(acc.file, disj.pos)]); acc3;
								}
							}
						}
						None(): {
							conf.onError("unknown var '" + v1 + "' in disjointed set, vars: " + 
							strGlue(map(getTreeValues(vars), \v -> "'" + v.name + "'"), ", "), 
							[RuPlace(acc.file, disj.pos)]); acc3;
						}
					}
				}
			)
		)
	)
}

ruCollectHyp(hyp : RuParseHyp, vars : Tree<string, RuVar>, acc : RuCollectAcc, conf : RuConf) -> Maybe<RuHyp> {
	maybeMap(
		ruCollectExp(hyp.expr, vars, acc, conf), 
		\expr -> RuHyp(hyp.ind, expr, hyp.pos)
	)
}

ruCreateTheorem(th : RuParseTheorem, acc : RuCollectAcc) -> Maybe<RuTheorem> {
	switch (ruLookupAssertion(acc, th.name)) {
		Some(__): {
			acc.conf.onError("assertion with name " + th.name + " is already defined", [RuPlace(acc.file, th.pos)]);
			None()
		}
		None(): {
			err = \-> { acc.conf.onError("error in theorem " + th.name, [RuPlace(acc.file, th.pos)]); None(); }
			conf1 = ruAddErr2Conf(acc.conf, "\nat theorem " + th.name);
			vars = ruCollectVars(th.vars, acc, conf1);
			disjs = ruCollectDisjs(th.disjs, vars, acc, conf1);
			meta = ruCollectMeta(th.meta, vars, acc, conf1);
			hyps = filtermap(th.hyps, \h -> ruCollectHyp(h, vars, acc, conf1));
			if (length(hyps) != length(th.hyps)) {
				err();
			} else {
				switch (ruCollectExp(th.prop, vars, acc, conf1)) { 
					Some(prop): {
						switch (ruCollectProof(th.proof, vars, acc, conf1)) {
							Some(proof): Some(RuTheorem(
								RuDescr(th.name, acc.module, [], th.pos), 
								RuStatement(vars, disjs, hyps, prop), 
								meta, proof
							));
							None(): err();
						}
					}
					None(): err();
				}
			}
		}
	}
}

ruCollectProof(proof : RuParseProof, vars : Tree<string, RuVar>, acc : RuCollectAcc, conf : RuConf) -> Maybe<RuProof> {
	if (acc.skipProofs) Some(RuProof(makeTree(), makeSet(), [], proof.pos)) else {
		proof_vars = ruCollectVars(proof.vars, acc, conf);
		all_vars = mergeTreeCustom(proof_vars, vars, \v, w,__ -> {
			conf.onError("duplicate variable " + v, [RuPlace(acc.file, proof.pos)]);
			w
		});
		proof_disjs = ruCollectDisjs(proof.disjs, all_vars, acc, conf);
		do_ref = \r -> switch (r) {
			RuParseHypRef(i, p): RuHypRef(i, p);
			RuParseStepRef(i, p): RuStepRef(i, p);
		}
		steps = filtermap(proof.steps,
			\s ->
				maybeMap(ruCollectExp(s.expr, all_vars, acc, conf),
					\expr -> RuStep(s.ind, s.assertion, map(s.refs, do_ref), expr, s.pos)
				)
		);
		if (length(steps) != length(proof.steps)) None() else {
			Some(RuProof(proof_vars, proof_disjs, steps, proof.pos));
		}
	}
}

ruCreateDef(df : RuParseDef, acc : RuCollectAcc) -> Maybe<RuDef> {
	switch (ruLookupAssertion(acc, df.name)) {
		Some(__): {
			acc.conf.onError("assertion with name " + df.name + " is already defined", [RuPlace(acc.file, df.pos)]);
			None()
		}
		None(): {
			err = \-> { acc.conf.onError("error in definition " + df.name, [RuPlace(acc.file, df.pos)]); None(); }
			conf1 = ruAddErr2Conf(acc.conf, "\nat definitoin " + df.name);
			vars = ruCollectVars(df.vars, acc, conf1);
			disjs = ruCollectDisjs(df.disjs, vars, acc, conf1);
			prop_ex = fold(df.prop.symbs, [], \ac, s ->
				if (s == "defiendum") concat(ac, df.defm.symbs) else
				if (s == "definiens") concat(ac, df.defs.symbs) else
				concat(ac, [s])
			);
			hyps = filtermap(df.hyps, \h -> ruCollectHyp(h, vars, acc, conf1));
			if (length(hyps) != length(df.hyps)) {
				err();
			} else {
				switch (ruCollectExp(RuParseExp(df.prop.type, prop_ex, df.prop.pos), vars, acc, conf1)) {
					Some(prop): {
						switch (ruCollectExp(df.defm, vars, acc, conf1)) {
							Some(defm): {
								switch (ruCollectExp(df.defs, vars, acc, conf1)) {
									Some(defs): 
										Some(RuDef(
											RuDescr(df.name, acc.module, [], df.pos), 
											RuStatement(vars, disjs, hyps, prop), 
											defm, defs
										));
									None(): err();
								}
							}
							None(): err();
						}
					}
					None(): err();
				}
			}
		}
	}
}

ruLookupConst(acc : RuCollectAcc, n : string) -> Maybe<RuConst> {
	switch(lookupTree(acc.global.consts, n)) {
		Some(c): Some(c);
		None(): lookupTree(acc.local.consts, n);
	}
}

ruLookupRule(acc : RuCollectAcc, n : string) -> Maybe<RuRule> {
	switch(lookupTree(acc.global.rules, n)) {
		Some(r): Some(r);
		None(): lookupTree(acc.local.rules, n);
	}
}

ruLookupAssertion(acc : RuCollectAcc, n : string) -> Maybe<RuAssertion> {
	switch(lookupTree(acc.global.assertions, n)) {
		Some(a): Some(a);
		None(): lookupTree(acc.local.assertions, n);
	}
}
