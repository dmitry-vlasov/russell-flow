import math/stringmath;
import ru/parse/exps/parser;
import ru/src;

export {
	ruParseExp(term : RuTerm, lang : RuLang, place : RuPlace, env : RuEnv) -> Maybe<RuExp>;
	ruCreateTypeParsers(term : RuTerm, lang : RuLang, env : RuEnv) -> Tree<int, RuTypeParser>;
}

ruCreateTypeParsers(term : RuTerm, lang : RuLang, env : RuEnv) -> Tree<int, RuTypeParser> {
	types = foldTree(lang.types, 
		makeTree(), 
		\id, type, acc -> setTree(acc, id, ruMakeTypeParser(type, env))
	);
	symbs = fold(term.nodes, makeSet(), \acc, node -> 
		switch (node) {
			RuVar(__, type): acc;
			RuConstRef(c): insertSet(acc, c);
		}
	);
	fold(
		filter(
			getTreeValues(lang.rules),
			\rule -> forall(rule.term.nodes, \node ->
				switch (node) {
					RuConstRef(c): containsSet(symbs, c);
					default: true;
				}
			)
		), 
		types,
		\acc, rule -> ruAddRule2TypeParserTree(rule, acc, env)
	);
}

ruParseExp(term : RuTerm, lang : RuLang, place : RuPlace, env : RuEnv) -> Maybe<RuExp> {
	if (length(term.nodes) == 0) {
		env.out.error("empty expression is met", []);
		None()
	} else {
		types = ruCreateTypeParsers(term, lang, env);
		switch (lookupTree(types, term.type)) {
			Some(tenv): {
				ret = maybeBind(
					ruParseExpr(RuParseEnv(0, term.nodes, types, lang, env), 0, RuParseStack(None(), 0, 0, term.type, tenv.trie, [])),
					\pair -> if (pair.second == length(term.nodes)) Some(RuExp(reverseA(pair.first), term.pos)) else {
						println("pair.second != length(term.nodes): " + i2s(pair.second) + " != " + i2s(length(term.nodes)) + "\n" +
							ruExpRPN2s(RuExp(reverseA(pair.first), term.pos)) + "\n" +
							ruTerm2s(term) + "\n"
						);
						None()
					}
				);
				if (ret == None()) {
					term_symbs = set2array(fold(term.nodes, makeSet(),
						\acc, node -> 
							switch (node) {
								RuVar(__, type): acc;
								RuConstRef(c): insertSet(acc, c);
							}
					));
					err = "expr: '" + ruTypedTerm2s(term) + "' was not parsed" +
						(if (env.conf.verbose <= 1) "" else 
						 "\n" + ruTypeParserTree2s(types) + "\n" +
						"term symbs: " + strGlue(map(term_symbs, id2s), ", ") + "\n" + 
						"all rules:\n" + concatStrings(map(getTreeValues(lang.rules), \r -> "\t" + ruTerm2s(r.term) + "\n")));
					env.out.error(err, [place]);
					//ruCrash("parser failure:\n" + err + "\n\n");
				}
				ret
			}
			None(): {
				env.out.error("unknown type: '" + id2s(term.type) + "', term: " + ruTypedTerm2s(term), [place]);
				fail("AAA");
				None();
			}
		}
	}
}

RuParseEnv(
	start: int,
	nodes : [RuTermNode],
	types : Tree<int, RuTypeParser>,
	lang : RuLang,
	env : RuEnv,
);

RuParseStack(
	parent : Maybe<RuParseStack>,
	start : int,
	curr : int,
	type_id : int,
	trie : RuTrie2<int, RuRuleNode>,
	expr : [[RuExpNode]],
);

ruDoConcatAReversed(i: int, expr : [[RuExpNode]], acc: [RuExpNode]) -> [RuExpNode] {
	if (i == -1) acc else {
		ruDoConcatAReversed(i - 1, expr, concat(acc, expr[i]))
	}
}

ruConcatAReversed(expr : [[RuExpNode]]) -> [RuExpNode] {
	ruDoConcatAReversed(length(expr) - 1, expr, [])
}


ruPopExprStack(env : RuParseEnv, pos : int, stack : RuParseStack) -> RuParseStack {
	switch (stack.trie.data) {
		Some(rf): {
			switch (stack.parent) {
				Some(parent): {
					subexpr = ruConcatAReversed(stack.expr);
					ruPopExprStack(env, pos, 
						RuParseStack(parent with
							expr = arrayPush(
								parent.expr,
								arrayPush(
									subexpr,
									RuRuleNode(
										rf.idType,
										ruMakeArityLen(ruArityLen2Arity(rf.arityLen), length(subexpr))
									),
								)
							)
						)
					);
				}
				None(): stack;
			}
		}
		None(): stack;
	}
}

ruParseExpr(env : RuParseEnv, pos : int, stack0 : RuParseStack) -> Maybe<Pair<[RuExpNode], int>> {
	stack = RuParseStack(stack0 with curr = pos);
	do_step = \st, n -> {
		poped = ruPopExprStack(env, pos, RuParseStack(st with trie = n));
		if (ruTrie2IsEnd(poped.trie) && isNone(poped.parent)) {
			switch (poped.trie.data) {
				Some(rf): {
					subexpr = ruConcatAReversed(poped.expr);
					expr = arrayPush(
						subexpr,
						RuRuleNode(
							rf.idType, 
							ruMakeArityLen(ruArityLen2Arity(rf.arityLen), length(subexpr))
						),
					);
					Some(Pair(expr, pos + 1));
				}
				None(): {
					Some(Pair(poped.expr[0], pos + 1));
				}
			}
		} else {
			ruParseExpr(env, pos + 1, poped);
		}
	}
	try_var = \t, n -> {
		switch (lookupTree(env.types, t)) {
			Some(tenv): {
				if (t == stack.type_id && stack.curr == stack.start) None() else {
					ruParseExpr(env, pos,
						RuParseStack(
							Some(RuParseStack(stack with trie = n)), 
							pos, pos, t, tenv.trie, []
						)
					)
				}
			}
			None(): None();
		}
	}
	try_supers = \v, t -> {
		supers = lookupTreeDef(env.types, t, ruNoTypeParser).supers;
		foldTree(stack.trie.map1, None(), \tp, m, ac -> 
			if (ac != None()) ac else {
				switch (lookupTree(supers, tp)) {
					Some(r): {
						ret = do_step(RuParseStack(stack with 
							expr = arrayPush(
								stack.expr,
								[
									RuVar(v, t),
									ruMakeParsedRule(r.info.id, r.term.type, 1, 1),
								], 
							)
						), m);
						if (isSome(ret)) ret else try_var(tp, m);
					}
					None(): try_var(tp, m);
				}
			}
		);
	}
	try_vars = \-> {
		foldTree(stack.trie.map1, None(), \t, n, ac ->
			if (ac != None()) ac else {
				try_var(t, n);
			}
		)
	}
	switch (env.nodes[pos]) {
		RuVar(v, t_id): {
			switch (lookupTree(stack.trie.map1, t_id)) {
				Some(n): {
					switch (do_step(RuParseStack(stack with expr = arrayPush(stack.expr, [RuVar(v, t_id)])), n)) {
						Some(ret): Some(ret);
						None(): try_supers(v, t_id);
					}
				}
				None(): try_supers(v, t_id);
			}
		}
		RuConstRef(id): {
			switch (lookupTree(stack.trie.map2, id)) {
				Some(n): {
					switch (do_step(stack, n)) {
						Some(ret): Some(ret);
						None(): try_vars();
					}
				}
				None(): try_vars();
			}
		}
	}
}
