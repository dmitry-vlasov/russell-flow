import base/stats;
import base/id_consts;
import ru/parse/exp;
import ru/parse/parse;
import ru/parse/exps/lang;
import ru/parse/exps/exp;
import ru/verify/math;

export {
	ruCollectSourcesAssertions(sources : [RuParsed], lang : RuLang, env : RuEnv) -> Tree<int, RuAssertion>;

	ruLang2Tenv(lang : RuLang, env : RuEnv) -> Tree<int, RuTypeEnv>;
	ruCreateAssertions(decls : [RuDeclAux], lang : RuLang, env : RuEnv) -> Tree<int, RuAssertion>;
}

ruLang2Tenv(lang : RuLang, env : RuEnv) -> Tree<int, RuTypeEnv> {
	fold(
		sortCustom(getTreeValues(lang.rules), \rule -> rule.info.src, true), 
		foldTree(lang.types, makeTree(), \id, type, acc ->
			setTree(acc, id, ruMakeTypeEnv(type, env))
		),
		\acc, rule -> ruAddRule2TypeEnvTree(rule, acc, env)
	);
}

ruCollectSourcesAssertions(sources : [RuParsed], lang : RuLang, env : RuEnv) -> Tree<int, RuAssertion> {
	tenv = ruLang2Tenv(lang, env);
	fold(
		ruConcurrent(map(sources, \src -> \-> ruCollectSourceAssertions(src, lang, tenv, env))), 
		makeTree(), 
		\acc, ass -> ruAssertionsMerge(acc, ass, env)
	);
}

ruCollectSourceAssertions(src : RuParsed, lang : RuLang, tenv : Tree<int, RuTypeEnv>, env : RuEnv) -> Tree<int, RuAssertion> {
	switch (src) {
		RuParseSource(__,decls,__,info): {
			fold(src.decls, makeTree(), \ac, decl ->
				eitherMap(
					ruCreateAssertion1(decl, lang, tenv, info, env), 
					\ass -> ruAssertionsAddDecl(ac, ass, env), 
					ac
				)
			)
		}
		RuSource(__,decls,__,info): {
			fold(src.decls, makeTree(), 
				\ac, decl -> ruAssertionsAddDecl(ac, decl, env)
			)
		}
	}
}

ruCreateAssertions(decls : [RuDeclAux], lang : RuLang, env : RuEnv) -> Tree<int, RuAssertion> {
	tenv = ruLang2Tenv(lang, env);
	fold(
		filtermap(ruConcurrent(map(decls, \ad -> \-> 
			switch (ad.decl) {
				RuParseDecl(): ruCreateAssertion1(ad.decl, lang, tenv, ad.srcInfo, env);
				RuAssertion(): Some(ad.decl);
				default:       None();
			}
		)), idfn),
		makeTree(),
		\acc, ass -> ruAssertionsAddDecl(acc, ass, env)
	);
}

ruCreateAssertion1(decl : RuParseDecl, lang : RuLang, tenv : Tree<int, RuTypeEnv>, src_info : RuFileInfo, env : RuEnv) -> Maybe<RuAssertion> {
	switch (decl) {
		RuParseAxiom(__,__,__,__,__,__,__):         ruCreateAxiom1(decl, lang, tenv, src_info, env);
		RuParseTheorem(__,__,__,__,__,__,__,__,__): ruCreateTheorem1(decl, lang, tenv, src_info, env);
		RuParseDef(__,__,__,__,__,__,__,__,__):     ruCreateDef1(decl, lang, tenv, src_info, env);
		default: None();
	}
}

ruCreateAxiom1(ax : RuParseAxiom, lang : RuLang, tenv : Tree<int, RuTypeEnv>, src_info : RuFileInfo, env : RuEnv) -> Maybe<RuAxiom> {
	vars = ruCreateVars(ax.vars);
	disjs = ruCreateDisjs(ax.disjs, vars, src_info, env);
	hyps = filtermap(ax.hyps, \h -> ruCreateHyp(h, vars, lang, tenv, src_info, env));
	if (length(hyps) != length(ax.hyps)) {
		None();
	} else {
		maybeMap(ruCreateExp(ax.prop, vars, lang, tenv, src_info, env), \prop ->
			RuAxiom(
				RuDeclInfo(
					ax.id, 
					src_info.id, 
					ruCreateComments(ax.comments, src_info, env), 
					ax.pos
				), 
				RuStatement(RuVarsDisjs(vars, disjs), hyps, prop)
			)
		);
	}
}

ruCreateExp(ex : RuParseExp, vars : Tree<int, RuVar>, lang : RuLang, tenv : Tree<int, RuTypeEnv>, src_info : RuFileInfo, env : RuEnv) -> Maybe<RuExp> {
	//t = ruMakeTimer();
	ruParseExp1(ruCreateTerm(ex, vars, env), lang, RuPlace(src_info.path, ex.pos), env);
	/*if (ruTimePassed(t) > 1000.0) {
		term = ruCreateTerm(ex, vars, env);
		parsers = ruCreateTypeParsers(term, lang, env);
		traverseInOrder(parsers, \tp, parser ->
			env.out.onMessage("type: " + id2s(tp) + " parser:\n" + strIndent(ruTypeParser2s(parser)))
		);
		env.out.onMessage("term: " + ruTerm2s(term) + " is " + (if (isNone(exp)) "NOT" else "") + " parsed in " + ruTimePassed2s(t));
		if (isNone(exp)) {
			ruCrash("expr is not parsed");
		}
	}*/
}

ruCreateMeta(m : RuParseMeta, vars : Tree<int, RuVar>, src_info : RuFileInfo, env : RuEnv) -> Set<int> {
	fold(m.meta, makeSet(), \ac, v -> {
		if (containsKeyTree(vars, v)) insertSet(ac, v) else {
			env.out.onError("unknown var '" + id2s(v) + "'", [RuPlace(src_info.path, m.pos)]);
			ac
		}
	});
}

ruCreateDisjs(disjs : [RuParseDisj], vars : Tree<int, RuVar>, src_info : RuFileInfo, env : RuEnv) -> Set<RuDisj> {
	make_var = \v, t -> RuVar(v, t);
	fold(disjs, makeSet(), \acc1, disj -> 
		fold(disj.vars, acc1, \acc2, v1 ->
			fold(disj.vars, acc2, \acc3, v2 -> {
					switch (lookupTree(vars, v1)) {
						Some(vr1): {
							switch (lookupTree(vars, v2)) {
								Some(vr2): {
									if (vr1.id < vr2.id) 
										insertSet(acc3, 
											RuDisj(
												make_var(vr1.id, vr1.type), 
												make_var(vr2.id, vr2.type)
											)
										) 
									else if (vr2.id < vr1.id) 
										insertSet(acc3, 
											RuDisj(
												make_var(vr2.id, vr2.type), 
												make_var(vr1.id, vr1.type)
											)
										) 
									else acc3 
								}
								None(): {
									env.out.onError("unknown var '" + id2s(v2) + "' in disjointed set, vars: " + 
									strGlue(map(getTreeValues(vars), \v -> "'" + id2s(v.id) + "'"), ", "), 
									[RuPlace(src_info.path, disj.pos)]); acc3;
								}
							}
						}
						None(): {
							env.out.onError("unknown var '" + id2s(v1) + "' in disjointed set, vars: " + 
							strGlue(map(getTreeValues(vars), \v -> "'" + id2s(v.id) + "'"), ", "), 
							[RuPlace(src_info.path, disj.pos)]); acc3;
						}
					}
				}
			)
		)
	)
}

ruCreateHyp(hyp : RuParseHyp, vars : Tree<int, RuVar>, lang : RuLang, tenv : Tree<int, RuTypeEnv>, src_info : RuFileInfo,  env : RuEnv) -> Maybe<RuHyp> {
	maybeMap(
		ruCreateExp(hyp.expr, vars, lang, tenv, src_info, env), 
		\expr -> RuHyp(hyp.ind, expr, hyp.pos)
	)
}

ruCreateTheorem1(th : RuParseTheorem, lang : RuLang, tenv : Tree<int, RuTypeEnv>, src_info : RuFileInfo, env : RuEnv) -> Maybe<RuTheorem> {
	vars = ruCreateVars(th.vars);
	disjs = ruCreateDisjs(th.disjs, vars, src_info, env);
	meta = ruCreateMeta(th.meta, vars, src_info, env);
	hyps = filtermap(th.hyps, \h -> ruCreateHyp(h, vars, lang, tenv, src_info, env));
	if (length(hyps) != length(th.hyps)) {
		None();
	} else {
		maybeMap2(
			\prop, proof -> RuTheorem(
				RuDeclInfo(
					th.id, 
					src_info.id, 
					ruCreateComments(th.comments, src_info, env), 
					th.pos
				), 
				RuStatement(RuVarsDisjs(vars, disjs), hyps, prop), 
				meta, proof
			)
		)(
			ruCreateExp(th.prop, vars, lang, tenv, src_info, env),
			ruCreateProof(th.proof, vars, lang, tenv, src_info, env)
		);
	}
}

ruCreateProof(proof : RuParseProof, vars : Tree<int, RuVar>, lang : RuLang, tenv : Tree<int, RuTypeEnv>, src_info : RuFileInfo, env : RuEnv) -> Maybe<RuProof> {
	proof_vars = ruCreateVars(proof.vars);
	all_vars = mergeTreeCustom(proof_vars, vars, \v, w,__ -> {
		env.out.onError("duplicate variable '" + id2s(v) + "'", [RuPlace(src_info.path, proof.pos)]);
		w
	});
	proof_disjs = ruCreateDisjs(proof.disjs, all_vars, src_info, env);
	do_ref = \r -> switch (r) {
		RuParseHypRef(i): RuHypRef(i);
		RuParseStepRef(i): RuStepRef(i);
	}
	steps = filtermap(proof.steps,
		\s ->
			maybeMap2(\expr, subproof -> 
				RuStep(s.ind, s.assertion, map(s.refs, do_ref), expr, subproof, s.pos)
			)(
				ruCreateExp(s.expr, all_vars, lang, tenv, src_info, env),
				ruCreateProof(s.proof, all_vars, lang, tenv, src_info, env)
			)
	);
	if (length(steps) != length(proof.steps)) None() else {
		Some(RuProof(RuVarsDisjs(proof_vars, proof_disjs), steps, proof.pos));
	}
}

ruCreateDef1(df : RuParseDef, lang : RuLang, tenv : Tree<int, RuTypeEnv>, src_info : RuFileInfo, env : RuEnv) -> Maybe<RuDef> {
	vars = ruCreateVars(df.vars);
	disjs = ruCreateDisjs(df.disjs, vars, src_info, env);
	prop_ex = fold(df.prop.symbs, [], \ac, s ->
		if (s == ruIdDefiendum()) concat(ac, df.defm.symbs) else
		if (s == ruIdDefiniens()) concat(ac, df.defs.symbs) else
		concat(ac, [s])
	);
	hyps = filtermap(df.hyps, \h -> ruCreateHyp(h, vars, lang, tenv, src_info, env));
	if (length(hyps) != length(df.hyps)) {
		None();
	} else {
		maybeBind(
			ruCreateExp(RuParseExp(df.prop.type, prop_ex, df.prop.pos), vars, lang, tenv, src_info, env), 
			\prop ->
				maybeMap2(\dfm, dfs ->
					RuDef(
						RuDeclInfo(
							df.id, 
							src_info.id, 
							ruCreateComments(df.comments, src_info, env), 
							df.pos
						), 
						RuStatement(RuVarsDisjs(vars, disjs), hyps, prop), 
						dfm, dfs
					)
				)(
					ruCreateExp(df.defm, vars, lang, tenv, src_info, env),
					ruCreateExp(df.defs, vars, lang, tenv, src_info, env)
				)
		);
	}
}
