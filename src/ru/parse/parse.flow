
import ds/vector;
import ru/parse/load;
import ru/parse/file;
import ru/parse/ast;
import ru/math;

export {
	ruParseAll(loaded : [RuLoaded], env : RuEnv) -> Maybe<Tree<int, RuParsed>>;
	ruParseOne(loaded : RuLoaded, env : RuEnv) -> Maybe<RuParsed>;
	ruParseSource(loaded : RuRawSource, env : RuEnv) -> Maybe<RuParseSource>;

	// Admit parsing errors
	ruParseSourceAnyway(loaded : RuRawSource, env : RuEnv) -> RuParseSource;
}

ruParseAll(loaded : [RuLoaded], env0 : RuEnv) -> Maybe<Tree<int, RuParsed>> {
	err_count = ref 0;
	env = ruAddErrCounter2Env(env0, err_count);
	timer = ruMakeTimer();
	parsed : [RuParsed] = filtermap(ruConcurrent(map(loaded, \l -> \env1 -> ruParseOne(l, env1)), env), idfn);
	ret = fold(parsed, makeTree(),
		\acc, src -> setTree(acc, ruSourceModule(src), src)
	);
	if (ruVerbose(env.conf) > 0) {
		env.out.println("ru parsed: " +i2s(sizeTree(ret)) + " files in " + ruTimePassed2s(timer));
	}
	if (^err_count == 0) {
		if (ruDetectCyclicImports(ret, env)) None() else {
			Some(ret);
		}
	} else {
		env.out.error("ru failed to parse", []);
		None();
	}
}

ruParseOne(loaded : RuLoaded, env : RuEnv) -> Maybe<RuParsed> {
	switch (loaded) {
		RuSource(__,__,__,__): Some(loaded);
		RuRawSource(__,__,__): cast(ruParseSource(loaded, env) : Maybe<RuParseSource> -> Maybe<RuParsed>);
	}
}

ruParseSource(loaded : RuRawSource, env : RuEnv) -> Maybe<RuParseSource> {
	timer = ruMakeTimer();
	pr1 = parse_ru_source(loaded.src);
	st = pr1.first;
	err = pr1.second;
	if (st.end != strlen(loaded.src)) {
		env.out.error(
			"Syntax error:\n" + 
				"\t" + ruExpectError2s(err) + "\n" +
			"source:\n" +
				strIndent(ruHighlightErrorPos(loaded.src, st.end)),  
			[RuPlace(loaded.info.path, st.end)]
		);
		None();
	} else {
		parsed = ruMakeSrc(loaded, st, env);
		if (ruVerbose(env.conf) > 1) {
			env.out.println("\tru parsed: '" + loaded.info.path + "' in " + ruTimePassed2s(timer));
		}
		Some(parsed);
	}
}

ruParseSourceAnyway(loaded : RuRawSource, env : RuEnv) -> RuParseSource {
	ruMakeSrc(loaded, parse_ru_source(loaded.src).first, env);
}

RuParseSourceAcc(
	imports : [RuParseImport],
	decls : Vector<RuParseDecl>,
	comments : [RuParseComment]
);

ruMakeSrc(loaded : RuRawSource, st : SyntaxTree, env : RuEnv) -> RuParseSource {
	parsed = fold(get_ru_source_ru_toplevel_s(st), RuParseSourceAcc([], makeVector(128), []),
		\acc, decl -> {
			if (decl.rule == st_ru_scomment || decl.rule == st_ru_mcomment) {
				RuParseSourceAcc(acc with comments = concat(acc.comments, [ruMakeComment(loaded, decl, env)]));
			} else if (decl.rule == st_ru_import) {
				RuParseSourceAcc(acc with imports = concat(acc.imports, [ruMakeImport(loaded, decl, env)]));
			} else if (decl.rule == st_ru_const) {
				pushVector(acc.decls, ruMakeConst(loaded, decl, acc.comments, env));
				RuParseSourceAcc(acc with comments = []);
			} else if (decl.rule == st_ru_type) {
				pushVector(acc.decls, ruMakeType(loaded, decl, acc.comments, env));
				RuParseSourceAcc(acc with comments = []);
			} else if (decl.rule == st_ru_rule) {
				pushVector(acc.decls, ruMakeRule(loaded, decl, acc.comments, env));
				RuParseSourceAcc(acc with comments = []);
			} else if (decl.rule == st_ru_axiom) {
				pushVector(acc.decls, ruMakeAxiom(loaded, decl, acc.comments, env));
				RuParseSourceAcc(acc with comments = []);
			} else if (decl.rule == st_ru_def) {
				pushVector(acc.decls, ruMakeDef(loaded, decl, acc.comments, env));
				RuParseSourceAcc(acc with comments = []);
			} else if (decl.rule == st_ru_theorem) {
				pushVector(acc.decls, ruMakeTheorem(loaded, decl, acc.comments, env));
				RuParseSourceAcc(acc with comments = []);
			} else {
				acc
			}
		}
	);
	RuParseSource(parsed.imports, vector2array(parsed.decls), parsed.comments, loaded.info);
}

ruMakeComment(loaded : RuRawSource, st : SyntaxTree, env : RuEnv) -> RuParseComment {
	all_text1 = trim2(grabSTText(st, loaded.src), " \n\t\r");
	all_text2 = if (startsWith(all_text1, "/*") || startsWith(all_text1, "//")) strRight(all_text1, 2) else all_text1;
	all_text = if (endsWith(all_text2, "*/")) strLeft(all_text2, strlen(all_text2) - 2) else all_text2;
	if (!startsWith(all_text, "*")) RuParseComment(all_text, "", st.start) else {
		json_end = strIndexOf(all_text, "\n-----");
		if (json_end == -1) {
			json = substring(all_text, 1, strlen(all_text));
			RuParseComment("", json, st.start);
		} else {
			json = substring(all_text, 1, json_end - 1);
			text_begin = for(
				json_end + 5, 
				\i -> (i < strlen(all_text)) && getCharAt(all_text, i) == "-", 
				\i -> i + 1
			);
			text = substring(all_text, text_begin + 1, strlen(all_text));
			RuParseComment(text, json, st.start)
		}
	}
}

ruMakeImport(loaded : RuRawSource, st : SyntaxTree, env : RuEnv) -> RuParseImport { 
	path = ruTrimPath(grabSTText(get_ru_source_ru_symb(st), loaded.src), env.conf, ".ru");
	RuParseImport(s2id(path), st.start);
}

ruMakeConst(loaded : RuRawSource, st : SyntaxTree, comments : [RuParseComment], env : RuEnv) -> RuParseConst {
	symb = s2id(rtrim(grabSTText(get_ru_source_ru_symb(get_ru_source_ru_symbol(st)), loaded.src)));
	ascii_st = get_ru_source_ru_ascii_s(st);
	ascii = if (ascii_st != []) s2id(rtrim(grabSTText(get_ru_source_ru_symb(ascii_st[0]), loaded.src))) else -1;
	latex_st = get_ru_source_ru_latex_s(st);
	latex = if (latex_st != []) trim(grabSTText(get_ru_source_ru_latex_symb(latex_st[0]), loaded.src)) else "";
	RuParseConst(symb, comments, ascii, latex, st.start);
}

ruMakeType(loaded : RuRawSource, st : SyntaxTree, comments : [RuParseComment], env : RuEnv) -> RuParseType {
	make_id = \s -> rtrim(grabSTText(get_ru_source_ru_id(s), loaded.src));
	name = make_id(st);
	supers_st = get_ru_source_ru_supers_s(st);
	supers = if (supers_st == []) [] else {
		concat(
			[make_id(supers_st[0])],
			map(get_ru_source_ru_super_s(supers_st[0]), make_id)
		)
	}
	RuParseType(s2id(name), comments, map(supers, s2id), st.start)
}

ruMakeRule(loaded : RuRawSource, st : SyntaxTree, comments : [RuParseComment], env : RuEnv) -> RuParseRule {
	name = grabSTText(get_ru_source_ru_id(st), loaded.src);
	vars_st = get_ru_source_ru_vars_s(st);
	vars = if (vars_st != []) ruMakeVars(loaded, vars_st[0], env) else [];
	term = ruMakeTerm(loaded, get_ru_source_ru_term(st), env);
	RuParseRule(s2id(name), comments, vars, term, st.start);
}

ruMakeVars(loaded : RuRawSource, st : SyntaxTree, env : RuEnv) -> [RuParseVar] {
	if (get_ru_source_ru_vars0_s(st) != []) {
		vars0 = get_ru_source_ru_vars0_s(st)[0];
		var = get_ru_source_ru_var(vars0);
		make_var = \t -> ruMakeVar(loaded, t, env);
		vars1 = get_ru_source_ru_vars1_s(vars0);
		concat([make_var(var)], map(vars1, \v -> make_var(get_ru_source_ru_var(v))))
	} else {
		[]
	}
}

ruMakeVar(loaded : RuRawSource, st : SyntaxTree, env : RuEnv) -> RuParseVar {
	name = rtrim(grabSTText(get_ru_source_ru_var_id(st), loaded.src));
	type = grabSTText(get_ru_source_ru_id(get_ru_source_ru_type_id(st)), loaded.src);
	RuParseVar(s2id(name), s2id(type), st.start)
}

ruMakeTerm(loaded : RuRawSource, st : SyntaxTree, env : RuEnv) -> RuParseTerm {
	type = grabSTText(get_ru_source_ru_id(get_ru_source_ru_type_id(st)), loaded.src);
	symbs_st = get_ru_source_ru_symbws_s(st);
	symbs = map(symbs_st, \s -> rtrim(grabSTText(get_ru_source_ru_symb(s), loaded.src)));
	RuParseTerm(s2id(type), map(symbs, s2id), symbs_st[0].start)
}

ruMakeStmt(loaded : RuRawSource, st : SyntaxTree, env : RuEnv) -> RuParseStmt {
	type = grabSTText(get_ru_source_ru_id(get_ru_source_ru_type_id(st)), loaded.src);
	class = grabSTText(get_ru_source_ru_id(get_ru_source_ru_class_id(st)), loaded.src);
	symbs_st = get_ru_source_ru_symbws_s(st);
	symbs = map(symbs_st, \s -> rtrim(grabSTText(get_ru_source_ru_symb(s), loaded.src)));
	RuParseStmt(s2id(class), 
		RuParseTerm(s2id(type), map(symbs, s2id), symbs_st[0].start),
		get_ru_source_ru_class_id(st).start
	)
}

ruMakeAxiom(loaded : RuRawSource, st : SyntaxTree, comments : [RuParseComment], env : RuEnv) -> RuParseAxiom {
	name  = grabSTText(get_ru_source_ru_id(st), loaded.src);
	vars  = if (get_ru_source_ru_vars_s(st) == []) [] else ruMakeVars(loaded, get_ru_source_ru_vars_s(st)[0], env);
	disjs = if (get_ru_source_ru_disjs_s(st) == []) [] else ruMakeDisjs(loaded, get_ru_source_ru_disjs_s(st)[0], env);
	hyps  = if (get_ru_source_ru_hyps_bar_s(st) == []) [] else {
		map(
			get_ru_source_ru_hyp_s(get_ru_source_ru_hyps_bar_s(st)[0]),  
			\h -> ruMakeHyp(loaded, h, env)
		)
	}
	prop  = ruMakeStmt(loaded, get_ru_source_ru_stmt(st), env);
	RuParseAxiom(s2id(name), comments, vars, disjs, hyps, prop, st.start);
}

ruMakeDisjs(loaded : RuRawSource, st : SyntaxTree, env : RuEnv) -> [RuParseDisj] {
	if (get_ru_source_ru_disjs0_s(st) != []) {
		disjs0    = get_ru_source_ru_disjs0_s(st)[0];
		disj      = get_ru_source_ru_disj(disjs0);
		disjs1    = get_ru_source_ru_disjs1_s(disjs0);
		make_disj = \d -> ruMakeDisj(loaded, d, env);
		concat([make_disj(disj)], map(disjs1, \d -> make_disj(get_ru_source_ru_disj(d))))
	} else {
		[]
	}
}

ruMakeDisj(loaded : RuRawSource, st : SyntaxTree, env : RuEnv) -> RuParseDisj {
	dis_v    = get_ru_source_ru_dis0_v(st);
	dis_vs   = get_ru_source_ru_dis1_v_s(st);
	make_var = \v -> s2id(rtrim(grabSTText(get_ru_source_ru_var_id(v), loaded.src)));
	RuParseDisj(concat(
		[make_var(dis_v)], 
		map(dis_vs, make_var)
	), st.start)
}

ruMakeMeta(loaded : RuRawSource, st : SyntaxTree, env : RuEnv) -> RuParseMeta {
	if (get_ru_source_ru_meta0_s(st) == []) RuParseMeta([], st.start) else {
		meta_v    = get_ru_source_ru_meta0_s(st)[0];
		meta_vs   = get_ru_source_ru_meta1_s(st);
		make_var = \v -> s2id(rtrim(grabSTText(get_ru_source_ru_var_id(v), loaded.src)));
		RuParseMeta(concat([make_var(meta_v)], map(meta_vs, make_var)), st.start);
	}
}

ruMakeHyp(loaded : RuRawSource, st : SyntaxTree, env : RuEnv) -> RuParseHyp {
	ind  = ruMakeInd(loaded, st, env);
	stmt = ruMakeStmt(loaded, get_ru_source_ru_stmt(st), env);
	RuParseHyp(ind, stmt, st.start)
}

ruMakeDef(loaded : RuRawSource, st : SyntaxTree, comments : [RuParseComment], env : RuEnv) -> RuParseDef {
	name  = grabSTText(get_ru_source_ru_id(st), loaded.src);
	vars  = if (get_ru_source_ru_vars_s(st) == []) [] else ruMakeVars(loaded, get_ru_source_ru_vars_s(st)[0], env);
	disjs = if (get_ru_source_ru_disjs_s(st) == []) [] else ruMakeDisjs(loaded, get_ru_source_ru_disjs_s(st)[0], env);
	hyps  = if (get_ru_source_ru_hyp_s(st) == []) [] else map(get_ru_source_ru_hyp_s(st), \h -> ruMakeHyp(loaded, h, env));
	defm  = ruMakeTerm(loaded, get_ru_source_ru_term(get_ru_source_ru_defm(st)), env);
	defs  = ruMakeTerm(loaded, get_ru_source_ru_term(get_ru_source_ru_defs(st)), env);
	prop  = ruMakeStmt(loaded, get_ru_source_ru_stmt(st), env);
	RuParseDef(s2id(name), comments, vars, disjs, hyps, defm, defs, prop, st.start);
}

ruMakeTheorem(loaded : RuRawSource, st : SyntaxTree, comments : [RuParseComment], env : RuEnv) -> RuParseTheorem {
	name  = grabSTText(get_ru_source_ru_id(st), loaded.src);
	vars  = if (get_ru_source_ru_vars_s(st) == []) [] else ruMakeVars(loaded, get_ru_source_ru_vars_s(st)[0], env);
	disjs = if (get_ru_source_ru_disjs_s(st) == []) [] else ruMakeDisjs(loaded, get_ru_source_ru_disjs_s(st)[0], env);
	meta  = if (get_ru_source_ru_meta_s(st) == []) RuParseMeta([], st.start) else ruMakeMeta(loaded, get_ru_source_ru_meta_s(st)[0], env);
	hyps  = if (get_ru_source_ru_hyps_bar_s(st) == []) [] else {
		map(
			get_ru_source_ru_hyp_s(get_ru_source_ru_hyps_bar_s(st)[0]),  
			\h -> ruMakeHyp(loaded, h, env)
		)
	}
	prop  = ruMakeStmt(loaded, get_ru_source_ru_stmt(st), env);
	proof = ruMakeProof(loaded, get_ru_source_ru_proof(st), env);
	RuParseTheorem(s2id(name), comments, vars, disjs, meta, hyps, prop, proof, st.start);
}

ruMakeProof(loaded : RuRawSource, st : SyntaxTree, env : RuEnv) -> RuParseProof {
	vars  = if (get_ru_source_ru_vars_s(st) == []) [] else ruMakeVars(loaded, get_ru_source_ru_vars_s(st)[0], env);
	disjs = if (get_ru_source_ru_disjs_s(st) == []) [] else ruMakeDisjs(loaded, get_ru_source_ru_disjs_s(st)[0], env);
	steps = map(get_ru_source_ru_proof_step_s(st), \s -> ruMakeProofStep(loaded, s, env));
	RuParseProof(vars, disjs, steps, st.start);
}

ruMakeProofStep(loaded : RuRawSource, st : SyntaxTree, env : RuEnv) -> RuParseStep {
	if (st.rule == st_ru_step) {
		ruMakeStep(loaded, st, env);
	} else if (st.rule == st_ru_lemma) {
		ruMakeLemma(loaded, st, env);
	} else if (st.rule == st_ru_problem) {
		ruMakeProblem(loaded, st, env);
	} else {
		//ruCrash("illegal step kind: " + grabSTText(st, loaded.src));
		RuParseStep(-1, -1, [], RuParseStmt(-1, RuParseTerm(-1, [], -1), -1), ruParseNoProof, -1);
	}
}

ruMakeStep(loaded : RuRawSource, st : SyntaxTree, env : RuEnv) -> RuParseStep {
	ind  = ruMakeInd(loaded, st, env);
	type = grabSTText(get_ru_source_ru_id(get_ru_source_ru_type_id(st)), loaded.src);
	class = grabSTText(get_ru_source_ru_id(get_ru_source_ru_class_id(st)), loaded.src);
	assertion = grabSTText(get_ru_source_ru_id(st), loaded.src);
	refs = ruMakeRefs(loaded, get_ru_source_ru_refs(st), env);
	symbs = map(get_ru_source_ru_symbws_s(st), \s -> s2id(rtrim(grabSTText(get_ru_source_ru_symb(s), loaded.src))));
	exp_pos = if (get_ru_source_ru_symbws_s(st) == []) st.start else get_ru_source_ru_symbws_s(st)[0].start;
	RuParseStep(
		ind, s2id(assertion), refs, 
		RuParseStmt(
			s2id(class),
			RuParseTerm(s2id(type), symbs, exp_pos),
			get_ru_source_ru_class_id(st).start,
		),
		ruParseNoProof, 
		st.start
	);
}

ruMakeLemma(loaded : RuRawSource, st : SyntaxTree, env : RuEnv) -> RuParseStep {
	ind  = ruMakeInd(loaded, st, env);
	type = grabSTText(get_ru_source_ru_id(get_ru_source_ru_type_id(st)), loaded.src);
	class = grabSTText(get_ru_source_ru_id(get_ru_source_ru_class_id(st)), loaded.src);
	refs = ruMakeRefs(loaded, get_ru_source_ru_refs(st), env);
	symbs = map(get_ru_source_ru_symbws_s(st), \s -> s2id(rtrim(grabSTText(get_ru_source_ru_symb(s), loaded.src))));
	proof = ruMakeProof(loaded, get_ru_source_ru_proof(st), env);
	exp_pos = if (get_ru_source_ru_symbws_s(st) == []) st.start else get_ru_source_ru_symbws_s(st)[0].start;
	RuParseStep(
		ind, ruParseLemma, refs, 
		RuParseStmt(
			s2id(class),
			RuParseTerm(s2id(type), symbs, exp_pos),
			get_ru_source_ru_class_id(st).start,
		), 
		proof,
		st.start,
	);
}

ruMakeProblem(loaded : RuRawSource, st : SyntaxTree, env : RuEnv) -> RuParseStep {
	ind  = ruMakeInd(loaded, st, env);
	type = grabSTText(get_ru_source_ru_id(get_ru_source_ru_type_id(st)), loaded.src);
	symbs = map(get_ru_source_ru_symbws_s(st), \s -> s2id(rtrim(grabSTText(get_ru_source_ru_symb(s), loaded.src))));
	exp_pos = if (get_ru_source_ru_symbws_s(st) == []) st.start else get_ru_source_ru_symbws_s(st)[0].start;
	RuParseStep(
		ind, ruParseProblem, [], 
		RuParseStmt(-1,
			RuParseTerm(s2id(type), symbs, exp_pos),
			exp_pos
		), 
		ruParseNoProof, 
		st.start
	);
}

ruMakeRefs(loaded : RuRawSource, st : SyntaxTree, env : RuEnv) -> [RuParseRef] {
	if (get_ru_source_ru_refs0_s(st) == []) [] else {
		refs0 = get_ru_source_ru_refs0_s(st)[0];
		refs_st = concat([get_ru_source_ru_ref(refs0)], map(get_ru_source_ru_refs1_s(refs0), get_ru_source_ru_ref));
		map(refs_st, \r->
			if (r.rule == st_ru_hyp_ref) {
				RuParseHypRef(ruMakeInd(loaded, r, env));
			} else {
				RuParseStepRef(ruMakeInd(loaded, r, env));
			}
		)	
	}
}

ruMakeInd(loaded : RuRawSource, st : SyntaxTree, env : RuEnv) -> int {
	s2i(grabSTText(get_ru_source_ru_ind(st), loaded.src)) - 1
}
