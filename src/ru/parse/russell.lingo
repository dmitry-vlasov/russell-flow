source   = '\uFEFF'? ws toplevel*;
toplevel = scomment | mcomment | import | const | type | rule | axiom | def | theorem;

scomment = "//" (!'\n' char_)* '\n' ws;
mcomment = "/*" (!"*/" char_)* "*/" ws;

import   = "import" ws symb ws ";;" ws;

const   = "constant" ws '{' ws symbol ascii? latex? '}' ws ;
symbol  = "symbol" ws symb ws ";;" ws;
ascii   = "ascii"  ws symb ws ";;" ws;
latex   = "latex"  ws latex_symb ws ";;" ws;

type    = "type" ws id ws supers? ";;" ws;
supers  = ':' ws id ws super*;
super   = ',' ws id ws;

rule    = "rule"       ws id ws vars? '{' ws "term" ws expr '}' ws;
axiom   = "axiom"      ws id ws vars? disjs? '{' ws hyps_bar? "prop" ws expr '}' ws;
def     = "definition" ws id ws vars? disjs? meta? '{' ws hyp* defm defs "-----" '-'* ws "prop" ws expr '}' ws;
theorem = "theorem"    ws id ws vars? disjs? '{' ws hyps_bar? "prop" ws expr '}' ws proof;
proof   = "proof"      ws vars? disjs? '{' ws proof_step* '}' ws;

hyps_bar = hyp+ "-----" '-'* ws;
hyps  = hyp+;
hyp   = "hyp" ws ind ws expr;
defm  = "defiendum" ws expr;
defs  = "definiens" ws expr;

proof_step = lemma | problem | step;
step     = "step" ws ind ws type_id '=' ws id      ws refs "|-" ws symbws+ ";;" ws;
lemma    = "step" ws ind ws type_id '=' ws "lemma" ws refs "|-" ws symbws+ ";;" ws proof;
problem  = "step" ws ind ws type_id '=' ws "?"     ws      "|-" ws symbws+ ";;" ws;

refs     = '(' ws refs0? ')' ws;
refs0    = ref refs1*;
refs1    = ',' ws ref;
ref      = hyp_ref | step_ref;
hyp_ref  = "hyp" ws ind ws;
step_ref = "step" ws ind ws;

disjs  = "disjointed" ws "(" ws disjs0? ')' ws;
disjs0 = disj disjs1*;
disjs1 = ',' ws disj;
disj   = dis0_v dis1_v+;
dis0_v = var_id ws;
dis1_v = var_id ws;

meta  = "meta" ws "(" ws meta0? ')' ws;
meta0 = var_id ws meta1*;
meta1 = ',' ws var_id;

vars  = '(' ws vars0? ')' ws;
vars0 = var vars1*;
vars1 = ',' ws var;
var   = var_id ws type_id;
type_id = ':' ws id ws;

expr  = type_id '=' ws id ws symbws+ ";;" ws;
symbws = symb ws;

symb    = (!";;" non_space_char)+;
latex_symb = (!";;" any_char)+;
ind     = digit+;
id      = id_char+;
var_id  = var_id_char+;
id_char = 'a'-'z' | 'A'-'Z' | '0'-'9' | '_' | '.' | '-' | '#' | "|-" | '\'';
letter  = 'a'-'z' | 'A'-'Z';
digit   = '0'-'9';

non_space_char = '\u0021'-'\uFFFF';
var_id_char = '\u0021'-'\u0028' | '\u002A' | '\u002B' | '\u002D'-'\u0039' | '\u003B'-'\uFFFF';
char_ = '\u0000'-'\uFFFF';
ws    = space*;
space = ' ' | '\t' | '\n' | '\r';
any_char = '\u0000'-'\uFFFF';
