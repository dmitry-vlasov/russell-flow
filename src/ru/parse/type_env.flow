import ru/parse/trie;
import ru/src_utils;

export {
	RuTypeEnv(
		trie   : RuTrie<string, RuRuleNode>,
		supers : Tree<string, RuRule>,
	);
	ruNoTypeEnv = RuTypeEnv(ruTrieEmpty, makeTree());

	ruAddTerm2RuTrie(term : RuTerm, rule : Maybe<RuRuleNode>, trie : RuTrie<string, RuRuleNode>, conf : RuConf) -> RuTrie<string, RuRuleNode>;
	ruRemoveTermFromTrie(term : RuTerm, trie : RuTrie<string, RuRuleNode>) -> RuTrie<string, RuRuleNode>;
	
	ruMergeTypeEnv(e1 : RuTypeEnv, e2 : RuTypeEnv) -> RuTypeEnv;
	ruMergeTypeEnvTree(e1 : Tree<string, RuTypeEnv>, e2 : Tree<string, RuTypeEnv>) -> Tree<string, RuTypeEnv>;
	ruMakeTypeEnv(type : RuType, conf : RuConf) -> RuTypeEnv;

	ruTypeEnv2s(tenv : RuTypeEnv) -> string;
	ruTypeEnvTree2s(tree : Tree<string, RuTypeEnv>) -> string;
}

ruAddTerm2RuTrie(term : RuTerm, rule : Maybe<RuRuleNode>, trie : RuTrie<string, RuRuleNode>, conf : RuConf) -> RuTrie<string, RuRuleNode> {
	term_key = \x -> switch (x) {
		RuConstRef(c):  c;
		RuVar(v, t): t;
	}
	ruAdd2Trie(map(term.nodes, term_key), rule, trie, 
		\a1, a2 -> {
			if (a1 != a2) {
				conf.onError("rule term: '" + ruTerm2s(term) + "' is already used", []);
			}
			a1
		}
	)
}

ruRemoveTermFromTrie(term : RuTerm, trie : RuTrie<string, RuRuleNode>) -> RuTrie<string, RuRuleNode> {
	term_key = \x -> switch (x) {
		RuConstRef(c):  c;
		RuVar(v, t): t;
	}
	ruRemoveFromTrie(map(term.nodes, term_key), trie);
}

ruMergeTypeEnv(e1 : RuTypeEnv, e2 : RuTypeEnv) -> RuTypeEnv {
	RuTypeEnv(
		ruMergeRuTrie(e1.trie, e2.trie, \a1, a2 -> a1),
		mergeTree(e1.supers, e2.supers)
	)
}

ruMergeTypeEnvTree(t1 : Tree<string, RuTypeEnv>, t2 : Tree<string, RuTypeEnv>) -> Tree<string, RuTypeEnv> {
	mergeTreeCustom(t1, t2, \__, e1, e2 -> ruMergeTypeEnv(e1, e2));
}

ruMakeTypeEnv(type : RuType, conf : RuConf) -> RuTypeEnv {
	RuTypeEnv(
		ruAddTerm2RuTrie(
			RuTerm(type.descr.name, [RuVar("x", type.descr.name)], type.descr.pos), 
			None(),
			ruTrieEmpty, conf
		),
		fold(type.supers, makeTree(), \ac, super -> 
			setTree(ac, super, ruMakeSuperRule(super, type.descr.name, type.descr.src, type.descr.pos))
		)
	)
}

ruTypeEnv2s(tenv : RuTypeEnv) -> string {
	ruTrie2s(tenv.trie, idfn, \n -> n.name);
}

ruTypeEnvTree2s(tree : Tree<string, RuTypeEnv>) -> string {
	foldTree(tree, "", \type, tenv, acc ->
		acc + "\ntype: " + type + "\n" + strIndent(ruTypeEnv2s(tenv))
	);
}