import base/trie;
import ru/src_utils;

export {
	RuTypeEnv(
		trie   : RuTrie<int, RuRuleNode>,
		supers : Tree<int, RuRule>,
	);
	ruNoTypeEnv = RuTypeEnv(ruTrieEmpty, makeTree());

	ruAddTerm2RuTrie(term : RuTerm, rule : Maybe<RuRuleNode>, trie : RuTrie<int, RuRuleNode>, conf : RuConf) -> RuTrie<int, RuRuleNode>;
	ruRemoveTermFromTrie(term : RuTerm, trie : RuTrie<int, RuRuleNode>) -> RuTrie<int, RuRuleNode>;
	
	ruMergeTypeEnv(e1 : RuTypeEnv, e2 : RuTypeEnv) -> RuTypeEnv;
	ruMergeTypeEnvTree(e1 : Tree<int, RuTypeEnv>, e2 : Tree<int, RuTypeEnv>) -> Tree<int, RuTypeEnv>;
	ruMakeTypeEnv(type : RuType, conf : RuConf) -> RuTypeEnv;

	ruTypeEnv2s(tenv : RuTypeEnv) -> string;
	ruTypeEnvTree2s(tree : Tree<int, RuTypeEnv>) -> string;
}

ruAddTerm2RuTrie(term : RuTerm, rule : Maybe<RuRuleNode>, trie : RuTrie<int, RuRuleNode>, conf : RuConf) -> RuTrie<int, RuRuleNode> {
	term_key = \x -> switch (x) {
		RuConstRef(id):  id;
		RuVar(id, type): type;
	}
	ruAdd2Trie(trie, map(term.nodes, term_key), rule, 
		\a1, a2 -> {
			if (a1 != a2) {
				conf.onError("rule term: '" + ruTerm2s(term) + "' is already used", []);
			}
			a1
		}
	)
}

ruRemoveTermFromTrie(term : RuTerm, trie : RuTrie<int, RuRuleNode>) -> RuTrie<int, RuRuleNode> {
	term_key = \x -> switch (x) {
		RuConstRef(id):  id;
		RuVar(id, type): type;
	}
	ruRemoveFromTrie(map(term.nodes, term_key), trie);
}

ruMergeTypeEnv(e1 : RuTypeEnv, e2 : RuTypeEnv) -> RuTypeEnv {
	RuTypeEnv(
		ruMergeRuTrie(e1.trie, e2.trie, \a1, a2 -> a1),
		mergeTree(e1.supers, e2.supers)
	)
}

ruMergeTypeEnvTree(t1 : Tree<int, RuTypeEnv>, t2 : Tree<int, RuTypeEnv>) -> Tree<int, RuTypeEnv> {
	mergeTreeCustom(t1, t2, \__, e1, e2 -> ruMergeTypeEnv(e1, e2));
}

ruMakeTypeEnv(type : RuType, conf : RuConf) -> RuTypeEnv {
	RuTypeEnv(
		ruAddTerm2RuTrie(
			RuTerm(type.descr.id, [RuVar(s2id("x"), type.descr.id)], type.descr.pos), 
			None(),
			ruTrieEmpty, conf
		),
		fold(type.supers, makeTree(), \ac, super -> 
			setTree(ac, super, ruMakeSuperRule(super, type.descr.id, type.descr.src, type.descr.pos))
		)
	)
}

ruTypeEnv2s(tenv : RuTypeEnv) -> string {
	ruTrie2s(tenv.trie, id2s, \n -> id2s(n.rule));
}

ruTypeEnvTree2s(tree : Tree<int, RuTypeEnv>) -> string {
	foldTree(tree, "", \type, tenv, acc ->
		acc + "\ntype: " + id2s(type) + "\n" + strIndent(ruTypeEnv2s(tenv))
	);
}