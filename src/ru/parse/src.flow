import base/fileinfo;

export {
	RuParseSource(imports : [RuParseImport], decls : [RuParseBlock], info : RuFileInfo);
	RuParseImport(module : int, pos : int);
	RuParseBlock ::= RuParseDecl, RuParseComment;
	RuParseDecl ::= RuParseConst, RuParseType, RuParseRule, RuParseAxiom, RuParseDef, RuParseTheorem;

	RuParseComment(text : string, json : string, pos : int);
	RuParseConst(id : int, ascii : int, latex : int, pos : int);
	RuParseType(id : int, supers : [int], pos : int);
	RuParseRule(id : int, vars : [RuParseVar], term : RuParseExp, pos : int);
	RuParseAxiom(id : int, vars : [RuParseVar], disjs : [RuParseDisj], hyps : [RuParseHyp], prop : RuParseExp, pos : int);
	RuParseTheorem(id : int, vars : [RuParseVar], disjs : [RuParseDisj], meta : RuParseMeta, hyps : [RuParseHyp], prop : RuParseExp, proof : RuParseProof, pos : int);
	RuParseDef(id : int, vars : [RuParseVar], disjs : [RuParseDisj], hyps : [RuParseHyp], defm : RuParseExp, defs : RuParseExp, prop : RuParseExp, pos : int);
	
	RuParseHyp(ind : int, expr : RuParseExp, pos : int);
	RuParseVar(id : int, type : int, pos : int);
	RuParseDisj(vars : [int], pos : int);
	RuParseProof(vars : [RuParseVar], disjs : [RuParseDisj], steps : [RuParseStep], pos : int);
	RuParseStep(ind : int, assertion : int, refs : [RuParseRef], expr : RuParseExp, pos : int);
	RuParseRef ::= RuParseHypRef, RuParseStepRef;
	RuParseHypRef(ind : int, pos : int);
	RuParseStepRef(ind : int, pos : int);

	RuParseExp(type : int, symbs : [int], pos : int);
	RuParseMeta(meta : [int], pos : int);

	RuParseAny ::= 
		RuParseSource, RuParseImport, RuParseConst, RuParseType, RuParseRule, RuParseAxiom, RuParseDef, RuParseTheorem, RuParseComment,
		RuParseStep, RuParseExp, RuParseHyp, RuParseProof, RuParseDisj, RuParseVar, RuParseHypRef, RuParseStepRef;

	ruParse2s(a : RuParseAny) -> string;
	ruParseBlockId(b : RuParseBlock) -> int;
}

ruParse2s(a : RuParseAny) -> string {
	vars2s  = \vars -> if (vars == []) "" else "(" + strGlue(map(vars, ruParse2s), ", ") + ") ";
	disjs2s = \disjs -> if (disjs == []) "" else "disjointed(" + strGlue(map(disjs, ruParse2s), ", ") + ") ";
	meta2s  = \m -> if (m.meta == []) "" else "meta(" + strGlue(map(m.meta, id2s), ", ") + ") ";
	exp2s   = \expr, delim -> ": " + id2s(expr.type) + " = " + delim + " " + strGlue(map(expr.symbs, id2s), " ") + " ;;";
	prop2s  = \prop -> "\tprop " + exp2s(prop, "|-") + "\n";
	refs2s  = \refs -> "(" + strGlue(map(refs, ruParse2s), ", ") + ")";
	hyps2s  = \hyps -> if (hyps == []) "" else strGlue(map(hyps, ruParse2s), "\n") + "\n";
	switch (a) {
		RuParseSource(imps, decls, info): 
			(if (imps == []) "" else strGlue(map(imps, ruParse2s), "\n") + "\n\n") +
			strGlue(map(decls, ruParse2s), "\n");
		RuParseImport(path,__):
			"import " + id2s(path) + ";;";
		RuParseConst(symbol, ascii, latex,__): 
			"constant {\n" + 
				"\tsymbol " + id2s(symbol) + " ;;\n" +
				(if (id2s(ascii) != "") "\tascii " + id2s(ascii) + " ;;\n" else "") + 
				(if (id2s(latex) != "") "\tlatex " + id2s(latex) + " ;;\n" else "") +
			"}\n";
		RuParseType(name, supers,__): 
			"type " + id2s(name) + (if (supers == []) "" else " : " + strGlue(map(supers, id2s), ", ")) + " ;;\n";
		RuParseRule(name, vars, term,__):
			"rule " + id2s(name) + " " + vars2s(vars) + "{\n" +
				"\tterm " + exp2s(term, "#") + "\n" +
			"}\n";
		RuParseAxiom(name, vars, disjs, hyps, prop,__): 
			"axiom " + id2s(name) + " " + vars2s(vars) + disjs2s(disjs) + "{\n" +
				hyps2s(hyps) +
				(if (hyps == []) "" else "\t----------\n") +
				prop2s(prop) +
			"}\n";
		RuParseDef(name, vars, disjs, hyps, defm, defs, prop,__): 
			"definition " + id2s(name) + " " + vars2s(vars) + disjs2s(disjs) + "{\n" +
				hyps2s(hyps) +
				"\tdefiendum " + exp2s(defm, "#") + "\n" +
				"\tdefiniens " + exp2s(defs, "#") + "\n" +
				"\t-------------\n" +
				prop2s(prop) +
			"}\n";
		RuParseTheorem(name, vars, disjs, meta, hyps, prop, proof,__): 
			"theorem " + id2s(name) + " " + vars2s(vars) + disjs2s(disjs) + meta2s(meta) + "{\n" +
				hyps2s(hyps) + 
				(if (hyps == []) "" else "\t----------\n") +
				prop2s(prop) +
			"} " + ruParse2s(proof);
		RuParseHyp(ind, expr,__): 
			"\thyp " + i2s(ind + 1) + " " + exp2s(expr, "|-");
		RuParseProof(vars, disjs, steps,__): 
			"proof " + vars2s(vars) + disjs2s(disjs) + "{\n" +
				strGlue(map(steps, ruParse2s), "\n") + "\n" +
			"}\n";
		RuParseStep(ind, assertion, refs, expr,__):
			"\tstep " + i2s(ind + 1) + " : " + id2s(expr.type) + " = " + 
				id2s(assertion) + " " + refs2s(refs) + " |- " + strGlue(map(expr.symbs, id2s), " ") + " ;;";
		RuParseComment(text, json, __): 
			if (json == "") "/* " + text + " */\n" else "/** " + json + "\n-----\n" + text + " */\n";
		RuParseDisj(disj,__): strGlue(map(disj, id2s), " ");
		RuParseExp(type, symbs,__): strGlue(map(symbs, id2s), " ");
		RuParseHypRef(i,__): "hyp " + i2s(i + 1);
		RuParseStepRef(i,__): "step " + i2s(i + 1);
		RuParseVar(name, type,__): id2s(name) + " : " + id2s(type);
	}
}

ruParseBlockId(b : RuParseBlock) -> int {
	switch (b) {
		RuParseConst(symbol, ascii, latex,__): symbol;
		RuParseType(name, supers,__): name;
		RuParseRule(name, vars, term,__): name;
		RuParseAxiom(name, vars, disjs, hyps, prop,__): name;
		RuParseDef(name, vars, disjs, hyps, defm, defs, prop,__): name;
		RuParseTheorem(name, vars, disjs, meta, hyps, prop, proof,__): name;
		RuParseComment(text, json, __): s2id("<comment>");
	}
}
