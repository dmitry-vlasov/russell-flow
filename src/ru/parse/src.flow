import base/fileinfo;

export {
	RuParseSource(imports : [RuParseImport], decls : [RuParseDecl], comments : [RuParseComment], info : RuFileInfo);
	RuParseImport(module : int, pos : int);
	RuParseDecl ::= RuParseAssertion, RuParseLangDecl;
		RuParseAssertion ::= RuParseAxiom, RuParseDef, RuParseTheorem;
		RuParseLangDecl ::= RuParseConst, RuParseType, RuParseRule;

	RuParseComment(text : string, json : string, pos : int);
	RuParseConst(id : int, comments : [RuParseComment], ascii : int, latex : string, pos : int);
	RuParseType(id : int, comments : [RuParseComment], supers : [int], pos : int);
	RuParseRule(id : int, comments : [RuParseComment], vars : [RuParseVar], term : RuParseTerm, pos : int);
	RuParseAxiom(id : int, comments : [RuParseComment], vars : [RuParseVar], disjs : [RuParseDisj], hyps : [RuParseHyp], prop : RuParseStmt, pos : int);
	RuParseTheorem(id : int, comments : [RuParseComment], vars : [RuParseVar], disjs : [RuParseDisj], meta : RuParseMeta, hyps : [RuParseHyp], prop : RuParseStmt, proof : RuParseProof, pos : int);
	RuParseDef(id : int, comments : [RuParseComment], vars : [RuParseVar], disjs : [RuParseDisj], hyps : [RuParseHyp], defm : RuParseTerm, defs : RuParseTerm, prop : RuParseStmt, pos : int);
	
	RuParseHyp(ind : int, stmt : RuParseStmt, pos : int);
	RuParseVar(id : int, type : int, pos : int);
	RuParseDisj(vars : [int], pos : int);
	RuParseProof(vars : [RuParseVar], disjs : [RuParseDisj], steps : [RuParseStep], pos : int);
	RuParseStep(ind : int, assertion : int, refs : [RuParseRef], stmt : RuParseStmt, proof : RuParseProof, pos : int);
	RuParseRef ::= RuParseHypRef, RuParseStepRef;
	RuParseHypRef(ind : int);
	RuParseStepRef(ind : int);

	RuParseStmt(class : int, types : [int], expr : RuParseExpr, pos : int);
	RuParseTerm(type  : int,                expr : RuParseExpr, pos : int);
	RuParseExpr(symbs : [int], pos : int);
	RuParseMeta(meta  : [int], pos : int);

	RuParseAny ::= 
		RuParseSource, RuParseImport, RuParseConst, RuParseType, RuParseRule, RuParseAxiom, RuParseDef, RuParseTheorem, RuParseComment,
		RuParseStep, RuParseStmt, RuParseTerm, RuParseExpr, RuParseHyp, RuParseProof, RuParseDisj, RuParseVar, RuParseHypRef, RuParseStepRef;
	
	// Structs with 'pos' field
	RuParsePositioned ::= 
		RuParseImport, RuParseConst, RuParseType, RuParseRule, RuParseAxiom, RuParseDef, RuParseTheorem, RuParseComment,
		RuParseStep, RuParseStmt, RuParseTerm, RuParseExpr, RuParseHyp, RuParseProof, RuParseMeta, RuParseVar, RuParseDisj;


	ruParse2s(a : RuParseAny) -> string;
	ruSetParseDeclComments(decl : RuParseDecl, comms : [RuParseComment]) -> RuParseDecl;

	ruParsePositionsTree(src : RuParseSource) -> Tree<int, [RuParsePositioned]>;
	ruParsePositionedVars(x : RuParsePositioned) -> [RuParseVar];

	ruParseLemma = -1;
	ruParseProblem = -2;
	ruParseNoProof = RuParseProof([], [], [], -1);
	ruParseNoComment = RuParseComment("", "", -1);
	ruParseNoVar = RuParseVar(-1, -1, -1);
	ruParseNoType = RuParseType(-1, [], [], -1);
}

ruParse2s(a : RuParseAny) -> string {
	vars2s  = \vars -> if (vars == []) "" else "(" + superglue(vars, ruParse2s, ", ") + ") ";
	disjs2s = \disjs -> if (disjs == []) "" else "disjointed(" + superglue(disjs, ruParse2s, ", ") + ") ";
	meta2s  = \m -> if (m.meta == []) "" else "meta(" + superglue(m.meta, id2s, ", ") + ") ";
	expr2s  = \expr -> superglue(expr.symbs, id2s, " ") + " ;;";
	term2s  = \term -> ": " + id2s(term.type) + " = " + expr2s(term.expr);
	stmt2s  = \stmt -> ": " + superglue(stmt.types, id2s, ", ") + " = " + id2s(stmt.class) + " " + expr2s(stmt.expr);
	prop2s  = \prop -> "\tprop " + stmt2s(prop) + "\n";
	refs2s  = \refs -> "(" + superglue(refs, ruParse2s, ", ") + ")";
	hyps2s  = \hyps -> if (hyps == []) "" else superglue(hyps, ruParse2s, "\n") + "\n";
	comms2s = \comms -> superglue(comms, ruParse2s, "\n");
	switch (a) {
		RuParseSource(imps, decls, comms, info): 
			comms2s(comms) + "\n" +
			(if (imps == []) "" else superglue(imps, ruParse2s, "\n") + "\n\n") +
			superglue(decls, ruParse2s, "\n");
		RuParseImport(path,__):
			"import " + id2s(path) + ";;";
		RuParseConst(symbol, comms, ascii, latex,__): 
			comms2s(comms) + "\n" +
			"constant {\n" + 
				"\tsymbol " + id2s(symbol) + " ;;\n" +
				(if (id2s(ascii) != "") "\tascii " + id2s(ascii) + " ;;\n" else "") + 
				(if (latex != "") "\tlatex " + latex + " ;;\n" else "") +
			"}\n";
		RuParseType(name, comms, supers,__): 
			comms2s(comms) + "\n" +
			"type " + id2s(name) + (if (supers == []) "" else " : " + superglue(supers, id2s, ", ")) + " ;;\n";
		RuParseRule(name, comms, vars, term,__):
			comms2s(comms) + "\n" +
			"rule " + id2s(name) + " " + vars2s(vars) + "{\n" +
				"\tterm " + term2s(term) + "\n" +
			"}\n";
		RuParseAxiom(name, comms, vars, disjs, hyps, prop,__):
			comms2s(comms) + "\n" +
			"axiom " + id2s(name) + " " + vars2s(vars) + disjs2s(disjs) + "{\n" +
				hyps2s(hyps) +
				(if (hyps == []) "" else "\t----------\n") +
				prop2s(prop) +
			"}\n";
		RuParseDef(name, comms, vars, disjs, hyps, defm, defs, prop,__): 
			comms2s(comms) + "\n" +
			"definition " + id2s(name) + " " + vars2s(vars) + disjs2s(disjs) + "{\n" +
				hyps2s(hyps) +
				"\tdefiendum " + term2s(defm) + "\n" +
				"\tdefiniens " + term2s(defs) + "\n" +
				"\t-------------\n" +
				prop2s(prop) +
			"}\n";
		RuParseTheorem(name, comms, vars, disjs, meta, hyps, prop, proof,__):
			comms2s(comms) + "\n" + 
			"theorem " + id2s(name) + " " + vars2s(vars) + disjs2s(disjs) + meta2s(meta) + "{\n" +
				hyps2s(hyps) + 
				(if (hyps == []) "" else "\t----------\n") +
				prop2s(prop) +
			"} " + ruParse2s(proof);
		RuParseHyp(ind, expr,__): 
			"\thyp " + i2s(ind + 1) + " " + stmt2s(expr);
		RuParseProof(vars, disjs, steps,__): 
			"proof " + vars2s(vars) + disjs2s(disjs) + "{\n" +
				superglue(steps, ruParse2s, "\n") + "\n" +
			"}\n";
		RuParseStep(ind, assertion, refs, stmt, proof, __): {
			type = superglue(stmt.types, id2s, ", ");
			expr = superglue(stmt.expr.symbs, id2s, " ");
			if (assertion == ruParseLemma) {
				"\tstep " + i2s(ind + 1) + " : " + type + " = " + 
					"lemma " + refs2s(refs) + " " + id2s(stmt.class) + " " + expr + " ;; " + ruParse2s(proof);
			} else if (assertion == ruParseLemma) {
				"\tstep " + i2s(ind + 1) + " : " + type + " = " + 
					" ? |- " + expr + " ;;";
			} else {
				"\tstep " + i2s(ind + 1) + " : " + type + " = " + 
					id2s(assertion) + " " + refs2s(refs) + " |- " + expr + " ;;";
			}
		}
		RuParseComment(text, json, __): 
			if (json == "") "/* " + text + " */\n" else "/** " + json + "\n-----\n" + text + " */\n";
		RuParseDisj(disj,__): superglue(disj, id2s, " ");
		RuParseTerm(type, symbs,__): term2s(a);
		RuParseStmt(__,__,__,__): stmt2s(a);
		RuParseExpr(__,__): expr2s(a);
		RuParseHypRef(i): "hyp " + i2s(i + 1);
		RuParseStepRef(i): "step " + i2s(i + 1);
		RuParseVar(name, type,__): id2s(name) + " : " + id2s(type);
	}
}

ruSetParseDeclComments(decl : RuParseDecl, comms : [RuParseComment]) -> RuParseDecl {
	switch (decl) {
		RuParseConst(__,__,__,__,__):
			RuParseConst(decl with comments = comms);
		RuParseType(__,__,__,__):
			RuParseType(decl with comments = comms);
		RuParseRule(__,__,__,__,__):
			RuParseRule(decl with comments = comms);
		RuParseAxiom(__,__,__,__,__,__,__):
			RuParseAxiom(decl with comments = comms);
		RuParseTheorem(__,__,__,__,__,__,__,__,__):
			RuParseTheorem(decl with comments = comms);
		RuParseDef(__,__,__,__,__,__,__,__,__):
			RuParseDef(decl with comments = comms);
	}
}

ruParsePositionsTree(src : RuParseSource) -> Tree<int, [RuParsePositioned]> {
	fold(src.decls, makeTree(), \acc, decl : RuParseDecl ->
		ruDoParsePositionsTree(decl, [], acc)
	);
}

ruDoParsePositionsTree(positioned : RuParsePositioned, path : [RuParsePositioned], acc : Tree<int, [RuParsePositioned]>) -> Tree<int, [RuParsePositioned]> {
	path1 = concat(path, [positioned]);
	acc1 = setTree(acc, positioned.pos, path1);
	add_many = \ac, arr : [RuParsePositioned] -> fold(arr, ac, \a, x ->
		ruDoParsePositionsTree(x, path1, a)
	);
	switch (positioned) {
		RuParseTerm(type, expr, pos): {
			ruDoParsePositionsTree(expr, path1, acc1);
		}
		RuParseStmt(class, types, expr, pos): {
			ruDoParsePositionsTree(expr, path1, acc1);
		}
		RuParseHyp(ind, stmt, pos): {
			ruDoParsePositionsTree(stmt, path1, acc1);
		}
		RuParseRule(__,comms, vars, term, __): {
			add_many(ruDoParsePositionsTree(term, path1, acc1), vars);
		}
		RuParseStep(ind, assertion, refs, stmt, proof, pos): {
			acc2 = ruDoParsePositionsTree(stmt, path1, acc1);
			if (proof.steps == []) acc2 else {
				ruDoParsePositionsTree(proof, path1, acc1);
			}
		}
		RuParseProof(vars, disjs, steps, pos): {
			acc2 = add_many(acc1, concat(vars, disjs));
			fold(steps, acc2, \ac, step -> ruDoParsePositionsTree(step, path1, ac));
		}
		RuParseAxiom(__, comms, vars, disjs, hyps, prop,__ ): {
			add_many(acc1, concatA([vars, disjs, hyps, [prop]]));
		}
		RuParseDef(__, comms, vars, disjs, hyps, defm, defs, prop, __): {
			acc2 = add_many(acc1, concatA([vars, disjs, hyps, [prop]]));
			acc3 = ruDoParsePositionsTree(defm, path1, acc2);
			ruDoParsePositionsTree(defs, path1, acc3);
		}
		RuParseTheorem(__, comms, vars, disjs, meta, hyps, prop, proof, pos): {
			acc2 = add_many(acc1, concatA([vars, disjs, hyps, [prop]]));
			ruDoParsePositionsTree(proof, path1, acc2);
		}
		RuParseMeta(__,__):           acc1;
		RuParseImport(__,__):         acc1;
		RuParseComment(__,__,_):      acc1;
		RuParseConst(__,__,__,__,__): acc1;
		RuParseType(__,__,__,__):     acc1;
		RuParseExpr(__,__)   :        acc1;
		RuParseVar(id, type, pos):    acc1;
		RuParseDisj(vars, pos):       acc1;
	}
}

ruParsePositionedVars(x : RuParsePositioned) -> [RuParseVar] {
	switch (x) {
		RuParseRule(__,__,vars,__,__):                vars;
		RuParseProof(vars,__,__,__):                  vars;
		RuParseAxiom(__,__,vars,__,__,__,__ ):        vars;
		RuParseDef(__,__,vars,__,__,__,__,__,__):     vars;
		RuParseTheorem(__,__,vars,__,__,__,__,__,__): vars;
		RuParseStep(__,__,__,__,__,__): [];
		RuParseHyp(__,__,__):         [];
		RuParseMeta(__,__):           [];
		RuParseImport(__,__):         [];
		RuParseComment(__,__,_):      [];
		RuParseConst(__,__,__,__,__): [];
		RuParseType(__,__,__,__):     [];
		RuParseStmt(__,__,__,__):     [];
		RuParseTerm(__,__,__):        [];
		RuParseExpr(__,__):           [];
		RuParseVar(__,__,__):         [x];
		RuParseDisj(__,__):           [];
	}
}
