import math/stringmath;
import ru/parse/type_env;

export {
	ruParseExp(term : RuTerm, types : Tree<string, RuTypeEnv>, place : RuPlace, conf : RuConf) -> Maybe<RuExp>;
}

ruParseExp(term : RuTerm, types : Tree<string, RuTypeEnv>, place : RuPlace, conf : RuConf) -> Maybe<RuExp> {
	if (length(term.nodes) == 0) {
		conf.onError("empty expression met", []);
		None()
	} else {
		switch (lookupTree(types, id2s(term.type_id))) {
			Some(tenv): {
				ret = maybeMap(
					ruParseExpr(RuParseEnv(term.nodes, types, conf), 0, RuParseStack(None(), 0, 0, term.type_id, tenv.trie, [])),
					\nodes -> RuExp(nodes, term.pos)
				);
				if (ret == None()) {
					conf.onError(
						"expr: '" + ruTerm2s(term) + "' was not parsed\n" + 
						ruTypeEnvTree2s(types), 
						[place]
					);
					//ruCrash("expr: '" + ruTerm2s(term) + "' was not parsed");
					quit(0);
				}
				ret
			}
			None(): {
				conf.onError("unknown type: '" + id2s(term.type_id) + "'", []);
				None();
			}
		}
	}
}

RuParseEnv(
	nodes : [RuTermNode],
	types : Tree<string, RuTypeEnv>, 
	conf : RuConf,
);

RuParseStack(
	parent : Maybe<RuParseStack>,
	start : int,
	curr : int,
	type_id : int,
	trie : RuTrie<int, RuRuleNode>,
	expr : [[RuExpNode]],
);

ruPopExprStack(env : RuParseEnv, pos : int, stack : RuParseStack) -> RuParseStack {
	switch (stack.trie.data) {
		Some(ref): {
			switch (stack.parent) {
				Some(parent): {
					subexpr = concatA(reverseA(stack.expr));
					ruPopExprStack(env, pos, 
						RuParseStack(parent with
							expr = concat(
								[
									concat(
										[RuRuleNode(ref.name, ref.id, ref.type_id, ref.arity, length(subexpr))], 
										subexpr
									)
								], 
								parent.expr
							)
						)
					);
				}
				None(): stack;
			}
		}
		None(): stack;
	}
}

ruParseExpr(env : RuParseEnv, pos : int, stack0 : RuParseStack) -> Maybe<[RuExpNode]> {
	stack = RuParseStack(stack0 with curr = pos);
	if (pos == length(env.nodes)) {
		expr = concatA(reverseA(stack.expr));
		switch (stack.trie.data) {
			Some(ref): Some(concat(
				[RuRuleNode(ref.name, ref.id, ref.type_id, ref.arity, length(expr))], 
				expr
			));
			None(): Some(expr);
		}
	} else {
		do_step = \st, n -> {
			poped = ruPopExprStack(env, pos, RuParseStack(st with trie = n));
			ruParseExpr(env, pos + 1, poped);
		}
		try_supers = \v, t -> {
			supers = lookupTreeDef(env.types, id2s(t), ruNoTypeEnv).supers;
			foldTree(stack.trie.map, None(), \tp, m, ac -> 
				if (ac != None()) ac else {
					switch (lookupTree(supers, tp)) {
						Some(r): 
							do_step(RuParseStack(stack with 
								expr = concat(
									[[
										RuRuleNode(
											r.descr.name, 
											r.descr.id,  
											r.term.type_id, 
											1, 1
										), 
										RuVar(v, s2id(v), t)
									]], 
									stack.expr
								)
							), m);
						None(): ac;
					}
				}
			);
		}
		try_vars = \-> {
			foldTree(stack.trie.map, None(), \t, n, ac ->
				if (ac != None()) ac else {
					switch (lookupTree(env.types, id2s(t))) {
						Some(tenv): {
							if (t == stack.type_id && stack.curr == stack.start) ac else {
								ruParseExpr(env, pos, 
									RuParseStack(
										Some(RuParseStack(stack with trie = n)), 
										pos, pos, t, tenv.trie, []
									)
								)
							}
						}
						None(): ac;
					}
				}
			)
		}
		switch (env.nodes[pos]) {
			RuVar(v,id, t_id): {
				//t = id2s(t_id);
				switch (lookupTree(stack.trie.map, t_id)) {
					Some(n): {
						switch (do_step(RuParseStack(stack with expr = concat([[RuVar(v, id, t_id)]], stack.expr)), n)) {
							Some(ret): Some(ret);
							None(): 
								switch (try_supers(v, t_id)) {
									Some(ret): Some(ret);
									None(): try_vars();
								}
						}
					}
					None(): 
						switch (try_supers(v, t_id)) {
							Some(ret): Some(ret);
							None(): try_vars();
						}
				}
			}
			RuConstRef(id): {
				switch (lookupTree(stack.trie.map, id)) {
					Some(n): {
						switch (do_step(stack, n)) {
							Some(ret): Some(ret);
							None(): try_vars();
						}
					}
					None(): try_vars();
				}
			}
		}
	}
}
