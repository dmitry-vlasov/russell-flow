import math/stringmath;
import ru/parse/type_env;

export {
	ruParseExp(term : RuTerm, types : Tree<int, RuTypeEnv>, place : RuPlace, env : RuEnv) -> Maybe<RuExp>;
}

ruParseExp(term : RuTerm, types : Tree<int, RuTypeEnv>, place : RuPlace, env : RuEnv) -> Maybe<RuExp> {
	if (length(term.nodes) == 0) {
		env.out.onError("empty expression is met", []);
		None()
	} else {
		switch (lookupTree(types, term.type)) {
			Some(tenv): {
				ret = maybeMap(
					ruParseExpr(RuParseEnv(term.nodes, types, env), 0, RuParseStack(None(), 0, 0, term.type, tenv.trie, [])),
					\nodes -> RuExp(nodes, term.pos)
				);
				if (ret == None()) {
					env.out.onError(
						"expr: '" + ruTerm2s(term) + "' was not parsed\n" + 
						ruTypeEnvTree2s(types), 
						[place]
					);
				}
				ret
			}
			None(): {
				env.out.onError("unknown type: '" + id2s(term.type) + "'", []);
				None();
			}
		}
	}
}

RuParseEnv(
	nodes : [RuTermNode],
	types : Tree<int, RuTypeEnv>, 
	env : RuEnv,
);

RuParseStack(
	parent : Maybe<RuParseStack>,
	start : int,
	curr : int,
	type_id : int,
	trie : RuTrie<int, RuRuleNode>,
	expr : [[RuExpNode]],
);

ruPopExprStack(env : RuParseEnv, pos : int, stack : RuParseStack) -> RuParseStack {
	switch (stack.trie.data) {
		Some(ref): {
			switch (stack.parent) {
				Some(parent): {
					subexpr = concatA(reverseA(stack.expr));
					ruPopExprStack(env, pos, 
						RuParseStack(parent with
							expr = concat(
								[
									concat(
										[RuRuleNode(ref.rule, ref.type, ref.arity, length(subexpr))], 
										subexpr
									)
								], 
								parent.expr
							)
						)
					);
				}
				None(): stack;
			}
		}
		None(): stack;
	}
}

ruParseExpr(env : RuParseEnv, pos : int, stack0 : RuParseStack) -> Maybe<[RuExpNode]> {
	stack = RuParseStack(stack0 with curr = pos);
	if (pos == length(env.nodes)) {
		expr = concatA(reverseA(stack.expr));
		switch (stack.trie.data) {
			Some(ref): Some(concat(
				[RuRuleNode(ref.rule, ref.type, ref.arity, length(expr))], 
				expr
			));
			None(): Some(expr);
		}
	} else {
		do_step = \st, n -> {
			poped = ruPopExprStack(env, pos, RuParseStack(st with trie = n));
			ruParseExpr(env, pos + 1, poped);
		}
		try_supers = \v, t -> {
			supers = lookupTreeDef(env.types, t, ruNoTypeEnv).supers;
			foldTree(stack.trie.map, None(), \tp, m, ac -> 
				if (ac != None()) ac else {
					switch (lookupTree(supers, tp)) {
						Some(r): 
							do_step(RuParseStack(stack with 
								expr = concat(
									[[
										RuRuleNode(r.info.id, r.term.type, 1, 1), 
										RuVar(v, t)
									]], 
									stack.expr
								)
							), m);
						None(): ac;
					}
				}
			);
		}
		try_vars = \-> {
			foldTree(stack.trie.map, None(), \t, n, ac ->
				if (ac != None()) ac else {
					switch (lookupTree(env.types, t)) {
						Some(tenv): {
							if (t == stack.type_id && stack.curr == stack.start) ac else {
								ruParseExpr(env, pos, 
									RuParseStack(
										Some(RuParseStack(stack with trie = n)), 
										pos, pos, t, tenv.trie, []
									)
								)
							}
						}
						None(): ac;
					}
				}
			)
		}
		switch (env.nodes[pos]) {
			RuVar(v, t_id): {
				switch (lookupTree(stack.trie.map, t_id)) {
					Some(n): {
						switch (do_step(RuParseStack(stack with expr = concat([[RuVar(v, t_id)]], stack.expr)), n)) {
							Some(ret): Some(ret);
							None(): 
								switch (try_supers(v, t_id)) {
									Some(ret): Some(ret);
									None(): try_vars();
								}
						}
					}
					None(): 
						switch (try_supers(v, t_id)) {
							Some(ret): Some(ret);
							None(): try_vars();
						}
				}
			}
			RuConstRef(id): {
				switch (lookupTree(stack.trie.map, id)) {
					Some(n): {
						switch (do_step(stack, n)) {
							Some(ret): Some(ret);
							None(): try_vars();
						}
					}
					None(): try_vars();
				}
			}
		}
	}
}
