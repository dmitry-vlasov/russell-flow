import ru/parse/parse;
import ru/math;

export {
	ruUpdateSourceNames(math : RuMath) -> RuMath;
	ruUpdateMathPositions(math : RuMath, conf : RuConf) -> RuMath;
	ruUpdateSourcePositions(src : RuSource, conf : RuConf) -> RuSource;
}

ruUpdateSourceNames(math : RuMath) -> RuMath {
	foldTree(math.sources, math, \name, src, acc -> {
		module = src.info.module;
		p = fold(src.decls, Pair(acc, []), \ac, decl -> 
			switch (decl) {
				RuConst(descr,__,__,__): {
					upd_const = RuConst(decl with descr = RuDescr(descr with source = module));
					Pair(
						RuMath(ac.first with consts = setTree(ac.first.consts, descr.name, upd_const)), 
						concat(ac.second, [upd_const])
					);
				}
				RuType(descr,__,__): {
					upd_type = RuType(decl with descr = RuDescr(descr with source = module));
					upd_math = switch (lookupTree(ac.first.types, descr.name)) {
						Some(env): {
							foldTree(
								env.supers,
								RuMath(ac.first with types = setTree(ac.first.types, descr.name, RuTypeEnv(env with type = upd_type))),
								\__, super, a -> 
									RuMath(a with rules = setTree(a.rules, super.descr.name, RuRule(super with descr = RuDescr(super.descr with source = module))))
							);
						}
						None(): ac.first;
					}
					Pair(upd_math, concat(ac.second, [upd_type]));
				}
				RuRule(descr,__,__,__): {
					upd_rule = RuRule(decl with descr = RuDescr(descr with source = module));
					Pair(
						RuMath(ac.first with rules = setTree(ac.first.rules, descr.name, upd_rule)), 
						concat(ac.second, [upd_rule])
					);
				}
				RuAxiom(descr,__,__,__,__,__): {
					upd_axiom = RuAxiom(decl with descr = RuDescr(descr with source = module));
					Pair(
						RuMath(ac.first with assertions = setTree(ac.first.assertions, descr.name, upd_axiom)),
						concat(ac.second, [upd_axiom])
					);
				}
				RuTheorem(descr,__,__,__,__,__,__,__): {
					upd_theorem = RuTheorem(decl with descr = RuDescr(descr with source = module));
					Pair(
						RuMath(ac.first with assertions = setTree(ac.first.assertions, descr.name, upd_theorem)),
						concat(ac.second, [upd_theorem])
					);
				}
				RuDef(descr,__,__,__,__,__,__,__): {
					upd_def = RuDef(decl with descr = RuDescr(descr with source = module));
					Pair(
						RuMath(ac.first with assertions = setTree(ac.first.assertions, descr.name, upd_def)),
						concat(ac.second, [upd_def])
					);
				}
			}
		);
		upd_src = RuSource(src with decls = p.second);
		RuMath(p.first with sources = setTree(p.first.sources, module, upd_src));
	});
}

ruUpdateDeclPositions(decl1 : RuBlock, decl2 : RuParseBlock) -> RuBlock {
	if (ruParseBlockName(decl2) != ruBlockName(decl1)) {
		println("DECLS DIFFER: " + ruParseBlockName(decl2) + " != " + ruBlockName(decl1));
		quit(-1);
	}
	switch (decl1) {
		RuConst(__,__,__,__):
			RuConst(decl1 with pos = decl2.pos);
		RuType(__,__,__):
			RuType(decl1 with pos = decl2.pos);
		RuRule(__,__,term,__): {
			rule = cast(decl2 : RuParseBlock -> RuParseRule);
			RuRule(decl1 with
				pos = rule.pos,
				term = RuTerm(term with pos = rule.term.pos)
			);
		}
		RuAxiom(__,__,__,hyps, prop,__): {
			axiom = cast(decl2 : RuParseBlock -> RuParseAxiom);
			RuAxiom(decl1 with 
				pos = axiom.pos,
				hyps = mapi(hyps, \i, hyp -> 
					RuHyp(hyp with 
						expr = RuExp(hyp.expr with pos = axiom.hyps[i].expr.pos),
						pos = axiom.hyps[i].pos
					)
				),
				prop = RuExp(prop with pos = axiom.prop.pos)
			);
		}
		RuTheorem(__,__,__,__,hyps, prop, proof,__): {
			theorem = cast(decl2 : RuParseBlock -> RuParseTheorem);
			RuTheorem(decl1 with 
				pos = theorem.pos,
				hyps = mapi(hyps, \i, hyp -> 
					RuHyp(hyp with 
						expr = RuExp(hyp.expr with pos = theorem.hyps[i].expr.pos),
						pos = theorem.hyps[i].pos
					)
				),
				prop = RuExp(prop with pos = theorem.prop.pos),
				proof = RuProof(proof with 
					steps = mapi(proof.steps, \i, step -> 
						RuStep(step with
							expr = RuExp(step.expr with pos = theorem.proof.steps[i].expr.pos),
							refs = mapi(step.refs, \j, ref -> 
								switch (ref) {
									RuHypRef(ind,__): RuHypRef(ind, theorem.proof.steps[i].refs[j].pos);
									RuStepRef(ind,__): RuStepRef(ind, theorem.proof.steps[i].refs[j].pos);
								}
							),
							pos = theorem.proof.steps[i].pos
						)
					)
				)
			);
		}
		RuDef(__,__,__, hyps, defm, defs, prop,__): {
			def = cast(decl2 : RuParseBlock -> RuParseDef);
			RuDef(decl1 with 
				pos = def.pos,
				hyps = mapi(hyps, \i, hyp -> 
					RuHyp(hyp with 
						expr = RuExp(hyp.expr with pos = def.hyps[i].expr.pos),
						pos = def.hyps[i].pos
					)
				),
				defm = RuExp(defm with pos = def.defm.pos),
				defs = RuExp(defs with pos = def.defs.pos),
				prop = RuExp(prop with pos = def.prop.pos)
			);
		}
		RuComment(__,__,__): RuComment(decl1 with pos = decl2.pos);
	}
}

ruUpdateSourcePositions(src : RuSource, conf : RuConf) -> RuSource {
	switch (ruLoadRawSource(src.info.path, conf, ruLoadDescr)) {
		Some(loaded): {
			switch (ruParseSource(loaded, conf)) {
				Some(parsed): {
					blocks = ruSrc2Blocks(src);
					ruBclocks2Src(
						RuSource(src with 
							imports = mapi(src.imports, \i, imp -> RuImport(imp with pos = parsed.imports[i].pos))
						),
						mapi(blocks, \i, d -> ruUpdateDeclPositions(d, parsed.decls[i]))
					);
				}
				None(): src;
			}
		}
		None(): src;
	}
}

ruUpdateMathPositions(math : RuMath, conf : RuConf) -> RuMath {
	foldTree(math.sources, math, \n, src, acc -> {
		module = src.info.module;
		updated = ruUpdateSourcePositions(src, conf);
		fold(updated.decls, 
			RuMath(acc with sources = setTree(acc.sources, n, updated)),
			\ac, decl -> {
				switch (decl) {
					RuConst(descr,__,__,__):
						RuMath(ac with consts = setTree(ac.consts, descr.name, decl));
					RuType(descr,__,__): {
						switch (lookupTree(ac.types, descr.name)) {
							Some(env): 
								foldTree(
									env.supers,
									RuMath(ac with 
										types = setTree(ac.types, descr.name, RuTypeEnv(env with type = decl))
									),
									\__, super, a -> 
										RuMath(a with rules = setTree(a.rules, super.descr.name, super))
								);
							None(): ac;
						}
					}
					RuRule(descr,__,__,__):
						RuMath(ac with rules = setTree(ac.rules, descr.name, decl));
					RuAxiom(descr,__,__,__,__,__):
						RuMath(ac with assertions = setTree(ac.assertions, descr.name, decl));
					RuTheorem(descr,__,__,__,__,__,__,__):
						RuMath(ac with assertions = setTree(ac.assertions, descr.name, decl));
					RuDef(descr,__,__,__,__,__,__,__):
						RuMath(ac with assertions = setTree(ac.assertions, descr.name, decl));
				}
			}
		);
	});
}
