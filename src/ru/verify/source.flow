import ru/subst;
import ru/verify/theorem;

export {
	ruVerifySource(src : RuSource, acc : RuVerifyAcc, env : RuEnv) -> RuVerifyAcc;
	ruMakeVerifyAcc() -> RuVerifyAcc;
}

ruMakeVerifyAcc() -> RuVerifyAcc {
	RuVerifyAcc(ruEmptyMath, makeSet(), makeSet());
}

RuVerifySourceAcc(
	decls  : [RuDecl],
	local  : RuVerifyAcc,
	global : RuVerifyAcc
);

ruVerifySource(src : RuSource, acc : RuVerifyAcc, env : RuEnv) -> RuVerifyAcc {
	approve_acc = \ac, decl : RuDecl -> 
		RuVerifyAcc(ac with
			math = ruMathSetDeclInDecls(ac.math, decl, env),
			approved = insertSet(ac.approved, decl.descr.id)
		);
	disprove_acc = \ac, decl -> 
		RuVerifyAcc(ac with
			math = ruMathSetDeclInDecls(ac.math, decl, env),
			disproved = insertSet(ac.disproved, decl.descr.id)
		);
	approve = \ac, decl -> RuVerifySourceAcc(
		concat(ac.decls, [decl]), 
		approve_acc(ac.local, decl),
		approve_acc(ac.global, decl),
	);
	disprove = \ac, decl -> RuVerifySourceAcc(
		concat(ac.decls, [decl]), 
		disprove_acc(ac.local, decl),
		disprove_acc(ac.global, decl),
	);
	timer = ruMakeTimer();
	pre_math = ruMathAddSource(acc.math, src, env);
	src_acc = fold(src.decls, 
		RuVerifySourceAcc([], ruMakeVerifyAcc(), acc), 
		\ac, decl -> {
			switch (decl) {
				RuTheorem(descr,__,__,__): {
					res = ruDoVerifyTheorem(decl, ac.global, env);
					filtered = ruFilterOutVerifyErrs(res, ruIsNonVerifiedRef);
					ruOutputVerifyErrs(filtered, pre_math, env);
					if (ruIsVerified(filtered)) {
						if (ruVerbose(env.conf) > 2) {
							env.out.onMessage("Theorem '" + id2s(descr.id) + "' is approved");
						}
						approve(ac, res.theorem);
					} else {
						if (ruVerbose(env.conf) > 0 && length(filtered.errs) > 0) {
							env.out.onMessage("Theorem '" + id2s(descr.id) + "' is disproved");
							env.out.onMessage(ru2s(decl, pre_math));
						}
						disprove(ac, decl);
					}
				}
				default: {
					approve(ac, decl);
				}
			}
		}
	);
	if (ruVerbose(env.conf) > 1) {
		env.out.onMessage("\tru verified: '" + src.info.module + "' in " + ruTimePassed2s(timer));
	}
	RuVerifyAcc(src_acc.local with
		math = RuMath(src_acc.local.math with 
			sources = setTree(src_acc.local.math.sources, src.info.id, 
				RuSource(src with decls = src_acc.decls)
			)
		)
	);
}
