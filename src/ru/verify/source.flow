import ru/subst;
import ru/verify/theorem;
import command;

export {
	ruVerifySource(src : RuSource, acc : RuVerifyAcc, conf : RuConf) -> RuVerifyAcc;
	ruMakeVerifyAcc() -> RuVerifyAcc;
}

ruMakeVerifyAcc() -> RuVerifyAcc {
	RuVerifyAcc(ruEmptyMath, makeSet(), makeSet());
}

RuVerifySourceAcc(
	decls  : [RuDecl],
	local  : RuVerifyAcc,
	global : RuVerifyAcc
);

ruVerifySource(src : RuSource, acc : RuVerifyAcc, conf : RuConf) -> RuVerifyAcc {
	approve_acc = \ac, decl : RuDecl -> 
		RuVerifyAcc(ac with
			math = ruAddDecl(decl, ac.math, conf),
			approved = insertSet(ac.approved, ruDeclName(decl))
		);
	disprove_acc = \ac, decl -> 
		RuVerifyAcc(ac with
			math = ruAddDecl(decl, ac.math, conf),
			disproved = insertSet(ac.disproved, ruDeclName(decl))
		);
	start = timestamp();
	approve = \ac, decl -> RuVerifySourceAcc(
		concat(ac.decls, [decl]), 
		approve_acc(ac.local, decl),
		approve_acc(ac.global, decl),
	);
	disprove = \ac, decl -> RuVerifySourceAcc(
		concat(ac.decls, [decl]), 
		disprove_acc(ac.local, decl),
		disprove_acc(ac.global, decl),
	);
	src_acc = fold(src.decls, 
		RuVerifySourceAcc([], ruMakeVerifyAcc(), acc), 
		\ac, decl -> {
			switch (decl) {
				RuTheorem(descr,__,__,__): {
					res = ruDoVerifyTheorem(decl, ac.global, conf);
					ruOutputVerifyErrs(res, acc.math, conf);
					if (ruIsVerified(res)) approve(ac, res.theorem) else disprove(ac, decl);
				}
				default: approve(ac, decl);
			}
		}
	);
	if (conf.verbose > 1) {
		conf.onMessage("\tru verified: '" + src.info.module + "' in " + d2st((timestamp() - start)/ 1000.0, 4) + " s");
	}
	RuVerifyAcc(src_acc.local with
		math = RuMath(src_acc.local.math with 
			sources = setTree(src_acc.local.math.sources, src.info.id, 
				RuSource(src with decls = src_acc.decls)
			)
		)
	);
}
