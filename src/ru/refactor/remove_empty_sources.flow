import ru/cache/cache;
import comm;

export {
	ruCommRemoveEmptySources : RuComm;
}

ruCommRemoveEmptySources = RuComm(
	RuCommDescr(
		"remove-empty-sources", "refactor", "self evident", "", false, false, 
		[RuCommArg("delete", true, "delete files from filesystem. To prevent deleting set to 0", "1")], []
	),
	ruRunRemoveEmptySources
);

ruRunRemoveEmptySources(unit : RuUnit) -> Maybe<RuState> {
	env = ruUnitEnv(unit);
	state = unit.state;
	math = ruMathFromSources(state.ru, env);
	delete_files = lookupTreeDef(unit.task.args, "delete", "1") == "1";
	empty_removed = ruRemoveEmptySources(math, env, delete_files);
	Some(RuState(state with ru = empty_removed.sources));
}

ruRemoveEmptySources(math : RuMath, env : RuEnv, delete_files : bool) -> RuMath {
	timer = ruMakeTimer();
	all_sources = getTreeValues(math.sources);

	empty_sources = filter(all_sources, \src -> 
		src.decls == [] && 
		src.comments == [] && 
		forall(env.conf.projectRoots, \root -> root != src.info.module && root != src.info.path)
	);
	if (ruVerbose(env.conf) > 1) {
		if (empty_sources != []) {
			env.out.onMessage("empty sources (" + i2s(length(empty_sources)) + "):\n" + 
				strIndent(strGlue(map(empty_sources, \src -> 
					src.info.module + ": [" + i2s(length(src.decls)) + "] decls"
				), "\n"))
			);
		} else {
			env.out.onMessage("no empty sources");
		}
	}
	ret = fold(empty_sources, math, \acc, src -> ruRemoveEmptySource(acc, src, env, all_sources));
	if (delete_files) {
		iter(empty_sources, \src -> {
			err = deleteFile(src.info.path);
			if (err != "") {
				env.out.onMessage(err + " while deleting " + src.info.path);
			} else {
				if (ruVerbose(env.conf) > 0) {
					env.out.onMessage("\tfile " + src.info.path + " is deleted");
				}
			}
		});	
	}
	if (ruVerbose(env.conf) > 0 && empty_sources != []) {
		operation = if (delete_files) "deleted" else "removed";
		env.out.onMessage("empty sources (" + i2s(length(empty_sources)) + ") are " + operation + " in " + ruTimePassed2s(timer));
	}
	ret;
}

ruRemoveEmptySource(math : RuMath, src : RuSource, env : RuEnv, all_sources : [RuSource]) -> RuMath {
	imported_by = filtermap(all_sources, \s -> 
		maybeMap(findi(s.imports, \imp -> imp.id == s.info.id), \i -> Pair(s, i))
	);
	ruCacheRemove(src.info.id, env);
	m = fold(imported_by, math, \acc, pair -> {
		s = pair.first;
		ind = pair.second;
		import_replaced = concat(removeIndex(s.imports, ind), src.imports);
		new_s = RuSource(s with imports = import_replaced);
		ruCacheAdd(new_s, true, env);
		RuMath(math with sources = setTree(math.sources, s.info.id, new_s));
	});
	RuMath(m with sources = removeFromTree(m.sources, src.info.id));
}
