import ru/parse/write;
import ru/prover/stats;
import ru/prover/proof2theorem;
import ru/util/match_assertions;
import apply_map;

export {
	ruCommGeneralize : RuComm;
}

ruCommGeneralize = RuComm(
	RuCommDescr(
		"generalize", "refactor", "automatically generalize theorems by proofs", "target", false, false, 
		[
			RuCommArg("target", true, "the target: a theorem, file or all", "all"),
			RuCommArg("min-sub-level", true, "min generalization substitution level. Default is 0", "0"),
			RuCommArg("min-sub-len", true, "min generalization substitution length. Default is 0", "0"),
			RuCommArg("min-fitness", true, "min generalization fitness. Default is 0.0", "0.0"),
			RuCommArg("info", true, "show info about generalized theorems (number: 0,1,2). In this case math is not updated", "0"),
			RuCommArg("save", true, "save the result. Default is 1", "1"),
		], []
	),
	ruRunGeneralize
);

RuGeneralized(
	thm : RuTheorem,
	gen : RuTheorem,
	match : RuAssertionMatch
);

ruRunGeneralize(unit : RuUnit) -> Maybe<RuState> {
	env = ruUnitEnv(unit);
	task = unit.task;
	state = unit.state;
	conf = state.conf;
	math = ruMakeMathFromSources(state.ru, env);
	gen_map = initConcurrentHashMap();
	gen_level = s2d(lookupTreeDef(task.args, "level", "0.0"));
	if (ruVerbose(conf) > 0) {
		env.out.onMessage("starting generalization ... ");
	}
	min_sub_level = s2i(lookupTreeDef(task.args, "min-sub-level", "0"));
	min_sub_len = s2i(lookupTreeDef(task.args, "min-sub-len", "0"));
	min_fitness = s2d(lookupTreeDef(task.args, "min-fitness", "0.0"));
	fitness_func = \g : RuGeneralized -> {
		if (ruArity(g.gen) > 0) {
			i2d(ruSubstLen(g.match.sub)) / i2d(ruArity(g.gen));
		} else {
			100.0
		}
	}
	fn = RuApplyFn(
		RuFnDescr(
			"generalize", 
			ruDeclIsTheorem,
			\decl -> i2d(length(cast(decl : RuDecl -> RuTheorem).proof.steps)),
		),
		\decl, time_limit -> {
			th = cast(decl : RuDecl -> RuTheorem);
			gen_id = s2id(id2s(th.descr.id) + "_gen");
			if (ruVerbose(conf) >= 3) {
				env.out.onMessage("\t\ttrying to generalize '" + id2s(th.descr.id) + "'");
			}
			if (containsKeyTree(math.assertions, gen_id)) {
				if (ruVerbose(conf) >= 2) {
					env.out.onMessage("\tseems that '" + id2s(th.descr.id) + "' is already generalized");
				}
				true;
			} else {
				timer = ruMakeTimer();
				proof_graph = ruProofToDAG(th.proof);
				switch (ruProof2Theorem(gen_id, th.descr.src, proof_graph, math, env, time_limit)) {
					Some(gen): {
						if (length(gen.stat.hyps) > 3 * length(th.stat.hyps)) {
							if (ruVerbose(conf) > 1) {
								env.out.onMessage("Generalized theorem '" + id2s(th.descr.id) + "' has too much hypothesis: " + i2s(length(gen.stat.hyps)) + " while original has: " + i2s(length(th.stat.hyps)));
							}
							true;
						} else {
							switch (ruMatchAssertions1(gen, th, math, env)) {
								None(): {
									env.out.onError(
										"Generalization doesn't match !!!: " + id2s(th.descr.id) + "\n" + 
										"theorem:\n" + ru2s(th, math) + "\n" +
										"generalized:\n" + ru2s(gen, math) + "\n"
										, []
									);
									vr = ruVerifyTheorem(gen, math, env);
									iter(vr.errs, \err -> 
										env.out.onError(ruVerifyErr2s(err, math), [])
									);
									if (length(vr.errs) == 0) {
										env.out.onMessage("Generalized is verified and is Ok\n" + ru2s(vr.theorem, math));
									}
									quit(-1);
								}
								Some(match): {
									if (ruIsVarRenaming(match.sub)) {
										switch (ruMatchAssertions1(th, gen, math, env)) {
											Some(m1): {
												//env.out.onMessage("Not really a generalization...");
											}
											None(): {
												/*xxx = \s : Tree<RuVar, RuExp> -> {
													image_vars = buildSet(filtermap(getTreeValues(s), ruExp2Var));
													env.out.onMessage(
														"sizeTree(s) == sizeSet(image_vars): " + i2s(sizeTree(s)) + " == " + i2s(sizeSet(image_vars))
													);
												};
												xxx(match.sub);*/
												env.out.onError(
													"MUST BE SOME\n" +
													"substitution level: " + i2s(ruSubstLevel(match.sub)) + "\n" + 
													"substitution len: " + i2s(ruSubstLen(match.sub)) + "\n" + 
													"sub:\n" + ruSubst2s(match.sub, math) + "\n" + 
													"original:\n" + ruDeclShort2s(th, math) +
													"generalized:\n" + ruDeclShort2s(gen, math) + "\n\n"
												, []);
												quit(-1);
											}
										}
									} else {
										g = RuGeneralized(th, gen, match);
										sub_level = ruSubstLevel(match.sub);
										sub_len = ruSubstLen(match.sub);
										fitness = fitness_func(g);
										if (sub_level >= min_sub_level && sub_len>= min_sub_len && fitness >= min_fitness) {
											setConcurrentHashMap(gen_map, id2s(th.descr.id), g);
											if (ruVerbose(conf) >= 2) {
												env.out.onMessage(
													"\t" + i2s(sizeConcurrentHashMap(gen_map)) + " theorem '" + id2s(th.descr.id) + "' was GENERALIZED " + 
													"in " + ruTimePassed2s(timer) + ", " + 
													"time limit: " + ruTime2s(time_limit) + ", " + 
													"substitution level: " + i2s(sub_level) + ", " + 
													"substitution len: " + i2s(sub_len) + ", " +
													"fitness: " + d2s(fitness) + 
													(if (ruVerbose(conf) < 3) "" else "\n" + 
														"sub:\n" + ruSubst2s(match.sub, math) + "\n" + 
														"original:\n" + ruDeclShort2s(th, math) +
														"generalized:\n" + ruDeclShort2s(gen, math) + "\n\n"
													)
												);
											}
										}
									}
								}
							}
							/*if (isNone(ruMatchAssertions1(th, gen, math, conf))) {
								maybeApply(ruMatchAssertions1(gen, th, math, conf), \match ->
									if (ruSubstLevel(match.sub) >= gen_level) {
										setConcurrentHashMap(generalized, id2s(th.descr.id), Triple(th, gen, match));
										if (ruVerbose(conf) >= 2) {
											env.out.onMessage(
												"\t" + i2s(sizeConcurrentHashMap(generalized)) + " theorem '" + id2s(th.descr.id) + "' was GENERALIZED " + 
												"in " + ruTime2s(ruTime() - start) + ", " + 
												"time limit: " + ruTime2s(time_limit) + "\n" + 
												"substitution level: " + i2s(ruSubstLevel(match.sub)) + "\n" + 
												"substitution len: " + i2s(ruSubstLen(match.sub)) + "\n" + 
												"sub:\n" + ruSubst2s(match.sub, math) + "\n" + 
												"original:\n" + ru2s(th, math.rules) +
												"generalized:\n" + ru2s(gen, math.rules) + "\n\n"
											);
										}
									}
								);
							}*/
							//env.out.onMessage("\t\theorem '" + id2s(th.descr.id) + "' was processed");
							true;
						}
					}
					None(): {
						passed = ruTimePassed(timer);
						if (ruVerbose(conf) >= 2) {
							env.out.onMessage(
								"\t" + i2s(sizeConcurrentHashMap(gen_map)) + " theorem '" + id2s(th.descr.id) + "' was NOT generalized " + 
								"in " + ruTime2s(passed) + " " + 
								"time limit: " + ruTime2s(time_limit)
							);
							if (passed < time_limit) {
								env.out.onError("theorem:\n" + ru2s(th, math) + "\n" + ruProofDAG2s(proof_graph), []);
								quit(-1);
							}
						}
						false;
					}
				}
			}
		}
	);
	ruApplyFnComm(unit, fn);
	ret = if (lookupTreeDef(task.args, "info", "") != "") {
		info_level = s2i(lookupTreeDef(task.args, "info", ""));
		generalized = sortCustom(
			valuesConcurrentHashMap(gen_map), 
			fitness_func, 
			false
		);
		env.out.onMessage("Generalized theorems:\n" +
			strGlue(map(generalized, \g -> 
				"\t" + id2s(g.thm.descr.id) + ", " + 
				"sub level: " + i2s(ruSubstLevel(g.match.sub)) + ", " + 
				"sub len: " + i2s(ruSubstLen(g.match.sub)) + ", " +
				"fit: " + d2s(fitness_func(g)) + 
				(if (info_level <= 1) "" else 
					"\n" + strIndent("original:\n" + 
						strIndent(ruDeclShort2s(g.thm, math))
					 ) +
					"\n" + strIndent("generalized:\n" + 
						strIndent(ruDeclShort2s(g.gen, math))
					) +
					"\n" + strIndent("substitution:\n" + 
					strIndent(ruSubst2s(g.match.sub, math))
					 ) + "\n"
				)
			), "\n")
		);
		state;
	} else {
		ruInsertGeneralized(gen_map, task, state, env);
	}
	if (ruVerbose(conf) > 0) {
		env.out.onMessage("\tgeneralized: " + i2s(sizeConcurrentHashMap(gen_map)) + " theorems");
	}
	Some(ret)
}

ruInsertGeneralized(generalized : native, task : ScTask, state : RuState, env : RuEnv) -> RuState {
	conf = state.conf;
	math = ruMakeMathFromSources(state.ru, env);
	gen_math = fold(valuesConcurrentHashMap(generalized), math, \acc, triple : Triple<RuTheorem, RuTheorem, RuAssertionMatch> -> {
		th = triple.first;
		gen = triple.second;
		eitherMap(lookupTree(acc.assertions, th.descr.id), \ass -> 
			eitherMap(lookupTree(acc.sources, ass.descr.src), \src -> {
					if (ruVerbose(env.conf) >= 3) {
						env.out.onMessage("\tru adding generalized sources: '" + id2s(gen.descr.id) + "'");
					}
					ind = ruIndexOfDecl(th.descr.id, src);
					ruAddAssertion(gen, ind, src, acc, env);
				}, acc
			), acc
		);
	});
	if (lookupTreeDef(task.args, "save", "") == "1") {
		modified_sources = fold(valuesConcurrentHashMap(generalized), makeSet(), \acc, triple : Triple<RuTheorem, RuTheorem, RuAssertionMatch> ->
			eitherMap(lookupTree(gen_math.assertions, triple.first.descr.id), \ass -> insertSet(acc, ass.descr.src), acc)
		);
		ruWriteRefactoredSources(modified_sources, gen_math, ruDecVerboseEnv(env));
	}
	RuState(state with ru = gen_math.sources);
}
