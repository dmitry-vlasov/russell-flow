import ru/refactor/remove_unused_steps;
import ru/verify/math;
import ru/prover/util;
import ru/unify/prod;
import ru/util/target;
import ru/map_decls;

export {
	ruCommShortenProofs : RuComm;
}

ruCommShortenProofs = RuComm(
	RuCommInfo(
		"shorten-proofs", "refactor", "try to make proofs shorter", "target", false, false, 
		[RuCommArg("target", true, "the target: a theorem, file or all", "all")],
		[
			RuCommRet("shorten-proofs.shortened-theorems", "int", "total number of theorems, which were shortened by optimization"),
			RuCommRet("shorten-proofs.shortened-steps", "int", "total number of steps in proofs, which were shortened by optimization")
		]
	),
	ruRunShortenProofs
);

ruSubProofVolume(i : int, steps : [RuStep]) -> int {
	fold(steps[i].refs, 1, \acc, ref -> acc + ruRefVolume(ref, steps));
}

ruRefVolume(ref : RuRef, steps : [RuStep]) -> int {
	switch (ref) {
		RuHypRef(__): 0;
		RuStepRef(j): ruSubProofVolume(j, steps);
	}
}

RuShortenEnv(
	assertions : RuIndex<RuAssertion>,
	imports : Tree<int, Set<int>>,
	math : RuMath,
	env : RuEnv
);

RuShortenAcc(
	env : RuShortenEnv,
	theorem : RuTheorem,
	optimized : [RuStep],
	premises : RuIndex<RuRef>,
	steps : [RuStep],
	cenv : RuConstEnv,
	timer : RuTimer
);

ruShortenTheorem(th : RuTheorem, time_limit : double, env : RuShortenEnv) -> Pair<Maybe<RuTheorem>, bool> {
	timer = ruMakeTimer();
	math0 = env.math;
	conf = env.env.conf;
	out = env.env.out;
	cth = ruConstTheorem(th, math0);
	math = cth.env.math;
	
	if (ruVerbose(conf) > 1) {
		out.println("started shortening theorem '" + id2s(th.info.id) + "' with time limit: " + ruTime2s(time_limit) + " ...");
	}

	initPremises = fold(cth.const.infer.hyps, ruMakeIndex(), \acc, hyp -> ruSetIndex(ruExp(hyp.stmt.exps[0]), RuHypRef(hyp.ind), acc));
	interrupted = ref false;
	optimized_proof = foldi(cth.const.proof.steps, 
		RuShortenAcc(env, th, [], initPremises, cth.const.proof.steps, cth.env, ruMakeLimitedTimer(time_limit)), 
		\i, acc, step -> {
			least = ruStepMinVariant(step.ind, cth.const.proof.steps, th, acc);
			RuShortenAcc(acc with
				optimized = concat(acc.optimized, [least]),
				premises = ruSetIndex(ruExp(step.stmt.exps[0]), RuStepRef(step.ind), acc.premises)
			);
		}
	).optimized;
	optimized_th = ruVerifyTheorem(RuTheorem(th with 
		proof = ruTransformProof(RuProof(th.proof with steps = optimized_proof), ruMakeExpTransformer(cth.env.unconst))
	), math0, env.env);
	if (!ruIsSubset(optimized_th.theorem.infer.vars.disjs, th.infer.vars.disjs)) {
		out.println(
			"!ruIsSubset(optimized_th.theorem.infer.vars.disjs, th.infer.vars.disjs)\n" + 
			"optimized_th.theorem.infer.vars.disjs:\n" + ruDisjs2s(optimized_th.theorem.infer.vars.disjs) + "\n" +
			"th.infer.vars.disjs:\n" + ruDisjs2s(th.infer.vars.disjs) + "\n" +
			"th:\n" + ru2s(th, math) + 
			"optimized_th:\n" + ru2s(optimized_th.theorem, math)
		);
		Pair(None(), ^interrupted);
	} else if (optimized_th.errs != []) {
		ruCrash(
			"ERROR IN SHORTENING\n" +
			"optimized:\n" +
			ru2s(optimized_th.theorem, math) + "\n" +
			"original:\n" +
			ru2s(th, math) + "\n" +
			ruVerifyTheoremErrs2s(optimized_th, math)
		);
		Pair(None(), ^interrupted);
	} else {
		red = ruRemoveTheoremUnusedSteps(optimized_th.theorem, env.env);
		if (ruVerbose(conf) > 2) {
			out.println("finished shortening theorem '" + id2s(th.info.id) + "' ...");
		}
		time_taken = ruTimePassed(timer);
		late_for = 100.0 * (time_taken - time_limit) / time_limit;
		if (ruVerbose(conf) > 1 && late_for > 100.0) {
			out.println("*** " + id2s(th.info.id) + " was late for: " + d2st(late_for, 2) + "% (" + ruTime2s(time_taken - time_limit) + ")");
		}
		switch (red) {
			Some(reduced): {
				if (ruVerbose(conf) > 1) {
					out.println("theorem '" + id2s(th.info.id) + "' shortened by " + i2s(length(reduced.removed)) + " steps in " + ruTimePassed2s(timer));
				}
				res = ruVerifyTheorem(reduced.smaller, math, env.env);
				if (!ruIsVerified(res)) {
					ruCrash(
						"ERROR IN SHORTENING\n" +
						"optimized:\n" +
						ru2s(reduced.smaller, math) + "\n" +
						"original:\n" +
						ru2s(reduced.larger, math) + "\n" +
						ruVerifyTheoremErrs2s(res, math)
					);
				}
			}
			None(): 
				if (ruVerbose(conf) > 1) {
					out.println("theorem '" + id2s(th.info.id) + "' was not shortened in " + ruTimePassed2s(timer));
				}
		}
		Pair(maybeMap(red, \reduced -> reduced.smaller), ^interrupted);
	}
}

RuShortenLeast(
	step : RuStep, // Current least step
	vol  : int     // volume of current least step
);

ruStepMinVariant(i : int, steps : [RuStep], th : RuTheorem, acc : RuShortenAcc) -> RuStep {
	step = acc.steps[i];
	unified = filter(
		ruUnifyIndex(
			ruExp(step.stmt.exps[0]), acc.env.assertions, ruEmptySubst, 
			acc.env.math, acc.env.env
		),
		\unif -> ruDeclInfoPreceeds(unif.data.info, th.info, acc.env.imports)
	);
	fold(unified, 
		RuShortenLeast(step, ruSubProofVolume(i, acc.steps)), 
		\least, unif -> {
			unconst_sub = \s -> ruTransformSubst(s, acc.cenv.unconst);
			ass : RuAssertion = unif.data;
			check_disj = \s -> {
				unc_s = unconst_sub(s);
				ruCheckDisjs(unc_s, ass.infer.vars.disjs) && 
				ruCheckDisjsInherent(unc_s, ass.infer.vars.disjs, th.infer.vars.disjs);
			}
			if (ass.infer.hyps != [] && check_disj(unif.sub)) {
				hyps_prod = CartProd(map(ass.infer.hyps, \h -> {
					hyp_unified = ruUnifyIndex(
						ruApplySubst(ruExp(h.stmt.exps[0]), unif.sub), acc.premises, unif.sub, 
						acc.env.math, acc.env.env
					);
					sorted = mergeSort(hyp_unified, \u1, u2 -> 
						ruRefVolume(u1.data, acc.steps) - ruRefVolume(u2.data, acc.steps)
					);
					map(sorted, \un -> RuSubData(un.sub, un.data));
				}));
				sub_unified = if (sizeCartProd(hyps_prod) < 64) {
					filtermapCartProd(hyps_prod, \row -> 
						maybeMap(
							ruUnifySubs(map(row, \hp -> hp.sub), acc.env.math, acc.env.env),
							\s -> RuSubData(s, map(row, \hp -> hp.data))
						)
					);
				} else {
					ruUnifySubsProd(
						hyps_prod, \__-> true, acc.env.math, acc.env.env
					);
				}
				fold(sub_unified, least, 
					\le, sd -> {
						st = RuStep(le.step with refs = sd.data, assertion = ass.info.id);
						vol = ruSubProofVolume(st.ind, replace(acc.steps, st.ind, st));
						if (vol < le.vol && check_disj(sd.sub)) {
							RuShortenLeast(st, vol) 
						} else {
							le;
						}
					}
				);
			} else {
				least;
			}
		}
	).step;
}

ruRunShortenProofs(unit : RuUnit) -> Maybe<RuState> {
	env = ruUnitEnv(unit);
	state = unit.state;
	conf = state.conf;
	math = ruMathFromSources(state.ru, env);
	timer = ruMakeTimer();
	target = ruDetermineTarget(unit.task, math, env);
	if (target != RuTargetNone()) {
		prev_res = cast(lookupTreeDef(state.vars, "shorten-proofs", makeTree()) : flow -> Tree<string, flow>);
		prev_completed = cast(lookupTreeDef(prev_res, "completed", []) : flow -> [int]);
		prev_completed_set = buildSet(prev_completed);
		completed = ref prev_completed;
		interrupted = ref [];
		add_completed = ruMakeAtomicArrayPush(completed);
		add_interrupted = ruMakeAtomicArrayPush(interrupted);
		shortened_theorems = ref 0;
		shortened_steps = ref 0;
		all_imported = ruSourcesAllImported(math.sources, env);
		index = ruMakeIndexForTarget(target, math, env);
		max_time = ref 0.0;
		max_thid = ref 0;
		shorten_fn = \decl, time_limit, env1 -> {
			shorten_env = RuShortenEnv(index, all_imported, math, env1);
			th = cast(decl : RuDecl -> RuTheorem);
			th_timer = ruMakeTimer();
			p = ruShortenTheorem(th, time_limit, shorten_env);
			th_time = ruTimePassed(th_timer);
			if (^max_time < th_time) {
				max_time := th_time;
				max_thid := th.info.id;
			}
			finished = !p.second;
			if (!finished) {
				// Was interrupted - non complete traverse of all variants
				add_interrupted(th.info.id);
				{}
			} else {
				// Was not interrupted - complete traverse of all variants
				add_completed(th.info.id);
				{}
			}
			ret = p.first;
			switch (ret) {
				Some(shortened): {
					atomicRefIntAddition(shortened_theorems, 1);
					atomicRefIntAddition(shortened_steps, length(th.proof.steps) - length(shortened.proof.steps));
					RuMappedDecl(finished, true, shortened);
				}
				None(): {
					RuMappedDecl(finished, false, decl);
				}
			}
		}
		new_state = ruMapFnComm(
			RuUnit(unit.task, state, env.out),
			RuMapFn(
				RuMapFnInfo(
					"shorten-proofs", 
					\decl -> ruTargetPredicate(target)(decl) && ruDeclIsTheorem(decl) && !containsSet(prev_completed_set, decl.info.id), 
					ruProofLengthMeasure, 
					\ -> 
						"shortened: " + i2s(^shortened_theorems) + " theorems, " + i2s(^shortened_steps) + " steps; "
						//"completed: " + i2s(length(^completed)) + ", interrupted: " + i2s(length(^interrupted))
				),
				shorten_fn
			)
		);
		if (conf.verbose > 0) {
			env.out.println("Completed theorems: " + i2s(length(^completed)) + ", interrupted theorems: " + i2s(length(^interrupted)));
		}
		Some(RuState(new_state with 
			vars = setTree(new_state.vars, "shorten-proofs", pairs2tree(filtermap([
				Some(Pair("shortened-theorems", flow(^shortened_theorems))),
				Some(Pair("shortened-steps", flow(^shortened_steps))),
				Some(Pair("completed", flow(^completed))),
				Some(Pair("time", flow(ruTimePassed(timer)))),
				if (0.0 < ^max_time) Some(Pair("max_time", flow(^max_time))) else None(),
				if (0.0 < ^max_time) Some(Pair("max_time_id", flow(id2s(^max_thid)))) else None()
			], idfn)))
		));
	} else {
		env.out.println("no target for proof shortening");
		Some(state);
	}
}
