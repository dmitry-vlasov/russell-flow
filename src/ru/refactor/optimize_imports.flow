import comm;

export {
	ruCommOptimizeImports : RuComm;
	ruRunOptimizeImports(unit : RuUnit) -> Maybe<RuState>;
	ruOptimizeImports(math : RuMath, env : RuEnv) -> Tree<int, RuSource>;
}

ruCommOptimizeImports = RuComm(
	RuCommInfo("optimize-imports-ru", "refactor", "optimize imports", "", false, false, [], []),
	ruRunOptimizeImports
);

ruRunOptimizeImports(unit : RuUnit) -> Maybe<RuState> {
	env = ruUnitEnv(unit);
	state = unit.state;
	timer = ruMakeTimer();
	lang = ruLangFromSources(state.ru, env);
	foldTree(state.ru, makeTree(), \src_id, src, acc ->
		fold(src.decls, acc, \ac: Tree<int, Pair<RuSource, RuDecl>>, decl: RuDecl -> 
			switch (lookupTree(ac, decl.info.id)) {
				Some(p): {
					ruCrash("DUPLICATE ID: " + id2s(decl.info.id) + "\n" +
						"\tsrc1: " + id2s(p.first.info.id) + "\n" +
							strIndent(strIndent(ru2sLang(p.second, lang))) + "\n" +
						"\tsrc2: " + id2s(src.info.id) + "\n" +
							strIndent(strIndent(ru2sLang(decl, lang))) + "\n" +
						"\tsrc1 decls:\n" +
							superglue(p.first.decls, \d -> "\t\t" + id2s(d.info.id), "\n") + "\n" +
						"\tsrc2 decls:\n" +
							superglue(src.decls, \d -> "\t\t" + id2s(d.info.id), "\n") + "\n"
					);
					ac;
				}
				None(): setTree(ac, decl.info.id, Pair(src, decl));
			}
		)
	);
	math = ruMathFromSources(state.ru, env);
	optimized = ruDoOptimizeImports(
		math.sources, math, env, 
		RuOptImportsAcc(makeTree(), makeTree(), ruCollectMathDeps(math, env))
	).sources;
	if (ruVerbose(env.conf) > 0) {
		env.out.println("ru imports optimized: " +i2s(sizeTree(optimized)) + " files in " + ruTimePassed2s(timer));
	}
	Some(RuState(state with 
		ru = optimized,
		vars = setTree(state.vars, "optimize-imports-ru", pairs2tree([
			Pair("time", flow(ruTimePassed(timer))), 
			Pair("size", flow(sizeTree(optimized)))
		]))
	));
}

ruCollectMathDeps(math : RuMath, env : RuEnv) -> Tree<int, Set<int>> {
	pairs2tree(
		ruConcurrent(map(
			getTreeValues(math.sources),
			\src -> \env1 -> Pair(src.info.id, ruCollectSourceDependencies(src, math, env))
		), env)
	);
}

ruOptimizeImports(math : RuMath, env : RuEnv) -> Tree<int, RuSource> {
	ruDoOptimizeImports(
		math.sources, math, env, 
		RuOptImportsAcc(makeTree(), makeTree(), ruCollectMathDeps(math, env))
	).sources
}

RuOptImportsAcc(
	sources : Tree<int, RuSource>,
	cumulativeImports : Tree<int, Set<int>>,
	deps : Tree<int, Set<int>>
);

ruDoOptimizeImports(left : Tree<int, RuSource>, math : RuMath, env : RuEnv, acc : RuOptImportsAcc) -> RuOptImportsAcc {
	if (sizeTree(left) == 0) acc else {
		ready = filter(
			getTreeValues(left), 
			\src -> forall(src.imports, \imp -> containsKeyTree(acc.sources, imp.id))
		);
		if (ruVerbose(env.conf) >= 4) {
			env.out.println("\t\tready: " + strGlue(map(ready, \r -> r.info.module), ", "));
		}
		optimized = ruConcurrent(map(ready, \s -> \env1 -> ruOptimizeSourceImports(s, acc, math, env1)), env); 
		new_acc = RuOptImportsAcc(
			fold(optimized, acc.sources, \ac, src -> setTree(ac, src.info.id, src)),
			fold(optimized, acc.cumulativeImports, \ac, src ->
				setTree(ac, src.info.id, 
					fold(src.imports, makeSet1(src.info.id), \a, imp -> insertSet(a, imp.id))
				)
			),
			acc.deps
		);
		new_left = fold(ready, left, \ac, src -> removeFromTree(ac, src.info.id));
		if (ruVerbose(env.conf) >= 4) {
			env.out.println("\t\tnew_left: " + strGlue(map(getTreeKeys(new_left), id2s), ", "));
		}
		ruDoOptimizeImports(new_left, math, env, new_acc);
	}
}

ruOptimizeSourceImports(src : RuSource, acc : RuOptImportsAcc, math : RuMath, env : RuEnv) -> RuSource {
	if (ruVerbose(env.conf) >= 2) {
		env.out.println("\t\toptimizing imports of: " + id2s(src.info.id));
	}
	if (length(src.decls) == 0) src else {
		deps = set2array(mapSet(
			lookupTreeDef(acc.deps, src.info.id, makeSet()), 
			\id -> {
				src_id = ruFindSourceId(id, math.decls);
				if (src_id != -1) src_id else {
					env.out.error("unknown name: '" + id2s(id) + "' (" + i2s(id) + ")", []);
					fail("unknown name: '" + id2s(id) + "'");
					-1;
				}
			}
		));
		if (ruVerbose(env.conf) >= 3) {
			env.out.println("\t\t\tnon-optimized imports:\n" + 
				superglue(src.imports, \imp -> "\t\t\t\t" + id2s(imp.id), "\n")
			);
		}
		optimized = filter(deps, \d1 -> 
			(d1 != src.info.id) && forall(deps, \d2 -> {
				switch (lookupTree(acc.cumulativeImports, d2)) {
					Some(imps): (d1 == d2) || !containsSet(imps, d1);
					None(): true;
				}
			})
		);
		if (ruVerbose(env.conf) >= 3) {
			env.out.println("\t\t\toptimized imports:\n" + 
				superglue(optimized, \imp -> "\t\t\t\t" + id2s(imp), "\n")
			);
		}
		ret = RuSource(src with imports = map(optimized, \imp -> RuImport(imp, -1)));
		if (ruVerbose(env.conf) >= 2) {
			env.out.println(
				"\toptimized imports in module: " + src.info.module + ", was: " + i2s(length(src.imports)) + ", is: " + i2s(length(optimized))
			);
		}
		ret;
	}
}

ruCollectSourceDependencies(src : RuSource, math : RuMath, env : RuEnv) -> Set<int> {
	expr_deps = \acc, expr -> fold(ruExp(expr).nodes, acc, \ac, n ->
		switch (n) {
			RuRuleNode(rule_type, __): {
				rule = ruIdType2Id(rule_type);
				type = ruIdType2Type(rule_type);
				insertSet(insertSet(ac, rule), type);
			}
			RuVar(__, type): insertSet(ac, type);
		}
	);
	stmt_deps = \acc, stmt -> fold(stmt.exps, acc, \ac, ex -> expr_deps(acc, ruExp(ex)));
	termDeps = \acc, term -> fold(term.nodes, insertSet(acc, term.type), \ac, n ->
		switch (n) {
			RuConstRef(const): insertSet(ac, const);
			RuVar(__, type):   insertSet(ac, type);
		}
	);
	fold(src.decls, makeSet(), \acc, decl ->
		switch (decl) {
			RuConst(info,__,__): insertSet(acc, info.id);
			RuType(__, supers): fold(supers, acc, \ac, super -> insertSet(ac, super));
			RuRule(__,__, term): termDeps(acc, term);
			RuAxiom(__,stat):
				fold(stat.hyps, stmt_deps(acc, stat.prop), \ac, hyp -> stmt_deps(ac, hyp.stmt));
			RuTheorem(__,stat,__,proof):
				fold(proof.steps, 
					fold(stat.hyps, stmt_deps(acc, stat.prop), \ac, hyp -> stmt_deps(ac, hyp.stmt)), 
					\ac, step -> insertSet(stmt_deps(ac, step.stmt), step.assertion)
				);
			RuDef(__,stat,__,__):
				fold(stat.hyps, stmt_deps(acc, stat.prop), \ac, hyp -> stmt_deps(ac, hyp.stmt));
		}
	)
}
