import comm;
import script/value;
import ru/util/match_assertions;

export {
	ruCommRemoveTrivialTheorems : RuComm;
}

ruCommRemoveTrivialTheorems = RuComm(
	RuCommDescr(
		"remove-trivial-theorems", "refactor", "self evident", "target", false, false, 
		[RuCommArg("target", true, "the target: a theorem, file or all", "all")],
		[RuCommRet("remove-trivial-theorems.removed", "int", "total number of trivial theorems, which were removed by optimization")]
	),
	ruRunRemoveTrivialTheorems
);

ruMakeTrivialReplacement(th : RuTheorem, math : RuMath) -> Maybe<RuAssertionMatch> {
	if (length(th.proof.steps) != 1) None() else {
		step = th.proof.steps[0];
		Some(RuAssertionMatch(th, lookupTreeDef(math.decls.assertions, step.assertion, ruNoAxiom), makeTree(),
			filtermap(step.refs, \href -> 
				switch (href) {
					RuHypRef(i,__): Some(i);
					default: None();
				}
			)
		));
	}
}

ruRunRemoveTrivialTheorems(unit : RuUnit) -> Maybe<RuState> {
	env = ruUnitEnv(unit);
	task = unit.task;
	state = unit.state;
	timer = ruMakeTimer();
	conf = state.conf;
	math = ruMathFromSources(state.ru, env);
	trivial = filtermap(getTreeValues(ruDeclsTheorems(math.decls)), \th -> ruMakeTrivialReplacement(th, math));
	name2thms = ruAssertionsUses(math.decls.assertions);
	ret = fold(trivial, math, \acc, match -> {
		if (ruVerbose(conf) > 1) {
			unit.out.onMessage("\tremoving trivial theorem '" + id2s(match.less.descr.id) + "'");
		}
		ruReplaceMatch(match, acc, name2thms, env)
	});
	if (ruVerbose(conf) > 0) {
		unit.out.onMessage("found and removed " + i2s(length(trivial)) + " trivial theorems in " + ruTimePassed2s(timer));
	}
	Some(RuState(state with 
		ru = ret.sources,
		vars = setTree(setTree(state.vars, 
			"remove-trivial-theorems.removed", ValInt(length(trivial))),
			"remove-trivial-theorems.time", ValDouble(ruTimePassed(timer))
		)
	));
}
