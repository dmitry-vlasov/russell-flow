import ru/prover/tree;
import ru/index;
import ru/verify;

export {
	RuConstEnv(
		doconst  : (RuExp) -> RuExp,
		unconst  : (RuExp) -> RuExp,
		math     : RuMath
	);
	// Assertion with non-changeable variables replaced by constants
	RuConstAssertion(
		original : ?,
		const    : ?,
		env : RuConstEnv
	);

	ruMakeFresh(vars : [RuVar], fresher : (RuVar) -> RuVar) -> Tree<RuVar, RuExp>;
	ruInitTheoremAssertionsIndex(th : RuTheorem, math : RuMath, conf : RuConf) -> RuIndex<RuAssertion>;
	ruInitSourceAssertionsIndex(src : RuSource, math : RuMath, conf : RuConf) -> RuIndex<RuAssertion>;
	ruInitGlobalAssertionsIndex(math : RuMath) -> RuIndex<RuAssertion>;

	ruInitPremisesIndex(th : RuTheorem) -> RuIndex<RuHyp>;
	ruConstTheorem(th : RuTheorem, math0 : RuMath) -> RuConstAssertion<RuTheorem>;
	ruConstAxiom(ax : RuAxiom, math : RuMath) -> RuConstAssertion<RuAxiom>;
	ruConstDef(df : RuDef, math : RuMath) -> RuConstAssertion<RuDef>;
	ruConstAssertion(as : RuAssertion, math : RuMath) -> RuConstAssertion<RuAssertion>;

	ruSubSatisfies(s : Tree<RuVar, RuExp>, cond : (RuExp) -> bool) -> bool;
	ruUpdateTheoremWithSteps(th : RuTheorem, steps : [RuStep], math : RuMath, conf : RuConf) -> Maybe<RuTheorem>;

	ruFindLeastSourceContaining(assertions : Set<string>, math : RuMath, conf : RuConf) -> Maybe<string>;
}

ruMakeFresh(vars : [RuVar], fresher : (RuVar) -> RuVar) -> Tree<RuVar, RuExp> {
	fold(vars, makeTree(), \acc, v -> setTree(acc, v, RuExp([fresher(v)], -1)));
}

ruInitTheoremAssertionsIndex(th : RuTheorem, math : RuMath, conf : RuConf) -> RuIndex<RuAssertion> {
	t = timestamp();
	switch (lookupTree(math.sources, th.descr.source)) {
		Some(th_src): {
			all_imported = removeSet(ruNameAllImported(th.descr.name, math), th_src);
			th_index = ruIndexOfDecl(th.descr.name, th_src);
			index = foldTree(math.assertions, ruMakeIndex(), 
				\name, ass, acc -> {
					switch (ruFindSource(name, math)) {
						Some(ass_src): {
							if (containsSet(all_imported, ass_src)) {
								ruSetIndex(ass.stat.prop, ass, acc);
							} else if (ass_src == th_src) {
								if (ruIndexOfDecl(name, ass_src) < th_index) {
									ruSetIndex(ass.stat.prop, ass, acc);
								} else {
									acc
								}
							} else {
								acc
							}
						}
						None(): {
							conf.onMessage("assertion: '" + name + "' is not found");
							acc;
						}
					}
				}
			);
			stats = RuStatsRow("theorem: " + th.descr.name, 
				pairs2tree([Pair("size", i2d(sizeSet(all_imported))), Pair("time", timestamp() - t)])
			);
			ruAddStats("init_assertion_index", stats);
			index;
		}
		None(): {
			conf.onMessage("Source:  '" + th.descr.source + "' of a theorem: '" + th.descr.name + "' is not found");
			conf.onMessage("ruFindSourceName():  '" + ruFindSourceName(th.descr.name, math) + "'");
			conf.onMessage("math.sources:\n" + strIndent(strGlue(getTreeKeys(math.sources), "\n")));
			ruMakeIndex();
		}
	}
}

ruInitSourceAssertionsIndex(src : RuSource, math : RuMath, conf : RuConf) -> RuIndex<RuAssertion> {
	t = timestamp();
	src_name = src.info.module;
	all_imported = insertSet(ruSourceAllImported(src_name, math), src);
	index = foldTree(math.assertions, ruMakeIndex(), 
		\name, ass, acc -> {
			switch (ruFindSource(name, math)) {
				Some(ass_src): {
					if (!containsSet(all_imported, ass_src)) acc else {
						ruSetIndex(ass.stat.prop, ass, acc);
					}
				}
				None(): {
					conf.onMessage("assertion: '" + name + "' is not found");
					acc;
				}
			}
		}
	);
	stats = RuStatsRow("source: " + src_name, 
		pairs2tree([Pair("size", i2d(sizeSet(all_imported))), Pair("time", timestamp() - t)])
	);
	ruAddStats("init_assertion_index", stats);
	index;
}


ruInitGlobalAssertionsIndex(math : RuMath) -> RuIndex<RuAssertion> {
	foldTree(math.assertions, ruMakeIndex(), \name, ass, acc ->
		ruSetIndex(ass.stat.prop, ass, acc)
	);
}

ruInitPremisesIndex(th : RuTheorem) -> RuIndex<RuHyp> {
	fold(th.stat.hyps, ruMakeIndex(), \acc, h -> ruSetIndex(h.expr, h, acc));
}

ruMakeConstEnv(cv_vars : [RuVar], math0 : RuMath) -> RuConstEnv {
	cv_name = \cv -> "_" + cv.name;
	cv_rules = map(cv_vars, \cv -> RuRule(RuDescr(cv_name(cv), "__no", [], -1), makeTree(), RuTerm(cv.type, [RuConstRef(cv_name(cv))], -1)));
	math = RuMath(math0 with rules = mergeTree(math0.rules, values2tree(cv_rules, \r -> r.descr.name)));
	cv_map = keys2tree(cv_vars, \cv -> RuRuleNode(cv_name(cv), cv.type, 0, 0));
	cv_exp = \e -> RuExp(map(e.nodes, \n -> lookupTreeDef(cv_map, n, n)), e.pos);
	uncv_map = foldTree(cv_map, makeTree(), \v, cv, acc -> setTree(acc, cv, v));
	uncv_exp = \e -> RuExp(map(e.nodes, \n -> lookupTreeDef(uncv_map, n, n)), e.pos);
	RuConstEnv(cv_exp, uncv_exp, math);
}

ruConstTheorem(th : RuTheorem, math : RuMath) -> RuConstAssertion<RuTheorem> {
	th_vars = set2array(mergeSets(ruAssertion2Vars(th), ruProof2Vars(th.proof)));
	vars = filter(th_vars, \v -> !containsSet(th.meta, v.name));
	env = ruMakeConstEnv(vars, math);
	RuConstAssertion(th, ruTransformTheorem(th, env.doconst), env);
}

ruConstAxiom(ax : RuAxiom, math : RuMath) -> RuConstAssertion<RuAxiom> {
	env = ruMakeConstEnv(set2array(ruAssertion2Vars(ax)), math);
	RuConstAssertion(ax, ruTransformAxiom(ax, env.doconst), env);
}

ruConstDef(df : RuDef, math : RuMath) -> RuConstAssertion<RuDef> {
	env = ruMakeConstEnv(set2array(ruAssertion2Vars(df)), math);
	RuConstAssertion(df, ruTransformDef(df, env.doconst), env);
}

ruConstAssertion(as : RuAssertion, math : RuMath) -> RuConstAssertion<RuAssertion> {
	switch (as) {
		RuAxiom(__,__):         ruConstAxiom(as, math);
		RuDef(__,__,__,__):     ruConstDef(as, math);
		RuTheorem(__,__,__,__): ruConstTheorem(as, math);
	}
}

ruSubSatisfies(s : Tree<RuVar, RuExp>, cond : (RuExp) -> bool) -> bool {
	allTreeSatisfies(s, \__, e -> cond(e));
}

ruUpdateTheoremWithSteps(th : RuTheorem, steps : [RuStep], math : RuMath, conf : RuConf) -> Maybe<RuTheorem> {
	vs = values2tree(set2array(differenceSets(
		fold(steps, makeSet(), \acc, step -> mergeSets(acc, ruExp2Vars(step.expr))),
		ruAssertion2Vars(th)
	)), \v -> v.name);
	proof = RuProof(vs, makeSet(), steps, -1);
	updated = RuTheorem(th with proof = proof);
	//conf.onMessage("UPDATED: " + ru2s(updated, math.rules));
	ruVerifyTheorem(updated, math, conf, true);
}

ruFindLeastSourceContaining(assertions : Set<string>, math : RuMath, conf : RuConf) -> Maybe<string> {
	// A stub.
	None();
}
