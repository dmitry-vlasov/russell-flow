import ru/prover/tree;
import ru/index;
import ru/verify/math;
import ru/util/transform;
import ru/util/target;
import base/stats;

export {
	RuConstEnv(
		doconst  : (RuExp) -> RuExp,
		unconst  : (RuExp) -> RuExp,
		math     : RuMath
	);

	// Make anything with expressions be non-changeable by replacing variables with constants
	RuConstAny(
		original : ?,
		const    : ?,
		env : RuConstEnv
	);

	RuProverTask(
		info : string,
		args : Tree<string, string>,
		premises : [RuPremise],
		goal : RuStep
	);

	ruMakeFresh(vars : [RuVar], fresher : (RuVar) -> RuVar) -> RuSubst;
	ruInitTheoremAssertionsIndex(th : RuTheorem, math : RuMath, env : RuEnv) -> RuIndex<RuAssertion>;
	ruInitSourceAssertionsIndex(src : RuSource, math : RuMath, env : RuEnv) -> RuIndex<RuAssertion>;
	ruInitGlobalAssertionsIndex(math : RuMath) -> RuIndex<RuAssertion>;

	ruInitTheoremPremisesIndex(th : RuTheorem, step_ind : int) -> RuIndex<RuPremise>;
	ruInitTaskPremisesIndex(task : RuProverTask) -> RuIndex<RuPremise>;
	ruConstTheorem(th : RuTheorem, math0 : RuMath) -> RuConstAny<RuTheorem>;
	ruConstAxiom(ax : RuAxiom, math : RuMath) -> RuConstAny<RuAxiom>;
	ruConstDef(df : RuDef, math : RuMath) -> RuConstAny<RuDef>;
	ruConstAssertion(as : RuAssertion, math : RuMath) -> RuConstAny<RuAssertion>;
	ruConstProverTask(task : RuProverTask, math : RuMath) -> RuConstAny<RuProverTask>;

	ruSubSatisfies(s : RuSubst, cond : (RuExp) -> bool) -> bool;
	ruUpdateTheoremWithSteps(th : RuTheorem, steps : [RuStep], math : RuMath, env : RuEnv) -> Maybe<RuTheorem>;
	ruMakeProofFromSteps(task : RuProverTask, steps : [RuStep], math : RuMath, env : RuEnv) -> Maybe<RuProof>;

	ruFindLeastSourceContaining(assertions : Set<string>, math : RuMath, env : RuEnv) -> Maybe<string>;
	ruMakeIndexForTarget(target : RuTarget, math : RuMath, env : RuEnv) -> RuIndex<RuAssertion>;
}

ruMakeFresh(vars : [RuVar], fresher : (RuVar) -> RuVar) -> RuSubst {
	RuSubst(fold(vars, makeTree(), \acc, v -> 
		setTree(acc, v, RuExpVars(RuExp([fresher(v)], -1), makeSet1(v)))
	));
}

ruInitTheoremAssertionsIndex(th : RuTheorem, math : RuMath, env : RuEnv) -> RuIndex<RuAssertion> {
	t = ruMakeTimer();
	switch (lookupTree(math.sources, th.info.src)) {
		Some(th_src): {
			all_imported = removeSet(ruSourceAllImported(th.info.src, math.sources), th_src);
			th_index = ruIndexOfDecl(th.info.id, th_src);
			index = foldTree(math.decls.assertions, ruMakeIndex(), 
				\id, ass, acc -> {
					switch (ruFindSource(id, math)) {
						Some(ass_src): {
							if (containsSet(all_imported, ass_src)) {
								ruSetIndex(ass.stat.prop, ass, acc);
							} else if (ass_src == th_src) {
								if (ruIndexOfDecl(id, ass_src) < th_index) {
									ruSetIndex(ass.stat.prop, ass, acc);
								} else {
									acc
								}
							} else {
								acc
							}
						}
						None(): {
							env.out.onMessage("assertion: '" + id2s(id) + "' is not found");
							acc;
						}
					}
				}
			);
			stats = RuStatsRow("theorem: '" + id2s(th.info.id) + "'", 
				pairs2tree([
					Pair("size", i2d(sizeSet(all_imported))), 
					Pair("time", ruTimePassed(t))
				])
			);
			ruAddStats("init_assertion_index", stats);
			index;
		}
		None(): {
			env.out.onMessage("Source:  '" + id2s(th.info.src) + "' of a theorem: '" + id2s(th.info.id) + "' is not found");
			env.out.onMessage("ruFindSourceName():  '" + id2s(ruFindSourceId(th.info.id, math.decls)) + "'");
			env.out.onMessage("math.sources:\n" + strIndent(strGlue(map(getTreeKeys(math.sources), id2s), "\n")));
			ruMakeIndex();
		}
	}
}

ruInitSourceAssertionsIndex(src : RuSource, math : RuMath, env : RuEnv) -> RuIndex<RuAssertion> {
	t = ruMakeTimer();
	src_id = src.info.id;
	all_imported = insertSet(ruSourceAllImported(src_id, math.sources), src);
	index = foldTree(math.decls.assertions, ruMakeIndex(), 
		\id, ass, acc -> {
			switch (ruFindSource(id, math)) {
				Some(ass_src): {
					if (!containsSet(all_imported, ass_src)) acc else {
						ruSetIndex(ass.stat.prop, ass, acc);
					}
				}
				None(): {
					env.out.onMessage("assertion: '" + id2s(id) + "' is not found");
					acc;
				}
			}
		}
	);
	stats = RuStatsRow("source: " + id2s(src_id), 
		pairs2tree([Pair("size", i2d(sizeSet(all_imported))), Pair("time", ruTimePassed(t))])
	);
	ruAddStats("init_assertion_index", stats);
	index;
}


ruInitGlobalAssertionsIndex(math : RuMath) -> RuIndex<RuAssertion> {
	foldTree(math.decls.assertions, ruMakeIndex(), \name, ass, acc ->
		ruSetIndex(ass.stat.prop, ass, acc)
	);
}

ruInitTheoremPremisesIndex(th : RuTheorem, step_ind : int) -> RuIndex<RuPremise> {
	fold(enumFromTo(0, step_ind),
		fold(th.stat.hyps, ruMakeIndex(), \acc, h -> ruSetIndex(h.expr, h, acc)),
		\acc, i -> ruSetIndex(th.proof.steps[i].expr, th.proof.steps[i], acc)
	);
}

ruInitTaskPremisesIndex(task : RuProverTask) -> RuIndex<RuPremise> {
	fold(task.premises, ruMakeIndex(), \acc, p -> ruSetIndex(p.expr, p, acc));
}

ruMakeConstEnv(cv_vars : [RuVar], math0 : RuMath) -> RuConstEnv {
	cv_id = \cv -> s2id("_" + id2s(cv.id));
	cv_rules = map(cv_vars, \cv -> {
		id = cv_id(cv);
		RuRule(
			RuDeclInfo(id, -1, [], -1), 
			makeTree(), 
			RuTerm(cv.type, [RuConstRef(id)], -1)
		);
	});
	math = RuMath(math0 with 
		decls = RuDecls(math0.decls with
			lang = RuLang(math0.decls.lang with
				rules = mergeTree(math0.decls.lang.rules, values2tree(cv_rules, \r -> r.info.id))
			)
		)
	);
	cv_map = keys2tree(cv_vars, \cv -> RuRuleNode(cv_id(cv), cv.type, 0, 0));
	cv_exp = \e -> RuExp(map(e.nodes, \n -> lookupTreeDef(cv_map, n, n)), e.pos);
	uncv_map = foldTree(cv_map, makeTree(), \v, cv, acc -> setTree(acc, cv, v));
	uncv_exp = \e : RuExp -> RuExp(map(e.nodes, \n -> lookupTreeDef(uncv_map, n, n)), e.pos);
	RuConstEnv(cv_exp, uncv_exp, math);
}

ruConstTheorem(th : RuTheorem, math : RuMath) -> RuConstAny<RuTheorem> {
	th_vars = set2array(mergeSets(ruAssertion2Vars(th), ruProof2Vars(th.proof)));
	vars = filter(th_vars, \v -> !containsSet(th.meta, v.id));
	env = ruMakeConstEnv(vars, math);
	RuConstAny(th, ruTransformTheorem(th, env.doconst), env);
}

ruConstAxiom(ax : RuAxiom, math : RuMath) -> RuConstAny<RuAxiom> {
	env = ruMakeConstEnv(set2array(ruAssertion2Vars(ax)), math);
	RuConstAny(ax, ruTransformAxiom(ax, env.doconst), env);
}

ruConstDef(df : RuDef, math : RuMath) -> RuConstAny<RuDef> {
	env = ruMakeConstEnv(set2array(ruAssertion2Vars(df)), math);
	RuConstAny(df, ruTransformDef(df, env.doconst), env);
}

ruConstAssertion(as : RuAssertion, math : RuMath) -> RuConstAny<RuAssertion> {
	switch (as) {
		RuAxiom(__,__):         ruConstAxiom(as, math);
		RuDef(__,__,__,__):     ruConstDef(as, math);
		RuTheorem(__,__,__,__): ruConstTheorem(as, math);
	}
}

ruConstProverTask(task : RuProverTask, math : RuMath) -> RuConstAny<RuProverTask> {
	vars = set2array(fold(task.premises, 
		ruExp2Vars(task.goal.expr),
		\acc, premise -> mergeSets(acc, ruExp2Vars(premise.expr))
	));
	env = ruMakeConstEnv(vars, math);
	RuConstAny(
		task, 
		RuProverTask(
			task.info,
			task.args,
			map(task.premises, \p -> ruTransformPremise(p, env.doconst)),
			ruTransformStep(task.goal, env.doconst)
		),
		env
	);
}

ruSubSatisfies(s : RuSubst, cond : (RuExp) -> bool) -> bool {
	allTreeSatisfies(s.map, \__, e -> cond(e.exp));
}

ruMakeProofFromSteps(task : RuProverTask, steps : [RuStep], math : RuMath, env : RuEnv) -> Maybe<RuProof> {
	proof = RuProof(RuVarsDisjs(makeTree(), makeSet()), steps, -1);
	res = ruVerifyProof(proof, task.premises, task.goal.expr, math, env);
	if (length(res.errs) == 0) {
		Some(res.proof);
	} else {
		None();
	}
}

ruUpdateTheoremWithSteps(th : RuTheorem, steps : [RuStep], math : RuMath, env : RuEnv) -> Maybe<RuTheorem> {
	vs = values2tree(set2array(differenceSets(
		fold(steps, makeSet(), \acc, step -> mergeSets(acc, ruExp2Vars(step.expr))),
		ruAssertion2Vars(th)
	)), \v -> v.id);
	proof = RuProof(RuVarsDisjs(vs, makeSet()), steps, -1);
	verified = ruVerifyProof(proof, th.stat.hyps, th.stat.prop, math, env);
	updated = ruNormalizeTheoremVarsDisjs(RuTheorem(th with proof = verified.proof));
	if (length(verified.errs) == 0) {
		Some(updated);
	} else {
		ruOutputVerifyErrs(RuVerifyTheoremResult(th, verified.errs), math, env);
		None();
	}
}

ruFindLeastSourceContaining(assertions : Set<string>, math : RuMath, env : RuEnv) -> Maybe<string> {
	// A stub.
	None();
}

ruMakeIndexForTarget(target : RuTarget, math : RuMath, env : RuEnv) -> RuIndex<RuAssertion> {
	timer = ruMakeTimer();
	index = switch (target) {
		RuTargetMath(__): 
			ruInitGlobalAssertionsIndex(math);
		RuTargetSource(src,__): 
			ruInitSourceAssertionsIndex(src, math, env);
		RuTargetDecl(decl): {
			switch (decl) {
				RuTheorem(__,__,__,__): {
					ruInitTheoremAssertionsIndex(decl, math, env);
				}
				default: ruInitGlobalAssertionsIndex(math);
			}
		}
		RuTargetNone():
			ruMakeIndex();
	}
	if (ruVerbose(env.conf) > 0 && ruTimePassed(timer) > 100.0) {
		env.out.onMessage("index was built in " + ruTimePassed2s(timer));
	}
	index;
}
