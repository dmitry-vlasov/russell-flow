import ru/prover/prove;
import ru/prover/tactics;
import ru/prover/reprove_oracle;
import ru/util/info;

export {
	ruProverCommands : [RuComm] = [
		ruCommReproveOracle,
		ruCommProveStart,
		ruCommProveExpand,
		ruCommProveFinish,
		ruCommProveConfirmProof,
		ruCommProveTry,
		ruCommProveTacticExpandLeafs,
		ruCommProveTacticForbid
	]
}

ruCommProveStart = RuComm(
	RuCommInfo(
		"prove-start", "prover", "start proving a theorem", "theorem", true, false, 
		[
			RuCommArg("theorem", true, "a target theorem for proving", ""),
			RuCommArg("file", true, "file, within a position of which a proving should be started", ""),
			RuCommArg("line", true, "line of a position, where a proving should be started", ""),
			RuCommArg("col", true, "column of a position, where a proving should be started", ""),
			RuCommArg("step", true, "a proof step for proving. -1 means a QED step (last)", "-1"),
			RuCommArg("time-limit", true, "a default time limit for proving", ""),
		], []
	),
	ruRunProveStart
);

ruRunProveStart(unit : RuUnit) -> Maybe<RuState> {
	task = unit.task;
	th_name = lookupTreeDef(task.args, "theorem", "");
	if (th_name != "") {
		ruRunProveTheoremStart(unit, th_name);
	} else {
		ruRunProveStepStart(unit);
	}
}

ruRunProveStepStart(unit : RuUnit) -> Maybe<RuState> {
	env = ruUnitEnv(unit);
	state = unit.state;
	task = unit.task;
	conf = state.conf;
	math = ruMathFromSources(state.ru, env);
	time_limit = s2d(lookupTreeDef(task.args, "time-limit", "-1.0"));
	switch (ruArgs2LineCol(task.args)) {
		Some(lc): {
			switch (ruFindPositioned(lc, env)) {
				Some(path): {
					if (length(path) == 0) {
						env.out.onMessage("No entities are found at location " + ruLineCol2s(lc));
						Some(state);
					} else {
						switch (ruStepInTheorem(path)) {
							Some(step_path): {
								timer = ruMakeTimer();
								goal = step_path.path[length(step_path.path) - 1];
								msg = "start proving: '" + ruStepPath2s(step_path) + "'" + 
									(if (time_limit > 0.0) ", time limit: " + ruTime2s(time_limit) else "");
								if (ruVerbose(conf) > 0) {
									env.out.onMessage(
										"start proving: '" + ruStepPath2s(step_path) + "'" + 
										(if (time_limit > 0.0) ", time limit: " + ruTime2s(time_limit) else "")
									);
								}
								prover_task = RuProverTask(
									"proving: '" + ruStepPath2s(step_path) + "'",
									task.args,
									ruStepPathPremises(step_path),
									goal.step,
								);
								index = ruInitTheoremAssertionsIndex(step_path.theorem, math, env);
								prover_env = ruInitProver(prover_task, math, env, index, time_limit);
								json_tree = json2string(ruProverTree2json(prover_env.tree, prover_env.math, conf));
								//ruDebugLog("JSON TREE:\n" + json_tree, conf);
								env.out.onData(json_tree);
								Some(RuState(state with 
									prover = Some(RuProverState(
										step_path, 
										prover_env
									)),
									vars = setTree(state.vars, 
										"prove-start.time", ValDouble(ruTimePassed(timer))
									)
								));
							}
							None(): {
								env.out.onMessage("Proof step path expected, but " + strGlue(map(path, ruPositioned2s), ", ") + " is found");
								Some(state);
							}
						}
					}
				}
				None(): {
					env.out.onMessage("No entities are found at location " + ruLineCol2s(lc));
					Some(state);
				}
			}
		}
		None(): {
			env.out.onMessage("No target for proving is provided");
			Some(state);
		}
	}
}

ruRunProveTheoremStart(unit : RuUnit, th_name : string) -> Maybe<RuState> {
	env = ruUnitEnv(unit);
	state = unit.state;
	task = unit.task;
	conf = state.conf;
	math = ruMathFromSources(state.ru, env);
	time_limit = s2d(lookupTreeDef(task.args, "time-limit", "-1.0"));
	timer = ruMakeTimer();
	if (ruVerbose(conf) > 1) {
		env.out.onMessage(
			"going to start proving a theorem: '" + th_name + "'" + 
			(if (time_limit > 0.0) ", time limit: " + ruTime2s(time_limit) else "")
		);
	}
	switch (lookupTree(math.decls.assertions, s2id_(th_name))) {
		Some(th): {
			switch (th) {
				RuTheorem(info,stat,__,proof): {
					if (ruVerbose(conf) > 0) {
						env.out.onMessage(
							"start proving a theorem: '" + th_name + "'" + 
							(if (time_limit > 0.0) ", time limit: " + ruTime2s(time_limit) else "")
						);
					}
					step_ind = s2i(lookupTreeDef(task.args, "step", i2s(length(proof.steps) - 1)));
					prover_task = RuProverTask(
						"proving-" + id2s(info.id),
						task.args,
						concat(stat.hyps, take(proof.steps, step_ind)),
						proof.steps[step_ind],
					);
					index = ruInitTheoremAssertionsIndex(th, math, env);
					prover_env = ruInitProver(prover_task, math, env, index, time_limit);
					Some(RuState(state with 
						prover = Some(RuProverState(
							RuStepInTheorem(th, [RuStepInProof(th.proof, proof.steps[step_ind])]), 
							prover_env
						)),
						vars = setTree(state.vars, 
							"prove-start.time", ValDouble(ruTimePassed(timer))
						)
					));
				}
				default: {
					env.out.onError("'" + th_name + "' is not a theorem", []);
					None();
				}
			}
		}
		None(): {
			env.out.onError("theorem: '" + th_name + "' is not found", []);
			None();
		}
	}
}

ruCommProveExpand = RuComm(
	RuCommInfo(
		"prove-expand", "prover", "expand given proof variant tree nodes", "nodes", true, false, 
		[
			RuCommArg("nodes", false, "CSV list of node ids, which should be expanded", ""),
		], []
	),
	ruRunProveExpand
);

ruRunProveExpand(unit : RuUnit) -> Maybe<RuState> {
	env = ruUnitEnv(unit);
	state = unit.state;
	task = unit.task;
	conf = state.conf;
	math = ruMathFromSources(state.ru, env);
	timer = ruMakeTimer();
	nodes_arg = lookupTreeDef(task.args, "nodes", "");
	nodes = filtermap(strSplit(nodes_arg, ","), \n -> {
		node = trim(n);
		if (isDigits(node)) Some(s2i(node)) else None();
	});
	switch (state.prover) {
		Some(prover_state): {
			penv0 = prover_state.env;
			if (ruVerbose(conf) > 0) {
				env.out.onMessage("setting nodes to expand: '" + nodes_arg + "'");
			}
			curr_tactic = ^(prover_state.env.tactic);
			penv0.tactic := RuProverTactic(
				"prove-expand",
				nop1,
				\tree -> filtermap(nodes, \id -> lookupTree(tree.leafs, id)),
				nop1,
				\e -> true
			);
			penv1 = ruProve(penv0);
			penv1.tactic := curr_tactic;
			diff = ruProverTreeDiff(penv0.tree, penv1.tree);
			json_diff = json2string(ruProverNodes2json(diff, math, conf));
			ruDebugLog("JSON DIFF:\n" + json_diff, conf);
			env.out.onData(json_diff);
			Some(RuState(state with 
				prover = Some(RuProverState(prover_state with 
					env = penv1
				))
			));
		}
		None(): {
			env.out.onError("Prover is not started", []);
			None();
		}
	}
}

ruCommProveFinish = RuComm(
	RuCommInfo(
		"prove-finish", "prover", "finish proving a theorem", "", true, false, 
		[], [],
	),
	ruRunProveFinish
);

ruRunProveFinish(unit : RuUnit) -> Maybe<RuState> {
	env = ruUnitEnv(unit);
	state = unit.state;
	conf = state.conf;
	math = ruMathFromSources(state.ru, env);
	maybeMap(state.prover, \prover_state -> {
		penv = prover_state.env;
		if (ruVerbose(conf) > 0) {
			env.out.onMessage("finished proving: '" + penv.task.info + "', it was " + 
				(if (ruProvedEnvIsProved(penv)) "PROVED" else "NOT proved")
			);
		}
		RuState(state with prover = None());
	});
}

ruCommProveConfirmProof = RuComm(
	RuCommInfo(
		"prove-confirm-proof", "prover", "attach a proof to a target theorem", "index", true, false, 
		[RuCommArg("index", true, "an index of a choosen proof. -1 means automatic choice of the shortest", "-1")], []
	),
	ruRunProveConfirmProof
);

ruRunProveConfirmProof(unit : RuUnit) -> Maybe<RuState> {
	env = ruUnitEnv(unit);
	state = unit.state;
	task = unit.task;
	conf = state.conf;
	maybeMap(state.prover, \prover_state -> {
		penv = prover_state.env;
		theorem = prover_state.step.theorem;
		index = s2i(lookupTreeDef(task.args, "index", "-1"));
		confirm = \proof -> {
			if (ruVerbose(conf) > 0) {
				env.out.onMessage("proof of: '" + penv.task.info + "' is confirmed");
			}
			proved = RuTheorem(theorem with 
				proof = ruUpdateProofVars(RuProof(proof with steps = proof.steps), theorem, theorem)
			);
			math = ruMathFromSources(state.ru, env);
			new_math = ruMathReplaceDecl(math, proved, env);
			RuState(state with 
				ru = new_math.sources,
				prover = None()
			);
		}
		if (index >= length(^(penv.proofs))) {
			if (length(^(penv.proofs)) == 0) {
				env.out.onError("target '" + penv.task.info + "'' is NOT proved", []);
			} else {
				env.out.onError("proof index " + i2s(index) + " is out of bounds (must be <= " + i2s(length(^(penv.proofs))) + ")", []);
			}
			state;
		} else if (index < 0) {
			switch (ruProverEnvProof(penv)) {
				Some(proof):  confirm(proof);
				None(): {
					env.out.onError("target '" + penv.task.info + "'' is NOT proved", []);
					state;
				}
			}
		} else {
			confirm(^(penv.proofs)[index]);
		}
	});
}

ruCommProveTry = RuComm(
	RuCommInfo(
		"prove-try", "prover", "try to prove a theorem using current tactic", "theorem", true, false, 
		[RuCommArg("time-limit", true, "a default time limit for proving step", "")], []
	),
	ruRunProveTry
);

ruRunProveTry(unit : RuUnit) -> Maybe<RuState> {
	env = ruUnitEnv(unit);
	state = unit.state;
	task = unit.task;
	conf = state.conf;
	math = ruMathFromSources(state.ru, env);
	timer = ruMakeTimer();
	maybeMap(state.prover,
		\prover_state -> {
			penv0 = prover_state.env;
			time_limit = s2d(lookupTreeDef(task.args, "time-limit", "-1.0"));
			if (ruVerbose(conf) > 0) {
				env.out.onMessage(
					"trying to prove: '" + penv0.task.info + "'" + 
					(if (time_limit > 0.0) ", time limit: " + ruTime2s(time_limit) else "")
				);
			}
			penv1 = ruProve(penv0);
			RuState(state with 
				prover = Some(RuProverState(prover_state with env = penv1)),
				vars = setTree(setTree(state.vars, 
					"prove-try.time", ValDouble(ruTimePassed(timer))),
					"prove.success", ValBool(length(^(penv1.proofs)) > 0)
				)
			);
		}
	);
}

ruCommProveTacticExpandLeafs = RuComm(
	RuCommInfo(
		"prove-tactic-expand-leafs", "prover", "sets a tactic: expand all current leafs", "", false, false, 
		[], []
	),
	ruRunProveTacticExpandLeafs
);

ruRunProveTacticExpandLeafs(unit : RuUnit) -> Maybe<RuState> {
	env = ruUnitEnv(unit);
	state = unit.state;
	task = unit.task;
	conf = state.conf;
	math = ruMathFromSources(state.ru, env);
	timer = ruMakeTimer();
	maybeMap(state.prover,
		\prover_state -> {
			penv = prover_state.env;
			if (ruVerbose(conf) > 0) {
				env.out.onMessage("setting a tactic: expand all leafs in: '" + penv.task.info + "'");
			}
			penv.tactic := RuProverTactic("expand-leafs",
				nop1,
				\tree -> getTreeValues(tree.leafs),
				nop1,
				\e -> true
			);
			state;
		}
	);
}

ruCommProveTacticForbid = RuComm(
	RuCommInfo(
		"prove-tactic-forbid", "prover", "do not use these assertions is proof search", "forbidden", false, false, 
		[RuCommArg("forbidden", false, "a comma separated list of assertion names", "")], []
	),
	ruRunProveTacticForbid
);

ruRunProveTacticForbid(unit : RuUnit) -> Maybe<RuState> {
	env = ruUnitEnv(unit);
	state = unit.state;
	task = unit.task;
	conf = state.conf;
	timer = ruMakeTimer();
	maybeMap(state.prover,
		\prover_state -> {
			penv = prover_state.env;
			forbidden = buildSet(filtermap(strSplit(lookupTreeDef(task.args, "forbidden", ""), ","), \ass ->
				if (ass == "" || ass == "_") None() else Some(s2id_(ass))
			));
			if (ruVerbose(conf) > 0) {
				env.out.onMessage(
					"forbidding to use assertions: '" + strGlue(map(set2array(forbidden), id2s), ", ") + "' in proof search"
				);
			}
			penv.tactic := RuProverTactic("forbid", nop1, 
				\tree -> filter(^(penv.tactic).expand(tree), \prop -> !containsSet(forbidden, prop.id)),
				nop1,
				\e -> true
			);
			state;
		}
	);
}
