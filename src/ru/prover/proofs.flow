import ru/prover/env;
import ru/unify/prod;
import ru/ru_verify;

export {
	ruAddProof(prop : RuProverProp, hypProof : RuProverProofHyp, env : RuProverEnv) -> void;
	ruBuildPropProofs(parent : RuProverHyp, proofs : [RuProverProofProp], env : RuProverEnv) -> void;

max_cplx = ref 0;
max_cplx_subs : ref [Tree<RuVar, RuExp>] = ref [];
max_cplx_expr : ref RuExp = ref ruNoExp;
}

ruCheckTests(test1 : Tree<[RuProverProofHyp], RuProverProofProp>, test2 : Tree<[RuProverProofHyp], RuProverProofProp>) -> bool {
	ret = ref true;
	traverseInOrder(test1, \k1, p1 -> {
		switch (lookupTree(test2, k1)) {
			None(): ret := false;
			Some(p2): {
				switch (ruSubsAreSim(p1.sub, p2.sub)) {
					Some(__): { }
					None(): ret := false;
				}
			}
		}
	});
	^ret;
}

ruAddProof(prop : RuProverProp, hypProof : RuProverProofHyp, env : RuProverEnv) -> void {
	if (length(^(prop.children)) == ruArity(prop.assertion)) {
	
		prod = CartProd(map(^(prop.children), \hyp ->
			if (hyp == hypProof.node) [hypProof] else ^(hyp.proofs)
		));
		size = sizeCartProd(prod);
		new_proofs = if (size < 1024) {
			t = timestamp();
			new_proofs = ruAddProof1(prop, hypProof, env);
			if (lookupTreeDef(env.conf.opts, "reprove-stats", "") == "1") {
				stats = RuStatsRow("prod " + ruProd2s(prod), 
					pairs2tree([Pair("size", i2d(size)), Pair("time", timestamp() - t), Pair("rets", i2d(length(new_proofs)))])
				);
				ruAddStats("add_proof", stats);
				ruAddStats("add_proof_prod", stats);
			}
			new_proofs;
		} else {
			t = timestamp();
			new_proofs = ruAddProof2(prop, hypProof, env);
			if (lookupTreeDef(env.conf.opts, "reprove-stats", "") == "1") {
				stats = RuStatsRow("tree " + ruProd2s(prod), 
					pairs2tree([Pair("size", i2d(size)), Pair("time", timestamp() - t), Pair("rets", i2d(length(new_proofs)))])
				);
				ruAddStats("add_proof", stats);
				ruAddStats("add_proof_tree", stats);
			}
			new_proofs;
		}
		iter(new_proofs, \p -> 
			if (length(^(prop.proofs)) <= 128 || !env.timeLimit()) {
				refArrayPush(prop.proofs, p)
			}
		);
		/*
		if (size <= 100000) {
		k = ruLogN(size, 10);
			max_k := max(^max_k, k);
			max_size := max(^max_size, size);
		
			t1 = timestamp();
			proofs1 = ruAddProof1(prop, hypProof, env);
			time1 = timestamp() - t1;
			setConcurrentHashMap(prod_times, k, getConcurrentHashMap(prod_times, k, 0.0) + time1);

			t2 = timestamp();
			proofs2 = ruAddProof2(prop, hypProof, env);
			time2 = timestamp() - t2;
			setConcurrentHashMap(tree_times, k, getConcurrentHashMap(tree_times, k, 0.0) + time2);

			test1 = values2tree(proofs1, \p -> p.children);
			test2 = values2tree(proofs2, \p -> p.children);
			if (proofs1 != [] && (!ruCheckTests(test1, test2) || !ruCheckTests(test2, test1))) {
				conf.onMessage("proofs differ:\n");
				conf.onMessage("proofs1:");
				iteri(proofs1, \i, pr -> conf.onMessage(i2s(i) + "\n" + ruIndent(ruSubst2s(pr.sub, env.math))));
				conf.onMessage("proofs2:");
				iteri(proofs2, \i, pr -> conf.onMessage(i2s(i) + "\n" + ruIndent(ruSubst2s(pr.sub, env.math))));
				
				conf.onMessage("matrix:");
				subs = map(^(prop.children), \hyp ->
					if (hyp == hypProof.node) [RuSubData(hypProof.sub, hypProof)] else 
					map(^(hyp.proofs), \p -> RuSubData(p.sub, p))
				);
				conf.onMessage(ruSubsMatrix2s(ruMakeSubsMatrix(subs), env.math));
				debugUnifySubsProd := true;
				ruAddProof2(prop, hypProof, env);
				quit(-1);
			}
			iter(proofs1, \p -> refArrayPush(prop.proofs, p));
		}*/
	}
}

ruAddProof2(prop : RuProverProp, hypProof : RuProverProofHyp, env : RuProverEnv) -> [RuProverProofProp] {
	subs = map(^(prop.children), \hyp ->
		if (hyp == hypProof.node) [RuSubData(hypProof.sub, hypProof)] else 
		map(^(hyp.proofs), \p -> RuSubData(p.sub, p))
	);
	filtermap(ruUnifySubsProd(subs, env.admitExp, env.timeLimit, env.math, env.conf), \unif ->
		maybeBind(ruCompose(prop.sub, unif.sub), \theta ->
			if (!ruCheckDisjs(theta, prop.assertion.disjs)) None() else {
				Some(RuProverProofProp(prop, theta, unif.data));
			}
		)
	);
}

ruAddProof1(prop : RuProverProp, hypProof : RuProverProofHyp, env : RuProverEnv) -> [RuProverProofProp] {
	prod = CartProd(map(^(prop.children), \hyp ->
		if (hyp == hypProof.node) [hypProof] else ^(hyp.proofs)
	));
	if (sizeCartProd(prod) == 0) [] else
	if (sizeCartProd(prod) == 1) { 
		hypProofs = map(prod.sets, \s -> s[0]);
		subs = map(hypProofs, \p -> p.sub);
		cplx = fold(subs, 0, \acc, sub -> acc + ruSubComplexity(sub));
		t = timestamp();
		rets = eitherMap(ruUnifySubs(subs, env.math, env.conf),
			\s -> eitherMap(ruCompose(prop.sub, s), 
				\theta -> if (!ruCheckDisjs(theta, prop.assertion.disjs)) [] else [RuProverProofProp(prop, theta, hypProofs)],
				[]
			), []
		);
		if (cplx > ^max_cplx && length(rets) > 0) {
			max_cplx := cplx;
			max_cplx_subs := subs;
			max_cplx_expr := ruApplySubst(prop.parent.expr, rets[0].sub);
		}
		if (lookupTreeDef(env.conf.opts, "reprove-stats", "") == "1") {
				stats = RuStatsRow("prop " + prop.assertion.name, 
					pairs2tree([
						Pair("size", i2d(sizeCartProd(prod))), 
						Pair("time", timestamp() - t), 
						Pair("rets", i2d(length(rets))),
						Pair("cplx", i2d(cplx))
					])
				);
				ruAddStats("ruAddProof0", stats);
			}
		rets;
	} else {
		if (sizeCartProd(prod) > 100000) {
			env.conf.onMessage("ruSizeCart(prod)=" + i2s(sizeCartProd(prod)));
			[]
		} else {
			t = timestamp();
			rets = filtermapCartProd(prod, \hypProofs -> 
				maybeBind(ruUnifySubs(map(hypProofs, \hp -> hp.sub), env.math, env.conf),
					\s ->
						maybeBind(ruCompose(prop.sub, s), \theta ->
							if (!ruCheckDisjs(theta, prop.assertion.disjs)) None() else {
								Some(RuProverProofProp(prop, theta, hypProofs))
							} 
						)
				)
			);
			if (lookupTreeDef(env.conf.opts, "reprove-stats", "") == "1") {
				stats = RuStatsRow("prop " + prop.assertion.name, 
					pairs2tree([
						Pair("size", i2d(sizeCartProd(prod))), 
						Pair("time", timestamp() - t), 
						Pair("rets", i2d(length(rets)))
					])
				);
				ruAddStats("ruAddProof1", stats);
			}
			rets
		}
	}
}

ruBuildPropProofs(parent : RuProverHyp, proofs : [RuProverProofProp], env : RuProverEnv) -> void {
	n = length(^(parent.proofs));
	iter(proofs, \proof -> 
		refArrayPush(parent.proofs, 
			RuProverProofExp(
				parent,
				ruApplySubst(parent.expr, proof.sub),
				proof.sub,
				proof
			)
		)
	);
	switch (parent) {
		RuProverExp(__,__,ex,__,grand): 
			ruBuildHypProofs(grand, tailFrom(^(parent.proofs), n), env, ex);
		RuProverRoot(__,__,__):
			iter(tailFrom(^(parent.proofs), n), \root -> {
				//steps = map(ruMakeLinearProof(ruMakeProofNode(root)), \step -> RuStep(step with expr = env.unconst(step.expr)));
				//env.onProof(steps);
				env.onProof(ruMakeProofNode(root));
			});
	}
}

ruBuildHypProofs(parent : RuProverProp, proofs : [RuProverProofHyp], env : RuProverEnv, ex : RuExp) -> void {
	n = length(^(parent.proofs));
	t = timestamp();
	iter(proofs, \proof -> ruAddProof(parent, proof, env));
	if (lookupTreeDef(env.conf.opts, "reprove-stats", "") == "1") {
		stats = RuStatsRow("hyp " + ru2s(ex, env.math.rules), 
			pairs2tree([Pair("size", i2d(length(proofs))), Pair("time", timestamp() - t), Pair("rets", i2d(length(tailFrom(^(parent.proofs), n))))])
		);
		ruAddStats("ruBuildHypProofs", stats);
	}
	ruBuildPropProofs(parent.parent, tailFrom(^(parent.proofs), n), env);
}
