import ru/prover/expand;

export {
	ruInitProver(
		theorem : RuTheorem, 
		math : RuMath, 
		conf : RuConf, 
		asses : Maybe<RuIndex<RuAssertion>>, 
		ms_time_limit : double,
		proofs : ref [RuProof]
	) -> RuProverEnv;

	ruProve(env : RuProverEnv) -> RuProverEnv;
}

ruInitProver(
	th : RuTheorem, 
	math : RuMath, 
	conf : RuConf, 
	asses : Maybe<RuIndex<RuAssertion>>, 
	ms_time_limit : double,
	proofs : ref [RuProof]
) -> RuProverEnv {
	env = ruInitProverEnv(th, math, conf, asses, ms_time_limit, proofs);
	ruExpandHyp(env.tree.root, env);
}

ruProve(env : RuProverEnv) -> RuProverEnv {
	t0 = ruMakeTimer();
	choice = if (ruTimeIsOver(env.timer)) None() else env.tactic(env.tree);
	if (lookupTreeDef(env.conf.opts, "reprove-stats", "") == "1") {
		ruAddStats("choice", RuStatsRow("choice ", 
			pairs2tree([
				Pair("size", i2d(sizeTree(env.tree.leafs))), 
				Pair("time", ruTimePassed(t0)), 
				Pair("count", 1.0)
			])
		));
	}
	switch (choice) {
		Some(prop): {
			t1 = ruMakeTimer();
			env1 = ruExpandProp(prop, env);
			if (lookupTreeDef(env.conf.opts, "reprove-stats", "") == "1") {
				ruAddStats("ruExpandProp", RuStatsRow("expanding " + id2s(prop.assertion.descr.id), 
					pairs2tree([
						Pair("time", ruTimePassed(t1)), 
						Pair("count", 1.0)
					])
				));
			}
			t2 = ruMakeTimer();
			ruBuildPropProofs(prop.parent, ^(prop.proofs), env1);
			if (lookupTreeDef(env.conf.opts, "reprove-stats", "") == "1") {
				ruAddStats("ruBuildPropProofs", RuStatsRow("building proofs for " + id2s(prop.assertion.descr.id), 
					pairs2tree([
						Pair("time", ruTimePassed(t2)), 
						Pair("count", 1.0)
					])
				));
			}
			ruProve(env1);
		}
		None(): env;
	}
}
