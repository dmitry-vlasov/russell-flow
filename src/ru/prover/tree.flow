import ru/subst;
import ru/proof_tree;

export {
	ruMakeProofTree(proof : RuProverProof) -> RuProofTree;

	RuProverProof ::= RuProverProofHyp, RuProverProofProp;
	RuProverProofHyp ::= RuProverProofTop, RuProverProofStmt;
		RuProverProofTop(
			node : RuProverHyp,
			stmt : RuStmt,
			premise : RuPremise, 
			sub  : RuSubst,
		);
		RuProverProofStmt(
			node  : RuProverHyp,
			stmt  : RuStmt, 
			sub   : RuSubst,
			child : RuProverProofProp,
		);
		RuProverProofProp(
			node      : RuProverProp,
			sub       : RuSubst,
			children  : [RuProverProofHyp],
		);

	RuProverNode ::= RuProverHyp, RuProverProp;
	RuProverHyp ::= RuProverExp, RuProverRoot;
		RuProverProp(
			id        : int,
			assertion : RuAssertion,
			proofs    : Vector<RuProverProofProp>,
			sub       : RuSubst,
			outer     : RuSubst,
			children  : Vector<RuProverExp>,
			parent    : RuProverHyp,
			expanded  : ref bool
		);
		RuProverExp(
			ind      : int,
			proofs   : Vector<RuProverProofHyp>,
			stmt     : RuStmt,
			children : Vector<RuProverProp>,
			parent   : RuProverProp
		);
		RuProverRoot(
			proofs   : Vector<RuProverProofHyp>,
			stmt     : RuStmt,
			children : Vector<RuProverProp>
		);

	RuProverNodeSnapshot(
		proofs   : int,
		expanded : bool
	);
	RuProverTreeSnapshot(
		tree : Tree<int, RuProverNodeSnapshot>,
		root : RuProverNodeSnapshot
	);
	RuProverTreeDiff(
		nodes : [RuProverProp],
		root : Maybe<RuProverRoot>
	);

	RuProverTree(
		leafs : Tree<int, RuProverProp>,
		nodes : Tree<int, RuProverProp>,
		root  : RuProverRoot,
		proofs : ref [RuProof],
	);

	ruProverNode2s(node : RuProverNode, math : RuMath) -> string;
	ruProverTree2s(node : RuProverNode, math : RuMath, small : bool) -> string;
	ruProverNode2json(node : RuProverNode, math : RuMath, conf : RuConf) -> Json;
	ruProverTreeDiff2json(diff : RuProverTreeDiff, math : RuMath, conf : RuConf) -> Json;
	ruProverTree2json(node : RuProverTree, math : RuMath, conf : RuConf) -> Json;
	ruSizeProverTree(node : RuProverNode) -> int;

	ruProverNode2Snapshot(node : RuProverNode) -> RuProverNodeSnapshot;
	ruProverTree2Snapshot(tree : RuProverTree) -> RuProverTreeSnapshot;
	ruProverTreeDiff(snap : RuProverTreeSnapshot, tree : RuProverTree) -> RuProverTreeDiff;

	ruNoProverHyp = RuProverRoot(makeVector(0), ruNoStmt, makeVector(0));
	ruNoProverProp = RuProverProp(-1, ruNoAxiom, makeVector(0), ruEmptySubst, ruEmptySubst, makeVector(0), ruNoProverHyp, ref false);

	ruProverTreeDepth(tree : RuProverTree) -> int;
	ruProverTreeSize(tree : RuProverTree) -> int;

	ruShowProverTreePath(tree : RuProverTree, path : [string], math : RuMath) -> string;


	ruTreeIsProved(tree : RuProverTree) -> bool;
	ruProverEnvProof(tree : RuProverTree) -> Maybe<RuProof>;
}

ruSizeProverTree(node : RuProverNode) -> int {
	1 + switch (node) {
		RuProverProp(__,__,__,__,__, children,__,__): foldVector(children, 0, \ac, ch -> ac + ruSizeProverTree(ch));
		RuProverExp(__,__,__, children,__): foldVector(children, 0, \ac, ch -> ac + ruSizeProverTree(ch));
		RuProverRoot(__,__, children): foldVector(children, 0, \ac, ch -> ac + ruSizeProverTree(ch));
	}
}

ruApplySubst2ProofNode(s : RuSubst, n : RuProofTree) {
	switch (n) {
		RuProofStep(ass, children, stmt):
			RuProofStep(
				ass, 
				map(children, \ch -> ruApplySubst2ProofNode(s, ch)), 
				RuStmt(stmt with
					expr = ruApplySubst(stmt.expr, s)
				)
			);
		RuProofHyp(h, stmt): 
			RuProofHyp(h, RuStmt(stmt with expr = ruApplySubst(stmt.expr, s)));
	}
}

ruMakeProofTree(proof : RuProverProof) -> RuProofTree {
	switch (proof) {
		RuProverProofTop(node, stmt, hyp, sub): RuProofHyp(hyp.ind, stmt);
		RuProverProofStmt(node, stmt, sub, child): ruMakeProofTree(child);
		RuProverProofProp(node, sub, children):
			ruApplySubst2ProofNode(sub, 
				RuProofStep(
					node.assertion.info.id, 
					map(children, ruMakeProofTree),
					node.parent.stmt,
				)
			);
	}
}

ruProverNode2s(node : RuProverNode, math : RuMath) -> string {
	switch (node) {
		RuProverProp(id, ass, proofs, sub, outer, children, parent, expanded):
			"Prop<" + 
				"id=" + i2s(id) + ", " +
				"ass=" + id2s(ass.info.id) + ", " + 
				"proofs_num=" + i2s(sizeVector(proofs)) + ", " + 
				"children_num=" + i2s(sizeVector(children)) + ", " + 
				"expanded=" + b2s(^expanded) +
			">" + 
			(if (sizeVector(proofs) == 0) "" else
				"\n" + strIndent(strGlue(map(vector2array(proofs), \p -> ruProofTree2s(ruMakeProofTree(p), math)), "\n"))
			);
		RuProverExp(ind, proofs, expr, children, parent):
			"Hyp<" + "ind=" + i2s(ind) + ", " +"expr=" + ru2s(expr, math) + ", proofs_num=" + i2s(sizeVector(proofs)) + ", children num:" + i2s(sizeVector(children)) + ">" + 
			(if (sizeVector(proofs) == 0) "" else
				"\n" + strIndent(strGlue(map(vector2array(proofs), \p -> ruProofTree2s(ruMakeProofTree(p), math)), "\n"))
			);
		RuProverRoot(proofs, expr, children):
			"Root<" + "expr=" + ru2s(expr, math) + ", proofs_num=" + i2s(sizeVector(proofs)) + ", children num:" + i2s(sizeVector(children)) + ">" + 
			(if (sizeVector(proofs) == 0) "" else
				"\n" + strIndent(strGlue(map(vector2array(proofs), \p -> ruProofTree2s(ruMakeProofTree(p), math)), "\n"))
			);
	}
}

ruProverTree2s(node : RuProverNode, math : RuMath, small : bool) -> string {
	get_proofs = \n -> switch (n) {
		RuProverProp(id, ass, proofs, sub, outer, children, parent, expanded): proofs;
		RuProverExp(ind, proofs, expr, children, parent): proofs;
		RuProverRoot(proofs, expr, children): proofs;
	}
	ruProverNode2s(node, math) + 
	(if (sizeVector(node.children) == 0) "" else {
		all_children : [RuProverNode] = switch (node) {
			RuProverProp(id, ass, proofs, sub, outer, children, parent, expanded): vector2array(children);
			RuProverExp(ind, proofs, expr, children, parent): vector2array(children);
			RuProverRoot(proofs, expr, children): vector2array(children);
		}
		//all_children = vector2array(node.children);
		cildren_to_output = 
			if (!small || sizeVector(get_proofs(node)) == 0) all_children else 
			filter(all_children, \ch -> sizeVector(get_proofs(ch)) > 0);
		"\n" + strIndent(
			strGlue(
				map(cildren_to_output, \ch -> ruProverTree2s(ch, math, small)), 
				//map(vector2array(node.children), \ch -> ruProverTree2s(ch, math)), 
				"\n"
			)
		)
	});
}

ruProverNode2json(node : RuProverNode, math : RuMath, conf : RuConf) -> Json {
	maxlen = s2i(ruOptsString(conf.opts, "maxlen-expr", "32"));
	switch (node) {
		RuProverProp(ind, ass, proofs, sub, outer, children, parent, expanded): {
			expr = ruApplySubst(ruExp(parent.stmt.expr), sub);
			tooltip = ru2s(ass, math);
			JsonObject([
				Pair("id", JsonDouble(i2d(ind))),
				Pair("assertion", JsonString(id2s(ass.info.id))),
				Pair("proofs", JsonArray(map(vector2array(proofs), \proof ->
					JsonString(ru2sWrapExp(
							ruApplySubst(expr, proof.sub), 
							math, \s -> ruIndentBrackets(s, maxlen)
					))
				))),
				Pair("children", JsonArray(map(vector2array(children), \ch -> ruProverNode2json(ch, math, conf)))),
				Pair("grand", JsonDouble(i2d(
					switch (parent) {
						RuProverExp(__,__,__,__,grand): grand.id;
						RuProverRoot(__,__,__): -1;
					}
				))),
				Pair("expanded", JsonBool(^expanded)),
				Pair("tooltip", JsonString(tooltip)),
			]);
		}
		RuProverExp(ind, proofs, expr, children, parent): {
			expr_plain = ru2sWrapExp(expr, math, idfn);
			expr_multyline = ru2sWrapExp(expr, math, \s -> ruIndentBrackets(s, maxlen));
			JsonObject([
				Pair("ind", JsonDouble(i2d(ind))),
				Pair("expr_plain", JsonString(expr_plain)),
				Pair("expr_multyline", JsonString(expr_multyline)),
				Pair("proofs", JsonArray(map(vector2array(proofs), \proof -> 
					JsonString(ru2sWrapExp(
							proof.stmt, 
							math, \s -> ruIndentBrackets(s, maxlen)
					))
				))),
				Pair("children", JsonArray(map(vector2array(children), \ch -> JsonDouble(i2d(ch.id))))),
				Pair("parent", JsonDouble(i2d(parent.id))),
				Pair("tooltip", JsonString(expr_multyline)),
			]);
		}
		RuProverRoot(proofs, expr, children): {
			expr_plain = ru2sWrapExp(expr, math, idfn);
			expr_multyline = ru2sWrapExp(expr, math, \s -> ruIndentBrackets(s, maxlen));
			JsonObject([
				Pair("expr_plain", JsonString(expr_plain)),
				Pair("expr_multyline", JsonString(expr_multyline)),
				Pair("proofs", JsonArray(map(vector2array(proofs), \root -> {
					tree = ruMakeProofTree(root);
					linear = ruMakeLinearProof(tree);
					//steps = map(linear, \step -> RuStep(step with expr = prover_env.fns.unconst(step.expr)));
					proof = RuProof(ruEmptyVarsDisjs, linear, -1);
					JsonString(ru2sWrapExp(
							proof, 
							math, \s -> ruIndentBrackets(s, maxlen)
					));
				}))),
				Pair("children", JsonArray(map(vector2array(children), \ch -> JsonDouble(i2d(ch.id))))),
				Pair("tooltip", JsonString(expr_multyline)),
			]);
		}
	}
}

ruProverTree2json(tree : RuProverTree, math : RuMath, conf : RuConf) -> Json {
	JsonObject([
		Pair("nodes", JsonArray(map(getTreeValues(tree.nodes), \leaf -> ruProverNode2json(leaf, math, conf)))),
		Pair("root", ruProverNode2json(tree.root, math, conf)),
	]);
}

ruProverTreeDiff2json(diff : RuProverTreeDiff, math : RuMath, conf : RuConf) -> Json {
	nodes_json = JsonArray(map(diff.nodes, \leaf -> ruProverNode2json(leaf, math, conf)));
	switch (diff.root) {
		Some(root): {
			JsonObject([
				Pair("nodes", nodes_json),
				Pair("root", ruProverNode2json(root, math, conf))
			]);
		}
		None(): {
			JsonObject([
				Pair("nodes", nodes_json),
			]);
		}
	}
}

ruProverNode2Snapshot(node : RuProverNode) -> RuProverNodeSnapshot {
	switch (node) {
		RuProverProp(__,__,proofs,__,__,__,__,expanded): RuProverNodeSnapshot(sizeVector(proofs), ^expanded);
		RuProverExp(__,proofs,__,__,__): RuProverNodeSnapshot(sizeVector(proofs), true);
		RuProverRoot(proofs,__,__): RuProverNodeSnapshot(sizeVector(proofs), true);
	}
}

ruProverTree2Snapshot(tree : RuProverTree) -> RuProverTreeSnapshot {
	RuProverTreeSnapshot(
		mapTree(tree.nodes, ruProverNode2Snapshot),
		ruProverNode2Snapshot(tree.root)
	);
}

ruProverTreeDiff(snap : RuProverTreeSnapshot, tree : RuProverTree) -> RuProverTreeDiff {
	RuProverTreeDiff(
		filter(getTreeValues(tree.nodes), \n ->
			switch (lookupTree(snap.tree, n.id)) {
				Some(s): s != ruProverNode2Snapshot(n);
				None(): true;
			}
		),
		if (snap.root != ruProverNode2Snapshot(tree.root)) Some(tree.root) else None()
	);
}

ruProverTreeDepth(tree : RuProverTree) -> int {
	ruDoProverTreeDepth(tree.root, 0);
}

ruDoProverTreeDepth(node : RuProverNode, depth : int) -> int {
	foldVector(node.children, depth, \acc, n -> {
		d = ruDoProverTreeDepth(n, depth + 1);
		if (d > depth) d else acc;
	});
}

ruProverTreeSize(tree : RuProverTree) -> int {
	sizeTree(tree.nodes);
}

ruShowProverTreePath(tree : RuProverTree, path : [string], math : RuMath) -> string {
	ruDoShowProverTreePath(tree.root, path, math, "");
}

ruDoShowProverTreePath(node : RuProverNode, path : [string], math : RuMath, acc0 : string) -> string {
	if (length(path) == 0) acc0 else {
		acc = if (acc0 == "") path[0] + ": " else acc0 + "\n" + path[0] + ": ";
		switch (node) {
			RuProverExp(ind,__,__,children,__): {
				switch (find(vector2array(children), \ch -> id2s(ch.assertion.info.id) == path[0])) {
					Some(prop): {
						ruDoShowProverTreePath(prop, tail(path), math, acc + ruProverNode2s(prop, math));
					}
					None(): acc + "<NOT PRESSENT: " + path[0] + ">";
				}
			}
			RuProverProp(ind,__,__,__,__,children,__,__): {
				switch (find(vector2array(children), \ch -> ch.ind == s2i(path[0]))) {
					Some(hyp): {
						ruDoShowProverTreePath(hyp, tail(path), math, acc + ruProverNode2s(hyp, math));
					}
					None(): acc + "<NOT PRESSENT: " + path[0] + ">";
				}
			}
			RuProverRoot(__,__,children): {
				switch (find(vector2array(children), \ch -> id2s(ch.assertion.info.id) == path[0])) {
					Some(prop): {
						ruDoShowProverTreePath(prop, tail(path), math, acc + ruProverNode2s(prop, math));
					}
					None(): acc + "<NOT PRESSENT: " + path[0] + ">";
				}
			}
		}
	}
}

ruTreeIsProved(tree : RuProverTree) -> bool {
	length(^(tree.proofs)) > 0;
}

ruProverEnvProof(tree : RuProverTree) -> Maybe<RuProof> {
	proofs = ^(tree.proofs);
	if (length(proofs) == 0) None() else {
		Some(fold(tail(proofs), proofs[0], \acc, proof -> 
			if (length(proof.steps) < length(acc.steps)) proof else acc
		));
	}
}
