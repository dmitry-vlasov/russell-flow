import ru/prover/util;
import ru/unify/indexes;
import ru/verify/verify;

export {

	RuProverEnv(
		descr : string,
		timeLimit : () -> bool,
		admitExp : (RuExp) -> bool,
		tree : RuProverTree,
		unifyAssertion : (RuProverHyp) -> [RuUnified<RuAssertion>],
		unifyPremise   : (RuProverHyp) -> [RuUnified<RuHyp>],
		tactic : (RuProverTree) -> Maybe<RuProverProp>,
		math : RuMath,
		conf : RuConf,
		fresher : (RuVar) -> RuVar,
		unconst : (RuExp) -> RuExp,
		onProof : (RuProofNode) -> void,
		onProp : (RuProverProp) -> void,
		idxer : () -> int
	);
	ruInitProverEnv(
		theorem : RuTheorem, 
		math : RuMath, 
		conf : RuConf, 
		asses : Maybe<RuIndex<RuAssertion>>, 
		ms_time_limit : double,
		proofs : ref [RuProof]
	) -> RuProverEnv;
}

ruEstimateMaxExp(index : RuIndex<RuAssertion>) -> int {
	len_num_max = ruFoldIndex(index, Triple(0.0, 0, 0), \acc, ass ->
		eitherMap(ruAssertion2Theorem(ass), \th ->
			fold(th.proof.steps, acc, \ac, step -> {
				len = length(step.expr.nodes);
				Triple(ac.first + cast(len : int -> double), ac.second + 1, max(acc.third, len))
			}),
			acc
		)
	);
	if (len_num_max.second < 4096) 256 else {
		avg_len = len_num_max.first / cast(len_num_max.second : int -> double);
		sigma_sum = ruFoldIndex(index, 0.0, \acc, ass ->
			eitherMap(ruAssertion2Theorem(ass), \th ->
				fold(th.proof.steps, acc, \ac, step -> {
					delta = cast(length(step.expr.nodes) : int -> double) - avg_len;
					ac + delta * delta;
				}),
				acc
			)
		);
		sigma = sqrt(sigma_sum / cast(len_num_max.second : int -> double));
		cast(avg_len + 3.0 * sigma : double -> int);
	}
	len_num_max.third + 64;
}

ruInitProverEnv(
	th : RuTheorem, 
	math0 : RuMath, 
	conf : RuConf, 
	asses : Maybe<RuIndex<RuAssertion>>, 
	ms_time_limit : double,
	proofs : ref [RuProof]
) -> RuProverEnv {
	cth = ruConstTheorem(th, math0);
	math = cth.env.math;
	assertions = eitherFn(asses, idfn, \ -> ruInitTheoremAssertionsIndex(th, math, conf));	
	premises = ruInitPremisesIndex(cth.const);
	vars = ref makeTree();
	root = RuProverRoot(ref [], cth.const.stat.prop, ref []);
	id = ref 0;
	max_expr_len = ruEstimateMaxExp(assertions);
	RuProverEnv(id2s(th.descr.id),
		{ 
			timer = ruMakeLimitedTimer(ms_time_limit); 
			\-> {
				if (!ruTimeIsOver(timer)) false else {
					conf.onMessage("time limit: " + ruTime2s(ms_time_limit) + " for '" + id2s(th.descr.id) + "' has expired, " + ruTimePassed2s(timer) + " passed");
					true;
				} 
			}
		},
		\expr -> length(expr.nodes) < max_expr_len,
		RuProverTree(makeTree(), root),
		\hyp -> ruUnifyIndex(hyp.expr, assertions, makeTree(), math, conf, max(ms_time_limit / 100.0, 10.0)),
		\hyp -> ruUnifyIndex(hyp.expr, premises, makeTree(), math, conf, max(ms_time_limit/ 100.0, 10.0)),
		\tree -> {
			switch (tree.leafs) {
				TreeNode(__,prop,__,__,__): Some(prop);
				TreeEmpty(): None();
			}
		},
		math, conf,
		\v -> {
			vars := setTree(^vars, v, lookupTreeDef(^vars, v, 0) + 1);
			v_name = id2s(v.id) + "_" + i2s(lookupTreeDef(^vars, v, 0));
			RuVar(s2id(v_name), v.type);
		},
		cth.env.unconst,
		\proof_root -> {
			steps = map(ruMakeLinearProof(proof_root), \step -> RuStep(step with expr = cth.env.unconst(step.expr)));
			switch (ruUpdateTheoremWithSteps(th, steps, math, conf)) {
				Some(theorem): refArrayPush(proofs, theorem.proof);
				None(): {}
			}
		},
		nop1, 
		\-> { ret = ^id; id := ^id + 1; ret; }
	);
}
