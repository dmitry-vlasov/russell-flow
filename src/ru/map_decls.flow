import comm;
import base/util/progress;
import ru/util/target;

export {
	// A struct describing a general function, which operates on decls.
	// pred - the predicate, which selects decls to process.
	// measure - the function, which return the relative 'size' of the decl. Used to balance time limits.
	// message - the function, which is used to indicate the progress of applying of a function.
	RuMapFnInfo(
		name : string, 
		pred : (RuDecl) -> bool, 
		measure : (RuDecl) -> double, 
		message : () -> string
	);

	// Mapping function: transforms (possibly) a decl. If None is returned, old one is kept.
	RuMapFn(
		info : RuMapFnInfo,
		func : (RuDecl, double) -> Maybe<RuDecl>
	);

	// Map 'f.func' to the decls from 'target' task option. 
	// Selects decls to process with 'fn.info.pred' parameter.
	// Balances the time limit between all admitted decls
	// proportionally to the 'fn.info.measure'.
	ruMapFnComm(unit : RuUnit, f : RuMapFn) -> RuState;
}

ruDecls2Measures(decls : [RuDecl], f : RuMapFn) -> [double] {
	map(decls, \d -> f.info.measure(d));
}

ruCalcTimeLimit(decls : [RuDecl], f : RuMapFn, task : ScTask, conf : RuConf) -> double {
	opt = switch (lookupTree(task.args, "time-limit")) {
		Some(limit): limit;
		None(): lookupTreeDef(conf.opts, "time-limit", "300");
	}
	if (opt == "auto") {
		// By default we set 3 ms per one measure unit
		measures = ruDecls2Measures(decls, f);
		ruSumDoublesFromArray(measures) * 3.0;
	} else if (opt == "no") doubleMax else {
		i2d(s2i(opt) * 1000);
	}
}

RuMapFnStats(
	progress    : RuProgress,
	not_mapped  : ref int,
	was_mapped  : ref int,
	max_time    : ref double,
	max_time_id : ref int
);

ruMakeMapFnStats(time_limit : double, total : int, delta) -> RuMapFnStats {
	RuMapFnStats(
		ruMakeProgress(time_limit, total, delta),
		ref 0, ref 0, ref 0.0, ref -1
	);
}

ruWrapFnWithProgress(f : RuMapFn, stats : RuMapFnStats, env : RuEnv) -> (RuDecl, double) -> Maybe<RuDecl> {
	\decl, time_limit -> {
		if (ruVerbose(env.conf) >= 3) {
			env.out.onMessage("\t\tTime limit on decl '" + id2s(decl.info.id) + "': " + ruTime2s(time_limit));
		}
		timer = ruMakeTimer();
		ret = f.func(decl, time_limit);
		atomicRefIntAddition(stats.progress.counter, 1);
		if (isSome(ret)) {
			atomicRefIntAddition(stats.was_mapped, 1);
		} else {
			atomicRefIntAddition(stats.not_mapped, 1);
		} 
		time = ruTimePassed(timer);
		if (time > ^(stats.max_time)) {
			stats.max_time := time;
			stats.max_time_id := decl.info.id;
		}
		ret;
	}
}

ruMapDeclsSequentially(decls : [RuDecl], f : RuMapFn, time_limit : double, timer : RuTimer, math : RuMath, env : RuEnv) -> RuMath {
	measures = ruDecls2Measures(decls, f);
	sum_measure = ruSumDoublesFromArray(measures);
	foldi(decls, math, \i, acc, d ->
		eitherMap(
			f.func(d, time_limit * measures[i] / sum_measure),
			\mapped_d -> ruMathReplaceDecl(acc, mapped_d, env),
			acc
		)
	);
}

ruMapDeclsConcurrently(decls : [RuDecl], f : RuMapFn, time_limit : double, timer : RuTimer, math : RuMath, env : RuEnv) -> RuMath {
	split_by = lookupTreeDef(env.conf.opts, "decls-split-num", "");
	if (split_by == "") {
		ruMapDeclsConcurrentlyAll(decls, f, time_limit, timer, math, env);
	} else {
		chunks = splitByNumber(decls, s2i(split_by));
		ruMapDeclsConcurrentlyByChunks(chunks, f, time_limit, timer, math, env);
	}
}

ruMapDeclsConcurrentlyAll(decls : [RuDecl], f : RuMapFn, time_limit : double, timer : RuTimer, math : RuMath, env : RuEnv) -> RuMath {
	measures = ruDecls2Measures(decls, f);
	sum_measure = ruSumDoublesFromArray(measures);
	mapped = filtermap(ruConcurrent(mapi(decls, \i, d -> \ ->
		f.func(d, time_limit * i2d(availableProcessors()) * measures[i] / sum_measure)
	)), idfn);
	fold(mapped, math, \acc, decl -> ruMathReplaceDecl(acc, decl, env));
}

ruMapDeclsConcurrentlyByChunks(chunks : [[RuDecl]], f : RuMapFn, time_limit : double, timer : RuTimer, math : RuMath, env : RuEnv) -> RuMath {
	chunk_measures = map(chunks, \chunk -> ruDecls2Measures(chunk, f));
	sum_chunk_measures = map(chunk_measures, ruSumDoublesFromArray);
	total_measure = ruSumDoublesFromArray(sum_chunk_measures);
	planned_times = map(sum_chunk_measures, \m -> 
		time_limit * m / total_measure
	);
	planned_passed = mapi(sum_chunk_measures, \i,__ -> 
		ruSumDoublesFromArray(map(enumFromTo(0, i - 1), \j -> planned_times[j]))
	);
	planned_left = map(planned_passed, \passed -> time_limit - passed);
	foldi(chunks, math, \n, acc, chunk -> {
		left_time = ruTimeLeft(timer);
		if (left_time <= 0.0) acc else {
			time_planned_on_chunk = time_limit * sum_chunk_measures[n] / total_measure;
			time_left_on_chunk = time_planned_on_chunk * left_time / planned_left[n];
			scaled_time_limit_on_chunk = time_left_on_chunk * i2d(availableProcessors());
			mapped = ruConcurrent(mapi(chunk, \i, decl -> 
				\-> f.func(decl, scaled_time_limit_on_chunk * chunk_measures[n][i] / sum_chunk_measures[n])
			));
			fold(mapped, acc, \ac, d -> 
				eitherMap(d,
					\mapped_d -> ruMathReplaceDecl(ac, mapped_d, env),
					ac
				)
			)
		}
	});
}

ruMapFnComm(unit : RuUnit, f : RuMapFn) -> RuState {
	task = unit.task;
	state = unit.state;
	out = unit.out;
	file = lookupTreeDef(task.args, "file", "");
	conf = state.conf;
	env = RuEnv(conf, out);
	math = ruMathFromSources(state.ru, env);
	if (ruVerbose(conf) >= 3) {
		out.onMessage("\t\tMap '" + f.info.name + "' task: " + ruTask2s(task));
	}
	target = ruDetermineTarget(task, math, env);
	decls = filter(ruTargetDecls(target, math, env), f.info.pred);
	time_limit = ruCalcTimeLimit(decls, f, task, conf);
	stats = ruMakeMapFnStats(time_limit, length(decls), ruProgressUpdateMs(task, conf));
	prog_f = RuMapFn(f with func = ruWrapFnWithProgress(f, stats, env));
	ruMapDeclsStartMessage(decls, time_limit, env);
	ruTrackProgress(stats.progress, out, f.info.message);
		mapped_math = if (!ruTargetIsConcurrent(target)) {
			ruMapDeclsSequentially(decls, prog_f, time_limit, stats.progress.timer, math, env);
		} else {
			ruMapDeclsConcurrently(decls, prog_f, time_limit, stats.progress.timer, math, env);
		}
	stats.progress.isOn := false;
	ruMapDeclsEndMessage(stats, f.info.name, ruTarget2s(target), decls, env);
	ruMapDeclsResult(state, mapped_math.sources, f.info.name, stats);
}

ruMapDeclsResult(state : RuState, mapped : Tree<int, RuSource>, f_name : string, stats : RuMapFnStats) -> RuState {
	RuState(state with 
		ru = mapped,
		vars = mergeTree(state.vars, pairs2tree([
			Pair(f_name + ".counter", ValInt(^(stats.progress.counter))),
			Pair(f_name + ".total", ValInt(stats.progress.total)),
			Pair(f_name + ".was_mapped", ValInt(^(stats.was_mapped))),
			Pair(f_name + ".not_mapped", ValInt(^(stats.not_mapped))),
			Pair(f_name + ".max_time", ValDouble(^(stats.max_time))),
			Pair(f_name + ".max_time_id", ValString(id2s(^(stats.max_time_id)))),
			Pair(f_name + ".time", ValDouble(ruTimePassed(stats.progress.timer)))
		]))
	);
}

ruMapDeclsStartMessage(decls : [RuDecl], time_limit : double, env : RuEnv) -> void {
	conf = env.conf;
	out = env.out;
	if (ruVerbose(conf) > 0) {
		out.onMessage(
			"\tgoing to process " + i2s(length(decls)) + "\n" +
			"\ttime limit: " + ruTime2s(time_limit) + " (" + d2st(time_limit, 2) + "s)" +
			(if (ruVerbose(conf) >= 2) "\n\ttime limit per unit: " + ruTime2s(time_limit / i2d(length(decls))) else "")
		);
		if (ruVerbose(conf) > 2) {
			out.onMessage("\tdecls:\n" + 
				strGlue(map(decls, \d -> "\t" + id2s(d.info.id)), "\n") + "\n\n"
			);
		}
	}
}

ruMapDeclsEndMessage(stats : RuMapFnStats, f_name : string, target : string, decls : [RuDecl], env : RuEnv) -> void {
	conf = env.conf;
	out = env.out;
	if (ruVerbose(conf) > 0 && length(decls) > 0) {
		msg = if (length(decls) == 1) { 
			"ru mapped '" + f_name + "' to '" + target + "' in " + ruTime2s(ruTimePassed(stats.progress.timer));
		} else {
			"ru mapped '" + f_name + "' to '" + target + "'" + 
				"\n\ttotal time: " + ruTime2s(ruTimePassed(stats.progress.timer)) +
				"\n\tavg time:   " + ruTime2s(ruTimePassed(stats.progress.timer) / i2d(length(decls))) +
				"\n\tmax time:   " + ruTime2s(^(stats.max_time)) +
				"\n\tmax time id:" + id2s(^(stats.max_time_id)) +
				"\n\twas mapped: " + i2s(^(stats.was_mapped)) +
				"\n\tnot mapped: " + i2s(^(stats.not_mapped)) + 
				"\n\t%processed: " + d2st(100.0 * i2d(^(stats.was_mapped) + ^(stats.not_mapped)) / i2d(length(decls)), 2);
		}
		out.onMessage(msg);
	}
}
