import ru/subst;
import base/util/math;
import base/stats;

export {
	ruUnifyExps(es : [RuExp], sub : RuSubst, math : RuMath, env : RuEnv) -> Maybe<RuSubst>;
	ruUnifySubs(subs : [RuSubst], math : RuMath, env : RuEnv) -> Maybe<RuSubst>;
}

ruUnifyExps(es : [RuExp], sub : RuSubst, math : RuMath, env : RuEnv) -> Maybe<RuSubst> {
	if (length(es) <= 1) Some(sub) else {
		ruDoUnifyExps(es, RuUnifyAcc(sub, map(es, \__ -> 0), math, env))
	}
}

RuUnifyAcc(
	sub : RuSubst,
	inds : [int],
	math : RuMath,
	env : RuEnv
);

RuUnifyStepAcc(
	shift : [int],
	vars  : [RuVar],
	exprs : [RuExp],
	rule_id : int,
	type_id : int,
	ok    : bool,
	all_rules : bool
);

ruUnifyStepVar(step : RuUnifyStepAcc, math : RuMath, env : RuEnv) -> Maybe<RuVar> {
	maybeBind(
		foldi(step.vars, Some(step.vars[0]), \i, acc, v -> {
			if (i == 0) acc else 
			maybeBind(acc, \t -> 
				if (v.type == t.type || isSome(ruSuperRule(v.type, t.type, math.decls.lang, env))) Some(v) else
				if (v.type == t.type || isSome(ruSuperRule(t.type, v.type, math.decls.lang, env))) Some(t) else None()
			)
		}),
		\v -> if (step.type_id == -1 || v.type == step.type_id || isSome(ruSuperRule(v.type, step.type_id, math.decls.lang, env))) Some(v) else None()
	);
}

ruDoUnifyStep(es : [RuExp], acc : RuUnifyAcc) -> RuUnifyStepAcc {
	size = length(es);
	shift = makeVector(size);
	vars = makeVector(size);
	exprs = makeVector(size);
	rule_id = ref -1;
	type_id = ref -1;
	ok = ref true;
	all_rules = ref true;
	iteri(es, \i, e -> if (^ok) {
		n = e.nodes[acc.inds[i]];
		switch (n) {
			RuVar(__,__): {
				switch (ruLookupSubst(acc.sub, n)) {
					None(): {
						pushVector(shift, 1);
						pushVector(vars, n);
						all_rules := false;
					}
					Some(ex): {
						m = ex.nodes[0];
						switch (m) {
							RuRuleNode(rule, type, __,len): {
								pushVector(shift, 1);
								pushVector(exprs, ex);
								ok := (^rule_id == -1 || ^rule_id == rule);
								rule_id := rule;
								type_id := type;
								all_rules := false;
							}
							RuVar(__,__): {
								pushVector(shift, 1);
								pushVector(vars, m);
								all_rules := false;
							}
						}
					}
				}
			}
			RuRuleNode(rule, type,__, len): {
				pushVector(shift, len + 1);
				pushVector(exprs, RuExp(subrange(e.nodes, acc.inds[i], len + 1), e.pos));
				ok := (^rule_id == -1 || ^rule_id == rule);
				rule_id := rule;
				type_id := type;
			}
		}
	});
	if (^ok) {
		RuUnifyStepAcc(
			vector2array(shift),
			vector2array(vars),
			vector2array(exprs),
			^rule_id,
			^type_id,
			^ok,
			^all_rules
		);
	} else {
		RuUnifyStepAcc([], [], [], ^rule_id, ^type_id, ^ok, ^all_rules);
	}
}

ruDoUnifyExps(es : [RuExp], acc : RuUnifyAcc) -> Maybe<RuSubst> {
	if (all(mapi(acc.inds, \i, n -> n == length(es[i].nodes)))) {
		Some(acc.sub) 
	} else {
		step = ruDoUnifyStep(es, acc);
		if (!step.ok) None() else {
			new_inds = \-> mapi(acc.inds, \j, i -> i + step.shift[j]);
			if (step.rule_id != -1) {
				if (!step.all_rules) {
					maybeBind(ruUnifyExps(step.exprs, acc.sub, acc.math, acc.env), \s -> {
						ex = ruApplySubst(step.exprs[0], s);
						maybeBind(
							fold(step.vars, Some(s), \ac, v -> 
								maybeBind(ac, \s1 -> ruAdd2Subst1(s1, v, ex, acc.math, acc.env))
							),
							\s1 -> ruDoUnifyExps(es, 
								RuUnifyAcc(acc with 
									sub = s1,
									inds = new_inds()
								)
							)
						);
					});
				} else {
					ruDoUnifyExps(es, RuUnifyAcc(acc with inds = map(acc.inds, \i -> i + 1)));
				}
			} else {
				maybeBind(ruUnifyStepVar(step, acc.math, acc.env), \w -> {
					ex = RuExp([w], -1);
					maybeBind(
						fold(step.vars, Some(acc.sub), \ac, v -> 
							maybeBind(ac, \s1 -> ruAdd2Subst1(s1, v, ex, acc.math, acc.env))
						),
						\s1 -> ruDoUnifyExps(es, 
							RuUnifyAcc(acc with
								sub = s1,
								inds = new_inds()
							)
						)
					);
				});
			}
		}
	}
}

ruUnifySubs(subs : [RuSubst], math : RuMath, env : RuEnv) -> Maybe<RuSubst> {
	t1 = ruMakeTimer();
	splited = ruSplitSubs(subs);
	if (lookupTreeDef(env.conf.opts, "reprove-stats", "") == "1") {
		stats = RuStatsRow("", 
			pairs2tree([
				Pair("size", i2d(fold(subs, 0, \acc, sub -> acc + ruSubstSize(sub)))), 
				Pair("time", ruTimePassed(t1)), 
				//Pair("rets", length(rets)),
				Pair("cplx", i2d(fold(subs, 0, \acc, sub -> acc + ruSubComplexity(sub))))
			])
		);
		ruAddStats("ruUnifySubs-split", stats);
	}
	t2 = ruMakeTimer();
	ret1 = maybeMap(
		ruDoUnifySubs(splited.depend, math, env),
		\sub -> ruMergeSubsUnsafe(splited.indep, sub) //RuSubst(mergeTree(splited.indep, sub.map))
	);
	if (lookupTreeDef(env.conf.opts, "reprove-stats", "") == "1") {
		stats = RuStatsRow("", 
			pairs2tree([
				Pair("size", i2d(fold(splited.depend, 0, \acc, sub -> acc + ruSubstSize(sub)))), 
				Pair("time", ruTimePassed(t2)), 
				//Pair("rets", ruSubComplexity(splited.second)),
				Pair("cplx", i2d(fold(splited.depend, 0, \acc, sub -> acc + ruSubComplexity(sub))))
			])
		);
		ruAddStats("ruUnifySubs-ruDoUnifySubs", stats);
	}
	ret1
}

ruDoUnifySubs(subs : [RuSubst], math : RuMath, env : RuEnv) -> Maybe<RuSubst> {
	foldSet(
		fold(subs, makeSet(), \acc, s -> 
			mergeSets(acc, buildSet(ruSubstVars(s)))
		),
		Some(ruMakeSubst()), 
		\acc, v ->
			maybeBind(acc, \sub -> {
				exprs = filtermap(subs, \s -> 
					maybeMap(ruLookupSubst(s, v), \e -> ruApplySubst(e, sub))
				);
				maybeBind(ruUnifyExps(exprs, sub, math, env), \sub1 -> {
					e1 = ruApplySubst(exprs[0], sub1);
					switch (ruLookupSubst(sub, v)) {
						None(): ruAdd2Subst1(sub1, v, e1, math, env);
						Some(e2):
							maybeBind(ruUnifyExps([e1, e2], sub1, math, env), \unif -> 
								ruCompose(sub1, unif)
							);
					}
				});
			})
	);
}
