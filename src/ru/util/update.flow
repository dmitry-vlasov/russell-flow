import ru/parse/exps/lang;
import ru/parse/parse;
import ru/math;

export {
	ruUpdateSourcePositions(src : RuSource, env : RuEnv) -> RuSource;
}

ruUpdateDeclInfoPositions(info : RuDeclInfo, pos2 : int, comments : [RuParseComment], file_info : RuFileInfo, env : RuEnv) -> RuDeclInfo {
	RuDeclInfo(info with 
		pos = pos2,
		comments = ruCreateComments(comments, file_info, env)
	);
}

ruUpdateConstPositions(const1 : RuConst, decl2 : RuParseDecl, file_info : RuFileInfo, env : RuEnv) -> RuConst {
	switch (decl2) {
		RuParseConst(__,comments,__,__,pos2): 
			RuConst(const1 with info = ruUpdateDeclInfoPositions(const1.info, pos2, comments, file_info, env));
		default:
			ruClearConstPositions(const1);
	}
}

ruUpdateTypePositions(type1 : RuType, decl2 : RuParseDecl, file_info : RuFileInfo, env : RuEnv) -> RuType {
	switch (decl2) {
		RuParseType(__,comments,__,pos2):
			RuType(type1 with info = ruUpdateDeclInfoPositions(type1.info, pos2, comments, file_info, env));
		default:
			ruClearTypePositions(type1);
	}
}

ruUpdateRulePositions(rule1 : RuRule, decl2 : RuParseDecl, file_info : RuFileInfo, env : RuEnv) -> RuRule {
	switch (decl2) {
		RuParseRule(__,comments,__,term2, pos2): {
			RuRule(rule1 with
				info = ruUpdateDeclInfoPositions(rule1.info, pos2, comments, file_info, env),
				term = RuTerm(rule1.term with pos = term2.pos)
			);
		}
		default:
			ruClearRulePositions(rule1);
	}
}

ruUpdateAxiomPositions(axiom1 : RuAxiom, decl2 : RuParseDecl, file_info : RuFileInfo, env : RuEnv) -> RuAxiom {
	switch (decl2) {
		RuParseAxiom(__,comments,__,__,hyps2, prop2, pos2): {
			RuAxiom(axiom1 with 
				info = ruUpdateDeclInfoPositions(axiom1.info, pos2, comments, file_info, env),
				infer = RuInference(axiom1.infer with
					hyps = if (length(axiom1.infer.hyps) != length(hyps2)) {
						ruClearHypsPositions(axiom1.infer.hyps);
					} else {
						mapi(axiom1.infer.hyps, \i, hyp ->
							RuHyp(hyp with 
								stmt = RuStmt(hyp.stmt with
									expr = RuExp(ruExp(hyp.stmt.expr) with pos = hyps2[i].stmt.term.pos),
									pos = hyps2[i].stmt.pos
								),
								pos = hyps2[i].pos
							)
						)
					},
					prop = RuStmt(axiom1.infer.prop with expr = RuExp(ruExp(axiom1.infer.prop.expr) with pos = prop2.pos))
				)
			);
		}
		default: ruClearAxiomPositions(axiom1);
	}
}

ruUpdateTheoremPositions(th1 : RuTheorem, decl2 : RuParseDecl, file_info : RuFileInfo, env : RuEnv) -> RuTheorem {
	switch (decl2) {
		RuParseTheorem(__,comments,__,__,__,hyps2, prop2, proof2, pos2): {
			RuTheorem(th1 with 
				info = ruUpdateDeclInfoPositions(th1.info, pos2, comments, file_info, env),
				infer = RuInference(th1.infer with
					hyps = if (length(th1.infer.hyps) != length(hyps2)) {
						ruClearHypsPositions(th1.infer.hyps);
					} else {
						mapi(th1.infer.hyps, \i, hyp -> 
							RuHyp(hyp with 
								stmt = RuStmt(hyp.stmt with
									expr = RuExp(ruExp(hyp.stmt.expr) with pos = hyps2[i].stmt.term.pos),
									pos = hyps2[i].stmt.pos
								),
								pos = hyps2[i].pos
							)
						);
					},
					prop = RuStmt(th1.infer.prop with expr = RuExp(th1.infer.prop.expr with pos = prop2.pos)),
				),
				proof = RuProof(th1.proof with 
					steps = if (length(th1.proof.steps) != length(proof2.steps)) {
						ruClearStepsPositions(th1.proof.steps);
					} else {
						mapi(th1.proof.steps, \i, step -> 
							RuStep(step with
								stmt = RuStmt(step.stmt with
									expr = RuExp(ruExp(step.stmt.expr) with pos = proof2.steps[i].stmt.term.pos),
									pos = proof2.steps[i].stmt.pos
								),
								pos = proof2.steps[i].pos
							)
						);
					}
				)
			);
		}
		default: ruClearTheoremPositions(th1);
	}
}

ruUpdateDefPositions(def1 : RuDef, decl2 : RuParseDecl, file_info : RuFileInfo, env : RuEnv) -> RuDef {
	switch (decl2) {
		RuParseDef(__,comments,__,__,hyps2, defm2, defs2, prop2, pos2): {
			RuDef(def1 with 
				info = ruUpdateDeclInfoPositions(def1.info, pos2, comments, file_info, env),
				infer = RuInference(def1.infer with 
					hyps = if (length(def1.infer.hyps) != length(hyps2)) {
						ruClearHypsPositions(def1.infer.hyps);
					} else {
						mapi(def1.infer.hyps, \i, hyp -> 
							RuHyp(hyp with
								stmt = RuStmt(hyp.stmt with
									expr = RuExp(ruExp(hyp.stmt.expr) with pos = hyps2[i].stmt.pos)
								),
								pos = hyps2[i].pos
							)
						);
					},
					prop = RuStmt(def1.infer.prop with expr = RuExp(def1.infer.prop.expr with pos = prop2.pos))
				),
				defm = RuExp(def1.defm with pos = defm2.pos),
				defs = RuExp(def1.defs with pos = defs2.pos),
			);
		}
		default: ruClearDefPositions(def1);
	}
}

ruUpdateDeclPositions(decl1 : RuDecl, decl2 : RuParseDecl, file_info : RuFileInfo, env : RuEnv) -> RuDecl {
	if (decl1.info.id != decl2.id) {
		ruClearDeclPositions(decl1);
	} else {
		switch (decl1) {
			RuConst(__,__,__): ruUpdateConstPositions(decl1, decl2, file_info, env);
			RuType(__,__):     ruUpdateTypePositions(decl1, decl2, file_info, env);
			RuRule(__,__,__):  ruUpdateRulePositions(decl1, decl2, file_info, env);
			RuAxiom(__,__):    ruUpdateAxiomPositions(decl1, decl2, file_info, env);
			RuTheorem(__,__,__,__): ruUpdateTheoremPositions(decl1, decl2, file_info, env);
			RuDef(__,__,__,__):     ruUpdateDefPositions(decl1, decl2, file_info, env);
		}
	}
}

ruClearHypsPositions(hyps : [RuHyp]) -> [RuHyp] {
	map(hyps, \hyp ->
		RuHyp(hyp with
			stmt = RuStmt(hyp.stmt with
				expr = RuExp(ruExp(hyp.stmt.expr) with pos = -1)
			),
			pos = -1
		)
	);
}

ruClearStepsPositions(steps : [RuStep]) -> [RuStep] {
	map(steps, \step -> 
		RuStep(step with
			stmt = RuStmt(step.stmt with
				expr = RuExp(ruExp(step.stmt.expr) with pos = -1)
			),
			pos = -1
		)
	);
}

ruClearConstPositions(const : RuConst) -> RuConst {
	RuConst(const with info = RuDeclInfo(const.info with pos = -1));
}

ruClearTypePositions(type : RuType) -> RuType {
	RuType(type with info = RuDeclInfo(type.info with pos = -1));
}

ruClearRulePositions(rule : RuRule) -> RuRule {
	RuRule(rule with
		info = RuDeclInfo(rule.info with pos = -1),
		term = RuTerm(rule.term with pos = -1)
	);
}

ruClearAxiomPositions(axiom : RuAxiom) -> RuAxiom {
	RuAxiom(axiom with 
		info = RuDeclInfo(axiom.info with pos = -1),
		infer = RuInference(axiom.infer with
			hyps = ruClearHypsPositions(axiom.infer.hyps),
			prop = RuStmt(axiom.infer.prop with
				expr = RuExp(axiom.infer.prop.expr with pos = -1),
				pos = -1,
			)
		)
	);
}

ruClearTheoremPositions(th : RuTheorem) -> RuTheorem {
	RuTheorem(th with 
		info = RuDeclInfo(th.info with pos = -1),
		infer = RuInference(th.infer with
			hyps = ruClearHypsPositions(th.infer.hyps),
			prop = RuStmt(th.infer.prop with
				expr = RuExp(th.infer.prop.expr with pos = -1),
				pos = -1,
			),
		),
		proof = RuProof(th.proof with 
			steps = ruClearStepsPositions(th.proof.steps)
		)
	);
}

ruClearDefPositions(def : RuDef) -> RuDef {
	RuDef(def with 
		info = RuDeclInfo(def.info with pos = -1),
		infer = RuInference(def.infer with 
			hyps = ruClearHypsPositions(def.infer.hyps),
			prop = RuStmt(def.infer.prop with
				expr = RuExp(def.infer.prop.expr with pos = -1)
			)
		),
		defm = RuExp(def.defm with pos = -1),
		defs = RuExp(def.defs with pos = -1),
	);
}


ruClearDeclPositions(decl : RuDecl) -> RuDecl {
	switch (decl) {
		RuConst(__,__,__): ruClearConstPositions(decl);
		RuType(__,__):     ruClearTypePositions(decl);
		RuRule(__,__,__):  ruClearRulePositions(decl);
		RuAxiom(__,__):    ruClearAxiomPositions(decl);
		RuTheorem(__,__,__,__): ruClearTheoremPositions(decl);
		RuDef(__,__,__,__):     ruClearDefPositions(decl);
	}
}

ruUpdateSourcePositions(src : RuSource, env : RuEnv) -> RuSource {
	switch (ruLoadRawSource(src.info.id, env, ruLoader)) {
		Some(loaded): {
			switch (ruParseSource(loaded, env)) {
				Some(parsed): {
					imps_pos = fold(parsed.imports, makeTree(), \acc, imp -> setTree(acc, imp.module, imp.pos));
					decls_tree = fold(parsed.decls, makeTree(), \acc, decl -> setTree(acc, decl.id, decl));
					RuSource(src with 
						imports = mapi(src.imports, \i, imp -> 
							RuImport(imp with pos = lookupTreeDef(imps_pos, imp.id, -1))
						),
						comments = ruCreateComments(parsed.comments, src.info, env),
						decls = map(src.decls, \d1 -> 
							switch (lookupTree(decls_tree, d1.info.id)) {
								Some(d2): ruUpdateDeclPositions(d1, d2, src.info, env);
								None(): {
									env.out.error("declaration '" + id2s(d1.info.id) + "' is not found at position updating", []);
									ruClearDeclPositions(d1);
								}
							}
						)
					);
				}
				None(): {
					env.out.error("source '" + id2s(src.info.id) + "' failed parsing", []);
					src;
				}
			}
		}
		None(): {
			env.out.error("source '" + id2s(src.info.id) + "' failed loading raw source", []);
			src;
		}
	}
}
