import ru/util/exp2s;
import ru/util/src_base;
import ru/math;

export {
	ru2s(a : RuAny, math : RuMath) -> string;
	ru2sLang(a : RuAny, lang : RuLang) -> string;

	ru2sWrapExp(a : RuAny, math : RuMath, wrap_exp : (string) -> string) -> string;
	ruDeclShort2s(d : RuDecl, math : RuMath) -> string;

	ruVars2s(vars : Tree<int, RuVar>) -> string;
	ruDisj2s(d : RuDisj) -> string;
	ruDisjs2s(disjs : Set<RuDisj>) -> string;
	ruMeta2s(meta : Set<int>) -> string;

	ruRemoveProof(th : RuTheorem) -> RuTheorem;

	Ru2SEnv(
		indent : string,
		wrapExp : (string) -> string,
		forceDisjs : bool,
		lang : RuLang,
	);

	ruMakeRu2SEnv(math : RuMath) -> Ru2SEnv;
	ruSteps2s(steps : [RuStep], env : Ru2SEnv) -> string;
	ruAny2s(a : RuAny, env : Ru2SEnv) -> string;

	ruFindProofBeginning(code : string, start : int, end : int) -> int;

	ruExp(e : RuTermExp) -> RuExp;

	ruMakeDefStmt(prop : RuStmt, dfm : RuTermExp, dfs: RuTermExp) -> RuStmt;
}

ruIndentRu2SEnv(env : Ru2SEnv) -> Ru2SEnv {
	Ru2SEnv(env with indent = env.indent + "\t");
}

ruMakeRu2SEnv(math : RuMath) -> Ru2SEnv {
	Ru2SEnv("", idfn, true, math.decls.lang)
}

ru2s(a : RuAny, math : RuMath) -> string {
	ruAny2s(a, Ru2SEnv("", idfn, true, math.decls.lang));
}

ru2sLang(a : RuAny, lang : RuLang) -> string {
	ruAny2s(a, Ru2SEnv("", idfn, true, lang));
}

ru2sWrapExp(a : RuAny, math : RuMath, wrap_exp : (string) -> string) -> string {
	ruAny2s(a, Ru2SEnv("", wrap_exp, true, math.decls.lang));
}

ruVars2s(vars : Tree<int, RuVar>) -> string {
	if (sizeTree(vars) == 0) "" else 
	"(" + strGlue(map(ruOrderVars(vars), \v -> id2s(v.id) + " : " + id2s(v.type)), ", ") + ") ";
}

ruDisj2s(d : RuDisj) -> string {
	id2s(d.v1.id) + " " + id2s(d.v2.id);
}

ruDisjs2s(disjs : Set<RuDisj>) -> string {
	if (sizeSet(disjs) == 0) "" else 
	"disjointed(" + 
		strGlue(map(
			ruDisjs2Array(disjs), 
			\vs -> strGlue(map(vs, \v -> id2s(v.id)), " ")
		), ", ") + 
	") ";
}

ruVarsDisjs2s(vd : RuVarsDisjs, do_disjs : bool) -> string {
	ruVars2s(vd.vars) + (if (do_disjs) ruDisjs2s(vd.disjs) else "");
}

ruMeta2s(meta : Set<int>) -> string {
	if (sizeSet(meta) == 0) "" else 
	"meta(" + strGlue(map(set2array(meta), id2s), ", ") + ") ";
}

ruStmt2s(stmt : RuStmt, env : Ru2SEnv) -> string {
	exps = map(stmt.exps, \ex -> env.wrapExp(ruTermExp2s(ex, env.lang)));
	multiline = exists(exps, \ex -> strContains(ex, "\n"));
	types = superglue(stmt.exps, \ex -> id2s(ruTermExpType(ex)), ", ");
	": " + types + " = " + id2s(stmt.class) + " " +
	(if (!multiline) strGlue(exps, " ") else ltrim2(ruIndentWith(strGlue(exps, "\n"), env.indent), "\t")) + " ;;";
}

ruProp2s(prop : RuStmt, env : Ru2SEnv) -> string {
	env.indent + "prop " + ruStmt2s(prop, env) + "\n";
}

ruRef2s(rf : RuRef) -> string {
	switch (rf) {
		RuHypRef(i): "hyp " + i2s(i + 1);
		RuStepRef(i): "step " + i2s(i + 1);
	}
}

ruRefs2s(refs : [RuRef]) -> string {
	"(" + strGlue(map(refs, ruRef2s), ", ") + ")";
}

ruHyp2s(hyp : RuHyp, env : Ru2SEnv) -> string {
	env.indent + "hyp " + i2s(hyp.ind + 1) + " " + ruStmt2s(hyp.stmt, env) + "\n";
}

ruHyps2s(hyps : [RuHyp], env : Ru2SEnv) -> string {
	if (hyps == []) "" else concatStrings(map(hyps, \hyp -> ruHyp2s(hyp, env)));
}

ruComment2s(comm : RuComment, env : Ru2SEnv) -> string {
	str = if (length(comm.annotation.members) == 0) "/*" + comm.text + "*/\n" else
	"/**" +json2string(comm.annotation) + (if (comm.text == "") "" else "\n-----\n" + comm.text) + "*/\n";
	ruIndentWith(str, env.indent)
}

ruComments2s(comms : [RuComment], env : Ru2SEnv) -> string {
	if (comms == []) "" else strGlue(map(comms, \comm -> ruComment2s(comm, env)), "\n") + "\n";
}

ruImport2s(imp : RuImport, env : Ru2SEnv) -> string {
	env.indent + "import " + id2s(imp.id) + ";;\n";
}

ruImports2s(imps : [RuImport], env : Ru2SEnv) -> string {
	if (imps == []) "" else concatStrings(map(imps, \imp -> ruImport2s(imp, env))) + "\n";
}

ruConst2s(const : RuConst, env : Ru2SEnv) -> string {
	ruComments2s(const.info.comments, env) + 
	env.indent + "constant {\n" + 
		env.indent + "\tsymbol " + id2s(const.info.id) + " ;;\n" +
		env.indent + (if (const.ascii != -1) "\tascii " + id2s(const.ascii) + " ;;\n" else "") + 
		env.indent + (if (const.latex != "") "\tlatex " + const.latex + " ;;\n" else "") +
	env.indent + "}\n";
}

ruType2s(type : RuType, env : Ru2SEnv) -> string {
	ruComments2s(type.info.comments, env) + 
	env.indent + "type " + id2s(type.info.id) + 
	(if (type.supers == []) "" else " : " + strGlue(map(type.supers, id2s), ", ")) + 
	" ;;\n";
}

ruRule2s(rule : RuRule, env : Ru2SEnv) -> string {
	ruComments2s(rule.info.comments, env) + 
	env.indent + "rule " + id2s(rule.info.id) + " " + ruVars2s(rule.vars) + "{\n" +
		env.indent + "\tterm : " + id2s(rule.term.type) + " = " + ruTerm2s(rule.term) + " ;;\n" +
	env.indent + "}\n";
}

ruAxiom2s(ax : RuAxiom, env : Ru2SEnv) -> string {
	ind_env = ruIndentRu2SEnv(env);
	ruComments2s(ax.info.comments, env) + 
	env.indent + "axiom " + id2s(ax.info.id) + " " + ruVarsDisjs2s(ax.infer.vars, true) + "{\n" +
		ruHyps2s(ax.infer.hyps, ind_env) +
		(if (ax.infer.hyps == []) "" else env.indent + "\t----------\n") +
		ruProp2s(ax.infer.prop, ind_env) +
	env.indent + "}\n";
}

ruDef2s(def : RuDef, env : Ru2SEnv) -> string {
	ind_env = ruIndentRu2SEnv(env);
	ruComments2s(def.info.comments, env) + 
	env.indent + "definition " + id2s(def.info.id) + " " + ruVarsDisjs2s(def.infer.vars, true) + "{\n" +
		ruHyps2s(def.infer.hyps, ind_env) +
		env.indent + "\tdefiendum : " + id2s(ruExpType(def.defm)) + " = " + ruTermExp2s(def.defm, ind_env.lang) + " ;;\n" +
		env.indent + "\tdefiniens : " + id2s(ruExpType(def.defs)) + " = " + ruTermExp2s(def.defs, ind_env.lang) + " ;;\n" +
		env.indent + "\t-------------\n" +
		ruProp2s(ruMakeDefStmt(def.infer.prop, def.defm, def.defs), ind_env) +
	env.indent + "}\n";
}

ruTheorem2s(th : RuTheorem, env : Ru2SEnv) -> string {
	ind_env = ruIndentRu2SEnv(env);
	ruComments2s(th.info.comments, env) + 
	env.indent + "theorem " + id2s(th.info.id) + " " + ruVarsDisjs2s(th.infer.vars, env.forceDisjs) + ruMeta2s(th.meta) + "{\n" +
		ruHyps2s(th.infer.hyps, ind_env) + 
		(if (th.infer.hyps == []) "" else env.indent + "\t----------\n") +
		ruProp2s(th.infer.prop, ind_env) +
	env.indent + "} " + ruProof2s(th.proof, env);
}

ruProof2s(proof : RuProof, env : Ru2SEnv) -> string {
	"proof " + ruVarsDisjs2s(proof.vars, env.forceDisjs) + "{\n" + 
		ruSteps2s(proof.steps, ruIndentRu2SEnv(env)) +
	env.indent + "}\n";
}

ruStep2s(step : RuStep, env : Ru2SEnv) -> string {
	exps = map(step.stmt.exps, \ex -> env.wrapExp(ruTermExp2s(ex, env.lang)));
	multiline = exists(exps, \ex -> strContains(ex, "\n"));
	types = superglue(step.stmt.exps, \ex -> id2s(ruTermExpType(ex)), ", ");
	env.indent + "step " + i2s(step.ind + 1) + " : " + types + " = " + 
		(if (step.assertion == ruStepLemma) "lemma" else if (step.assertion == ruStepProblem) "?" else id2s(step.assertion)) + " " + 
		ruRefs2s(step.refs) + " " + id2s(step.stmt.class) + " " + 
		(if (!multiline) strGlue(exps, " ") else ltrim2(ruIndentWith(strGlue(exps, "\n"), env.indent), "\t")) + " ;; " + 
		(if (step.assertion == ruStepLemma) ruProof2s(step.proof, env) else "\n");
}

ruSteps2s(steps : [RuStep], env : Ru2SEnv) -> string {
	concatStrings(map(steps, \step -> ruStep2s(step, env)));
}

ruDecls2s(decls : [RuDecl], env : Ru2SEnv) -> string {
	if (decls == []) "" else concatStrings(map(decls, \decl -> ruAny2s(decl, env))) + "\n";
}

ruSource2s(src : RuSource, env : Ru2SEnv) -> string {
	ruImports2s(src.imports, env) +
	ruComments2s(src.comments, env) + 
	ruDecls2s(src.decls, env);
}

ruAny2s(a : RuAny, env : Ru2SEnv) -> string {
	switch (a) {
		RuSource(__,__,__,__):  ruSource2s(a, env);
		RuImport(__,__):        ruImport2s(a, env);
		RuConst(__,__,__):      ruConst2s(a, env);
		RuType(__,__):          ruType2s(a, env);
		RuRule(__,__,__):       ruRule2s(a, env);
		RuAxiom(__,__):         ruAxiom2s(a, env);
		RuDef(__,__,__,__):     ruDef2s(a, env);
		RuTheorem(__,__,__,__): ruTheorem2s(a, env);
		RuHyp(__,__,__):        ruHyp2s(a, env);
		RuProof(__,__,__):      ruProof2s(a, env);
		RuStep(__,__,__,__,__,__): ruStep2s(a, env);
		RuVarsDisjs(__,__):  ruVarsDisjs2s(a, env.forceDisjs);
		RuComment(__,__,__): ruComment2s(a, env);
		RuDisj(__,__):       ruDisj2s(a);
		RuStmt(__,__,__):    ruStmt2s(a, env);
		RuExp(__,__):        env.wrapExp(ruExp2s(a, env.lang));
		RuHypRef(__):        ruRef2s(a);
		RuStepRef(__):       ruRef2s(a);
		RuVar(id,__):        id2s(id);
		RuTerm(__,__,__):    ruTerm2s(a);
		RuConstRef(id):      id2s(id);
		RuRuleNode(rt,__):   id2s(ruIdType2Id(rt));
	}
}

ruRemoveProof(th : RuTheorem) -> RuTheorem {
	RuTheorem(th with proof = RuProof(th.proof with steps = []));
}

// ruFindProofBeginning

ruDeclShort2s(d : RuDecl, math : RuMath) -> string {
	switch (d) {
		RuTheorem(__,__,__,proof): {
			str = ru2s(ruRemoveProof(d), math);
			proof_ind = ruFindProofBeginning(str, 0, strlen(str));
			if (proof_ind == -1) str else substring(str, 0, proof_ind);
		}
		default: ru2s(d, math);
	}
}

ruFindProofBeginning(code : string, start : int, end : int) -> int {
	ruDoFindProofBeginning(code, end, start, false, false);
}

ruDoFindProofBeginning(code : string, end : int, i : int, ml : bool, sl : bool) -> int {
	if (i >= end) -1 else {
		ch1 = getCharAt(code, i);
		ch2 = if (i + 1 >= end) "" else getCharAt(code, i + 1);
		if (ml) {
			if (ch1 == "*" && ch2 == "/") {
				ruDoFindProofBeginning(code, end, i + 2, false, false);
			} else {
				ruDoFindProofBeginning(code, end, i + 1, true, false);
			}
		} else if (sl) {
			if (ch1 == "\n") {
				ruDoFindProofBeginning(code, end, i + 1, false, false);
			} else {
				ruDoFindProofBeginning(code, end, i + 1, false, true);
			}
		} else {
			if (ch1 == "/") {
				if (ch2 == "/") {
					ruDoFindProofBeginning(code, end, i + 2, false, true);
				} else if (ch2 == "*") {
					ruDoFindProofBeginning(code, end, i + 2, true, false);
				} else {
					ruDoFindProofBeginning(code, end, i + 1, false, false);
				}
			} else if (ch1 == "p" && ch2 == "r") {
				if (substring(code, i, 5) == "proof") i else
				ruDoFindProofBeginning(code, end, i + 2, ml, false);
			} else {
				ruDoFindProofBeginning(code, end, i + 1, false, false);
			}
		}
	}
}

ruExp(e : RuTermExp) -> RuExp {
	switch (e) {
		RuExp(__,__): e;
		RuTerm(__,__,__): {
			ruCrash("term instead of expressions: " + ruTerm2s(e));
			ruNoExp;
		}
	}
}

ruIndOfArr(arr1 : [?], arr2 : [?]) -> int {
	foldi(arr1, -1, \i, ac, x -> 
		if (ac >= 0) ac else {
			if (i <= length(arr1) - length(arr2)) {
				ind = fold(arr2, i, \j, y ->
					if (j == -1) -1 else
					if (y == arr1[j]) j + 1 else -1
				);
				if (ind != -1) i else -1;
			} else {
				-1
			}
		}
	)
}

ruMakeDefStmt(prop : RuStmt, dfm : RuTermExp, dfs: RuTermExp) -> RuStmt {
	prop_expr = ruExp(prop.exps[0]);
	dfm_expr = ruExp(dfm);
	dfs_expr = ruExp(dfs);
	dfs_i = ruIndOfArr(prop_expr.nodes, dfs_expr.nodes);
	dfm_i = ruIndOfArr(prop_expr.nodes, dfm_expr.nodes);
	dfs_len = length(dfs_expr.nodes);
	dfm_len = length(dfm_expr.nodes);
	if (dfs_i == -1 || dfm_i == -1) {
		ruNoStmt
	} else {
		RuStmt(
			prop.class,
			[RuExp(
				foldi(prop_expr.nodes, [], \i, ac, s ->
					if (i == dfm_i) concat(ac, [RuVar(ruIdDefiendum(), -1)]) else
					if (dfm_i < i && i < dfm_i + dfm_len) ac else
					if (i == dfs_i) concat(ac, [RuVar(ruIdDefiniens(), -1)]) else
					if (dfs_i < i && i < dfs_i + dfs_len) ac else
					concat(ac, [s])
				), -1
			)],
			-1
		)
	}
}
