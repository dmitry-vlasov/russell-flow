import ru/lang;
import base/db;

export {
	ruTermExpType(e : RuTermExp) -> int;
	ruTermExp2s(e : RuTermExp, lang : RuLang) -> string;

	ruExpType(exp : RuExp) -> int;
	ruExp2s(e : RuExp, lang : RuLang) -> string;
	ruExp2Term(e : RuExp, lang : RuLang) -> RuTerm;
	ruExp2Nodes(e : RuExp, lang : RuLang) -> [RuExpNode];
	ruTerm2s(term : RuTerm) -> string;
	ruTypedTerm2s(term : RuTerm) -> string;
	ruExpRPN2s(exp : RuExp) -> string;
	ruExp2latex(e : RuExp, lang : RuLang, env : RuEnv) -> string;
	ruTerm2latex(term : RuTerm, lang : RuLang, env : RuEnv) -> string;
}

ruTermExpType(e : RuTermExp) -> int {
	switch (e) {
		RuExp(nodes,__): {
			if (length(nodes) > 0) {
				switch (nodes[0]) {
					RuVar(id, type): type;
					RuRuleNode(ruleType, arityLen): ruRuleType2Type(ruleType);
				}
			} else {
				ruCrash("Empty expression");
				-1;
			}
		}
		RuTerm(type,__,__): type;
	}
}

ruTermExp2s(e : RuTermExp, lang : RuLang) -> string {
	switch (e) {
		RuExp(nodes,__): {
			if (length(nodes) > 0) {
				ruTerm2s(ruExp2Term(e, lang))
			} else {
				ruCrash("Empty expression");
				"";
			}
		}
		RuTerm(type,__,__): {
			ruTerm2s(e);
		}
	}
}

ruExpType(e : RuExp) -> int {
	if (length(e.nodes) > 0) {
		switch (e.nodes[0]) {
			RuVar(id, type): type;
			RuRuleNode(ruleType, arityLen): ruRuleType2Type(ruleType);
		}
	} else {
		ruCrash("Empty expression");
		-1;
	}
}

ruExp2s(e : RuExp, lang : RuLang) -> string {
	if (length(e.nodes) > 0) {
		ruTerm2s(ruExp2Term(e, lang))
	} else {
		ruCrash("Empty expression");
		"";
	}
}

ruTerm2s(term : RuTerm) -> string {
	strGlue(map(term.nodes, \n -> id2s(n.id)), " ")
}

ruTypedTerm2s(term : RuTerm) -> string {
	strGlue(map(term.nodes, \n -> 
		switch (n) {
			RuConstRef(c): id2s(c);
			RuVar(v, t): id2s(v) + ":" + id2s(t);
		}
	), " ")
}

ruExpRPN2s(exp : RuExp) -> string {
	strGlue(map(exp.nodes, \n -> {
		switch (n) {
			RuRuleNode(rule_type, arity_len): 
				id2s(ruRuleType2Rule(rule_type)) + "-" + i2s(ruArityLen2Arity(arity_len));
			RuVar(id, __): id2s(id);
		}
	}), " ")
}

// This function is strict: crashes in case of errors
ruExp2Term(e : RuExp, lang : RuLang) -> RuTerm {
	stack = foldr(e.nodes, RuExp2TermStack(None(), []), \acc, n -> ruExp2TermAccOperation(n, acc, lang, true));
	RuTerm(ruExpType(e), map(stack.nodes, \n -> n.first), e.pos);
}

// This function permits errors: no rules/empty exprs as it is used with cached files
ruExp2Nodes(e : RuExp, lang : RuLang) -> [RuExpNode] {
	stack = foldr(e.nodes, RuExp2TermStack(None(), []), \acc, n -> ruExp2TermAccOperation(n, acc, lang, false));
	map(stack.nodes, \n -> n.second);
}

RuExp2TermStack(
	parent : Maybe<RuExp2TermStack>,
	nodes : [Pair<RuTermNode, RuExpNode>]
);

ruExp2TermAccOperation(node : RuExpNode, stack : RuExp2TermStack, lang : RuLang, crash : bool) -> RuExp2TermStack {
	switch (node) {
		RuVar(__,__): 
			RuExp2TermStack(Some(stack), [Pair(node, node)]);
		RuRuleNode(rule_type, arity_len): {
			rule_id = ruRuleType2Rule(rule_type);
			//type_id = ruRuleType2Type(rule_type, lang);
			arity = ruArityLen2Arity(arity_len);
			len = ruArityLen2Len(arity_len);
			switch (lookupTree(lang.rules, rule_id)) {
				Some(rule): {
					fold(rule.term.nodes, RuExp2TermStack(Some(stack), []), 
						\acc, symb -> {
							switch (symb) {
								RuVar(__,__): {
									switch (acc.parent) {
										Some(st): RuExp2TermStack(st.parent, concat(acc.nodes, st.nodes));
										None(): {
											if (crash) {
												ruCrash("stack must not be empty");
											}
											acc
										}
									}
								}
								RuConstRef(__):
									RuExp2TermStack(acc.parent, concat(acc.nodes, [Pair(symb, node)]));
							}
						}
					);
				}
				None(): {
					if (crash) {
						ruCrash("rule '" + id2s(rule_id) + "' is not found (id: " + i2s(rule_id) +")");
					}
					stack;
				}
			}
		}
	}
}

ruExp2latex(e : RuExp, lang : RuLang, env : RuEnv) -> string {
	if (length(e.nodes) > 0) {
		ruTerm2latex(ruExp2Term(e, lang), lang, env);
	} else {
		"";
	}
}

ruTerm2latex(term : RuTerm, lang : RuLang, env : RuEnv) -> string {
	vars_typesetting_json = getJsonArrayValue(ruGetDbItem(env.conf.project, ["vars_typesetting"]), []);
	vars_typesetting = fold(vars_typesetting_json, makeTree(), \acc, var_typesetting_json -> {
		members = getJsonObjectValue(var_typesetting_json, []);
		id_json = findDef(members, \p -> p.first == "ascii", Pair("", JsonNull())).second;
		id = s2id(getJsonStringValue(id_json, ""));
		latex_json = findDef(members, \p -> p.first == "latex", Pair("", JsonNull())).second;
		latex = getJsonStringValue(id_json, "");
		setTree(acc, id, latex);
	});
	nodes = map(term.nodes, \n -> 
		switch (n) {
			RuConstRef(id): {
				lookupTreeDef(lang.consts, id, ruNoConst).latex;
			}
			RuVar(id, type): {
				lookupTreeDef(vars_typesetting, id, id2s(id));
			}
		}
	);
	strGlue(nodes, " ");
}
