import ru/math;
import ru/util/decls;
import ru/util/sources;
import script/script;

export {
	RuTarget ::= RuTargetMath, RuTargetSource, RuTargetDecl, RuTargetNone;
		RuTargetNone();
		RuTargetMath(concurrent : bool); // if all decls may be processed concurrently or not
		RuTargetSource(source : RuSource, concurrent : bool);
		RuTargetDecl(decl : RuDecl);
	
	ruDetermineTarget(task : ScTask, math : RuMath, env : RuEnv) -> RuTarget;
	ruTargetDecls(target : RuTarget, math : RuMath, env : RuEnv) -> [RuDecl];
	ruTargetIsConcurrent(target : RuTarget) -> bool;
	ruTarget2s(target : RuTarget) -> string;

	ruTargetPredicate(target : RuTarget) -> (RuDecl) -> bool;

	ruProofLengthMeasure(decl : RuDecl) -> double;
	ruProofExpSizeMeasure(decl : RuDecl) -> double;
}

ruDetermineTarget(task : ScTask, math : RuMath, env : RuEnv) -> RuTarget {
	target0 = lookupTreeDef(task.args, "target", "");
	if (target0 == "") RuTargetNone() else {
		concurrent = !endsWith(target0, "-seq");
		target1 = if (concurrent) target0 else strLeft(target0, strlen(target0) - 4);
		target = ruTrimPath(target1, env.conf, ".ru");
		if (ruVerbose(env.conf) > 2) {
			env.out.onMessage("determining target: '" + target + "'");
		}
		if (target == "all" || target == "all-seq") {
			RuTargetMath(concurrent);
		} else {
			target_id = s2id(target);
			switch (lookupTree(math.sources, target_id)) {
				Some(src): RuTargetSource(src, concurrent);
				None(): 
					switch (ruFindDecl(target_id, math.decls, env)) {
						Some(decl): RuTargetDecl(decl);
						None(): RuTargetNone();
					}
			}
		}
	}
}

ruTargetDecls(target : RuTarget, math : RuMath, env : RuEnv) -> [RuDecl] {
	switch (target) {
		RuTargetMath(__):       concatA(map(ruSourcesOrder(math.sources), \src -> src.decls));
		RuTargetSource(source,__): source.decls;
		RuTargetDecl(decl):     [decl];
		RuTargetNone():         [];
	}
}

ruTargetIsConcurrent(target : RuTarget) -> bool {
	switch (target) {
		RuTargetMath(concurr):      concurr;
		RuTargetSource(__,concurr): concurr;
		RuTargetDecl(decl):         false;
		RuTargetNone():             false;
	}
}

ruTarget2s(target : RuTarget) -> string {
	switch (target) {
		RuTargetMath(concurr):        "all" + (if (concurr) "" else "-seq");
		RuTargetSource(src, concurr): id2s(src.info.id) + (if (concurr) "" else "-seq");
		RuTargetDecl(decl):           id2s(decl.descr.id);
		RuTargetNone():               "";
	}
}

ruTargetPredicate(target : RuTarget) -> (RuDecl) -> bool {
	switch (target) {
		RuTargetMath(__):       \__ -> true;
		RuTargetSource(src,__): \d -> d.descr.src == src.info.id;
		RuTargetDecl(decl):     \d -> d.descr.id == decl.descr.id;
		RuTargetNone():         \__ -> false;
	}
}

ruProofLengthMeasure(decl : RuDecl) -> double {
	switch (decl) {
		RuTheorem(__,__,__,proof): i2d(length(proof.steps));
		default: 0.0;
	}
}

ruProofExpSizeMeasure(decl : RuDecl) -> double {
	switch (decl) {
		RuTheorem(__,__,__,proof): ruDoProofExpSizeMeasure(proof);
		default: 0.0;
	}
}

ruDoProofExpSizeMeasure(proof : RuProof) -> double {
	fold(proof.steps, 0.0, \acc, step ->
		acc + i2d(length(step.expr.nodes)) + ruDoProofExpSizeMeasure(step.proof)
	);
}
