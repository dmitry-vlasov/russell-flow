import ru/src;

export {
	ruMakeExpTransformer(f: (RuExp) -> RuExp) -> (RuTermExp) -> RuTermExp;
	ruTransformStatement(st: RuInference, f : (RuTermExp) -> RuTermExp) -> RuInference;
	ruTransformTheorem(th: RuTheorem, f : (RuTermExp) -> RuTermExp) -> RuTheorem;
	ruTransformProof(th: RuProof, f : (RuTermExp) -> RuTermExp) -> RuProof;
	ruTransformAxiom(ax: RuAxiom, f : (RuTermExp) -> RuTermExp) -> RuAxiom;
	ruTransformDef(def: RuDef, f : (RuTermExp) -> RuTermExp) -> RuDef;
	ruTransformPremise(premise: RuPremise, f : (RuTermExp) -> RuTermExp) -> RuPremise;
	ruTransformStep(step : RuStep, f : (RuTermExp) -> RuTermExp) -> RuStep;
}

ruMakeExpTransformer(f: (RuExp) -> RuExp) -> (RuTermExp) -> RuTermExp {
	\x -> switch (x) {
		RuExp(__,__): f(x);
		default: fail0("must be expression");
	}
}

ruTransformProof(proof: RuProof, f : (RuTermExp) -> RuTermExp) -> RuProof {
	RuProof(proof with 
		steps = map(proof.steps, \step -> ruTransformStep(step, f))
	);
}

ruTransformTheorem(th: RuTheorem, f : (RuTermExp) -> RuTermExp) -> RuTheorem {
	RuTheorem(th with
		infer = ruTransformStatement(th.infer, f),
		proof = ruTransformProof(th.proof, f)
	);
}

ruTransformStatement(st: RuInference, f : (RuTermExp) -> RuTermExp) -> RuInference {
	RuInference(st with
		hyps = map(st.hyps, \h -> RuHyp(h with expr = f(h.expr))),
		prop = f(st.prop)
	);
}

ruTransformAxiom(ax: RuAxiom, f : (RuTermExp) -> RuTermExp) -> RuAxiom {
	RuAxiom(ax with 
		infer = ruTransformStatement(ax.infer, f)
	);
}

ruTransformDef(def: RuDef, f : (RuTermExp) -> RuTermExp) -> RuDef {
	RuDef(def with
		infer = ruTransformStatement(def.infer, f),
		defm = f(def.defm),
		defs = f(def.defs)
	);
}

ruTransformPremise(premise: RuPremise, f : (RuTermExp) -> RuTermExp) -> RuPremise {
	switch (premise) {
		RuHyp(__, expr,__): RuHyp(premise with expr = f(expr));
		RuStep(__,__,__,expr,__,__): RuStep(premise with expr = f(expr));
	}
}

ruTransformStep(step : RuStep, f : (RuTermExp) -> RuTermExp) -> RuStep {
	RuStep(step with expr = f(step.expr));
}