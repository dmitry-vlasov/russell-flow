import base/ru_conf;
import parse/ru_trie;
import ru/ru_src;

export {
	RuTypeEnv(
		type   : RuType,
		trie   : RuTrie<string, RuRuleRef>,
		supers : Tree<string, RuRule>
	);
	ruNoTypeEnv = RuTypeEnv(ruNoType, ruTrieEmpty, makeTree());

	ruAddTerm2RuTrie(term : RuTerm, rule : Maybe<RuRuleRef>, trie : RuTrie<string, RuRuleRef>, conf : RuConf) -> RuTrie<string, RuRuleRef>;
	ruMergeRuTypeEnv(e1 : RuTypeEnv, e2 : RuTypeEnv) -> RuTypeEnv;
	ruMakeTypeEnv(type : RuType, types : Tree<string, RuWithSource<RuTypeEnv>>, file : string, conf : RuConf) -> RuTypeEnv;

	ruSuperTypeName(super : string, infer : string) -> string;
	ruMakeSuperRule(super : string, infer : string, pos : int) -> RuRule;
}

ruAddTerm2RuTrie(term : RuTerm, rule : Maybe<RuRuleRef>, trie : RuTrie<string, RuRuleRef>, conf : RuConf) -> RuTrie<string, RuRuleRef> {
	term_key = \x -> switch (x) {
		RuConstRef(c):  c;
		RuVar(v, t): t;
	}
	ruAdd2Trie(map(term.expr, term_key), rule, trie, 
		\a1, a2 -> {
			conf.onError("rule term: " + ruTerm2s(term.expr) + " is already used", []);
			a1
		}
	)
}

ruMergeRuTypeEnv(e1 : RuTypeEnv, e2 : RuTypeEnv) -> RuTypeEnv {
	RuTypeEnv(
		e1.type,
		ruMergeRuTrie(e1.trie, e2.trie, \a1, a2 -> a1),
		mergeTree(e1.supers, e2.supers)
	)
}

ruSuperTypeName(super : string, infer : string) -> string {
	infer + "-" + super
}

ruMakeSuperRule(super : string, infer : string, pos : int) -> RuRule {
	RuRule(
		ruSuperTypeName(super, infer), 
		makeTree1("x", RuVar("x", infer)), 
		RuTerm(super, [RuVar("x", infer)], pos),
		pos
	);
}

ruMakeTypeEnv(type : RuType, types : Tree<string, RuWithSource<RuTypeEnv>>, file : string, conf : RuConf) -> RuTypeEnv {
	supers = fold(type.supers, makeTree(), \ac, s ->
		switch (lookupTree(types, s)) {
			Some(ws): {
				foldTree(ws.item.supers,
					setTree(ac, s, ruMakeSuperRule(s, type.name, type.pos)), 
					\super, __, a ->
						setTree(a, super, ruMakeSuperRule(super, type.name, type.pos))
				)
			}
			None(): {
				conf.onError("unknown super type " + s + " for a type" + type.name, [RuPlace(file, type.pos)]);
				ac
			}
		}
	);
	RuTypeEnv(
		type,
		ruAddTerm2RuTrie(
			RuTerm(type.name, [RuVar("x", type.name)], type.pos), 
			None(),
			ruTrieEmpty, conf
		),
		supers
	)
}
