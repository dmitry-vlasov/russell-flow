import ru_load;
import ru_src;

export {
	RuCachedSource(source : RuSource, dependencyChanged : () -> bool);
	RuLoaded ::= RuRawSource, RuCachedSource;

	ruLoadAll(conf : RuConf, file : string, from_cache : (string, RuConf) -> Maybe<RuSource>) -> Maybe<Tree<string, RuLoaded>>;
	ruLoadOne(conf : RuConf, file : string, from_cache : (string, RuConf) -> Maybe<RuSource>) -> Maybe<RuLoaded>;
}

ruLoadDescr = RuLoadDescr("/*", "*/", "//", "\n", "import", ";;", ".ru");

ruLoadAll(conf : RuConf, file : string, from_cache : (string, RuConf) -> Maybe<RuSource>) -> Maybe<Tree<string, RuLoaded>> {
	err_count = ref 0;
	start = timestamp();
	if (conf.verbose > 1) {
		println("loading: " + file);
	}
	loaded = ruLoadImports([file], ruAddErrCounter(conf, err_count), from_cache, makeTree());
	if (conf.verbose > 1) {
		println(strRight(ruLoadDescr.ext, 1) + " loaded " +i2s(sizeTree(loaded)) + " files in: " + d2st((timestamp() - start)/ 1000.0, 2) + "s");
	}
	if (^err_count == 0) {
		Some(loaded);
	} else {
		None();
	}
}

ruLoadOne(conf : RuConf, file : string, from_cache : (string, RuConf) -> Maybe<RuSource>) -> Maybe<RuLoaded> {
	switch (from_cache(file, conf)) {
		Some(src): Some(RuCachedSource(src, \-> false));
		None():    cast(ruLoadRawSource(file, conf, ruLoadDescr) : Maybe<RuRawSource> -> Maybe<RuLoaded>);
	}
}

ruLoadImports(imports : [string], conf : RuConf, from_cache : (string, RuConf) -> Maybe<RuSource>, acc : Tree<string, RuLoaded>) -> Tree<string, RuLoaded> {
	loaded_file = \loaded -> {
		switch (loaded) {
			RuCachedSource(src, __):  src.info.file;
			RuRawSource(__,__, info): info.file;
		}
	}
	loaded_imports = \loaded -> {
		switch (loaded) {
			RuCachedSource(src, __):    map(src.imports, \imp -> imp.path);
			RuRawSource(imps,__, info): imps;
		}
	}
	if (imports == []) acc else {
		loaded : [RuLoaded]  = filtermap(ruConcurrent(map(imports, \imp -> \ -> ruLoadOne(conf, imp, from_cache))), idfn);
		acc1 = fold(loaded, acc, \ac, l -> setTree(ac, loaded_file(l), l));
		new_imports = fold(loaded, makeSet(), \ac, l -> 
			fold(loaded_imports(l), ac, \a, p ->
				if (containsKeyTree(acc1, ruTrimPath(p, conf, ruLoadDescr.ext))) a else insertSet(a, p)
			)
		);
		ruLoadImports(set2array(new_imports), conf, from_cache, acc1)
	}
}
