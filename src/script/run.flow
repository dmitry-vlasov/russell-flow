import script/script;
import comm;

export {
	ruApplyScriptVars(s : string, vars : Tree<string, flow>, conf : RuConf) -> string;
	ruRun(unit : RuUnit, run : (RuUnit) -> RuState, info : RuCommInfo) -> RuState;
}

ruUpdateStateConf(state : RuState, file : string) -> RuState {
	conf = state.conf;
	upd_conf = if (file == "") conf else {
		conf_dir = ruFindConfDir(file);
		if (conf_dir == "") conf else {
			file_conf = ruReadConfFromDir(conf_dir);
			missed = filterTree(file_conf, \opt, __ -> 
				!(containsKeyTree(conf.opts, opt) || contains(ru_conf_fields, opt))
			);
			ruSetConf(conf, missed);
		}
	} 
	RuState(state with conf = upd_conf);
}

ruApplyScriptVars(s : string, vars : Tree<string, flow>, conf : RuConf) -> string {
	if (!strContains(s, "$")) s else {
		vars_subst = fold(tree2pairs(vars), [], \acc, kv -> 
			concat(acc, ["$" + kv.first, ruScValue2s(kv.second)])
		);
		conf_subst = foldTree(ruConf2Tree(conf), vars_subst, \k, v, acc -> 
			concat(acc, ["$" + k, v])
		);
		strReplaces(s, conf_subst);
	}
}

ruCheckCommandArgs(task : RuTask, command : RuCommInfo, out : RuOut) -> bool {
	forall(command.args, \arg ->
		if (arg.optional || containsKeyTree(task.args, arg.name)) true else {
			out.println("Obligatory argument '" + arg.name + "' of the command '" + task.command + "' is missing");
			false;
		}
	);
}

ruAddCommandDefaults(task : RuTask, command : RuCommInfo, conf : RuConf) -> RuTask {
	args_w_defargs = foldTree(task.args, makeTree(), \name, value, acc ->
		if (command.defarg == "" || value != "" || name == command.defarg) {
			setTree(acc, name, value);
		} else {
			setTree(acc, command.defarg, name);
		}
	);
	args_w_defvals = fold(command.args, args_w_defargs, \acc, arg ->
		if (arg.defval == "") acc else {
			if (containsKeyTree(acc, arg.name)) {
				acc;
			} else {
				setTree(acc, arg.name, arg.defval);
			}
		}
	);
	RuTask(task with args = args_w_defvals);
}

ruEvalTaskArgs(task : RuTask, state : RuState) -> RuTask {
	conf = state.conf;
	apply = \v, vars -> if (runtimeValueType(v) != "string") v else {
		ruApplyScriptVars(cast(v : flow -> string), vars, conf);
	}
	args_evaled_1 = foldTree(task.args, makeTree(), \name, value, acc -> 
		setTree(acc, 
			ruApplyScriptVars(name, state.vars, conf),	
			apply(value, state.vars)
		)
	);
	all_vars = mergeTree(args_evaled_1, state.vars);
	args_evaled_2 = foldTree(args_evaled_1, makeTree(), \name, value, acc -> {
		key = ruApplyScriptVars(name, all_vars, conf);
		value_str = if (runtimeValueType(value) == "string") cast(value : flow -> string) else "";
		val = switch(lookupTree(all_vars, value_str)) {
			Some(v): apply(v, all_vars);
			None(): apply(value, all_vars);
		}
		setTree(acc, key, val);
	});
	RuTask(task with args = args_evaled_2);
}

ruMakeAutofileTasks(task : RuTask, comm : RuCommInfo, conf : RuConf) -> [RuTask] {
	if (containsKeyTree(task.args, "file") || conf.projectRoots == [] || !comm.autofile) {
		[task];
	} else {
		map(conf.projectRoots, \root -> 
			RuTask(task with args = setTree(task.args, "file", root))
		);
	}
}
/*
debugPrintInfo_total(header : string,  vars : Tree<string, flow>) -> void {
	switch (lookupTree(vars, "info_total")) {
		Some(v): {
			print ln(header + ") Var info_total: " + ruScValue2s(v) + " of type " + runtimeValueType(v));
		}
		None(): {
			print ln(header + ") No var: info_total");
		}
	}
}

debugPrintVars(header : string, vars : Tree<string, flow>) -> void {
	print ln(header + ":\n" + 
		strIndent(strGlue(
			map(tree2pairs(vars), \arg -> arg.first + "=" + ruScValue2s(arg.second)), 
			"\n")
		)
	);
}
*/
ruRun(unit : RuUnit, run : (RuUnit) -> RuState, info : RuCommInfo) -> RuState {
	out = if (!info.autoindent) unit.out else ruIndentOut(unit.out);
	state = unit.state;
	conf = state.conf;
	//debugPrintInfo_total("A", state.vars);
	//print ln("ruRun: conf:\n" + strIndent(ruConf2s(conf)));
	t0 = ruAddCommandDefaults(unit.task, info, conf);
	fold(ruMakeAutofileTasks(t0, info, conf), state,  \s0, t1 -> {
			t = ruEvalTaskArgs(t1, s0);
			//debugPrintVars("t.args", t.args);
			//debugPrintVars("s0.vars", s0.vars);
			s = RuState(s0 with 
				vars = mergeTree(s0.vars, 
					mapTree(t.args, \val ->
						if (runtimeValueType(val) == "string") {
							flow(ruApplyScriptVars(cast(val : flow -> string), s0.vars, s0.conf));
						} else {
							val;
						}
					)
				)
			);
			//debugPrintInfo_total("B", s.vars);
			if (!ruCheckCommandArgs(t, info, out)) s else {
				timer = ruMakeTimer();
				log_task = ruTaskIsLogging(t);
				task_src = ruTask2s(t);
				uuid = ruMakeUuid();
				if (log_task) {
					ruScriptLog("<" + uuid + "> Task started:    " + task_src + " ... ", conf);
				}
				ret = run(RuUnit(t, ruUpdateStateConf(s, ruTaskStringArg(t, "file", "")), out));
				if (log_task) {
					ruScriptLog(
						"<" + uuid + "> Task finished:   " +
						"in " + ruTimePassed2s(timer), conf
					);
				}
				ret;
			}
		}
	);
}
