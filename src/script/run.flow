import script/script;
import comm;

export {
	ruApplyScriptVars(s : string, vars : Tree<string, flow>, conf : RuConf) -> string;
	ruRun(unit : RuUnit, run : (RuUnit) -> RuState, info : RuCommInfo) -> RuState;
}

ruUpdateStateConf(state : RuState, file : string) -> RuState {
	conf = state.conf;
	upd_conf = if (file == "") conf else {
		conf_dir = ruFindConfDir(file);
		if (conf_dir == "") conf else {
			file_conf = ruReadConfFromDir(conf_dir);
			missed = filterTree(file_conf, \opt, __ -> 
				!(containsKeyTree(conf.opts, opt) || contains(ru_conf_fields, opt))
			);
			ruSetConf(conf, missed);
		}
	} 
	RuState(state with conf = upd_conf);
}

ruApplyScriptVars(s : string, vars : Tree<string, flow>, conf : RuConf) -> string {
	if (!strContains(s, "$")) s else {
		consts = ruConf2Tree(conf);
		subst = concatA(map(
			sortCustom(concat(getTreeKeys(vars), getTreeKeys(consts)), idfn, false),
			\key -> [
				"$" + key,
				switch (lookupTree(vars, key)) {
					Some(v): ruScValue2s(v);
					None(): switch (lookupTree(consts, key)) {
						Some(v): v;
						None(): {
							ruCrash("key: '" + key + "' must be a variable or constant. IMPOSSIBLE.");
							""
						}
					}
				}
			]
		));
		strReplaces(s, subst);
	}
}

ruCheckCommandArgs(task : RuTask, command : RuCommInfo, out : RuOut) -> bool {
	forall(command.args, \arg ->
		if (arg.optional || containsKeyTree(task.args, arg.name)) true else {
			out.println("Obligatory argument '" + arg.name + "' of the command '" + task.command + "' is missing");
			false;
		}
	);
}

ruAddCommandDefaults(task : RuTask, command : RuCommInfo, conf : RuConf) -> RuTask {
	args_w_defargs = foldTree(task.args, makeTree(), \name, value, acc ->
		if (command.defarg == "" || value != "" || name == command.defarg) {
			setTree(acc, name, value);
		} else {
			setTree(acc, command.defarg, name);
		}
	);
	args_w_defvals = fold(command.args, args_w_defargs, \acc, arg ->
		if (arg.defval == "") acc else {
			if (containsKeyTree(acc, arg.name)) {
				acc;
			} else {
				setTree(acc, arg.name, arg.defval);
			}
		}
	);
	RuTask(task with args = args_w_defvals);
}

ruEvalTaskArgs(task : RuTask, state : RuState) -> RuTask {
	conf = state.conf;
	apply = \v, vars -> if (runtimeValueType(v) != "string") v else {
		ruApplyScriptVars(cast(v : flow -> string), vars, conf);
	}
	args_evaled_1 = foldTree(task.args, makeTree(), \name, value, acc -> 
		setTree(acc, 
			ruApplyScriptVars(name, state.vars, conf),	
			apply(value, state.vars)
		)
	);
	all_vars = mergeTree(args_evaled_1, state.vars);
	args_evaled_2 = foldTree(args_evaled_1, makeTree(), \name, value, acc -> {
		key = ruApplyScriptVars(name, all_vars, conf);
		value_str = if (runtimeValueType(value) == "string") cast(value : flow -> string) else "";
		val = switch(lookupTree(all_vars, value_str)) {
			Some(v): apply(v, all_vars);
			None(): apply(value, all_vars);
		}
		setTree(acc, key, val);
	});
	RuTask(task with args = args_evaled_2);
}

ruMakeAutofileTasks(task : RuTask, comm : RuCommInfo, conf : RuConf) -> [RuTask] {
	if (containsKeyTree(task.args, "file") || conf.projectRoots == [] || !comm.autofile) {
		[task];
	} else {
		map(conf.projectRoots, \root -> 
			RuTask(task with args = setTree(task.args, "file", root))
		);
	}
}

ruRun(unit : RuUnit, run : (RuUnit) -> RuState, info : RuCommInfo) -> RuState {
	out = if (!info.autoindent) unit.out else ruIndentOut(unit.out);
	state = unit.state;
	conf = state.conf;
	t0 = ruAddCommandDefaults(unit.task, info, conf);
	fold(ruMakeAutofileTasks(t0, info, conf), state,  \s0, t1 -> {
			t = ruEvalTaskArgs(t1, s0);
			task_src = ruTask2s(t);
			s = RuState(s0 with 
				vars = mergeTree(s0.vars, 
					mapTree(t.args, \val ->
						if (runtimeValueType(val) == "string") {
							flow(ruApplyScriptVars(cast(val : flow -> string), s0.vars, s0.conf));
						} else {
							val;
						}
					)
				)
			);
			if (!ruCheckCommandArgs(t, info, out)) {
				ruScriptLog("Error: task  '" + task_src + "' " + "arguments didn't pass checking", conf);
				s 
			} else {
				timer = ruMakeTimer();
				log_task = ruTaskIsLogging(t);
				uuid = ruMakeUuid();
				if (log_task) {
					ruScriptLog("<" + uuid + "> Task started: " + task_src + " ... ", conf);
				}
				ret = run(RuUnit(t, ruUpdateStateConf(s, ruTaskStringArg(t, "file", "")), out));
				if (lookupTreeDef(conf.opts, "gc-after-task", "") == "1") {
					gc();
				}
				if (log_task) {
					ruScriptLog(
						"<" + uuid + "> Task finished: " +
						"in " + ruTimePassed2s(timer), conf
					);
				}
				RuState(ret with
					vars = foldTree(t.args, ret.vars, \var,__, acc ->
						if (containsKeyTree(state.vars,var)) acc else removeFromTree(acc, var)
					)
				);
			}
		}
	);
}
