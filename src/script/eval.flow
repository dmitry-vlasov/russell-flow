import comm;

import script/funcs/boolean;
import script/funcs/compare;
import script/funcs/convert;
import script/funcs/numeric;
import script/funcs/time;
import script/funcs/util;

export {
	ruEval(ex : ScExp, state : RuState, out : RuOut) -> ValValue;
}

ru_script_fns : ref Maybe<Tree<string, ScriptFn>> = ref None();

ruScriptFns() -> Tree<string, ScriptFn> {
	onlyOnce(ru_script_fns, 
		\-> values2tree(concatA([
			ruScriptBooleanFuncs(),
			ruScriptCompareFuncs(),
			ruScriptConvertFuncs(),
			ruScriptNumericFuncs(),
			ruScriptTimeFuncs(),
			ruScriptUtilFuncs()
		]), \sf -> sf.name)
	);
}

ruEval(ex : ScExp, state : RuState, out : RuOut) -> ValValue {
	conf = state.conf;
	switch (ex) {
		ScInfixOp(e, args): {
			vals = map(
				concat([e], map(args, \arg -> arg.exp)), 
				\arg -> ruEval(arg, state, out)
			);
			func = lookupTreeDef(ruScriptFns(), args[0].op, ruNoScriptFn);
			errs = concat(
				ruErrorArgs(vals),
				if (func.name != "") [] else ["infix operator: '" + args[0].op + "'' is not found"]
			);
			if (length(errs) > 0) {
				ValError(errs);
			} else {
				func.fn(vals, state, out);
			}
		}
		ScPrefixOp(op, e): {
			val = ruEval(e, state, out);
			func = lookupTreeDef(ruScriptFns(), op, ruNoScriptFn);
			errs = concat(
				ruErrorArgs([val]),
				if (func.name != "") [] else ["prefix operator: '" + op + "'' is not found"]
			);
			if (length(errs) > 0) {
				ValError(errs);
			} else {
				func.fn([val], state, out);
			}
		}
		ScFunc(fn, args): {
			vals = map(args, \arg -> ruEval(arg, state, out));
			func = lookupTreeDef(ruScriptFns(), fn, ruNoScriptFn);
			errs = concat(
				ruErrorArgs(vals),
				if (func.name != "") [] else ["function: '" + fn + "'' is not found"]
			);
			if (length(errs) > 0) {
				ValError(errs);
			} else {
				func.fn(vals, state, out);
			}
		}
		ScIndex(e, i): {
			e_val = ruEval(e, state, out);
			i_val = ruEval(i, state, out);
			errs = ruErrorArgs([e_val, i_val]);
			switch (e_val) {
				ValMap(mapping): {
					switch (lookupTree(mapping, i_val)) {
						Some(v): v;
						None():  ValUndef();
					}
				}
				ValArray(arr): {
					switch (i_val) {
						ValInt(ind): {
							if (0 <= ind && ind < length(arr)) {
								arr[ind];
							} else {
								ValUndef();
							}
						}
						default: {
							 ValError(concat(errs, ["second argument of array index operator must be an integer value, but got: " + ruValue2s(i_val)]));
						}
					}
				}
				default: ValError(concat(errs, ["first argument of index operator must be a map or array"]));
			}
		}
		ScUndefConst():     ValUndef();
		ScBoolConst(val):   ValBool(val);
		ScIntConst(val):    ValInt(val);
		ScDoubleConst(val): ValDouble(val);
		ScStringConst(val): ValString(val);
		ScVar(var):         ruEvalVar(var, state, out);
	}
}

ruEvalVar(var : string, state : RuState, out : RuOut) -> ValValue {
	conf = state.conf;
	switch (lookupTree(state.vars, var)) {
		Some(val): val;
		None(): {
			if (startsWith(var, "conf.")) {
				opt = strRight(var, 5);
				switch (lookupTree(conf.opts, opt)) {
					Some(val): ValString(val);
					None(): {
						if (opt == "working-dir") {
							ValString(conf.workingDir);
						} else if (opt == "verbose") {
							ValInt(conf.verbose);
						} else if (opt == "import-roots") {
							ValArray(map(conf.importRoots, \r -> ValString(r)));
						} else if (opt == "project-roots") {
							ValArray(map(conf.projectRoots, \r -> ValString(r)));
						} else {
							ValUndef();
						}
					}
				}
			} else if (var == "sources") {
				ValMap(foldTree(state.ru, makeTree(), \id, src, acc -> 
					setTree(acc, ValString(id2s(id)), ValData("RuSource", src))
				));
			} else if (var == "math") {
				math = ruMathFromSources(state.ru, RuEnv(conf, out));
				ValMap(pairs2tree([
					Pair(ValString("sources"), ValMap(foldTree(math.sources, makeTree(), \id, src, acc -> 
						setTree(acc, ValString(id2s(id)), ValData("RuSource", src))
					))),
					Pair(ValString("consts"), ValMap(foldTree(math.decls.consts, makeTree(), \id, const, acc -> 
						setTree(acc, ValString(id2s(id)), ValData("RuConst", const))
					))),
					Pair(ValString("types"), ValMap(foldTree(math.decls.types, makeTree(), \id, type, acc -> 
						setTree(acc, ValString(id2s(id)), ValData("RuType", type))
					))),
					Pair(ValString("rules"), ValMap(foldTree(math.decls.rules, makeTree(), \id, rule, acc -> 
						setTree(acc, ValString(id2s(id)), ValData("RuRule", rule))
					))),
					Pair(ValString("assertions"), ValMap(foldTree(math.decls.assertions, makeTree(), \id, assertion, acc -> 
						setTree(acc, ValString(id2s(id)), ValData("RuAssertion", assertion))
					))),
				]));
			} else {
				ValUndef();
			}
		}
	}
}
