script1 = ws (script | empty_seq);
non_seq = if_else | if | exit | stop | while | print | data | define | assign | task ;

script = non_seq:ns script2:j last_line_comment? { makeScComm(:ns, :j) };
	script2 = seq | empty_seq;
	empty_seq = '' ws { ScSeq(emptyArray()) };
	seq  = script+ :js { ScSeq(:js) };

	if_else  = "if" ws exp:c '{' ws script:p '}' ws "else" ws '{' ws script:n '}' ws { ScIf(:c, :p, Some(:n)) };
	if       = "if" ws exp:c '{' ws script:j '}' ws    { ScIf(:c, :j, None()) };
	while    = "while" ws exp:c '{' ws script:j '}' ws { ScWhile(:c, :j) };
	print    = "print" ws exp:e ';' ws                 { ScOutput("print", :e) };
	data     = "output" ws exp:e ';' ws                { ScOutput("output", :e) };
	define   = "let" ws id:v '=' ws exp:e ';' ws script:s { ScLet(:v, :e, :s) };
	assign   = id:v lvalue1:a ':=' ws exp:e ';' ws     { ScAssign(:v, :a, :e) };
	task     = id:i task_args:as ';' ws                { makeScTask(:i, :as) };
	exit     = "exit" ws ';' ws                        { ScAction("exit") };
	stop     = "stop" ws ';' ws                        { ScActoin("stop") };

type = type_arr | type_fn | type_infix | type_prefix | type_const ;
	type_arr     = "[" ws type:t "]" ws                                           { makeTypeArray(:t) } ;
	type_fn      = "(" ws (type_fn_arg)*:as ")" ws "->" ws type:v                 { makeTypeFn(:as, :v) };
	type_infix   = "(" ws type:t1 type_id:op ws type:t2 (type_inf_arg)*:as ")" ws { makeTypeInfix(:op, :t1, :t2, :as) };
	type_prefix  = type_id:op ws "(" ws (type_fn_arg)*:as ")" ws                  { makeTypePrefix(:op, :as) };
	type_const   = type_id:op ws                                                  { makeTypeConst(:op) };
	type_fn_arg  = type:t (',' ws)?  { :t };
	type_inf_arg = type_id ws type:t { :t };
	type_id   = (!bad_type_id_char any)+ $id { $id };
	bad_type_id_char = ws_ | '(' | ')' | '[' | ']' | '{' | '}' | ',';

exp = undef_const | bool_const | double_const | int_const | string_const | 
	cond_exp | infix_op | index1 | index2 | get1 | get2 | map |
	array | pref_op | call_exp | call_name | struct | lambda | var;

	cond_exp     = '(' ws exp:c '?' ws exp:p ':' ws exp:n ')' ws { ScCond(:c, :p, :n) };
	infix_op     = '(' ws exp:e infix_arg+:as ')' ws { ScInfixOp(:e, :as) };
	infix_arg    = op:o exp:v                        { ScInfixArg(:o, :v) };
	pref_op      = op:o exp:v                        { ScPrefixOp(:o, :v) };
	call_name    = id_fn:fn call_args:as               { ScCall(ScVar(:fn), :as) };
	call_exp     = '(' ws exp:fn ')' ws call_args:as { ScCall(:fn, :as) };
		call_args = zeroary_args | unary_args | nary_args;
			zeroary_args = '(' ws ')' ws                              { emptyArray() };
			unary_args = '(' ws exp:a ')' ws                          { array(:a) };
			nary_args =  '(' ws exp:a0 call_arg*:as ')' ws            { consArray(:a0, :as) };
				call_arg  = ',' ws exp:arg                            { :arg };
	//call_arg     = exp:arg (',' ws)?                 { :arg };

	index1        = var:m '[' ws exp :i ']' ws               { ScIndex(:m, :i) };
	index2        = '(' ws exp:m ')' ws '[' ws exp :i ']' ws { ScIndex(:m, :i) };
	lambda       = '\\' ws lam_args:as "->" ws exp:b         { ScLambda(:as, :b) };
		lam_args = nary_lamargs | unary_lamargs | zeroary_lamargs;
			zeroary_lamargs = ws                                        { emptyArray() };
			unary_lamargs = var:v ws                                    { array(:v) };
			nary_lamargs =  var:v0 lam_arg*:vs                          { consArray(:v0, :vs) };
				lam_arg  = ',' ws var:v                                 { :v };
	//lam_arg     =  var:v (',' ws)?                   { :v };
	array        = empty_array | unary_array | arbitrary_array;
		empty_array = '[' ws  ']' ws { ScArray(emptyArray()) };
		unary_array = '[' ws exp:e  ']' ws { ScArray(array(:e)) };
		arbitrary_array = '[' ws exp:f array_arg+:as  ']' ws { ScArray(consArray(:f, :as)) };
			array_arg = ',' ws exp:e { :e };
	
	get1        = var:m '.' ws id :f                { ScField(:m, :f) };
	get2        = '(' ws exp:m ')' ws '.' ws id:f   { ScField(:m, :f) };
	struct      = id_st:st call_args:as               { ScStruct(:st, :as) };

	map         = empty_map | unary_map | arbitrary_map;
		empty_map = '{' ws  '}' ws { ScMap(emptyArray()) };
		unary_map = '{' ws exp:k '=' ws exp:v  '}' ws { ScMap(array(Pair(:k, :v))) };
		arbitrary_map = '{' ws exp:k '=' ws exp:v map_arg+:as  '}' ws { ScMap(consArray(Pair(:k, :v), :as)) };
			map_arg = ',' ws exp:k '=' ws exp:v { Pair(:k, :v) };

	double_const = (digit+ '.' digit+) $s ws          { ScDoubleConst(s2d(:s)) };
	string_const = '"' ("\\\"" | !'"' any)* $s '"' ws { ScStringConst(:s) };
	int_const    = num:n                              { ScIntConst(s2i(:n)) };
	var          = id:i                               { ScVar(:i) };
	bool_const   = ("true" | "false") $s ws           { makeScBoolConst(:s) };
	undef_const  = "undef" ws                         { ScUndefConst() };

lvalue1 = lval_field | lval_array | lval_direct ;
	lval_field = '.' ws id:f lvalue1:n { ScFieldLval(:f, :n) };
	lval_array = '[' ws exp:i ']' ws lvalue1:n { ScIndexLval(:i, :n) };
	lval_direct = '' ws { ScDirectLval() };

num   = digit+ $s ws { :s };
id    = (id_start id_symb*) $s ws { :s };
id_fn = ('a'-'z' id_symb*) $s ws { :s };
id_st = ('A'-'Z' id_symb*) $s ws { :s };
str   = (!space any)+ $s ws { :s };
op    = (!n_op any)+ $s ws { :s };
n_op  = space | '(' | ')' | '"' | '[' | ']' | ',' | ':' | id_start | digit | '\\' | '/';
task_args = (!';' any)* $s ws { :s };

digit    = '0'-'9';
id_symb  = 'a'-'z' | 'A'-'Z' | '0'-'9' | '-' | '_';
id_start = 'a'-'z' | 'A'-'Z';

space = ' ' | '\t' | '\n' | '\r';
ws_ = space | comment1 | comment2 | comment3 | comment4;
ws = ws_*;
comment1 = "//" (!'\n' any)* '\n';
comment2 = "#" (!'\n' any)* '\n';
comment3 = "/**" (!"*/" any)* $s "*/" { makeDocComment(:s) };
comment4 = "/*" (!"*/" any)* "*/";

last_line_comment = "//" (!'\n' any)*;

any = '\u0000'-'\uFFFF';
