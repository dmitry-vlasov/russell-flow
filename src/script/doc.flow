import lingo/pegcode/driver;
import base/util/parse;
import comm;

export {
    ruParseScriptDoc(name : string, doc : string) -> RuCommInfo;
}

ru_script_doc_parser = compilePegGrammar("#include script/doc.lingo");

ruParseScriptDoc(name : string, doc : string) -> RuCommInfo {
    info = ref RuCommInfo(changeFileExt(name, ""), "", "", "", false, false, [], []);
    tr = \s -> trim2(s, " \t\r\n");
    ruParsic(ru_script_doc_parser, doc, 
            SemanticActions(mergeTree(defaultPegActions.t, 
            pairs2tree([
                Pair("addHelp", \s -> {
                    info := RuCommInfo(^info with help = (^info).help + " " + tr(s[0])); 0;
                }),
                Pair("addDefarg", \s -> {
                    info := RuCommInfo(^info with defarg = tr(s[0])); 0;
                }),
                Pair("addCategory", \s -> {
                    info := RuCommInfo(^info with category = tr(s[0])); 0;
                }),
                Pair("setAutofile", \s -> {
                    info := RuCommInfo(^info with autofile = true); 0;
                }),
				Pair("setAutoindent", \s -> {
                    info := RuCommInfo(^info with autoindent = true); 0;
                }),
                Pair("addArgWDefval", \s -> {
                    arg_name   = tr(s[0]);
                    arg_body_1 = tr(s[1]);
                    defval     = tr(s[2]);
                    arg_body_2 = tr(s[3]);
                    arg = RuCommArg(
                        arg_name, true, 
                        strReplace(arg_body_1 + (if (arg_body_2 == "") "" else " " + arg_body_2), "@optional", ""),
                        defval
                    );
                    info := RuCommInfo(^info with args = concat((^info).args, [arg])); 0;
                }),
                Pair("addArg", \s -> {
                    arg_name = tr(s[0]);
                    arg_body = tr(s[1]);
                    arg = RuCommArg(
                        arg_name, 
                        strContains(arg_body, "@optional"), 
                        strReplace(arg_body, "@optional", ""),
                        ""
                    );
                    info := RuCommInfo(^info with args = concat((^info).args, [arg])); 0;
                })
            ])
        ))
    );
    ^info;
}
