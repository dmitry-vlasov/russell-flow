import script/func;
import script/util;

export {
	ruScriptUtilFuncs() -> [ScriptFn];
}

ruScriptUtilFuncs() -> [ScriptFn] {
	[
		ScriptFn("cwd", \args, state, out -> 
			ruEvalValueFn(args, \as -> Some(ValString(ruCurrentWorkingDir())),  "cwd", 0)
		),
		ScriptFn("arr", \args, state, out ->
			if (ruAllArgsAreOfSameType(args)) ValArray(args) else 
			ValError(["array must be constructed of elements of the same type, actual arguments: " + ruValues2s(args)])
		),
		ScriptFn("map", \args, state, out -> {
			unzipped = unzipi(args, \i,__ -> i % 2 == 0);
			if (!ruAllArgsAreOfSameType(unzipped.first)) {
				ValError(["keys of a map must be constructed of elements of the same type, actual keys: " + ruValues2s(unzipped.first)]);
			} else if (!ruAllArgsAreOfSameType(unzipped.second)) {
				ValError(["values of a map must be constructed of elements of the same type, actual keys: " + ruValues2s(unzipped.second)]);
			} else {
				ValMap(
					pairs2tree(zipWith(unzipped.first, unzipped.second, \k, v -> Pair(k, v)))
				);
			}
		}),
		ScriptFn("isdef", \args, state, out ->
			ruEvalValueFn(args, \as -> Some(ValBool(!ruValueIsUndef(as[0]))), "isdef", 1)
		),
		ScriptFn("mem2s", \args, state, out ->
			ruEvalDoubleFn(args, \as -> Some(ValString(memory2s(as[0]))),  "mem2s", 1)
		),
		ScriptFn("ass2s", \args, state, out ->
			if (length(args) != 1) {
				ValError(["ass2s function must take exactly one argument, actual arguments: " + ruValues2s(args)]);
			} else {
				switch (args[0]) {
					ValData(type, data): {
						if (!contains(["RuAssertion", "RuAxiom", "RuDef", "RuTheorem"], type)) {
							ValError(["wrong type of data: '" + type + "', must be one of: RuAssertion, RuAxiom, RuDef, RuTheorem"]);
						} else {
							ass = cast(data : flow -> RuAssertion);
							math = ruMathFromSources(state.ru, RuEnv(state.conf, out));
							ValString(ru2s(ass, math));
						}
					}
					ValString(name): {
						math = ruMathFromSources(state.ru, RuEnv(state.conf, out));
						switch (lookupTree(math.decls.assertions, s2id(name))) {
							Some(ass): ValString(ru2s(ass, math));
							None(): ValUndef();
						}
					}
					default: ValUndef();
				}
			}
		),
	]
}
