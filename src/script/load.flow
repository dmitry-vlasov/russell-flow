import script/script;
import script/doc;

export {
	ruLoadScript(file : string, env : RuEnv) -> Maybe<ScScript>;
	ruLoadAllScripts(env : RuEnv) -> [ScScript];
}

ruFindScript(name : string, env : RuEnv) -> Maybe<RuFileInfo> {
	scripts_dir = getRussellDir() + "/" + "scripts";
	if (fileExists(name)) {
		ruLoadFileInfo(name, name); 
	} else  if (fileExists(name + ".rus")) {
		ruLoadFileInfo(name, name + ".rus");
	} else if (fileExists(env.conf.workingDir + "/" + name)) {
		ruLoadFileInfo(name, env.conf.workingDir + "/" + name);
	} else if (fileExists(pathCombine(env.conf.workingDir, name + ".rus"))) {
		ruLoadFileInfo(name, env.conf.workingDir + "/" + name + ".rus");
	} else if (fileExists(scripts_dir + "/" + name)) {
		ruLoadFileInfo(name, scripts_dir + "/" + name); 
	} else if (fileExists(scripts_dir + "/" + name + ".rus")) {
		ruLoadFileInfo(name, scripts_dir + "/" + name + ".rus");
	} else {
		None();
	}
}

ruLoadScript(name : string, env : RuEnv) -> Maybe<ScScript> {
	maybeBind(
		ruFindScript(name, env),
		\info -> {
			source = getFileContent(info.path);
			switch (ruParseScComm(source)) {
				Some(s): {
					if (ruVerbose(env.conf) >= 3) {
						env.out.println("\tScript '" + name + "' is loaded: " + ruScComm2s(s.comm));
					}
					Some(ScScript(s.comm, ruParseScriptDoc(name, s.docs), info));
				}
				None(): {
					env.out.println("\tSyntax error in script: '" + escapeStr(source) + "'");
					None();
				}
			}
		}
	);
}

ruLoadAllScripts(env : RuEnv) -> [ScScript] {
	scripts_dir = pathCombine(getRussellDir(), "scripts");
	script_names = concat3(
		filtermap(readDirectory(scripts_dir), \file -> 
			if (endsWith(file, ".rus")) Some(file) else None()
		),
		filtermap(readDirectory(env.conf.workingDir), \file -> 
			if (endsWith(file, ".rus")) Some(file) else None()
		),
		filtermap(readDirectory("."), \file -> 
			if (endsWith(file, ".rus")) Some(file) else None()
		),
	);
	filtermap(script_names, \name -> ruLoadScript(name, env));
}
