import ru_cache;
import ru_conf;
import ru_log;

export {
	ruProcessInfo(conf : RuConf) -> void;
}

ruProcessInfo(conf : RuConf) -> void {
	if (ruOptIsTrue("outline", conf)) {
		ruInfoOutline(conf);
	}  else {
		if (ruSomeOptsAreSet(["decl", "uses", "hover", "rename", "find"], conf)) {
			if (!ruInfoTask(conf, "decl", ruInfoDecl)) {
				if (!ruInfoTask(conf, "hover", ruInfoHover)) {
					if (!ruInfoTask(conf, "uses", ruInfoUses)) {
						name = ruOpt("find", conf);
						if (name != "") {
							ruInfoDecl(name, conf);
						}
					}
				}
			}
		}
	}
}

ruInfoTask(conf : RuConf, task_name : string, task_job : (string, RuConf) -> void) -> bool {
	opt = ruOpt(task_name, conf);
	if (opt == "") false else {
		line = s2i(ruOptDef("line", "-1", conf));
		col = s2i(ruOptDef("col", "-1", conf));
		name = if (opt != "1") opt else {
			if (line != -1 && col != -1) {
				ruNameByLineCol(line, col, getFileContent(conf.path));
			} else {
				ruPrintln("Provide line=<int> and col=<in> arguments", conf);
				""
			}
		}
		if (name != "") {
			task_job(name, conf);
		}
		true;
	}
}

ruInfoDecl(name : string, conf : RuConf) -> void {
	maybeApply(ruCachedDecl(name), \d ->
		maybeApply(ruFromCache(d.src, conf), \cached -> 
			ruPrintln(ruRange2s(cached.source.info, ruDeclRange(d.decl, cached.source), "declaration of " + name), conf)
		)
	);
}

ruInfoHover(name : string, conf : RuConf) -> void {
	maybeApply(ruCachedDecl(name), \d -> 
		maybeApply(ruFromCache(d.src, conf), \cached -> {
			range = ruDeclRange(d.decl, cached.source);
			text = ruFileSource(cached.source.info.path);
			ruPrintln(ruTrimAll(substring(text, range.start, range.end - range.start)), conf)
		})
	);
}

ruInfoOutline(conf : RuConf) -> void {
	maybeApply(ruFromCache(conf.file, conf), \cached -> {
		src = cached.source;
		code = ruFileSource(src.info.path);
		resolver = makeLineResolver(code);
		iter(src.imports, \imp -> 
			maybeApply(ruNameRangeByPos(imp.pos, code), \range ->
				ruPrintln(ruRangeWithResolver2s(src.info, range, "import " + imp.path, resolver), conf)
			)
		);
		iter(src.decls, \decl -> {
			switch (decl) {
				RuDecl(): {
					maybeApply(ruNameRangeByPos(ruDeclPos(decl), code),
						\range -> 
							ruPrintln(ruRangeWithResolver2s(src.info, range, ruDeclKind(decl) + " " + ruDeclName(decl), resolver), conf)
					);
				}
				default: { }
			}
		});
	});
}

ruInfoUses(name : string, conf : RuConf) -> void {
	maybeApply(ruCachedDecl(name), \decl ->
		maybeApply(ruFromCache(decl.src, conf), \cached ->
			iter(ruSourcesCache(), \s -> { 
				iter(s.decls, \d ->
					switch (d) {
						RuTheorem(__,__,__,__,__,__,proof,__):
							iter(proof.steps, \step ->
								if (step.assertion == name) {
									code = ruFileSource(s.info.path);
									maybeApply(ruNameRangeByPos(step.pos, code),
										\range -> 
											ruPrintln(ruRange2s(cached.source.info, range, "uses " + name), conf)
									);
								}
							);
						default: { }
					}
				);
			})
		)
	);
}
