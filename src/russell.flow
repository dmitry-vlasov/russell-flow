import lingo/linecolumn;
import math/stringmath;
import parse/ru_load;
import parse/ru_parse;
import parse/ru_collect_math;
import parse/mm_parse;
import parse/mm_collect_math;
import ru_usage;
import ru_verify;
import ru_2_mm;
import mm_2_ru;
import mm_verify;
import ru_split;
import ru_imports;
import ru_refactor;
import ru_shorten;
import prover/ru_prover;

ruOperateMath(comm : RuCommand, math : RuMath) -> RuMath {
	if (comm.name == "verify") {
		ruCommandVerify(math, comm.args);
	} else if (comm.name == "remove-duplicate-steps") {
		ruCommandRemoveMathDuplicateSteps(math, comm.args);
	} else if (comm.name == "remove-unused-steps") {
		ruCommandRemoveMathUnusedSteps(math, comm.args);
	} else if (comm.name == "remove-unused-hyps") {
		ruCommandRemoveMathUnusedHyps(math, comm.args);
	} else if (comm.name == "shorten-proofs") {
		ruCommandShortenProofs(math, comm.args);
	} else if (comm.name == "reprove") {
		ruCommandReproveTask(math, comm.args);
	} else {
		println("unknown command: " + comm.name + ", with args: " + strGlue(comm.args, ","));
		math
	}
}

ruProcessMath(math0 : RuMath) -> void {
	math1 = ruIf(ruOptIsTrue("ru-imports", math0.conf), math0, ruOptimizeMathImports);
	math = fold(ruConf2Commands(math1.conf), math1, \acc, comm -> ruOperateMath(comm, acc));
	if (ruOptIsTrue("ru-2-mm", math.conf)) {
		mmProcessMath(ruMath2mm(RuMath(math with conf = ruRemoveOpt("ru-2-mm", math.conf))));
	}
	prepare_srcs = \ -> {
		ruRoot0 = rtrim2(lookupTreeDef(math.conf.opts, "ru-root", ""), "/");
		ruRoot = if (ruRoot0 == "1") "." else ruRoot0 + "/";
		map(getTreeValues(math.sources), 
			\src : RuSource ->
				RuWriteSource(
					ru2s(src, math.rules),
					RuFileInfo(src.info with path = ruRoot + src.info.file + ".ru")
				)
		)
	}
	if (ruOptIsTrue("ru-write-all", math.conf)) {
		ruWriteSources(prepare_srcs(), math.conf);
	}
	write_src = \src -> if (!setFileContent(src.info.path, ru2s(src, math.rules))) {
		println("failed to save file: " + src.info.path);
	}
	if (ruOptIsSet("ru", math.conf)) {
		write_src(ruMath2SingleSource(math, ruRemoveExt(ruOpt("ru", math.conf), ".ru")));
	}
	if (ruOptIsSet("ru-write", math.conf)) {
		file = ruTrimPath(ruOpt("ru-write", math.conf), math.conf, ".ru");
		switch (lookupTree(math.sources, file)) {
			Some(src): write_src(src);
			None(): println("source: " + file + " is not found");
		}
	}
	if (ruOptIsSet("reprove", math.conf)) {
		ruCommandReproveTask(math, [ruOpt("reprove", math.conf)]); {}
	}
	if (ruOptIsSet("shorten-proofs", math.conf)) {
		ruCommandShortenProofs(math, [ruOpt("shorten-proofs", math.conf)]); {}
	}
	if (ruOptIsTrue("ru-stats", math.conf)) {
		println("ru stats:\n" + ruMathStats(math) + "\n");
	}
}

mmProcessMath(math : MmMath) -> void {
	if (ruOptIsSet("mm", math.conf)) {
		src = mmMath2SingleSource(math, ruRemoveExt(ruOpt("mm", math.conf), ".mm"));
		txt = mm2s(ruIf(ruOptIsTrue("strip-comments", math.conf), src, mmStripComments));
		if (!setFileContent(src.info.path, txt)) {
			println("failed to save file: " + src.info.path);
		}
	}
	if (ruOptIsTrue("mm-2-ru", math.conf)) {
		maybeApply(ruMakeMath1(mmMath2ru(math), math.conf), \verified ->
			ruProcessMath(
				ruIf(ruOptIsTrue("mm-split", math.conf), 
					ruIf(ruOptIsSet("mm-fix", math.conf), 
						RuMath(verified with conf = ruRemoveOpt("mm-2-ru", verified.conf)), 
						ruFixLeftRecursiveRules
					), 
					ruSplitMath
				)
			)
		);
	}
	if (ruOptIsTrue("mm-stats", math.conf)) {
		println("mm stats:\n" + mmMathStats(math) + "\n");
	}
}

ruParseMath(conf : RuConf) -> Maybe<Tree<string, RuParseSource>> {
	maybeBind(ruLoad(conf, ruLoadDescr), \loaded ->
		ruParse(getTreeValues(loaded), conf)
	)
}

ruMakeMath1(parsed : Tree<string, RuParseSource>, conf : RuConf) -> Maybe<RuMath> {
	maybeMap(maybeBind(ruCollectMath(parsed, conf), ruVerifyMath), 
		\math -> ruIf(ruOptIsTrue("ru-imports", conf), math, ruOptimizeMathImports)
	)
}

ruMakeMath(conf : RuConf) -> Maybe<RuMath> {
	maybeBind(ruParseMath(conf), \parsed -> ruMakeMath1(parsed, conf))
}

mmMakeMath(conf : RuConf) -> Maybe<MmMath> {
	maybeBind(ruLoad(conf, mmLoadDescr), \loaded ->
		maybeBind(mmParse(getTreeValues(loaded), conf), \parsed ->
			maybeBind(mmCollectMath(parsed, conf), \collected ->
				mmVerifyMath(collected)
			)
		)
	)
}

main() -> void {
	println("Russell prover (4th generation)");
	println("");
	onError = \err, places -> println(err + (if (places == []) "" else "\n" + strGlue(map(places, ruPlace2s), "\n")));
	switch (ruMakeConf(onError)) {
		Some(conf): {
			if (lookupTreeDef(conf.opts, "help", "0") != "0") {
				ruUsage(conf);
			} else {
				start = timestamp();
				if (conf.ext == ".ru") {
					maybeApply(ruMakeMath(conf), ruProcessMath);
				} else if (conf.ext == ".mm") {
					maybeApply(mmMakeMath(conf), mmProcessMath);
				} else {
					println("file extension should be *.ru or *.mm, while given is: '" + conf.ext + "'");
				}
				println("processed in " + d2st((timestamp() - start)/ 1000.0, 2) + "s");
			}
		}
		None(): { }
	}
	quit(0);
}
