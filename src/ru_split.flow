import ru_math;
import ru_cache;
import ru_update;

export {
	ruSplitMath(math : RuMath, conf : RuConf, file : string) -> RuMath;
}

ruSplitMath(math : RuMath, conf : RuConf, file : string) -> RuMath {
	start = timestamp();
	num1 = sizeTree(math.sources);
	split_sources = foldTree(math.sources, makeTree(), \name, src, acc -> {
			if (conf.verbose > 1) {
				conf.onMessage("going to split module: '" + src.info.module + "'");
			}
			mergeTree(acc, ruSplitSources(src, conf, math, file))
		}
	);
	ret = RuMath(math with sources = split_sources);
	created = timestamp();
	module = ruTrimPath(file, conf, ".ru");
	src = RuSource(
		map(getTreeValues(ret.sources), \src -> RuImport(src.info.module, -1)),
		[], ruNewFileInfo(module, conf, ".ru")
	);
	if (conf.verbose > 0) {
		conf.onMessage("ru split " + i2s(num1) + " files into " + i2s(sizeTree(ret.sources)) + " in " + d2st((timestamp() - start)/ 1000.0, 2) + "s");
	}
	ruUpdateSourceNames(RuMath(ret with sources = setTree(ret.sources, file, src)));
}

RuSplitSource(
	pattern : string,
	header : string,
	path : string,
	decls : ref [RuBlock],
	subs : ref [RuSplitSource]
);

strReplaceAll(str : string, chars : string, onto : string) -> string {
	char_codes = buildSet(s2a(chars));
	under_code = getCharCodeAt(onto, 0);
	fold(
		map(s2a(str), \i -> if (containsSet(char_codes, i)) under_code else i),
		"",
		\ac, i -> ac + fromCharCode(i)
	)
}

ruSplitLevel(src : RuSplitSource, pattern : string) -> RuSplitSource {
	makePath = \h -> strReplaceAll(h, " /:.?!;$\\\"'`#", "_");
	addBlock = \acc, block -> { refArrayPush(lastElement(^(acc.subs), acc).decls, block); acc }
	addSrc = \acc, h -> {
		path = (if (src.path != "") src.path + "/" else "") + makePath(h);
		refArrayPush(acc.subs, RuSplitSource(pattern, h, path, ref [], ref [])); 
		acc 
	}
	getHeader = \txt -> trim2(strSplit(txt, "\n")[2], " \t\n\r");
	isHeader = \txt -> {
		if (!(strContains(txt, pattern) && strCountOf(txt, "\n") > 1)) false else {
			getHeader(txt) != "";
		}
	}
	fold(^(src.decls), RuSplitSource(src with decls = ref [], subs = ref []), \acc, block ->
		switch (block) {
			RuComment(text, __): {
				if (!isHeader(text)) {
					addBlock(acc, block)
				} else {
					addBlock(addSrc(acc, getHeader(text)), block);
				}
			}
			default: addBlock(acc, block);
		}
	)
}

RuSplitPattern(
	pattern : string, 
	child : Maybe<RuSplitPattern>
);

ruSplitSourceRec(src : RuSplitSource, p : RuSplitPattern) -> RuSplitSource {
	parts = ruSplitLevel(src, p.pattern);
	switch (p.child) {
		None(): parts;
		Some(ch_p):
			RuSplitSource(parts with
				subs = ref map(^(parts.subs), \part -> ruSplitSourceRec(part, ch_p))
			);
	}
}

ruDoSplitSource(src : RuSource, conf : RuConf) -> RuSplitSource {
	ruSplitSourceRec(
		RuSplitSource("", "", "", ref src.decls, ref []), 
		RuSplitPattern("######", 
			Some(RuSplitPattern("#*#*#*#",
				Some(RuSplitPattern("=-=-=-=", 
					Some(RuSplitPattern("-.-.-.-.", 
						None()
					))
				))
			))
		)
	)
}

RuLinearizeAcc(
	srcs : Tree<string, RuSource>,
	last : Maybe<RuSource>
);

ruMakeSplittedFile(file : string, acc : RuLinearizeAcc) -> string {
	if (!containsKeyTree(acc.srcs, file)) file else {
		ruMakeSplittedFile(file + "1", acc)
	}
}

ruLinearizeSplitSources(src : RuSplitSource, acc : RuLinearizeAcc, conf : RuConf, math : RuMath, file0 : string) -> RuLinearizeAcc {
	file = ruMakeSplittedFile(if (src.path == "") file0 + "_init" else src.path, acc);
	module = ruTrimPath(file, conf, ".ru");
	header_pattern = lpad("", src.pattern, 40);
	curr_src = RuSource(
		eitherMap(acc.last, \last -> [RuImport(last.info.module, -1)], []),
		^(src.decls),
		ruNewFileInfo(module, conf, ".ru")
	);
	if (conf.verbose > 1) {
		conf.onMessage("\tru splitted file: '" + curr_src.info.module + "'");
	}
	fold(^(src.subs), 
		RuLinearizeAcc(setTree(acc.srcs, file, curr_src), Some(curr_src)), 
		\ac, sub -> ruLinearizeSplitSources(sub, ac, conf, math, file0)
	);
}

ruSplitSources(src : RuSource, conf : RuConf, math : RuMath, file : string) -> Tree<string, RuSource> {
	ruLinearizeSplitSources(
		ruDoSplitSource(src, conf), 
		RuLinearizeAcc(makeTree(), None()), 
		conf, math, file
	).srcs;
}
