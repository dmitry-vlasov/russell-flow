import net/http;
import base/comms;
import ru/comms;
import mm/comms;
import script/exec;
import help;

export {
	ruClearCommands : [RuComm];
}

ruClearCommands = [
	ruCommClearMath,
	ruCommClearVars,
	ruCommClearProofs,
];

ruCommClearMath = RuComm(
	RuCommDescr(
		"clear-math", "misc", "clear all math structures", "", false,
		[RuCommArg("what", true, "'ru', 'mm' or 'all', default is 'all'", "all")]
	), 
	ruChainRuns([ruRunClearMath, ruRunGC])
);

ruRunClearMath(task : ScTask, state : RuState) -> Maybe<RuState> {
	conf = state.conf;
	what = lookupTreeDef(task.args, "what", "all");
	new_ruMath = if (what == "ru" || what == "all") {
		conf.onMessage("ru math is cleared");
		ruEmptyMath;
	} else state.ruMath;
	new_mm = if (what == "mm" || what == "all") {
		conf.onMessage("mm math is cleared");
		makeTree();
	} else state.mm;
	Some(RuState(state with ruMath = new_ruMath, mm = new_mm)); 
}

ruCommClearProofs = RuComm(
	RuCommDescr("clear-proofs", "misc", "clear all proofs", "", false, []), 
	ruChainRuns([ruRunClearProofs, ruRunGC])
);

ruRunClearProofs(task : ScTask, state : RuState) -> Maybe<RuState> {
	cleared = mapTree(state.ruMath.sources, \src ->
		RuSource(src with 
			decls = map(src.decls, \decl -> 
				switch (decl) {
					RuTheorem(__,__,__,proof): RuTheorem(decl with proof = ruNoProof);
					default: decl;
				}
			)
		)
	);
	gc();
	Some(RuState(state with 
		ruMath = ruMakeMathFromSources(cleared, state.conf)
	)); 
}

ruCommClearVars = RuComm(
	RuCommDescr("clear-vars", "misc", "clear all script variable values", "", false, []),
	ruChainRuns([ruRunClearVars, ruRunGC])
);

ruRunClearVars(task : ScTask, state : RuState) -> Maybe<RuState> {
	Some(RuState(state with vars = makeTree()));
}
